// Процедура управляет доступностью ЭУ в форме
//
// Параметры
//  нет
//
&НаКлиенте
Процедура УправлениеДиалогом()
	
	Если Объект.ВидЗатрат = ПредопределенноеЗначение("Перечисление.ВидыЗатрат.Материальные") 
	   И Объект.ХарактерЗатрат = ПредопределенноеЗначение("Перечисление.ХарактерЗатрат.ПроизводственныеРасходы") Тогда
		Элементы.СтатусМатериальныхЗатрат.Доступность = Истина;
		Элементы.СтатусМатериальныхЗатрат.ОтметкаНезаполненного = Объект.СтатусМатериальныхЗатрат.Пустая();
	Иначе
		Элементы.СтатусМатериальныхЗатрат.Доступность = Ложь;
		Элементы.СтатусМатериальныхЗатрат.ОтметкаНезаполненного = Ложь;
	КонецЕсли;
	
КонецПроцедуры // УправлениеДиалогом()

// Процедура обработчик события "ПриИзменении" реквизита "ВидЗатрат".
// 

&НаКлиенте
Процедура ВидЗатратПриИзменении(Элемент)
	
	Если НЕ Объект.ВидЗатрат = ПредопределенноеЗначение("Перечисление.ВидыЗатрат.Материальные") Тогда
		Объект.СтатусМатериальныхЗатрат = Неопределено;
	КонецЕсли;
	
	УправлениеДиалогом();
	
КонецПроцедуры // ВидЗатратПриИзменении()

// Процедура обработчик события "ПриИзменении" реквизита "ХарактерЗатрат".
// 
&НаКлиенте
Процедура ХарактерЗатратПриИзменении(Элемент)
	
	Если Объект.ХарактерЗатрат <> ПредопределенноеЗначение("Перечисление.ХарактерЗатрат.ПроизводственныеРасходы") Тогда
		Объект.СтатусМатериальныхЗатрат = Неопределено;
	КонецЕсли;
	
	УправлениеДиалогом();
	
КонецПроцедуры // ХарактерЗатратПриИзменении()

// Процедура заполняет список значений для поля "Характер затрат".
//
&НаСервере
Процедура ЗаполнитьСписокХарактеровЗатрат()
	
	СписокВыбора = Элементы.ХарактерЗатрат.СписокВыбора;
	СписокВыбора.Очистить();
	
	Для Каждого Значение Из Перечисления.ХарактерЗатрат Цикл
		
		Если Значение = Перечисления.ХарактерЗатрат.ЗаготовлениеИПриобретениеМЦ Тогда
			Продолжить;
		КонецЕсли;
		
		СписокВыбора.Добавить(Значение);
	
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьСписокХарактеровЗатрат()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Обработчик события ПриОткрытии Формы.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ЗаполнитьСписокХарактеровЗатрат();
	
КонецПроцедуры // ПриОткрытии()

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УправлениеДиалогом();
КонецПроцедуры

Процедура СкопироватьСтрокиИВыражения(ОбъектКопирования) Экспорт
	Если ВидНастройки > 0 Тогда
		Для каждого ТекСтр Из ОбъектКопирования.Макеты Цикл
			НоваяСтрока = Макеты.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтр);
		КонецЦикла;
	КонецЕсли;
	
	Запрос=Новый Запрос("");
	Запрос.УстановитьПараметр("Настройка", ОбъектКопирования.Ссылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаСтрок.Ссылка КАК ИсходныйОбъект,
	|	ЗНАЧЕНИЕ(Справочник.isСтрокиОтчетовПЭО.ПустаяСсылка) КАК НовыйОбъект,
	|	0 КАК ВидОбъекта
	|ИЗ
	|	Справочник.isСтрокиОтчетовПЭО КАК ТаблицаСтрок
	|ГДЕ
	|	ТаблицаСтрок.Владелец = &Настройка
	|	И НЕ ТаблицаСтрок.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаВыражений.Ссылка,
	|	ЗНАЧЕНИЕ(справочник.isВыраженияОтчетовПЭО.ПустаяСсылка),
	|	1
	|ИЗ
	|	Справочник.isВыраженияОтчетовПЭО КАК ТаблицаВыражений
	|ГДЕ
	|	ТаблицаВыражений.Владелец = &Настройка
	|	И НЕ ТаблицаВыражений.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаПараметров.Ссылка,
	|	ЗНАЧЕНИЕ(справочник.isПараметрыОтчетовПЭО.ПустаяСсылка),
	|	2
	|ИЗ
	|	Справочник.isПараметрыОтчетовПЭО КАК ТаблицаПараметров
	|ГДЕ
	|	ТаблицаПараметров.Владелец = &Настройка
	|	И НЕ ТаблицаПараметров.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаСекций.Ссылка,
	|	ЗНАЧЕНИЕ(справочник.isСекцииОтчетов.ПустаяСсылка),
	|	3
	|ИЗ
	|	Справочник.isСекцииОтчетов КАК ТаблицаСекций
	|ГДЕ
	|	ТаблицаСекций.Владелец = &Настройка
	|	И НЕ ТаблицаСекций.ПометкаУдаления";
	ТаблицаОбъектов = Запрос.Выполнить().Выгрузить();
	Для каждого ТекСтр Из ТаблицаОбъектов Цикл
		Если НЕ ЗначениеЗаполнено(ТекСтр.НовыйОбъект) Тогда
			ТекСтр.НовыйОбъект = СоздатьОбъект(ТекСтр.ВидОбъекта, ТекСтр.ИсходныйОбъект, ТаблицаОбъектов);		
		КонецЕсли;
	КонецЦикла;
	Для каждого ТекСтр Из ТаблицаОбъектов Цикл
		ЗаполнитьОбъект(ТекСтр, ТаблицаОбъектов);
	КонецЦикла;
КонецПроцедуры

Процедура ЗаполнитьОбъект(ТекСтрока, ТаблицаОбъектов)
	НовыйОбъект = ТекСтрока.НовыйОбъект.ПолучитьОбъект();
	Если ТекСтрока.ВидОбъекта = 0 ИЛИ   // строка отчета
		 ТекСтрока.ВидОбъекта = 1 Тогда  // выражение отчета
		
		Для каждого ТекСтр Из ТекСтрока.ИсходныйОбъект.Источники Цикл
			НоваяСтрока = НовыйОбъект.Источники.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока,ТекСтр);
			Если ТекСтр.ТипСтроки <> 0 Тогда // это ссылка на строку
				НоваяСтрока.Источник = ПолучитьОбъект(ТекСтр.Источник, ТаблицаОбъектов);
			КонецЕсли;
		КонецЦикла;	
		
		Если ТекСтрока.ВидОбъекта = 1 Тогда
			Если ТекСтрока.ИсходныйОбъект.ТипИсточника1 <> 0 Тогда
				НовыйОбъект.Источник1 = ПолучитьОбъект(ТекСтрока.ИсходныйОбъект.Источник1, ТаблицаОбъектов);
			КонецЕсли;
			Если ТекСтрока.ИсходныйОбъект.ТипИсточника2 <> 0 Тогда
				НовыйОбъект.Источник2 = ПолучитьОбъект(ТекСтрока.ИсходныйОбъект.Источник2, ТаблицаОбъектов);
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли ТекСтрока.ВидОбъекта = 3 Тогда  // секция отчета
		Для каждого ТекСтр Из ТекСтрока.ИсходныйОбъект.КолонкиСекции Цикл
			ЗаполнитьЗначенияСвойств(НовыйОбъект.КолонкиСекции.Добавить(),ТекСтр);	
		КонецЦикла;	
	КонецЕсли;
	НовыйОбъект.Записать();
КонецПроцедуры


Функция ПолучитьОбъект(ИсходныйОбъект, ТаблицаОбъектов)
	Результат = Неопределено;
	СтрокаОбъекта = ТаблицаОбъектов.Найти(ИсходныйОбъект, "ИсходныйОбъект");
	Если СтрокаОбъекта <> Неопределено Тогда
		Если НЕ ЗначениеЗаполнено(СтрокаОбъекта.НовыйОбъект) Тогда
            СтрокаОбъекта.НовыйОбъект = СоздатьОбъект(СтрокаОбъекта.ВидОбъекта, ИсходныйОбъект, ТаблицаОбъектов);
		КонецЕсли;
		Результат = СтрокаОбъекта.НовыйОбъект;
	КонецЕсли;
    Возврат Результат;
КонецФункции // ПолучитьОбъект(ТекСтр.Источник, ТаблицаОбъектов)

Функция СоздатьОбъект(ВидОбъекта, ИсходныйОбъект, ТаблицаОбъектов)
	НовыйОбъект = Справочники[ИсходныйОбъект.Метаданные().Имя].СоздатьЭлемент();
	СтрокаИсключений = "Код, Владелец, Родитель";
	
	ЗаполнитьЗначенияСвойств(НовыйОбъект, ИсходныйОбъект,,СтрокаИсключений);
	НовыйОбъект.Владелец = Ссылка;
	Если ВидОбъекта = 0 Тогда
		НовыйОбъект.Родитель = ПолучитьОбъект(ИсходныйОбъект.Родитель, ТаблицаОбъектов);
	КонецЕсли;
	НовыйОбъект.Записать();
	Возврат НовыйОбъект.Ссылка;
КонецФункции

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	ОбъектКопирования = ДанныеЗаполнения;
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	А = 12;
КонецПроцедуры



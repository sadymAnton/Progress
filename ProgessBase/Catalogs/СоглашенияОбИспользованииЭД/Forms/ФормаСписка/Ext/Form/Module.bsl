////////////////////////////////////////////////////////////////////////////////
// Прочие

&НаКлиенте
Процедура ОткрытьФормуСоглашения(ИмяОткрываемойФормы, ПараметрыОткрываемойФормы)
	
	ОткрытьФорму(ИмяОткрываемойФормы, ПараметрыОткрываемойФормы);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ПОЛЕЙ ТАБЛИЦЫ СписокИнтеркампани

&НаКлиенте
Процедура СписокИнтеркампаниПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	Параметр = ?(Копирование, Новый Структура("ЗначениеКопирования", Элементы.СписокИнтеркампани.ТекущаяСтрока),
		Новый Структура);
	
	ОткрытьФорму("Справочник.СоглашенияОбИспользованииЭД.Форма.ФормаЭлементаИнтеркампани", Параметр,
		Элементы.СписокИнтеркампани);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокИнтеркампаниПередНачаломИзменения(Элемент, Отказ)
	
	ОткрытьФормуСоглашения("Справочник.СоглашенияОбИспользованииЭД.Форма.ФормаЭлементаИнтеркампани",
		Новый Структура("Ключ", Элементы.СписокИнтеркампани.ТекущаяСтрока));
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ПОЛЕЙ ТАБЛИЦЫ СписокСоглашенийЧерезОЭДО

&НаКлиенте
Процедура СписокСоглашенийЧерезОЭДОПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	
	Если Копирование Тогда
		СтруктураПараметров = Новый Структура("ЗначениеКопирования", Элементы.СписокСоглашенийЧерезОЭДО.ТекущаяСтрока);
		ОткрытьФорму("Справочник.СоглашенияОбИспользованииЭД.Форма.ФормаЭлементаЧерезОЭДО",
					 СтруктураПараметров,
					 Элементы.СписокСоглашенийЧерезОЭДО);
	Иначе
		ОткрытьФорму("Справочник.СоглашенияОбИспользованииЭД.Форма.ПомощникНового",,ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокСоглашенийЧерезОЭДОПередНачаломИзменения(Элемент, Отказ)
	
	ОткрытьФормуСоглашения("Справочник.СоглашенияОбИспользованииЭД.Форма.ФормаЭлементаЧерезОЭДО",
		Новый Структура("Ключ", Элементы.СписокСоглашенийЧерезОЭДО.ТекущаяСтрока));
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ПОЛЕЙ ТАБЛИЦЫ СписокСоглашенийСБанками

&НаКлиенте
Процедура СписокСоглашенийСБанкамиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	Параметр = ?(Копирование, Новый Структура("ЗначениеКопирования", Элементы.СписокСоглашенийСБанками.ТекущаяСтрока), Новый Структура);
	ОткрытьФорму("Справочник.СоглашенияОбИспользованииЭД.Форма.ФормаЭлементаБанк", Параметр, Элементы.СписокСоглашенийСБанками);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокСоглашенийСБанкамиПередНачаломИзменения(Элемент, Отказ)
	
	ОткрытьФормуСоглашения("Справочник.СоглашенияОбИспользованииЭД.Форма.ФормаЭлементаБанк",
		Новый Структура("Ключ", Элементы.СписокСоглашенийСБанками.ТекущаяСтрока));
	
КонецПроцедуры

&НаКлиенте
Процедура СписокСоглашенийПередНачаломИзменения(Элемент, Отказ)
	
	СтруктураРеквизитов = ЭлектронныеДокументыСлужебныйВызовСервера.СпособОбмена_ПризнакИнтеркампани_Соглашения(Элементы.СписокСоглашений.ТекущаяСтрока);
	
	Если СтруктураРеквизитов.ЭтоИнтеркампани Тогда
		ОткрытьФормуСоглашения("Справочник.СоглашенияОбИспользованииЭД.Форма.ФормаЭлементаИнтеркампани",
			Новый Структура("Ключ", Элементы.СписокСоглашений.ТекущаяСтрока));
	ИначеЕсли СтруктураРеквизитов.СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезОператораЭДОТакском") Тогда
		ОткрытьФормуСоглашения("Справочник.СоглашенияОбИспользованииЭД.Форма.ФормаЭлементаЧерезОЭДО",
			Новый Структура("Ключ", Элементы.СписокСоглашений.ТекущаяСтрока));
	ИначеЕсли СтруктураРеквизитов.СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезВебРесурсБанка") Тогда
		ОткрытьФормуСоглашения("Справочник.СоглашенияОбИспользованииЭД.Форма.ФормаЭлементаБанк",
			Новый Структура("Ключ", Элементы.СписокСоглашений.ТекущаяСтрока));
	КонецЕсли;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьЗначениеФункциональнойОпции("ИспользоватьОбменЭД") Тогда
		ТекстСообщения = ЭлектронныеДокументыСлужебныйВызовСервера.ТекстСообщенияОНеобходимостиНастройкиСистемы("РаботаСЭД");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , , , Отказ);
		Возврат;
	КонецЕсли;
	
	СпособыОЭДНапрямую = ЭлектронныеДокументыСлужебный.МассивСпособовОбменаЭД(Истина);
	ДатаАктуальности = НачалоДня(ТекущаяДатаСеанса());
	
	ИмяСправочникаБанки = ЭлектронныеДокументыПовтИсп.ПолучитьИмяПрикладногоСправочника("Банки");
	Если ЗначениеЗаполнено(ИмяСправочникаБанки) Тогда
		СписокСоглашенийСБанками.ТекстЗапроса = СтрЗаменить(СписокСоглашенийСБанками.ТекстЗапроса,
															"КлассификаторБанковРФ",
															ИмяСправочникаБанки);
	КонецЕсли;
	
	ТекстЗапросаПартнеры = "ИСТИНА";
	Если ЭлектронныеДокументыПовтИсп.ИспользуетсяДополнительнаяАналитикаКонтрагентовСправочникПартнеры()
		И Параметры.Свойство("Партнер") Тогда
		
		ТекстЗапросаПартнеры = "ВЫБОР
		|	КОГДА Соглашение.Контрагент.Партнер В ИЕРАРХИИ (&Партнер)
		|		ТОГДА ИСТИНА
		|	ИНАЧЕ ЛОЖЬ
		|КОНЕЦ";
		
		СписокСоглашенийСКонтрагентами.Параметры.УстановитьЗначениеПараметра("Партнер",            Параметры.Партнер);
		СписокСоглашенийМеждуОрганизациями.Параметры.УстановитьЗначениеПараметра("Партнер",        Параметры.Партнер);
		СписокСоглашенийЧерезОЭДО.Параметры.УстановитьЗначениеПараметра("Партнер",                 Параметры.Партнер);
	КонецЕсли;
	СписокСоглашенийСКонтрагентами.ТекстЗапроса = СтрЗаменить(СписокСоглашенийСКонтрагентами.ТекстЗапроса,
		"&Партнер", ТекстЗапросаПартнеры);
	СписокСоглашенийМеждуОрганизациями.ТекстЗапроса = СтрЗаменить(СписокСоглашенийМеждуОрганизациями.ТекстЗапроса,
		"&Партнер", ТекстЗапросаПартнеры);
	СписокСоглашенийЧерезОЭДО.ТекстЗапроса = СтрЗаменить(СписокСоглашенийЧерезОЭДО.ТекстЗапроса,
		"&Партнер", ТекстЗапросаПартнеры);
	
	СписокСоглашенийСКонтрагентами.Параметры.УстановитьЗначениеПараметра("ДатаАктуальности",        ДатаАктуальности);
	СписокСоглашенийСКонтрагентами.Параметры.УстановитьЗначениеПараметра("Контрагент",              Параметры.Контрагент);
	СписокСоглашенийСКонтрагентами.Параметры.УстановитьЗначениеПараметра("Организация",             Параметры.Организация);
	СписокСоглашенийСКонтрагентами.Параметры.УстановитьЗначениеПараметра("СпособыОбменаЭДНапрямую", СпособыОЭДНапрямую);
	
	СписокСоглашенийМеждуОрганизациями.Параметры.УстановитьЗначениеПараметра("ДатаАктуальности",     ДатаАктуальности);
	СписокСоглашенийМеждуОрганизациями.Параметры.УстановитьЗначениеПараметра("Контрагент",           Параметры.Контрагент);
	СписокСоглашенийМеждуОрганизациями.Параметры.УстановитьЗначениеПараметра("Организация",          Параметры.Организация);
	
	СписокСоглашенийЧерезОЭДО.Параметры.УстановитьЗначениеПараметра("ДатаАктуальности",        ДатаАктуальности);
	СписокСоглашенийЧерезОЭДО.Параметры.УстановитьЗначениеПараметра("Контрагент",              Параметры.Контрагент);
	СписокСоглашенийЧерезОЭДО.Параметры.УстановитьЗначениеПараметра("Организация",             Параметры.Организация);
	
	СписокСоглашенийСБанками.Параметры.УстановитьЗначениеПараметра("ДатаАктуальности", ДатаАктуальности);
	СписокСоглашенийСБанками.Параметры.УстановитьЗначениеПараметра("Банк",             Параметры.Банк);
	СписокСоглашенийСБанками.Параметры.УстановитьЗначениеПараметра("Организация",      Параметры.Организация);
	
	СписокСоглашений.Параметры.УстановитьЗначениеПараметра("ДатаАктуальности", ДатаАктуальности);
	
	// Скроем группу "Всех соглашений" от пользователей с ограниченными правами или принудительно.
	СкрытьЗакладкуВсехСоглашений = НЕ ПользователиСервер.ЭтоПолноправныйПользовательИБ()
									ИЛИ (Параметры.Свойство("Партнер") И ЗначениеЗаполнено(Параметры.Партнер))
									ИЛИ ЗначениеЗаполнено(Параметры.Контрагент)
									ИЛИ ЗначениеЗаполнено(Параметры.Организация)
									ИЛИ ЗначениеЗаполнено(Параметры.Банк);

	Если ЗначениеЗаполнено(Параметры.Банк) Тогда
		Элементы.ГруппаСоглашения.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
		Элементы.ГруппаСоглашения.ТекущаяСтраница    = Элементы.ГруппаСоглашенияСБанками;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.Контрагент)
		ИЛИ (Параметры.Свойство("Партнер")И ЗначениеЗаполнено(Параметры.Партнер)) Тогда
		
		Элементы.ГруппаСоглашенияСБанками.Видимость = Ложь;
	КонецЕсли;

	Элементы.ГруппаВсеСоглашения.Видимость = НЕ СкрытьЗакладкуВсехСоглашений;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНовое(Команда)
	
	ОткрытьФорму("Справочник.СоглашенияОбИспользованииЭД.Форма.ФормаЭлементаЧерезОЭДО", , ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновитьСостояниеЭД" Тогда
		Элементы.СписокСоглашенийЧерезОЭДО.Обновить();
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ОчиститьСообщения();
	ПараметрыСоглашения = ПараметрыСоглашения(ПараметрКоманды);
		
	Если ПараметрыСоглашения.СтатусСоглашения <> ПредопределенноеЗначение("Перечисление.СтатусыСоглашенийЭД.Действует") Тогда
		ТекстСообщения = НСтр("ru = 'Обмен производится только по соглашениям со статусом Действует.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ПараметрКоманды, "Статус");
	КонецЕсли;
	
	Если ПараметрыСоглашения.СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезЭлектроннуюПочту") Тогда
		
		Состояние(
				НСтр("ru = 'Тест настроек соглашения.'"),
				,
				НСтр("ru = 'Выполняется тестирование  обмена ЭД через электронную почту. Подождите...'"));
		ПроверитьШифрование(ПараметрыСоглашения);
		РаботаСПочтовымиСообщениямиКлиент.ПроверитьУчетнуюЗапись(ПараметрыСоглашения.РесурсВходящихДокументов);
		
	ИначеЕсли ПараметрыСоглашения.СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезКаталог") Тогда

		
		Состояние(
				НСтр("ru = 'Тест настроек соглашения.'"),
				,
				НСтр("ru = 'Выполняется тестирование прямого обмена ЭД. Подождите...'"));
		ПроверитьШифрование(ПараметрыСоглашения);

		ТестСвязиПрямогоОбменаНаСервере(ПараметрыСоглашения);
		
	ИначеЕсли ПараметрыСоглашения.СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезFTP") Тогда
		
		Состояние(
			НСтр("ru = 'Тест настроек соглашения.'"),
			,
			НСтр("ru = 'Выполняется тестирование обмена через FTP. Подождите...'"));
		ПроверитьШифрование(ПараметрыСоглашения);
		ТестСвязиОбменаЧерезFTPНаСервере(ПараметрКоманды);
		
	ИначеЕсли ПараметрыСоглашения.СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезОператораЭДОТакском") Тогда
		
		// Блок проверки связи с оператором.
		Состояние(
				НСтр("ru = 'Тест настроек соглашения.'"),
				,
				НСтр("ru = 'Выполняется тестирование связи с оператором. Подождите...'"));
		
		ЗавершитьТесты = Ложь;
		
		// Блок проверки версии платформы.
		СистемнаяИнформация = Новый СистемнаяИнформация;
		
		РезультатТеста = НСтр("ru = 'Пройден успешно.'");
		Если СтроковыеФункцииКлиентСервер.СравнитьВерсии(СистемнаяИнформация.ВерсияПриложения, "8.2.16.0") < 0 Тогда
			РезультатТеста = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьСообщениеОбОшибке("106");
			ЗавершитьТесты = Истина;
		КонецЕсли;
		ШаблонСообщения = НСтр("ru = 'Тест. Проверка версии платформы 1С.
			|%1'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, РезультатТеста);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
		// Критичная ошибка - дальше тесты не проводим.
		Если ЗавершитьТесты Тогда
			Возврат;
		КонецЕсли;

		Если ЭлектронныеДокументыСлужебныйВызовСервера.ВыполнятьАвторизациюНаСервере() Тогда
			Контекст = ПредопределенноеЗначение("Перечисление.КонтекстыРаботыСЭД.НаСервере");
		Иначе
			Контекст = ПредопределенноеЗначение("Перечисление.КонтекстыРаботыСЭД.НаКлиенте");
		КонецЕсли;
		
		СоотвСертификатовИИхСтруктур = ПараметрыСоглашения.СертификатыПодписейОрганизации;
		Для Каждого Элемент Из СоотвСертификатовИИхСтруктур Цикл
			ПарольПользователя = Неопределено;
			Сертификат = Элемент.Ключ;
			ЭлектронныеДокументыСлужебныйКлиент.ТестНастроекСертификата(Сертификат, Контекст, ПарольПользователя);
			Если ПарольПользователя <> Неопределено Тогда
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Тест. Проверка связи с сервисом Такском.'"));
				
				// Блок проверки заполненности идентификатора организации.
				Если Не ЗначениеЗаполнено(ПараметрыСоглашения.ИдентификаторОрганизации) Тогда
					ШаблонСообщения = НСтр("ru = 'Тест не пройден.
					|%1'");
					РезультатТеста = ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Идентификатор организации");
					ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, РезультатТеста);
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ПараметрКоманды, "Идентификатор организации");
					Возврат;
				КонецЕсли;
				
				СтруктураСертификата = Элемент.Значение;
				СтруктураСертификата.Вставить("ПарольПользователя", ПарольПользователя);
				Если Контекст = ПредопределенноеЗначение("Перечисление.КонтекстыРаботыСЭД.НаКлиенте") Тогда
					ЭлектронныеДокументыСлужебныйКлиент.ТестСвязиСОператоромЭДО(СтруктураСертификата);
				Иначе
					ЭлектронныеДокументыСлужебныйВызовСервера.ТестСвязиСОператоромЭДО(СтруктураСертификата);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПараметрыСоглашения(Соглашение)
	
	СтПараметров = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Соглашение,
		"СтатусСоглашения, СпособОбменаЭД, РесурсВходящихДокументов, РесурсИсходящихДокументов, СертификатОрганизацииДляРасшифровки, Наименование, ИдентификаторОрганизации, СертификатыПодписейОрганизации");
	ВыборкаСертификатов = СтПараметров.СертификатыПодписейОрганизации.Выбрать();
	СоотвСертификатовИИхСтруктур = Новый Соответствие;
	Если ВыборкаСертификатов.Количество() > 0 Тогда
		Пока ВыборкаСертификатов.Следующий() Цикл
			Сертификат = ВыборкаСертификатов.Сертификат;
			СтруктураСертификата = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Сертификат,
				"ЗапомнитьПарольКСертификату, ПарольПользователя, ФайлСертификата");
			СтруктураСертификата.Вставить("СертификатПодписи", Сертификат);
			СтруктураСертификата.Вставить("ФайлСертификата", СтруктураСертификата.ФайлСертификата.Получить());
			СоотвСертификатовИИхСтруктур.Вставить(Сертификат, СтруктураСертификата);
		КонецЦикла;
	КонецЕсли;
	СтПараметров.Вставить("СертификатыПодписейОрганизации", СоотвСертификатовИИхСтруктур);
	
	Возврат СтПараметров;
	
КонецФункции

&НаСервере
Процедура ТестСвязиПрямогоОбменаНаСервере(ПараметрКоманды)
	
	// Блок проверки доступа к каталогам.
	ШаблонСообщения = НСтр("ru = 'Тест. Проверка доступа к каталогам обмена.
	|%1'");
	Попытка
		Если ЭлектронныеДокументыСлужебныйВызовСервера.ПроверитьДоступностьКаталогаДляПрямогоОбмена(ПараметрКоманды.РесурсВходящихДокументов)
			И ЭлектронныеДокументыСлужебныйВызовСервера.ПроверитьДоступностьКаталогаДляПрямогоОбмена(ПараметрКоманды.РесурсИсходящихДокументов) Тогда
			РезультатТеста = НСтр("ru = 'Пройден успешно.'");
		Иначе
			РезультатТеста = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьСообщениеОбОшибке("105");
		КонецЕсли;
	Исключение
		ШаблонРезультата = НСтр("ru = '%1
		|%2'");
		ТекстОшибки = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьСообщениеОбОшибке("105");
		РезультатТеста = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонРезультата, ТекстОшибки,
			КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, РезультатТеста);
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	
КонецПроцедуры

&НаСервере
Процедура ТестСвязиОбменаЧерезFTPНаСервере(ПараметрКоманды)
	
	ЭлектронныеДокументыСлужебный.ТестСвязиОбменаЧерезFTP(ПараметрКоманды);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьШифрование(ПараметрыСоглашения)
	
	Если ЗначениеЗаполнено(ПараметрыСоглашения.СертификатОрганизацииДляРасшифровки) Тогда
			
		ПараметрыСертификата = ЭлектронныеДокументыСлужебныйВызовСервера.ПараметрыСертификата(
																				ПараметрыСоглашения.СертификатОрганизацииДляРасшифровки);
		
		// Для дальнейших операций необходим пароль сертификата.
		Если Не ПараметрыСертификата.ЗапомнитьПарольКСертификату Тогда
			ПараметрыФормы = Новый Структура("ПредставлениеСертификата, ВидОперации",
				ПараметрыСоглашения.СертификатОрганизацииДляРасшифровки, НСтр("ru = 'Тестовая проверка шифрования'"));
			КодВозврата = ОткрытьФормуМодально("ОбщаяФорма.ЗапросПароляНаЭЦПШифрование", ПараметрыФормы);
			
			Если ТипЗнч(КодВозврата) = Тип("Строка") Тогда
				ПарольПользователя = КодВозврата;
			КонецЕсли;
		Иначе
			
			ПарольПользователя = ПараметрыСертификата.ПарольПользователя;
		КонецЕсли;
		
		Если НЕ ПарольПользователя = Неопределено Тогда
			Если ЭлектронныеДокументыСлужебныйВызовСервера.ВыполнятьКриптооперацииНаСервере() Тогда
				ПроверитьШифрованиеНаСервере(ПараметрыСертификата);
			Иначе
				ПроверитьШифрованиеНаКлиенте(ПараметрыСертификата);
			КонецЕсли
		КонецЕсли;
			
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьШифрованиеНаКлиенте(ПараметрыСертификата)
	
	ШаблонСообщения = НСтр("ru = '%1
								 |%2'");
				
	// Блок проверки установленных криптосредств на компьютере.
	ОписаниеТеста = НСтр("ru = 'Тест. Проверка средства криптографии на компьютере.'");
	Попытка
		МенеджерКриптографии = ЭлектроннаяЦифроваяПодписьКлиент.ПолучитьМенеджерКриптографии();
		РезультатТеста = НСтр("ru = 'Пройден успешно.'");
	Исключение
		ТекстОшибки = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьСообщениеОбОшибке("100");
		ШаблонРезультата = НСтр("ru = %1.
									 |%2'");
		РезультатТеста = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
															ШаблонРезультата,
															ТекстОшибки,
															КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		МенеджерКриптографии = Неопределено;
	КонецПопытки;
	ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
														ШаблонСообщения,
														ОписаниеТеста,
														РезультатТеста);
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	
	// Критичная ошибка - дальше тесты не проводим.
	Если МенеджерКриптографии = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// Блок проверки шифрования/расшифрования.
	ОтпечатокСертификатаПодписи = ПараметрыСертификата.Отпечаток;
	Сертификат = ЭлектроннаяЦифроваяПодписьКлиент.ПолучитьСертификатПоОтпечатку(ОтпечатокСертификатаПодписи);
	
	// Критичная ошибка - дальше тесты не проводим(платформа падает).
	Если Сертификат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеТеста = НСтр("ru = 'Тест. Проверка операций шифрования/расшифровки.'");
	ОтпечатокДвоичныеДанные = Base64Значение(ОтпечатокСертификатаПодписи);
	ДвоичныеДанные = МенеджерКриптографии.Зашифровать(ОтпечатокДвоичныеДанные, Сертификат);
	Попытка
		РасшифрованныеДвоичныеДанные = МенеджерКриптографии.Расшифровать(ДвоичныеДанные);
		РезультатТеста = НСтр("ru = 'Пройден успешно.'");
	Исключение
		РезультатТеста = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьСообщениеОбОшибке("103");
	КонецПопытки;
	ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							ШаблонСообщения,
							ОписаниеТеста,
							РезультатТеста);
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);

КонецПроцедуры

&НаСервере
Процедура ПроверитьШифрованиеНаСервере(ПараметрыСертификата)
	
	ШаблонСообщения = НСтр("ru = '%1
								 |%2'");
				
	// Блок проверки установленных криптосредств на компьютере.
	ОписаниеТеста = НСтр("ru = 'Тест. Проверка средства криптографии на сервере.'");
	Попытка
		МенеджерКриптографии = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьМенеджерКриптографии();
		РезультатТеста = НСтр("ru = 'Пройден успешно.'");
	Исключение
		ТекстОшибки = ЭлектронныеДокументыПовтИсп.ПолучитьСообщениеОбОшибке("110");
		ШаблонРезультата = НСтр("ru = %1.
									 |%2'");
		РезультатТеста = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
															ШаблонРезультата,
															ТекстОшибки,
															КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		МенеджерКриптографии = Неопределено;
	КонецПопытки;
	ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
														ШаблонСообщения,
														ОписаниеТеста,
														РезультатТеста);
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	
	// Критичная ошибка - дальше тесты не проводим.
	Если МенеджерКриптографии = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// Блок проверки шифрования/расшифрования.
	ОтпечатокСертификатаПодписи = ПараметрыСертификата.Отпечаток;
	Сертификат = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьСертификатПоОтпечатку(ОтпечатокСертификатаПодписи);
	
	// Критичная ошибка - дальше тесты не проводим(платформа падает).
	Если Сертификат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеТеста = НСтр("ru = 'Тест. Проверка операций шифрования/расшифровки.'");
	ОтпечатокДвоичныеДанные = Base64Значение(ОтпечатокСертификатаПодписи);
	ДвоичныеДанные = МенеджерКриптографии.Зашифровать(ОтпечатокДвоичныеДанные, Сертификат);
	Попытка
		РасшифрованныеДвоичныеДанные = МенеджерКриптографии.Расшифровать(ДвоичныеДанные);
		РезультатТеста = НСтр("ru = 'Пройден успешно.'");
	Исключение
		РезультатТеста = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьСообщениеОбОшибке("113");
	КонецПопытки;
	ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							ШаблонСообщения,
							ОписаниеТеста,
							РезультатТеста);
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);

КонецПроцедуры
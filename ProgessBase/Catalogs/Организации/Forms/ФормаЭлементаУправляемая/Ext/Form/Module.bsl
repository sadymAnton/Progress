&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЮрФизЛицо_ЮрЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьИННОрганизацииФизЛица(ДанныеДляЗаполнения)
	Возврат ДанныеДляЗаполнения.ИНН;
КонецФункции

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступность()
	
	ЭтоЮрЛицо = (Объект.ЮрФизЛицо = ЮрФизЛицо_ЮрЛицо);

	Элементы.КПП.Доступность                           = ЭтоЮрЛицо;
	Элементы.ГоловнаяОрганизация.Доступность           = ЭтоЮрЛицо;

	Элементы.ИндивидуальныйПредприниматель.Доступность = НЕ ЭтоЮрЛицо;
	
	Если ЭтоЮрЛицо Тогда
		Элементы.ИНН.Доступность = 	НЕ ЗначениеЗаполнено(Объект.ГоловнаяОрганизация);
	Иначе
		Элементы.ИНН.Доступность = 	Ложь; // ИНН задается у физ. лица (заданного в поле "Физическое лицо")
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	Если ПустаяСтрока(Объект.НаименованиеПолное) Тогда
		Объект.НаименованиеПолное = Объект.Наименование;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЮрФизЛицоПриИзменении(Элемент)
	
	ЭтоЮрЛицо = (Объект.ЮрФизЛицо = ЮрФизЛицо_ЮрЛицо);
	
	Если ЭтоЮрЛицо Тогда
		// Поле не имеет смысла для юрлица
		Объект.ИндивидуальныйПредприниматель = Неопределено;
	Иначе
		// Поля не имеет смысла для физлица
		Объект.ГоловнаяОрганизация = Неопределено;
		Объект.КПП                 = "";
	КонецЕсли;
	
	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ЮрФизЛицоОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ПрефиксПриИзменении(Элемент)
	Если Не ЗначениеЗаполнено(Объект.Префикс) Тогда
		Возврат;
	КонецЕсли;
	
	Для НомерПозиции = 1 По СтрДлина(Объект.Префикс) Цикл
		СимволПрефикса = Сред(Объект.Префикс, НомерПозиции, 1);
		Если СимволПрефикса >= "0"
			И СимволПрефикса <= "9" Тогда
			СтрокаСообщения = Нстр("ru = 'Не рекомендуется в префиксах использовать цифры. Это может привести к ошибочной нумерации объектов или к ошибкам при обмене данными.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаСообщения,, "Префикс");
			Возврат;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ГоловнаяОрганизацияПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.ГоловнаяОрганизация) Тогда
		
		Если Объект.ГоловнаяОрганизация = Объект.Ссылка Тогда
			Объект.ГоловнаяОрганизация = Неопределено;
		КонецЕсли;
		
		Объект.ИНН = ПолучитьИННОрганизацииФизЛица(Объект.ГоловнаяОрганизация);
		
	КонецЕсли;
	
	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ИндивидуальныйПредпринимательПриИзменении(Элемент)
	Если ЗначениеЗаполнено(Объект.ИндивидуальныйПредприниматель) Тогда
		Объект.ИНН = ПолучитьИННОрганизацииФизЛица(Объект.ИндивидуальныйПредприниматель);
	КонецЕсли;
	УстановитьДоступность();
КонецПроцедуры

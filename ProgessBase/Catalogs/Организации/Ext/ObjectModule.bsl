Перем ПрошлыйИзмененныйГоловнаяОрганизация;

Процедура ЗаполнитьРеквизитыОбъекта(Отказ, ТекстСообщенияОшибки)
	
	Если Не ЗначениеЗаполнено(ЮрФизЛицо) Тогда
		ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо;	
	КонецЕсли;
		
	ТекстСообщенияОшибки = "";
	Если Ссылка.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо И ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо Тогда
		ЗапросОрганизации = Новый Запрос;
		ЗапросОрганизации.УстановитьПараметр("ГоловнаяОрганизация", Ссылка);
		ЗапросОрганизации.Текст =
		"ВЫБРАТЬ
		|	Организации.Ссылка
		|ИЗ
		|	Справочник.Организации КАК Организации
		|ГДЕ
		|	Организации.ГоловнаяОрганизация = &ГоловнаяОрганизация";
		Если Не ЗапросОрганизации.Выполнить().Пустой() Тогда
			ТекстСообщенияОшибки = ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("Организация не может быть индивидуальным предпринимателем, для неё зарегистрированы обособленные подразделения!") + Символы.ПС;
			Отказ = Истина;
			Возврат;
		КонецЕсли;
	КонецЕсли;
	Если ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо И НЕ ЗначениеЗаполнено(ИндивидуальныйПредприниматель) Тогда
		ТекстСообщенияОшибки = ТекстСообщенияОшибки + ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("Организации - индивидуальному предпринимателю не сопоставлено физ. лицо!") + Символы.ПС;
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо И ЗначениеЗаполнено(ИндивидуальныйПредприниматель) Тогда
		// Проверим соответствие ИНН организации и выбранного физического лица
		Если НЕ ПустаяСтрока(ИНН) И (ИНН <> ИндивидуальныйПредприниматель.ИНН) Тогда
			ТекстСообщенияОшибки = ТекстСообщенияОшибки +  "ИНН организации не соответствует ИНН выбранного физического лица!" + Символы.ПС;
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	// Проверим соответствие ИНН требованиям
	Если НЕ ПустаяСтрока(ИНН) И НЕ РегламентированнаяОтчетность.ИННСоответствуетТребованиям(ИНН, ЮрФизЛицо) Тогда
		ТекстСообщенияОшибки = ТекстСообщенияОшибки +  "ИНН " + ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("организации") + " задан неверно!" + Символы.ПС;
		Отказ = Истина;
	КонецЕсли;
	
	Если ЮрФизЛицо <> Перечисления.ЮрФизЛицо.ФизЛицо И Ссылка.КПП <> КПП Тогда
		Если НЕ ПустаяСтрока(КПП) И СтрДлина(КПП) <> 9 Тогда
			ОшибкаКПП = Не ОбщегоНазначенияЗКПереопределяемый.КППОрганизацииСоответствуетТребованиям(КПП, ТекстСообщенияОшибки);
			Отказ = ?(ОшибкаКПП, ОшибкаКПП, Отказ);
		КонецЕсли;
	КонецЕсли;
	Если Ссылка.ОГРН <> ОГРН Тогда
		Если НЕ ПустаяСтрока(ОГРН) Тогда
			ОшибкаОГРН = Не ОбщегоНазначенияЗКПереопределяемый.ОГРНСоответствуетТребованиям(ОГРН, ЮрФизЛицо, ТекстСообщенияОшибки);
			Отказ = ?(ОшибкаОГРН, ОшибкаОГРН, Отказ);
		КонецЕсли;
	КонецЕсли;
	
	// проверка на уместность указания кода ИФНС-получателя отчетности
	Если ЗначениеЗаполнено(КодИФНСПолучателя) Тогда
		Если ЮрФизЛицо <> Перечисления.ЮрФизЛицо.ЮрЛицо Тогда
			ТекстСообщенияОшибки = ТекстСообщенияОшибки + "Код ИФНС-получателя отчетности заполняется только для организаций-юрлиц, являющихся крупнейшими налогоплательщиками." + Символы.ПС;
			Отказ = Истина;
		//ИначеЕсли ЗначениеЗаполнено(КПП) И Сред(КПП, 5, 4) <> "5000" Тогда
		//	ТекстСообщенияОшибки = ТекстСообщенияОшибки + "Код ИФНС-получателя отчетности заполняется только для организаций-юрлиц, являющихся крупнейшими налогоплательщиками.
		//										|КПП такой организации должен соответствовать шаблону ""XXXX5000X""." + Символы.ПС;
		//	Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
	// проверка правильности заполнения различных кодов
	Если ЗначениеЗаполнено(РегистрационныйНомерПФР) И СтрДлина(СокрЛП(РегистрационныйНомерПФР)) <> 14 И СокрЛП(РегистрационныйНомерПФР) <> "-   -" Тогда
		ТекстСообщенияОшибки = ТекстСообщенияОшибки + "Некорректно задан регистрационный номер в ПФР." + Символы.ПС;
		Отказ = Истина;
	КонецЕсли;
	Если ЗначениеЗаполнено(КодИМНС) И СтрДлина(СокрЛП(КодИМНС)) <> 4 Тогда
		ТекстСообщенияОшибки = ТекстСообщенияОшибки + "Некорректно задан код ИФНС." + Символы.ПС;
		Отказ = Истина;
	КонецЕсли;
	Если ЗначениеЗаполнено(КодИФНСПолучателя) И СтрДлина(СокрЛП(КодИФНСПолучателя)) <> 4 Тогда
		ТекстСообщенияОшибки = ТекстСообщенияОшибки + "Некорректно задан код ИФНС-получателя отчетности." + Символы.ПС;
		Отказ = Истина;
	КонецЕсли;
	Если ЗначениеЗаполнено(КодОрганаПФР) И СтрДлина(СокрЛП(КодОрганаПФР)) <> 7 И СокрЛП(КодОрганаПФР) <> "-" Тогда
		ТекстСообщенияОшибки = ТекстСообщенияОшибки + "Некорректно задан код территориального органа ПФР." + Символы.ПС;
		Отказ = Истина;
	КонецЕсли;
	Если ЗначениеЗаполнено(КодОрганаФСГС) И СтрДлина(СокрЛП(КодОрганаФСГС)) <> 5 Тогда
		ТекстСообщенияОшибки = ТекстСообщенияОшибки + "Некорректно задан код территориального органа Федеральной службы государственной статистики." + Символы.ПС;
		Отказ = Истина;
	КонецЕсли;
	
	Если ВидОбменаСКонтролирующимиОрганами = Перечисления.ВидыОбменаСКонтролирующимиОрганами.ОбменВУниверсальномФормате И УчетнаяЗаписьОбмена.Пустая() Тогда
		Отказ = Истина;
		ТекстСообщенияОшибки = ТекстСообщенияОшибки + "Не выбрана учетная запись для документооборота с ФНС!" + Символы.ПС;
	КонецЕсли;
	Если НЕ Отказ Тогда
		Если ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо Тогда
			КПП								= "";
			КодОКОНХ						= "";
			ГоловнаяОрганизация 			= Неопределено;
		ИначеЕсли ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо Тогда
			ИндивидуальныйПредприниматель 	= Неопределено;
		КонецЕсли;
		ИНН = СокрЛП(ИНН);
		ОГРН = СокрЛП(ОГРН);
		Если ВидОбменаСКонтролирующимиОрганами.Пустая() Тогда
			ВидОбменаСКонтролирующимиОрганами = Перечисления.ВидыОбменаСКонтролирующимиОрганами.ОбменОтключен;
		КонецЕсли;
	КонецЕсли;
	
	Если ОбщегоНазначения.ПолучитьРабочуюДату() < ПроведениеРасчетов.ДатаЗаменыЕСНСтраховымиВзносами() И Не ЗначениеЗаполнено(ВидСтавокЕСНиПФР) Тогда
		ВидСтавокЕСНиПФР = Перечисления.ВидыСтавокЕСНиПФР.ДляНеСельскохозяйственныхПроизводителей;	
	КонецЕсли;
	
КонецПроцедуры

// Обработчик события "ПередЗаписью" Объекта
//
Процедура ПередЗаписью(Отказ)
	
	Если НЕ ОбменДанными.Загрузка Тогда
		
		ТекстСообщенияОшибки = "";
		
		ЗаполнитьРеквизитыОбъекта(Отказ, ТекстСообщенияОшибки);
		
		Если Отказ Тогда
			ОбщегоНазначения.СообщитьОбОшибке(ТекстСообщенияОшибки, Отказ);
		ИначеЕсли ЗначениеЗаполнено(ТекстСообщенияОшибки) Тогда
			ОбщегоНазначения.СообщитьОбОшибке(ТекстСообщенияОшибки, , , СтатусСообщения.Внимание);
		КонецЕсли;
		
		Если НЕ Отказ Тогда
			ПрошлыйИзмененныйГоловнаяОрганизация = ?(Не ЭтоНовый() и Не Ссылка.ГоловнаяОрганизация = ГоловнаяОрганизация, Ссылка.ГоловнаяОрганизация, Неопределено);
			НастройкаПравДоступа.ПередЗаписьюНовогоОбъектаСПравамиДоступаПользователей(ЭтотОбъект, Отказ, ГоловнаяОрганизация);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Функция записывает новое значение ИНН для обособленных организаций.
//
// Параметры
//
// Возвращаемое значение:
//   Строка   – пустая строка, если записали ИНН,
//				текст об ошибке, если ИНН не удалось записать. 
//
Функция ОбновитьИННОбособленныхОрганизаций()

	Если ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстСообщения = "";
	ЗапросОрганизации = Новый Запрос;
	ЗапросОрганизации.УстановитьПараметр("ГоловнаяОрганизация", Ссылка);
	ЗапросОрганизации.УстановитьПараметр("ИНН", ИНН);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Организации.Ссылка
	|ИЗ
	|	Справочник.Организации КАК Организации
	|
	|ГДЕ
	|	Организации.ГоловнаяОрганизация = &ГоловнаяОрганизация И
	|	Организации.ИНН <> &ИНН";
	
	ЗапросОрганизации.Текст = ТекстЗапроса;
	ВыборкаЗапроса = ЗапросОрганизации.Выполнить().Выбрать();
	Пока ВыборкаЗапроса.Следующий() Цикл
		ОбособленнаяОрганизация = ВыборкаЗапроса.Ссылка.ПолучитьОбъект();
		Попытка
		    ОбособленнаяОрганизация.Заблокировать();
		Исключение
			ТекстСообщения = "Организация: " + ВыборкаЗапроса.Ссылка + " - объект заблокирован.";
			Возврат ТекстСообщения
		КонецПопытки;
	КонецЦикла;	    
	
	ВыборкаЗапроса.Сбросить(); 
	Пока ВыборкаЗапроса.Следующий() Цикл
		ОбособленнаяОрганизация = ВыборкаЗапроса.Ссылка.ПолучитьОбъект();
		ОбособленнаяОрганизация.ИНН = ИНН;
		ОбособленнаяОрганизация.Записать()
	КонецЦикла; 

	Возврат ТекстСообщения

КонецФункции

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗаписиИНН = ОбновитьИННОбособленныхОрганизаций();
	Если НЕ ПустаяСтрока(ТекстЗаписиИНН) Тогда
		Сообщить(ТекстЗаписиИНН + Символы.ПС + "Элемент не записан!");
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	НастройкаПравДоступа.ОбновитьПраваДоступаКИерархическимОбъектамПриНеобходимости(Ссылка, ПрошлыйИзмененныйГоловнаяОрганизация, Отказ);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	ВидСтавокЕСНиПФР					= Перечисления.ВидыСтавокЕСНиПФР.ДляНеСельскохозяйственныхПроизводителей;
	ЮрФизЛицо							= Перечисления.ЮрФизЛицо.ЮрЛицо;
	РайонныйКоэффициент					= 1;
	РайонныйКоэффициентРФ				= 1;
	ОтражатьВРегламентированномУчете	= Истина;
КонецПроцедуры


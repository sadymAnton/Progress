
///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест"
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;

	РабочееМесто = МенеджерОборудованияСервер.ПолучитьРабочееМестоКлиента();

	ОткрыватьПриПервомОбращении = Параметры.ОткрыватьПриПервомОбращении;
	ИдентификаторКлиента        = Параметры.ИдентификаторКлиента;

	// Если передали имя компьютера, то значит необходимо выбрать рабочее место
	// из уже существующего (не пустого) списка рабочих мест
	Если Не ПустаяСтрока(ИдентификаторКлиента) Тогда
		НовыйМассив = Новый Массив();
		НовыйМассив.Добавить(Новый ПараметрВыбора("Отбор.ПометкаУдаления", Ложь));
		НовыйМассив.Добавить(Новый ПараметрВыбора("Отбор.Код", ИдентификаторКлиента));
		НовыйФиксированныйМассив = Новый ФиксированныйМассив(НовыйМассив);
		Элементы.РабочееМесто.ПараметрыВыбора = НовыйФиксированныйМассив;

		Элементы.НадписьНастройки.Доступность = Ложь;
		Элементы.ПодключитьИНастроитьПО.Доступность = Ложь;
	КонецЕсли;
	
	Элементы.ПодключитьИНастроитьПО.Доступность = ЗначениеЗаполнено(РабочееМесто);
	Элементы.ОткрыватьПриПервомОбращении.Доступность = МенеджерОборудованияСервер.ПравоДоступаСохранениеДанныхПользователя();
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура РабочееМестоПриИзменении(Элемент)
	
	Элементы.ПодключитьИНастроитьПО.Доступность = ЗначениеЗаполнено(РабочееМесто);
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ПодключитьИНастроитьПО(Команда)
	
	Если ЗначениеЗаполнено(РабочееМесто) Тогда
		
		МенеджерОборудованияСервер.УстановитьРабочееМестоКлиента(РабочееМесто);
		Оповестить("ИзмененоРабочееМестоТекущегоСеанса", РабочееМесто);
		
		Закрыть();
		 
		ОткрытьФормуМодально("Справочник.ПодключаемоеОборудование.ФормаСписка");
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВыбратьИЗакрыть(Команда)

	Если ЗначениеЗаполнено(РабочееМесто) Тогда
		Параметры.РабочееМесто = РабочееМесто;

		Если МенеджерОборудованияСервер.ПравоДоступаСохранениеДанныхПользователя() Тогда
			СписокНастроек = Новый Структура();
			СписокНастроек.Вставить("ОткрытьФормуВыбораРМПриПервомОбращении", ОткрыватьПриПервомОбращении);
			МенеджерОборудованияКлиент.СохранитьПользовательскиеНастройкиПодключаемогоОборудования(СписокНастроек);
		КонецЕсли;
		
		ОчиститьСообщения();
		
		СтруктураВозврата = Новый Структура("РабочееМесто", РабочееМесто);
		Закрыть(СтруктураВозврата);
		
	Иначе
		
		ОчиститьСообщения();
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Выберите рабочее место'"), РабочееМесто, "РабочееМесто");
		
	КонецЕсли;

КонецПроцедуры


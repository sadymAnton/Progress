Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	//Эмулируем заполнение необходимых реквизитов здесь. В будущем лучше вынести в код модуля.
	
	Если Направление = "Исходящее" Тогда
		Архив = ((Статус = "Доставлен") 
			Или (Статус = "ИсходящийПодписанВДиадок" )
			Или (Статус = "Подписан покупателем" ) /// Кунов О.В., 11.08.2017 - 69392
			Или (Статус = "Подписан получателем" ) /// Кунов О.В., 09.11.2017 - 72501
			Или (Статус = "Подписан и отправлен" ));
	Иначе
		Архив = (Статус = "Загружен" )
//		Или (Статус = "Отклонен" )
		И Не ОтклоненоОтправителем
		И Не ТребуемоеДействие = "СозданИмпортированныйЗаказ";//по отклоненным сообщениям цепочку продолжать не будем
	КонецЕсли;	
	
	//укажем ДатуЗагрузки для исходящих сообщений, будем также и по ней сортировать
	Если Не ЗначениеЗаполнено(ДатаЗагрузки) И ЭтоНовый() Тогда
		ДатаЗагрузки = ТекущаяДата();//могут быть проблемы с разным временем на разных компах
	КонецЕсли;	
	
	//И сюда же проставим Партнера, НомерЗаказа, ТочкуДоставки
	СтруктураСообщения = Хранилище.Получить();
	Если ТипЗнч(СтруктураСообщения) = Тип("Структура") Тогда
		Если Не ЗначениеЗаполнено(Партнер) Тогда
			Если Направление = "Исходящее" Тогда
				Если СтруктураСообщения.Свойство("Получатель1С")
					Тогда
					Партнер = СтруктураСообщения.Получатель1С;
				КонецЕсли;	
			Иначе
				Если СтруктураСообщения.Свойство("Отправитель1С")
					Тогда
					Партнер = СтруктураСообщения.Отправитель1С;
				КонецЕсли;	
			КонецЕсли;		
		КонецЕсли;//Партнер
		
		Если Не ЗначениеЗаполнено(ДатаПоставки) 
			И СтруктураСообщения.Свойство("ДатаПоставки") Тогда
			ДатаПоставки = СтруктураСообщения.ДатаПоставки;
		КонецЕсли; //ДатаПоставки
		
		Если Не ЗначениеЗаполнено(НомерЗаказа) Тогда
			Если СтруктураСообщения.Свойство("ТипСообщения") 
				И СтруктураСообщения.ТипСообщения = "ORDERS"
				И СтруктураСообщения.Свойство("ДокументEDI")
				И СтруктураСообщения.ДокументEDI.Свойство("Номер")
				Тогда
				НомерЗаказа = СтруктураСообщения.ДокументEDI.Номер;
			ИначеЕсли СтруктураСообщения.Свойство("ЗаказEDI")
				И ТипЗнч(СтруктураСообщения.ЗаказEDI) = Тип("Структура")
				И СтруктураСообщения.ЗаказEDI.Свойство("Номер")
				Тогда
				НомерЗаказа = СтруктураСообщения.ЗаказEDI.Номер;
			КонецЕсли;
		КонецЕсли; //НомерЗаказа
		
		Если Не ЗначениеЗаполнено(ДатаЗаказа) Тогда
			Если СтруктураСообщения.Свойство("ТипСообщения") 
				И СтруктураСообщения.ТипСообщения = "ORDERS"
				И СтруктураСообщения.Свойство("ДокументEDI")
				И СтруктураСообщения.ДокументEDI.Свойство("Дата")
				Тогда
				ДатаЗаказа = СтруктураСообщения.ДокументEDI.Дата;
			ИначеЕсли СтруктураСообщения.Свойство("ЗаказEDI")
				И ТипЗнч(СтруктураСообщения.ЗаказEDI) = Тип("Структура")
				И СтруктураСообщения.ЗаказEDI.Свойство("Дата")
				Тогда
				ДатаЗаказа = СтруктураСообщения.ЗаказEDI.Дата;
			КонецЕсли;
		КонецЕсли; //ДатаЗаказа
		
	КонецЕсли;
	
	//Еще отдельно проверим, нет ли в статусах сообщений записи по данному документу "Принят частично"
	//Пока статус не сменится на "Принят частично (обработан)", не помещаем сообщение в архив.
	Если ТипСообщения = "RECADV" И Направление = "Входящее" 
		И ЗначениеЗаполнено(Документ)
		Тогда
		Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	КонтурEDI_СтатусыДокументов.Документ
		|ИЗ
		|	РегистрСведений.КонтурEDI_СтатусыДокументов КАК КонтурEDI_СтатусыДокументов
		|ГДЕ
		|	КонтурEDI_СтатусыДокументов.Документ = &Документ
		|	И КонтурEDI_СтатусыДокументов.ИмяСтатуса = ""Приемка""
		|	И КонтурEDI_СтатусыДокументов.Статус = ""Принят частично"""
		);
		Запрос.УстановитьПараметр("Документ",Документ);
		Если Не Запрос.Выполнить().Пустой() Тогда
			Архив = Ложь;
		КонецЕсли;	
	КонецЕсли;
	
	// аналогично для торговых сетей проверим 
	Если ТипСообщения = "ORDRSP" И Направление = "Входящее" И ЗначениеЗаполнено(Документ) Тогда
		Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	КонтурEDI_СтатусыДокументов.Документ
		|ИЗ
		|	РегистрСведений.КонтурEDI_СтатусыДокументов КАК КонтурEDI_СтатусыДокументов
		|ГДЕ
		|	КонтурEDI_СтатусыДокументов.Документ = &Документ
		|	И КонтурEDI_СтатусыДокументов.ИмяСтатуса = ""Заказ""
		|	И КонтурEDI_СтатусыДокументов.Статус = ""Уточнен"""
		);
		Запрос.УстановитьПараметр("Документ",Документ);
		Если Не Запрос.Выполнить().Пустой() Тогда
			Архив = Ложь;
		КонецЕсли;	
	КонецЕсли;

КонецПроцедуры

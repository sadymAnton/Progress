Перем ПрошлыйИзмененныйРодительОбъектаДоступа;

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Обработчик события ПередЗаписью объекта.
//
Процедура ПередЗаписью(Отказ)

	Если Не ОбменДанными.Загрузка
	   И Не ЭтоГруппа
	   И Не ЭтоНовый()
	   И ВидСклада <> Ссылка.ВидСклада
	   И ПолныеПрава.Склад_СуществуютСсылки(Ссылка) Тогда

		ОбщегоНазначения.СообщитьОбОшибке("Существуют документы, в которых выбран склад """ + Наименование + """.
		                |Реквизит ""Вид склада"" не может быть изменен, элемент не записан.");

		Отказ = Истина;
	КонецЕсли;

	// Не дадим записать розничный склад или НТТ без указания типа цен.
	Если Не ОбменДанными.Загрузка
	   И Не ЭтоГруппа
	   И (ВидСклада = Перечисления.ВидыСкладов.Розничный Или ВидСклада = Перечисления.ВидыСкладов.НТТ)
	   И НЕ ЗначениеЗаполнено(ТипЦенРозничнойТорговли) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Для склада вида """+ВидСклада+""" необходимо указать тип цен розничной торговли,
		                 |элемент не записан.");

		Отказ = Истина;
	КонецЕсли;

	Если НЕ ОбменДанными.Загрузка Тогда
		
		ПрошлыйИзмененныйРодительОбъектаДоступа = ?(Не ЭтоНовый() и Не Ссылка.Родитель = Родитель, Ссылка.Родитель, Неопределено);
		НастройкаПравДоступа.ПередЗаписьюНовогоОбъектаСПравамиДоступаПользователей(ЭтотОбъект, Отказ, Родитель);
		
		//m_ionov@mail.ru 31.08.2016
		Если Не Отказ 
			И Не ЭтоГруппа
			И Не ЭтоНовый() 
			И Ссылка.НСИ_ОбменСПролайт
			И НЕ НСИ_ОбменСПролайт Тогда
			//Выполнили снятие признака обмена с Пролайт, 
			//проверим нет ли объектов с данным видом номенклатуры выгруженных в Пролайт
			Если Не МЗ_ОбменПролайт.ПроверкаВозможностиИзмененияПризнакаОбменасПролайт(Ссылка) Тогда
				//Возвращаем обмен с Пролайт
				НСИ_ОбменСПролайт = Истина;
			КонецЕсли;	
		КонецЕсли;
		//------- m_ionov@mail.ru -------
		
	КонецЕсли;

КонецПроцедуры // ПередЗаписью()

// Обработчик события ПриЗаписи.
//
Процедура ПриЗаписи(Отказ)

	Если НЕ ОбменДанными.Загрузка Тогда
		НастройкаПравДоступа.ОбновитьПраваДоступаКИерархическимОбъектамПриНеобходимости(Ссылка, ПрошлыйИзмененныйРодительОбъектаДоступа, Отказ);
	КонецЕсли;

КонецПроцедуры // ПриЗаписи()

Процедура ПриКопировании(ОбъектКопирования)
	//m_ionov@mail.ru 07.09.2016
	Если НЕ ЭтотОбъект.ЭтоГруппа Тогда
		НСИ_ОбменСПролайт = Ложь;	
	КонецЕсли;
	//------- m_ionov@mail.ru -------
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТИРУЕМЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ


&НаКлиенте
Процедура УстановитьДоступность()
	
	ЭтоЮрЛицо = (Объект.ЮрФизЛицо = ЮрФизЛицо_ЮрЛицо);
	Элементы.КПП.Доступность = ЭтоЮрЛицо;
	Элементы.ДокументУдостоверяющийЛичность.Доступность = НЕ ЭтоЮрЛицо;
	Элементы.НаименованиеПолное.Заголовок = ?(ЭтоЮрЛицо, "Полное наименование", "ФИО");
	Элементы.ИНН.Доступность = НЕ Объект.ОбособленноеПодразделение;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.ВидКонтрагента.СписокВыбора.Добавить("ЮридическоеЛицо", 			Строка(Перечисления.ЮрФизЛицо.ЮрЛицо));
	Элементы.ВидКонтрагента.СписокВыбора.Добавить("ФизическоеЛицо", 			Строка(Перечисления.ЮрФизЛицо.ФизЛицо));
	Элементы.ВидКонтрагента.СписокВыбора.Добавить("ОбособленноеПодразделение", 	"Обособленное подразделение");
	
	ЮрФизЛицо_ЮрЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо;
	
	Если Объект.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо Тогда
		ВидКонтрагента	= "ФизическоеЛицо";
	ИначеЕсли Объект.ОбособленноеПодразделение Тогда
		ВидКонтрагента	= "ОбособленноеПодразделение";
	Иначе
		ВидКонтрагента	= "ЮридическоеЛицо";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	
	Если НЕ ЗначениеЗаполнено(Объект.НаименованиеПолное) Тогда
		Объект.НаименованиеПолное = Объект.Наименование;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидКонтрагентаПриИзменении(Элемент)
	
	Если ВидКонтрагента = "ФизическоеЛицо" Тогда
		Объект.ЮрФизЛицо 							= ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ФизЛицо");
		Объект.КПП									= "";
		Объект.ОбособленноеПодразделение			= Ложь;
	ИначеЕсли ВидКонтрагента = "ОбособленноеПодразделение" Тогда
		Объект.ЮрФизЛицо							= ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ЮрЛицо");
		Объект.ДокументУдостоверяющийЛичность		= "";
		Объект.ОбособленноеПодразделение			= Истина;
	Иначе
		Объект.ЮрФизЛицо							= ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ЮрЛицо");
		Объект.ДокументУдостоверяющийЛичность		= "";
		Объект.ОбособленноеПодразделение			= Ложь;
	КонецЕсли;
	
	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидКонтрагентаОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	ПараметрыЗаписи.Вставить("ЭтоНовый", НЕ ЗначениеЗаполнено(Объект.Ссылка));
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если НЕ ПараметрыЗаписи.ЭтоНовый ИЛИ ТекущийОбъект.ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	Если  НЕ ЗначениеЗаполнено(ТекущийОбъект.ОсновнойДоговорКонтрагента) Тогда
		ПроверитьОсновнойДоговорКонтрагента(ТекущийОбъект, Отказ);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ТекущийОбъект.ГоловнойКонтрагент) Тогда
		ТекущийОбъект.ГоловнойКонтрагент = ТекущийОбъект.Ссылка;
	КонецЕсли; 

КонецПроцедуры

&НаСервере
Процедура ПроверитьОсновнойДоговорКонтрагента(ТекущийОбъект, Отказ)
	ВыборкаДоговоров = Справочники.ДоговорыКонтрагентов.Выбрать(, ТекущийОбъект.Ссылка);
	Если ВыборкаДоговоров.Следующий() Тогда
		ТекущийОбъект.ОсновнойДоговорКонтрагента = ВыборкаДоговоров.Ссылка;
	Иначе
		НайденныйДоговорОбъект              = Справочники.ДоговорыКонтрагентов.СоздатьЭлемент();
		НайденныйДоговорОбъект.Наименование = "Основной договор";

		НайденныйДоговорОбъект.ВалютаВзаиморасчетов = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ОсновнаяВалютаВзаиморасчетов");
		Если НЕ ЗначениеЗаполнено(НайденныйДоговорОбъект.ВалютаВзаиморасчетов) Тогда
			НайденныйДоговорОбъект.ВалютаВзаиморасчетов = Константы.ВалютаУправленческогоУчета.Получить();
		КонецЕсли;

		НайденныйДоговорОбъект.Организация = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ОсновнаяОрганизация");
		Если НЕ ЗначениеЗаполнено(НайденныйДоговорОбъект.Организация) Тогда
			Запрос = Новый Запрос();
			Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
			               |	Организации.Ссылка
			               |ИЗ
			               |	Справочник.Организации КАК Организации";
			Результат = Запрос.Выполнить();
			Если НЕ Результат.Пустой() Тогда
				Выборка = Результат.Выбрать();
				Выборка.Следующий();
				НайденныйДоговорОбъект.Организация = Выборка.Ссылка;
			Иначе
				СтрокаСообщения = Нстр("ru = 'Не удалось записать основной договор контрагента (не найдена организация)'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаСообщения, ТекущийОбъект,,, Отказ);
				Возврат;
			КонецЕсли;
		КонецЕсли;

		НайденныйДоговорОбъект.Владелец           = ТекущийОбъект.Ссылка;
		НайденныйДоговорОбъект.ВидУсловийДоговора = Перечисления.ВидыУсловийДоговоровВзаиморасчетов.БезДополнительныхУсловий;

		Если ТекущийОбъект.Покупатель Тогда
			НайденныйДоговорОбъект.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СПокупателем;
			НайденныйДоговорОбъект.ТипЦен      = Справочники.ТипыЦенНоменклатуры.ПустаяСсылка();
		ИначеЕсли ТекущийОбъект.Поставщик Тогда
			НайденныйДоговорОбъект.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СПоставщиком;
			НайденныйДоговорОбъект.ТипЦен      = Справочники.ТипыЦенНоменклатурыКонтрагентов.ПустаяСсылка();
		Иначе
			НайденныйДоговорОбъект.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.Прочее;
		КонецЕсли;

		Если НайденныйДоговорОбъект.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.Прочее Тогда
			НайденныйДоговорОбъект.ВедениеВзаиморасчетов = Перечисления.ВедениеВзаиморасчетовПоДоговорам.ПоДоговоруВЦелом;
		Иначе
			НайденныйДоговорОбъект.ВедениеВзаиморасчетов = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ОсновноеВедениеВзаиморасчетовПоДоговорам");
			Если НЕ ЗначениеЗаполнено(НайденныйДоговорОбъект.ВедениеВзаиморасчетов) Тогда
				НайденныйДоговорОбъект.ВедениеВзаиморасчетов = Перечисления.ВедениеВзаиморасчетовПоДоговорам.ПоДоговоруВЦелом;
			КонецЕсли;
			НайденныйДоговорОбъект.ВестиПоДокументамРасчетовСКонтрагентом = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ОсновныеРасчетыПоДокументамСКонтрагентами");
		КонецЕсли;


		Если (НайденныйДоговорОбъект.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СПоставщиком
		 ИЛИ  НайденныйДоговорОбъект.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СПокупателем)
		   И НайденныйДоговорОбъект.ВалютаВзаиморасчетов <> Константы.ВалютаРегламентированногоУчета.Получить() Тогда
			НайденныйДоговорОбъект.РасчетыВУсловныхЕдиницах = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ОсновныеРасчетыПоДоговоруВУсловныхЕдиницах");
		КонецЕсли;

		Если НайденныйДоговорОбъект.ВедениеВзаиморасчетов = Перечисления.ВедениеВзаиморасчетовПоДоговорам.ПоДоговоруВЦелом
		 ИЛИ НайденныйДоговорОбъект.ВедениеВзаиморасчетов = Перечисления.ВедениеВзаиморасчетовПоДоговорам.ПоЗаказам Тогда
			НайденныйДоговорОбъект.ОбособленныйУчетТоваровПоЗаказамПокупателей = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ОсновнойОбособленныйУчетТоваровПоЗаказамПокупателей");
		КонецЕсли;

		Если НайденныйДоговорОбъект.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СПокупателем Тогда
			НайденныйДоговорОбъект.ПроцентПредоплаты = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ОсновнойРазмерПредоплатыПоЗаказуПокупателя");
		КонецЕсли;
		СтрокаСообщения = "";
		Попытка
			НайденныйДоговорОбъект.Записать();
		Исключение
			СтрокаСообщения = Нстр("ru = 'Не удалось записать основной договор контрагента: '") + ОписаниеОшибки();
		КонецПопытки;
		Если СтрокаСообщения <> "" Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаСообщения, ТекущийОбъект,,, Отказ);
			Возврат;
		КонецЕсли;
		
		ТекущийОбъект.ОсновнойДоговорКонтрагента = НайденныйДоговорОбъект.Ссылка;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ИННПриИзменении(Элемент)
	
	// заполним КПП по первым 4-м цифрам ИНН
	Если (СтрДлина(Объект.ИНН) < 4) Тогда
		Возврат;
	КонецЕсли;
	ПравыеСимволыИНН = Лев(Объект.ИНН, 4);
	Объект.КПП = ПравыеСимволыИНН + "01001";
	
КонецПроцедуры

&НаКлиенте
Процедура ГоловнойКонтрагентПриИзменении(Элемент)
	
	Если Объект.ОбособленноеПодразделение Тогда
		ГоловнойКонтрагентПриИзмененииНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ГоловнойКонтрагентПриИзмененииНаСервере()

	Если Объект.ОбособленноеПодразделение Тогда
		Объект.ИНН = ОбщегоНазначения.ПолучитьЗначениеРеквизита(Объект.ГоловнойКонтрагент, "ИНН");	
	КонецЕсли;

КонецПроцедуры 

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

&НаСервереБезКонтекста
Функция МассивФайловСбербанка(ЭД)
	
	МассивВозврата = Новый Массив;
	
	СтруктураДайджест = Новый Структура;
	ОсноваИмениФайла = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(ЭД.Наименование);
	
	СтрокаBase64 = ЭлектронныеДокументыСлужебныйВызовСервера.ПодписанныеДанныеBase64(ЭД);
	ИмяФайлаДанных = "doc_" + ОсноваИмениФайла + ".txt";
	ДвоичныеДанные = Base64Значение(СтрокаBase64);
	СсылкаНаДайджест =  ПоместитьВоВременноеХранилище(ДвоичныеДанные);
	СтруктураДайджест = Новый Структура("СсылкаНаФайл, ИмяФайла", СсылкаНаДайджест, ИмяФайлаДанных);
	МассивВозврата.Добавить(СтруктураДайджест);
	
	Для Каждого ЭЦП из ЭД.ЭлектронныеЦифровыеПодписи Цикл
		ДвоичныеДанныеЭЦП = ЭЦП.Подпись.Получить();
		ИмяФайлаДанных = "sign_" + ОсноваИмениФайла+ "_" + ЭЦП.НомерСтроки + ".txt";
		СсылкаНаПодпись = ПоместитьВоВременноеХранилище(ДвоичныеДанныеЭЦП);
		МассивВозврата.Добавить(Новый Структура("СсылкаНаФайл, ИмяФайла", СсылкаНаПодпись, ИмяФайлаДанных));
				
		ДвоичныеДанныеСертификата = ЭЦП.Сертификат.Получить();
		ИмяФайлаДанных = "cert_" + ОсноваИмениФайла + "_" + ЭЦП.НомерСтроки + ".cer";
		СсылкаНаСертификат = ПоместитьВоВременноеХранилище(ДвоичныеДанныеСертификата);
		МассивВозврата.Добавить(Новый Структура("СсылкаНаФайл, ИмяФайла", СсылкаНаСертификат, ИмяФайлаДанных));
	КонецЦикла;

	ДанныеФайла = ПрисоединенныеФайлы.ПолучитьДанныеФайла(ЭД);
	ИмяФайлаДанных = ОсноваИмениФайла + ".xml";
	СтруктураФайла = Новый Структура("СсылкаНаФайл, ИмяФайла", ДанныеФайла.СсылкаНаДвоичныеДанныеФайла, ИмяФайлаДанных);
	МассивВозврата.Добавить(СтруктураФайла);
	
	Возврат МассивВозврата;
	
КонецФункции

&НаСервере
Процедура ИзменитьСтатусОтклонить()
	
	ОбъектДокумент = РеквизитФормыВЗначение("Объект");
	СтруктураПараметров = Новый Структура("СтатусЭД", Перечисления.СтатусыЭД.Отклонен);
	ЭлектронныеДокументыСлужебный.ИзменитьПоСсылкеПрисоединенныйФайл(Объект.Ссылка, СтруктураПараметров, Ложь);
	ЗначениеВРеквизитФормы(ОбъектДокумент, "Объект");
	
КонецПроцедуры

// Если у текущего (отклоняемого) ЭД есть подчиненные (не служебные) ЭД,
// со статусом отличным от Отклонен||ОтклоненПолучателем, то его отклонять нельзя.
//
&НаСервере
Функция МожноОтклонитьЭтотЭД()
	
	ВозвращаемоеЗначение = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЭДПрисоединенныеФайлы.Ссылка КАК ПодчиненныеЭД
	|ИЗ
	|	Справочник.ЭДПрисоединенныеФайлы КАК ЭДПрисоединенныеФайлы
	|ГДЕ
	|	ЭДПрисоединенныеФайлы.ЭлектронныйДокументВладелец = &ЭлектронныйДокументВладелец
	|	И (ЭДПрисоединенныеФайлы.ТипЭлементаВерсииЭД = ЗНАЧЕНИЕ(Перечисление.ТипыЭлементовВерсииЭД.ПервичныйЭД)
	|			ИЛИ ЭДПрисоединенныеФайлы.ТипЭлементаВерсииЭД = ЗНАЧЕНИЕ(Перечисление.ТипыЭлементовВерсииЭД.ЭСФ))
	|	И ЭДПрисоединенныеФайлы.СтатусЭД <> ЗНАЧЕНИЕ(Перечисление.СтатусыЭД.Отклонен)
	|	И ЭДПрисоединенныеФайлы.СтатусЭД <> ЗНАЧЕНИЕ(Перечисление.СтатусыЭД.ОтклоненПолучателем)";
	
	Запрос.УстановитьПараметр("ЭлектронныйДокументВладелец", Объект.Ссылка);
	
	УстановитьПривилегированныйРежим(Истина);
	Результат = Запрос.Выполнить();
	
	Если НЕ Результат.Пустой() Тогда
		ВозвращаемоеЗначение = Ложь;
		СписокЭлектронныхДокументов = "";
		
		ШаблонСообщения = НСтр("ru = 'Для отклонения электронного документа необходимо отклонить следующие электронные документы: %1.'");
		
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			СписокЭлектронныхДокументов = СписокЭлектронныхДокументов + Символы.ПС + Выборка.ПодчиненныеЭД;
		КонецЦикла;
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, СписокЭлектронныхДокументов);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

&НаСервере
Процедура ИзменитьВидимостьДоступность()
	
	Если ЗначениеЗаполнено(СсылкаНаМассивСертификатов) Тогда
		МассивСтруктурСертификатов = ПолучитьИзВременногоХранилища(СсылкаНаМассивСертификатов);
	Иначе
		МассивСтруктурСертификатов = Неопределено;
	КонецЕсли;
	
	Элементы.ГруппаСодержимоеДокумента.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	Элементы.ГруппаВправо.ПодчиненныеЭлементы.СтраницыКоманд.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	
	Если НЕ Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.ВыпискаБанка И НЕ Элементы.Найти("КомандаПрочитать") = Неопределено Тогда
		Элементы.КомандаПрочитать.Видимость = Ложь;
	КонецЕсли;
	
	Если Объект.Ссылка.НаправлениеЭД = Перечисления.НаправленияЭД.Входящий Тогда
		
		Элементы.ГруппаВправо.ПодчиненныеЭлементы.СтраницыКоманд.ТекущаяСтраница = Элементы.ГруппаКомандВх;
		
		Элементы.КомандаОтправитьЭД.Видимость = Ложь;
		Элементы.КомандаПодписать.Видимость   = Ложь;
		
		Если Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.ИзвещениеОПолучении
			ИЛИ Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.Подтверждение
			ИЛИ Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.УведомлениеОбУточнении
			ИЛИ Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.ВыпискаБанка
			ИЛИ Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.Квитанция Тогда
			
			Элементы.ПерезаполнитьДокумент.Видимость = Ложь;
		ИначеЕсли Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.КаталогТоваров Тогда
			Элементы.ПерезаполнитьДокумент.Заголовок = НСтр("ru = 'Сопоставить номенклатуру'");
		ИначеЕсли Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.СчетФактура
			ИЛИ Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.КорректировочныйСчетФактура
			ИЛИ Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.ТОРГ12Продавец
			ИЛИ Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.АктИсполнитель
			ИЛИ Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиОтправитель
			ИЛИ Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.Квитанция Тогда
			
			Элементы.КомандаОтправитьПодтверждениеЭД.Видимость = Ложь;
			Элементы.КомандаУтвердитьЭД.Видимость = Истина;
		ИначеЕсли Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.ТОРГ12Покупатель
			ИЛИ Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.АктЗаказчик
			ИЛИ Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиПолучатель Тогда

			
			Элементы.КомандаОтправитьПодтверждениеЭД.Видимость = Ложь;
			Элементы.КомандаУтвердитьЭД.Видимость = Ложь;
			Элементы.ПерезаполнитьДокумент.Видимость = Ложь;
		КонецЕсли;
		
	ИначеЕсли Объект.Ссылка.НаправлениеЭД = Перечисления.НаправленияЭД.Исходящий Тогда
		
		Элементы.ГруппаВправо.ПодчиненныеЭлементы.СтраницыКоманд.ТекущаяСтраница = Элементы.ГруппаКомандИсх;
		
		Элементы.КомандаОтправитьПодтверждениеЭД.Видимость = Ложь;
		Элементы.КомандаВыбратьДокумент.Видимость          = Ложь;
		Элементы.КомандаПодписать.Видимость                = Ложь;
		
	ИначеЕсли Объект.Ссылка.НаправлениеЭД = Перечисления.НаправленияЭД.Интеркампани Тогда
		
		Элементы.ГруппаВправо.ПодчиненныеЭлементы.СтраницыКоманд.ТекущаяСтраница = Элементы.ГруппаКомандИсх;
		
		Элементы.КомандаОтправитьПодтверждениеЭД.Видимость = Ложь;
		Элементы.КомандаВыбратьДокумент.Видимость          = Ложь;
		Элементы.КомандаОтправитьЭД.Видимость              = Ложь;
		Элементы.ОбработкаЭлектронныеДокументыПереотправитьЭД.Видимость = Ложь;
		
	КонецЕсли;
	
	Если НЕ (ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьЗначениеФункциональнойОпции("ИспользоватьЭлектронныеЦифровыеПодписи")
		ИЛИ НЕ ЭлектронныеДокументыСлужебный.НемедленнаяОтправкаЭД()) Тогда
		
		Элементы.КомандаУтвердитьЭД.Заголовок = "Утвердить и отправить";
	КонецЕсли;
	
	ЭДОтклонен = ЭДОтклонен();

	ЕстьВозможностьПодписания = (ЗначениеЗаполнено(МассивСтруктурСертификатов)
		И ДоступныДляПодписиСертификаты(МассивСтруктурСертификатов));
	
	Если ЭлектронныеДокументыСлужебный.НемедленнаяОтправкаЭД() Тогда
		КомандаПодписиОтправки = Элементы.КомандаОтправитьЭД;
	Иначе
		КомандаПодписиОтправки = Элементы.КомандаПодписать;
	КонецЕсли;
	
	Если Объект.Ссылка.НаправлениеЭД = Перечисления.НаправленияЭД.Входящий Тогда
		
		Элементы.КомандаОтправитьПодтверждениеЭД.Видимость = ЕстьВозможностьПодписания
		И Объект.Ссылка.ВидЭД <> Перечисления.ВидыЭД.СчетФактура
		И Объект.Ссылка.ВидЭД <> Перечисления.ВидыЭД.КорректировочныйСчетФактура
		И Объект.Ссылка.ВидЭД <> Перечисления.ВидыЭД.ТОРГ12Продавец
		И Объект.Ссылка.ВидЭД <> Перечисления.ВидыЭД.ТОРГ12Покупатель
		И Объект.Ссылка.ВидЭД <> Перечисления.ВидыЭД.АктИсполнитель
		И Объект.Ссылка.ВидЭД <> Перечисления.ВидыЭД.АктЗаказчик
		И Объект.Ссылка.ВидЭД <> Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиОтправитель
		И Объект.Ссылка.ВидЭД <> Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиПолучатель;
		
		Элементы.КомандаУтвердитьЭД.Видимость = Объект.Ссылка.ВидЭД <> Перечисления.ВидыЭД.ТОРГ12Покупатель
		И Объект.Ссылка.ВидЭД <> Перечисления.ВидыЭД.АктЗаказчик
		И Объект.Ссылка.ВидЭД <> Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиПолучатель
		И Объект.Ссылка.ВидЭД <> Перечисления.ВидыЭД.ВыпискаБанка
		И (НЕ ЕстьВозможностьПодписания
		ИЛИ Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.СчетФактура
		ИЛИ Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.КорректировочныйСчетФактура
		ИЛИ Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.ТОРГ12Продавец
		ИЛИ Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.АктИсполнитель
		ИЛИ Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиОтправитель);
		
		ЭтоПолученныйКаталогТоваров = Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.КаталогТоваров
										И Объект.Ссылка.СтатусЭД = Перечисления.СтатусыЭД.Получен;
		Элементы.ПерезаполнитьДокумент.Доступность = НЕ ЭДОтклонен И НЕ(ЭтоПолученныйКаталогТоваров);
		Элементы.ЗаголовокОтраженВУчете.Доступность = НЕ ЭДОтклонен;
		Элементы.КомандаОтправитьПодтверждениеЭД.Доступность = ЕстьВозможностьПодписания
		И НЕ ЭДОтклонен И (ОтраженВУчете ИЛИ ЭтоПолученныйКаталогТоваров)
		И (Объект.Ссылка.СтатусЭД = Перечисления.СтатусыЭД.Получен
		ИЛИ Объект.Ссылка.СтатусЭД = Перечисления.СтатусыЭД.Утвержден);
		Элементы.КомандаУтвердитьЭД.Доступность = (Объект.Ссылка.СтатусЭД = Перечисления.СтатусыЭД.Получен
		И НЕ(Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.ИзвещениеОПолучении
		ИЛИ Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.Подтверждение
		ИЛИ Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.УведомлениеОбУточнении));
	ИначеЕсли Объект.Ссылка.НаправлениеЭД = Перечисления.НаправленияЭД.Исходящий Тогда
		
		Если Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.ПлатежноеПоручение Тогда
			КоличествоНеустановленныхПодписей =  ПолучитьКоличествоНеустановленныхПодписей();
			Элементы.КомандаОтправитьЭД.Видимость = (КоличествоНеустановленныхПодписей=1) И ЕстьВозможностьПодписания;
			Элементы.КомандаПодписать.Видимость   = КоличествоНеустановленныхПодписей>1 И ЕстьВозможностьПодписания;
			ЕстьОшибкаПередачи = (Объект.СтатусЭД = Перечисления.СтатусыЭД.ОшибкаПередачи);
			Элементы.ОбработкаЭлектронныеДокументыПереотправитьЭД.Видимость = ЕстьОшибкаПередачи;
			Если ПрограммаБанка = Перечисления.ПрограммыБанка.СбербанкОнлайн Тогда
				Элементы.КомандаОтправитьЭД.Видимость = Ложь;
				Элементы.ОбработкаЭлектронныеДокументыПереотправитьЭД.Видимость = Ложь;
				Элементы.КомандаПодписать.Видимость = КоличествоНеустановленныхПодписей > 0 И ЕстьВозможностьПодписания;
			КонецЕсли;
		Иначе
			КомандаПодписиОтправки.Видимость   = ЕстьВозможностьПодписания И ТребуетсяПодпись;
			КомандаПодписиОтправки.Доступность = НЕ ЭДОтклонен
			И (ОтраженВУчете ИЛИ Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.ТОРГ12Покупатель
			ИЛИ Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.АктЗаказчик
			ИЛИ Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.АктНаПередачуПрав
			ИЛИ Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиПолучатель
			ИЛИ Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.ИзвещениеОПолучении)
			И (Объект.Ссылка.СтатусЭД = Перечисления.СтатусыЭД.Сформирован ИЛИ Объект.Ссылка.СтатусЭД = Перечисления.СтатусыЭД.Утвержден);
			Элементы.ОбработкаЭлектронныеДокументыПереотправитьЭД.Доступность = НЕ ЭДОтклонен
			И ОтраженВУчете
			ИЛИ (Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.ИзвещениеОПолучении
			ИЛИ Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.Подтверждение
			ИЛИ Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.УведомлениеОбУточнении);
		КонецЕсли;
		Элементы.КомандаУтвердитьЭД.Видимость   = НЕ (ЕстьВозможностьПодписания И ТребуетсяПодпись);
		Элементы.КомандаУтвердитьЭД.Доступность = Объект.Ссылка.СтатусЭД = Перечисления.СтатусыЭД.Сформирован;
		

	ИначеЕсли Объект.Ссылка.НаправлениеЭД = Перечисления.НаправленияЭД.Интеркампани Тогда
		
		Элементы.КомандаПодписать.Видимость     = ЕстьВозможностьПодписания;
		Элементы.КомандаПодписать.Доступность   = (ЕстьВозможностьПодписания
		И НЕ ЭДОтклонен И Объект.Ссылка.СтатусЭД <> Перечисления.СтатусыЭД.ПолностьюПодписан);
		Элементы.КомандаУтвердитьЭД.Видимость   = НЕ ЕстьВозможностьПодписания;
		Элементы.КомандаУтвердитьЭД.Доступность = (Объект.Ссылка.СтатусЭД = Перечисления.СтатусыЭД.Сформирован);
		
	КонецЕсли;
	
	Элементы.КомандаОтклонить.Доступность = НЕ (ЭДОтклонен
		ИЛИ ЭлектронныеДокументыСлужебныйВызовСервера.ЭтоСлужебныйДокумент(Объект.Ссылка));
	
	Если ЭДОтклонен Тогда
		
		ПричиныОтклонения.Очистить();
		НовСтрока = ПричиныОтклонения.Добавить();
		НовСтрока.ПричинаОтклонения = Объект.Ссылка.ПричинаОтклонения;
		Элементы.ГруппаСтраницыПодвала.ТекущаяСтраница = Элементы.ГруппаСтраницыПодвала.ПодчиненныеЭлементы.ГруппаСтраницаОтклонение;
		
		Если Объект.Ссылка.СтатусЭД = Перечисления.СтатусыЭД.ОшибкаПередачи Тогда
			Элементы.ПричиныОтклоненияПричинаОтклонения.Заголовок = НСтр("ru = 'Ошибка обмена'");
		КонецЕсли;
		
		Если Объект.ВидЭД = Перечисления.ВидыЭД.ПлатежноеПоручение
			И ПрограммаБанка = Перечисления.ПрограммыБанка.СбербанкОнлайн Тогда
			Элементы.ГруппаСтраницыПодвала.ТекущаяСтраница = Элементы.ГруппаКвитанция;
			Квитанция = ЭлектронныеДокументыВнутренний.ПодчиненныйДокумент(Объект.Ссылка, Перечисления.ВидыЭД.Квитанция);
		КонецЕсли;
		Если НЕ РазрешеноОтклонитьЭД() Тогда
			Элементы.КомандаОтклонить.Доступность = Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	ОчиститьОповещениеОНеактуальности();
	УстановитьЗаголовокФормы();
	
КонецПроцедуры

&НаСервере
Функция РазрешеноОтклонитьЭД()
	
	Если Объект.ВидЭД = Перечисления.ВидыЭД.ПлатежноеПоручение Тогда
		Если Объект.СтатусЭД = перечисления.СтатусыЭД.НеОтправлен 
			ИЛИ Объект.СтатусЭД = перечисления.СтатусыЭД.ПодготовленКОтправке
			ИЛИ Объект.СтатусЭД = перечисления.СтатусыЭД.Подписан
			ИЛИ Объект.СтатусЭД = перечисления.СтатусыЭД.Сформирован
			ИЛИ Объект.СтатусЭД = перечисления.СтатусыЭД.Утвержден
			ИЛИ Объект.СтатусЭД = перечисления.СтатусыЭД.ЧастичноПодписан Тогда
				Возврат Истина;
		Иначе
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	Возврат Истина;
	
КонецФункции

&НаСервере
Процедура УстановитьЗаголовокФормы()
	
	Если НЕ ОтраженВУчете И НЕ (Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.ИзвещениеОПолучении
								ИЛИ Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.Подтверждение
								ИЛИ Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.УведомлениеОбУточнении
								ИЛИ Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.ВыпискаБанка
								ИЛИ Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.Квитанция
								ИЛИ Объект.Ссылка.ВидЭД = Перечисления.ВидыЭД.ЗапросВыписки) Тогда
		
		Если НЕ ЗначениеЗаполнено(ВерсияЭД) ИЛИ Объект.Ссылка.ДатаФормированияЭДОтправителем <= ВерсияЭД Тогда
			ЭтаФорма.Заголовок = ЭтаФорма.Заголовок + НСтр("ru = ' - неактуальный'");
		ИначеЕсли Объект.ДатаФормированияЭДОтправителем > ВерсияЭД Тогда
			ЭтаФорма.Заголовок = ЭтаФорма.Заголовок + НСтр("ru = ' - новый'");
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьОповещениеОНеактуальности()
	
	ЭтаФорма.Заголовок = СтрЗаменить(ЭтаФорма.Заголовок, НСтр("ru = ' - неактуальный'"), "");
	ЭтаФорма.Заголовок = СтрЗаменить(ЭтаФорма.Заголовок, НСтр("ru = ' - новый'"), "");
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСтатусЭД(СтатусЭД = Неопределено, ДатаИзмененияСтатусаЭД = Неопределено)
	
	Если НЕ ЗначениеЗаполнено(СтатусЭД) Тогда
		СтатусЭД = Объект.Ссылка.СтатусЭД;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(ДатаИзмененияСтатусаЭД) Тогда
		ДатаИзмененияСтатусаЭД = Объект.Ссылка.ДатаИзмененияСтатусаЭД;
	КонецЕсли;
	
	ТекстСтатусЭД = " " +  СтатусЭД + ", " + Формат(ДатаИзмененияСтатусаЭД, "ДЛФ=");
	ТекстСостояния = ЭлектронныеДокументыКлиентСервер.ПолучитьТекстСостоянияЭД(Объект.ВладелецФайла);
	ТекстДокументИБ = Строка(Объект.ВладелецФайла)
					+ ?(ЗначениеЗаполнено(ТекстСостояния), Нстр("ru = ', состояние:'") + " " + ТекстСостояния, "");
	
	ЗапросПоОтражению = Новый Запрос;
	ЗапросПоОтражению.УстановитьПараметр("СсылкаНаОбъект", Объект.ВладелецФайла);
	
	ЗапросПоОтражению.Текст =
	"ВЫБРАТЬ
	|	СостоянияЭД.СсылкаНаОбъект,
	|	СостоянияЭД.ЭлектронныйДокумент
	|ИЗ
	|	РегистрСведений.СостоянияЭД КАК СостоянияЭД
	|ГДЕ
	|	СостоянияЭД.СсылкаНаОбъект = &СсылкаНаОбъект";
	
	Выборка = ЗапросПоОтражению.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ВерсияЭД = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Выборка.ЭлектронныйДокумент, "ДатаФормированияЭДОтправителем");
		ОтраженВУчете = (Объект.Ссылка = Выборка.ЭлектронныйДокумент);
	КонецЕсли;
	
	ТаблицаСтатусов = ПолучитьТаблицуСтатусовЭД(Объект.Ссылка);
	Если ЗначениеЗаполнено(ТаблицаСтатусов) Тогда
		ЗначениеВРеквизитФормы(ТаблицаСтатусов, "СтатусыЭД");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуЭЦП()
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка.СоглашениеЭД)
		ИЛИ НЕ Объект.Ссылка.СоглашениеЭД.ПроверятьСертификатыПодписей Тогда
		
		ТаблицаЭЦП = РеквизитФормыВЗначение("ЭЦП");
		ТаблицаЭЦП.Очистить();
		
		Для Каждого ТекСтрока Из Объект.Ссылка.ЭлектронныеЦифровыеПодписи Цикл
			НоваяСтрока = ТаблицаЭЦП.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока);
			ЗаполнитьСтатусПодписи(НоваяСтрока, ТекСтрока);
		КонецЦикла;
		
		ЗначениеВРеквизитФормы(ТаблицаЭЦП, "ЭЦП");
		Возврат;
	КонецЕсли;
	
	МассивОтпечатковОжидаемыхСертификатов = ПолучитьОтпечаткиОжидаемыхСертификатов(Объект.Ссылка.СоглашениеЭД);
	
	ТаблицаЭЦП = РеквизитФормыВЗначение("ЭЦП");
	ТаблицаЭЦП.Очистить();
	
	Для Каждого ТекСтрока Из Объект.Ссылка.ЭлектронныеЦифровыеПодписи Цикл
		НоваяСтрока = ТаблицаЭЦП.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока);
		Если МассивОтпечатковОжидаемыхСертификатов.Найти(ТекСтрока.Отпечаток) = Неопределено Тогда
			НоваяСтрока.ОтсутствуетВСписке = Истина;
			НоваяСтрока.ВыводКартинки = 1;
		Иначе
			НоваяСтрока.ВыводКартинки = 0;
		КонецЕсли;
		ЗаполнитьСтатусПодписи(НоваяСтрока, ТекСтрока);
	КонецЦикла;
	ЗначениеВРеквизитФормы(ТаблицаЭЦП, "ЭЦП");
	
КонецПроцедуры

&НаСервере
Функция ПолучитьОтпечаткиОжидаемыхСертификатов(ТекущееСоглашение)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СертификатыЭЦП.Отпечаток КАК Отпечаток
	|ИЗ
	|	Справочник.СертификатыЭЦП КАК СертификатыЭЦП
	|ГДЕ
	|	СертификатыЭЦП.Организация = &Организация
	|	И СертификатыЭЦП.Отозван = ЛОЖЬ
	|	И СертификатыЭЦП.ПометкаУдаления = ЛОЖЬ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СоглашенияОбИспользованииЭДСертификатыПодписейКонтрагента.Отпечаток
	|ИЗ
	|	Справочник.СоглашенияОбИспользованииЭД.СертификатыПодписейКонтрагента КАК СоглашенияОбИспользованииЭДСертификатыПодписейКонтрагента
	|ГДЕ
	|	СоглашенияОбИспользованииЭДСертификатыПодписейКонтрагента.Ссылка = &СоглашениеОбИспользованииЭД";
	Запрос.УстановитьПараметр("Организация", ТекущееСоглашение.Организация);
	Запрос.УстановитьПараметр("СоглашениеОбИспользованииЭД", ТекущееСоглашение);
	
	МассивОтпечатков = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Отпечаток");
	
	Возврат МассивОтпечатков;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьСтатусПодписи(НоваяСтрока, ТекСтрока)
	
	Если ЗначениеЗаполнено(ТекСтрока.ДатаПроверкиПодписи) Тогда
		НоваяСтрока.ПодписьВерна = ?(ТекСтрока.ПодписьВерна, НСтр("ru = 'Верна'"), НСтр("ru = 'Неверна'"))
			+" (" + ТекСтрока.ДатаПроверкиПодписи + ")";
	Иначе
		НоваяСтрока.ПодписьВерна = НСтр("ru = 'Не проверена'");
	КонецЕсли
	
КонецПроцедуры

&НаСервере
Функция НеобходимоПодписать()
	
	ФлагПодписи = Ложь;
	// Ответ на заказ никогда не подписывает покупатель или документ отклонен.
	Если НЕ ЭДОтклонен Тогда
		
		Если Объект.Ссылка.СоглашениеЭД.СпособОбменаЭД = Перечисления.СпособыОбменаЭД.ЧерезОператораЭДОТакском Тогда
			ФлагПодписи = Истина;
		Иначе
			
			УстановитьПривилегированныйРежим(Истина);
			
			Запрос = Новый Запрос;
			Запрос.Текст =
			"ВЫБРАТЬ
			|	СоглашенияОбИспользованииЭДВходящиеДокументы.ИспользоватьЭЦП
			|ИЗ
			|	Справочник.СоглашенияОбИспользованииЭД.ВходящиеДокументы КАК СоглашенияОбИспользованииЭДВходящиеДокументы
			|ГДЕ
			|	СоглашенияОбИспользованииЭДВходящиеДокументы.ВходящийДокумент = &ВидЭД
			|	И &НаправлениеЭД = ЗНАЧЕНИЕ(Перечисление.НаправленияЭД.Входящий)
			|	И СоглашенияОбИспользованииЭДВходящиеДокументы.Ссылка = &Ссылка
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	СоглашенияОбИспользованииЭДИсходящиеДокументы.ИспользоватьЭЦП
			|ИЗ
			|	Справочник.СоглашенияОбИспользованииЭД.ИсходящиеДокументы КАК СоглашенияОбИспользованииЭДИсходящиеДокументы
			|ГДЕ
			|	СоглашенияОбИспользованииЭДИсходящиеДокументы.ИсходящийДокумент = &ВидЭД
			|	И (&НаправлениеЭД = ЗНАЧЕНИЕ(Перечисление.НаправленияЭД.Исходящий)
			|			ИЛИ &НаправлениеЭД = ЗНАЧЕНИЕ(Перечисление.НаправленияЭД.Интеркампани))
			|	И СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка = &Ссылка";
			Запрос.УстановитьПараметр("Ссылка",        Объект.Ссылка.СоглашениеЭД);
			Запрос.УстановитьПараметр("ВидЭД",         Объект.Ссылка.ВидЭД);
			Запрос.УстановитьПараметр("НаправлениеЭД", Объект.Ссылка.НаправлениеЭД);
	
			Результат = Запрос.Выполнить().Выбрать();
			Результат.Следующий();
	
			ФлагПодписи = Результат.ИспользоватьЭЦП;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ФлагПодписи;
	
КонецФункции

&НаСервере
Функция ДоступныДляПодписиСертификаты(МассивСтруктурСертификатов)
	
	МассивОтпечатков = Новый Массив;
	Для Каждого ЭлементСтруктуры Из МассивСтруктурСертификатов Цикл
		МассивОтпечатков.Добавить(ЭлементСтруктуры.Отпечаток);
	КонецЦикла;
	
	ЗапросПоСертификатам = Новый Запрос;
	ЗапросПоСертификатам.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	СертификатыЭЦПВидыДокументов.Ссылка
	|ИЗ
	|	Справочник.СертификатыЭЦП.ВидыДокументов КАК СертификатыЭЦПВидыДокументов
	|ГДЕ
	|	НЕ СертификатыЭЦПВидыДокументов.Ссылка.Отозван
	|	И (СертификатыЭЦПВидыДокументов.Ссылка.Пользователь = &ТекущийПользователь
	|			ИЛИ СертификатыЭЦПВидыДокументов.Ссылка.Пользователь = ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка))
	|	И НЕ СертификатыЭЦПВидыДокументов.Ссылка.ПометкаУдаления
	|	И СертификатыЭЦПВидыДокументов.ВидДокумента = &ВидДокумента
	|	И СертификатыЭЦПВидыДокументов.ИспользоватьДляПодписи
	|	И СертификатыЭЦПВидыДокументов.Ссылка.Организация = &Организация
	|	И %1";
	
	Если Объект.ВидЭД = Перечисления.ВидыЭД.ПлатежноеПоручение
			И ПрограммаБанка = Перечисления.ПрограммыБанка.СбербанкОнлайн Тогда
		МассивСертификатовНеобходимыхПодписей = Объект.СоглашениеЭД.СертификатыПодписейОрганизации.ВыгрузитьКолонку(
																										"Сертификат");
		ДопУсловие = " СертификатыЭЦПВидыДокументов.Ссылка В(&МассивСертификатовНеобходимыхПодписей)";
		ЗапросПоСертификатам.УстановитьПараметр("МассивСертификатовНеобходимыхПодписей",
												МассивСертификатовНеобходимыхПодписей);
	Иначе
		МассивОтпечатков = Новый Массив;
		Для Каждого ЭлементСтруктуры Из МассивСтруктурСертификатов Цикл
			МассивОтпечатков.Добавить(ЭлементСтруктуры.Отпечаток);
		КонецЦикла;	
		ЗапросПоСертификатам.УстановитьПараметр("МассивОтпечатков", МассивОтпечатков);
		ДопУсловие = " СертификатыЭЦПВидыДокументов.Ссылка.Отпечаток В(&МассивОтпечатков)";
	КонецЕсли;
	
	ЗапросПоСертификатам.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
																	ЗапросПоСертификатам.Текст,
																	ДопУсловие);
	
	ЗапросПоСертификатам.УстановитьПараметр("ТекущийПользователь", ПользователиСервер.АвторизованныйПользователь());
	ЗапросПоСертификатам.УстановитьПараметр("ВидДокумента",        Объект.ВидЭД);
	ЗапросПоСертификатам.УстановитьПараметр("Организация",         Объект.Организация);

	ИспользуютсяЭЦП = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьЗначениеФункциональнойОпции(
																	"ИспользоватьЭлектронныеЦифровыеПодписи");
	ВозвращаемыйПараметр = ИспользуютсяЭЦП И НЕ ЗапросПоСертификатам.Выполнить().Пустой() И ТребуетсяПодпись;
		
	Возврат ВозвращаемыйПараметр;
	
КонецФункции

&НаКлиенте
Процедура УстановитьВидимостьДоступностьНаКлиенте()

	Попытка
		Если ЭлектронныеДокументыСлужебныйВызовСервера.ВыполнятьКриптооперацииНаСервере() Тогда
			МассивСтруктурСертификатов = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьМассивСтруктурСертификатов(Истина);
		Иначе
			МассивСтруктурСертификатов = ЭлектроннаяЦифроваяПодписьКлиент.ПолучитьМассивСтруктурСертификатов(Истина);
		КонецЕсли;
	Исключение
		МассивСтруктурСертификатов = Новый Массив;
	КонецПопытки;
	СсылкаНаМассивСертификатов = ПоместитьВоВременноеХранилище(МассивСтруктурСертификатов, УникальныйИдентификатор);
	ИзменитьВидимостьДоступность();
	
КонецПроцедуры

&НаСервере
Функция ФайлДанныхЭД(СсылкаНаЭД = Неопределено, Знач ИмяФайлаПодчиненногоЭД = Неопределено)
	
	Если СсылкаНаЭД = Неопределено Тогда
		СсылкаНаЭД = Объект.Ссылка;
	КонецЕсли;
	
	ДопИнформацияПоЭД = ПрисоединенныеФайлы.ПолучитьДанныеФайла(СсылкаНаЭД, СсылкаНаЭД.УникальныйИдентификатор(), Истина);
	
	Если ДопИнформацияПоЭД.Свойство("СсылкаНаДвоичныеДанныеФайла")
		И ЗначениеЗаполнено(ДопИнформацияПоЭД.СсылкаНаДвоичныеДанныеФайла) Тогда
		
		ДанныеЭД = ПолучитьИзВременногоХранилища(ДопИнформацияПоЭД.СсылкаНаДвоичныеДанныеФайла);
		
		Если ЗначениеЗаполнено(ДопИнформацияПоЭД.Расширение) Тогда
			ИмяФайла = ЭлектронныеДокументыСлужебный.ТекущееИмяВременногоФайла(ДопИнформацияПоЭД.Расширение);
		Иначе
			ИмяФайла = ЭлектронныеДокументыСлужебный.ТекущееИмяВременногоФайла("xml");
		КонецЕсли;
		
		Если ИмяФайла = Неопределено Тогда
			ТекстОшибки = НСтр("ru = 'Не удалось просмотреть электронный документ. Проверьте настройку рабочего каталога'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
			Возврат Неопределено;
		КонецЕсли;
		
		ВыборкаЭДДопДанных = ЭлектронныеДокументыСлужебный.ВыборкаДопДанныеЭД(СсылкаНаЭД);
		Если ВыборкаЭДДопДанных.Следующий() Тогда
			ДопДанныеЭД = ПрисоединенныеФайлы.ПолучитьДанныеФайла(ВыборкаЭДДопДанных.Ссылка,
				ВыборкаЭДДопДанных.Ссылка.УникальныйИдентификатор(), Истина);
			СсылкаНаДДДопДанныхЭД = "";
			Если ДопДанныеЭД.Свойство("СсылкаНаДвоичныеДанныеФайла", СсылкаНаДДДопДанныхЭД)
				И ЗначениеЗаполнено(СсылкаНаДДДопДанныхЭД) Тогда
				ДанныеДопФайла = ПолучитьИзВременногоХранилища(СсылкаНаДДДопДанныхЭД);
			
				Если ЗначениеЗаполнено(ДопДанныеЭД.Расширение) Тогда
					ИмяФайлаДопДанных = ЭлектронныеДокументыСлужебный.ТекущееИмяВременногоФайла(ДопДанныеЭД.Расширение);
				Иначе
					ИмяФайлаДопДанных = ЭлектронныеДокументыСлужебный.ТекущееИмяВременногоФайла("xml");
				КонецЕсли;
			
				Если ИмяФайлаДопДанных = Неопределено Тогда
					ТекстОшибки = НСтр("ru = 'Не удалось получить доп. данные электронного документа. Проверьте настройку рабочего каталога'");
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
					Возврат Неопределено;
				КонецЕсли;
				ДанныеДопФайла.Записать(ИмяФайлаДопДанных);
			КонецЕсли;
		КонецЕсли;
		
		ДанныеЭД.Записать(ИмяФайла);
		
		Если СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.ТОРГ12Покупатель
			ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.АктЗаказчик
			ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиПолучатель Тогда
			
			ТабличныйДокумент = ФайлДанныхЭД(СсылкаНаЭД.ЭлектронныйДокументВладелец, ИмяФайла);
			Возврат ТабличныйДокумент;
		ИначеЕсли Найти(ДопИнформацияПоЭД.Расширение, "zip") > 0 Тогда
			
			ЗИПЧтение = Новый ЧтениеZipФайла(ИмяФайла);
			ПапкаДляРаспаковки = ЭлектронныеДокументыСлужебный.РабочийКаталог(,СсылкаНаЭД.УникальныйИдентификатор());
			
			Если ПапкаДляРаспаковки = Неопределено Тогда
				ТекстОшибки = НСтр("ru = 'Не удалось просмотреть электронный документ. Проверьте настройку рабочего каталога'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
				Возврат Неопределено;
			КонецЕсли;
			
			Попытка
				ЗипЧтение.ИзвлечьВсе(ПапкаДляРаспаковки);
			Исключение
				ТекстОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
				Если НЕ ЭлектронныеДокументыСлужебный.ВозможноИзвлечьФайлы(ЗипЧтение, ПапкаДляРаспаковки) Тогда
					ТекстСообщения = ЭлектронныеДокументыПовтИсп.ПолучитьСообщениеОбОшибке("006");
				КонецЕсли;
				ЭлектронныеДокументыСлужебныйВызовСервера.ОбработатьИсключениеПоЭДНаСервере(НСтр("ru = 'Распаковка пакета ЭД'"),
					ТекстОшибки, ТекстСообщения);
				Возврат Неопределено;
			КонецПопытки;
			
			ФлагПросмотра = Ложь;
			
			Если СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.АктНаПередачуПрав Тогда
				ФайлыАрхиваPDF = НайтиФайлы(ПапкаДляРаспаковки, "*.pdf");
				Для Каждого РаспакованныйФайл Из ФайлыАрхиваPDF Цикл
					Возврат РаспакованныйФайл;
				КонецЦикла;
			КонецЕсли;
			
			ФайлыАрхиваXML = НайтиФайлы(ПапкаДляРаспаковки, "*.xml");
			
			Для Каждого РаспакованныйФайл Из ФайлыАрхиваXML Цикл
				
				ИмяФайлаДанных = РаспакованныйФайл.ПолноеИмя;
				Если Найти(РаспакованныйФайл.Имя,"packageDescription") Тогда
					Возврат ИмяФайла;
				КонецЕсли;
				
				ТабличныйДокумент = ЭлектронныеДокументыВнутренний.СформироватьПечатнуюФормуЭД(РаспакованныйФайл.ПолноеИмя,
																								СсылкаНаЭД.НаправлениеЭД,
																								СсылкаНаЭД.УникальныйИдентификатор(),
																								,
																								СсылкаНаЭД.Наименование);
					
				Если ТипЗнч(ТабличныйДокумент) = Тип("ТабличныйДокумент") Тогда
					Возврат ТабличныйДокумент;
				КонецЕсли;
				
			КонецЦикла;
			
			ФайлыАрхиваMXL = НайтиФайлы(ПапкаДляРаспаковки, "*.mxl");
			Для Каждого РаспакованныйФайл Из ФайлыАрхиваMXL Цикл
				ИмяФайлаДанных = РаспакованныйФайл.ПолноеИмя;
				ТабличныйДокумент = Новый ТабличныйДокумент;
				ТабличныйДокумент.Прочитать(ИмяФайлаДанных);
				УдалитьФайлы(ПапкаДляРаспаковки);
				Возврат ТабличныйДокумент;
			КонецЦикла;
			
			ФайлыАрхиваHTML = НайтиФайлы(ПапкаДляРаспаковки, "*.html");
			Для Каждого РаспакованныйФайл Из ФайлыАрхиваHTML Цикл
				Возврат РаспакованныйФайл;
			КонецЦикла;
			
			ФайлыАрхиваDOCX = НайтиФайлы(ПапкаДляРаспаковки, "*.docx");
			Для Каждого РаспакованныйФайл Из ФайлыАрхиваDOCX Цикл
				Возврат РаспакованныйФайл;
			КонецЦикла;
			
			ФайлыАрхиваXLS = НайтиФайлы(ПапкаДляРаспаковки, "*.xls");
			Для Каждого РаспакованныйФайл Из ФайлыАрхиваXLS Цикл
				Возврат РаспакованныйФайл;
			КонецЦикла;
			
			ФайлыАрхиваXML = НайтиФайлы(ПапкаДляРаспаковки, "*.xml");
			Для Каждого РаспакованныйФайл Из ФайлыАрхиваXML Цикл
				Возврат РаспакованныйФайл;
			КонецЦикла;
			
		ИначеЕсли Найти(ДопИнформацияПоЭД.Расширение, "xml") > 0 Тогда
			Если СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.Подтверждение
				ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.ИзвещениеОПолучении
				ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.УведомлениеОбУточнении
				ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.ТОРГ12Продавец
				ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.АктИсполнитель
				ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиОтправитель
				ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.СчетФактура
				ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.КорректировочныйСчетФактура Тогда
			
				ИмяФайлаДанных = ИмяФайла;
				ТабличныйДокумент = ЭлектронныеДокументыВнутренний.СформироватьПечатнуюФормуЭД(
																			ИмяФайла,
																			СсылкаНаЭД.НаправлениеЭД,
																			СсылкаНаЭД.УникальныйИдентификатор(),
																			ИмяФайлаПодчиненногоЭД,
																			СсылкаНаЭД.Наименование,
																			ИмяФайлаДопДанных);
			
				Если ТипЗнч(ТабличныйДокумент) = Тип("ТабличныйДокумент") Тогда
					Возврат ТабличныйДокумент;
				КонецЕсли;
			
			ИначеЕсли СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.ПлатежноеПоручение
				ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.ЗапросВыписки
				ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.ВыпискаБанка
				ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.Квитанция Тогда
			
				ИмяФайлаДанных = ИмяФайла;
				ТабличныйДокумент = ЭлектронныеДокументыВнутренний.СформироватьПечатнуюФормуЭД(
																			ИмяФайла,
																			СсылкаНаЭД.НаправлениеЭД,
																			СсылкаНаЭД.УникальныйИдентификатор(),
																			ИмяФайлаПодчиненногоЭД,
																			СсылкаНаЭД.УникальныйИдентификатор());
			
				Если ТипЗнч(ТабличныйДокумент)=Тип("ТабличныйДокумент") Тогда
					Возврат ТабличныйДокумент;
				КонецЕсли;

			КонецЕсли;
		Иначе
			
			Возврат ИмяФайла;
		КонецЕсли;
		
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура ВыполнитьПросмотрЭДИзБДСервер(Отказ = Ложь)
	
	ДанныеЭД = ФайлДанныхЭД();
	Если ДанныеЭД = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если ТипЗнч(ДанныеЭД) = Тип("ТабличныйДокумент") Тогда
		ТабличныйДокументФормы = ДанныеЭД;
		ИсходныйТабличныйДокумент = ДанныеЭД;
		ОпределитьНаличиеДопДанныхСкрытьФлаг();
		Если ЕстьДопДанные Тогда
			СкрытьДопДанные(ОтключитьВыводДопДанных);
		КонецЕсли;
		Элементы.ГруппаСодержимоеДокумента.ТекущаяСтраница = Элементы.СтраницаТабличныйДокумент;
	Иначе
		Если ТипЗнч(ДанныеЭД) = Тип("Файл") Тогда
			ПутьКФайлу = ДанныеЭД.ПолноеИмя;
			РасширениеФайла = СтрЗаменить(ДанныеЭД.Расширение, ".", "");
			ИмяФайла = ДанныеЭД.Имя;
		ИначеЕсли ТипЗнч(ДанныеЭД) = Тип("Строка") Тогда
			ПутьКФайлу = ДанныеЭД;
			ФрагментыПути = ФайловыеФункцииКлиентСервер.РазложитьСтрокуПоТочкамИСлэшам(ПутьКФайлу);
			РасширениеФайла = ФрагментыПути[ФрагментыПути.Количество() - 1];
			ИмяФайла = Строка(Новый УникальныйИдентификатор()) + "." + РасширениеФайла;
		Иначе
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		ДДФайла = Новый ДвоичныеДанные(ПутьКФайлу);
		
		// Передадим на клиента двоичные данные файла для просмотра:
		АдресФайлаВХранилище = ПоместитьВоВременноеХранилище(ДДФайла, УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьОбработкуОповещенияНаСервере()
	
	ЗаполнитьТаблицуЭЦП();
	ОбновитьСтатусЭД();
	ИзменитьВидимостьДоступность();
	
КонецПроцедуры

&НаСервере
Процедура ПерезаполнитьДокументПоЭД(ДокументЗагружен)
	
	Попытка
		ПерезаполнитьДокументИБПоЭД(Объект.ВладелецФайла, Объект.Ссылка, ДокументЗагружен);
	Исключение
	КонецПопытки;
	
	Если ДокументЗагружен Тогда
		УстановитьСостояниеЭДПоСтатусу();
	КонецЕсли;
	
	ЭтаФорма.Заголовок = ЭлектронныеДокументыСлужебный.ПолучитьПредставлениеЭД(Объект.Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеЭДПоСтатусу()
	
	Если НЕ ЭлектронныеДокументыСлужебныйВызовСервера.ЭтоСлужебныйДокумент(Объект.Ссылка) Тогда
		
		НаборЗаписей = РегистрыСведений.СостоянияЭД.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.СсылкаНаОбъект.Установить(Объект.ВладелецФайла);
		НаборЗаписей.Прочитать();
		
		Если НаборЗаписей.Количество() > 0 Тогда
			
			НоваяЗаписьНабора = НаборЗаписей.Получить(0);
			НоваяЗаписьНабора.СостояниеВерсииЭД   = ЭлектронныеДокументыСлужебный.ОпределитьСостояниеВерсииПоСтатусуЭД(Объект.Ссылка);
			НоваяЗаписьНабора.ЭлектронныйДокумент = Объект.Ссылка;
			НаборЗаписей.Записать();
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПерезаполнитьДокументИБПоЭД(СсылкаНаВладельца, СсылкаНаЭД, ОбъектПерезаполнен = Ложь)
	
	Если НЕ ЗначениеЗаполнено(СсылкаНаВладельца) ИЛИ НЕ ЗначениеЗаполнено(СсылкаНаЭД) Тогда
		Возврат;
	КонецЕсли;
	
	// Имя файла данных заполняться должно при чтении данных.
	ДанныеНаДиске = Новый Файл(ИмяФайлаДанных);
	Если НЕ ДанныеНаДиске.Существует() Тогда
		ШаблонСообщения = НСтр("ru = 'Файл данных не обнаружен: %1.'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, ИмяФайлаДанных);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ВладелецЭД",     СсылкаНаВладельца);
	СтруктураПараметров.Вставить("ПолноеИмяФайла", ИмяФайлаДанных);
	
	СтруктураЭД = ЭлектронныеДокументыВнутренний.РазобратьФайлДанных(СтруктураПараметров);
	ОбъектПерезаполнен =  СтруктураЭД.Загружен;
	
КонецПроцедуры

&НаСервере
Процедура ПерепривязатьЭлектронныйДокумент(ВыбранноеЗначение)
	
	ЭлектронныйДокумент = РеквизитФормыВЗначение("Объект");
	СтарыйВладелец = ЭлектронныйДокумент.ВладелецФайла;
	ЭлектронныйДокумент.ВладелецФайла = ВыбранноеЗначение;
	Если Не ЗначениеЗаполнено(ЭлектронныйДокумент.Автор) Тогда
		ЭлектронныйДокумент.Автор = ПользователиСервер.АвторизованныйПользователь();
	КонецЕсли;
	ЭлектронныйДокумент.Записать();
	
	ОбновитьСостояниеЭДВладельцев(ЭлектронныйДокумент.Ссылка, СтарыйВладелец, ВыбранноеЗначение);
	ЗначениеВРеквизитФормы(ЭлектронныйДокумент, "Объект");
	
	ТекстДокументИБ = Строка(Объект.ВладелецФайла.Ссылка)
		+ ?(ЗначениеЗаполнено(ТекстСостояния), Нстр("ru = ', состояние:'") + " " + ТекстСостояния,"");
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСостояниеЭДВладельцев(ЭД, СтарыйВладелец, НовыйВладелец)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос();
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СостоянияЭД.СсылкаНаОбъект
	|ИЗ
	|	РегистрСведений.СостоянияЭД КАК СостоянияЭД
	|ГДЕ
	|	СостоянияЭД.ЭлектронныйДокумент = &ЭлектронныйДокумент
	|	И СостоянияЭД.СсылкаНаОбъект = &СсылкаНаСтарыйДокумент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	СостоянияЭД.СсылкаНаОбъект
	|ИЗ
	|	РегистрСведений.СостоянияЭД КАК СостоянияЭД
	|ГДЕ
	|	(НЕ СостоянияЭД.ЭлектронныйДокумент = ЗНАЧЕНИЕ(Справочник.ЭДПрисоединенныеФайлы.ПустаяСсылка))
	|	И СостоянияЭД.СсылкаНаОбъект = &СсылкаНаНовыйДокумент";
	
	Запрос.УстановитьПараметр("ЭлектронныйДокумент", ЭД);
	Запрос.УстановитьПараметр("СсылкаНаСтарыйДокумент", СтарыйВладелец);
	Запрос.УстановитьПараметр("СсылкаНаНовыйДокумент", НовыйВладелец);
	
	Выборка = Запрос.ВыполнитьПакет();
	
	Результат1 = Выборка[0].Выбрать();
	Если Результат1.Следующий() Тогда
		ЗапросПоПослВерсии = Новый Запрос;
		ЗапросПоПослВерсии.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ЭДПрисоединенныеФайлы.Ссылка
		|ИЗ
		|	Справочник.ЭДПрисоединенныеФайлы КАК ЭДПрисоединенныеФайлы
		|ГДЕ
		|	ЭДПрисоединенныеФайлы.ВладелецФайла = &ВладелецФайла
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЭДПрисоединенныеФайлы.НомерВерсииЭД УБЫВ";
		
		ЗапросПоПослВерсии.УстановитьПараметр("ВладелецФайла", СтарыйВладелец);
		
		РезультатЗапросаПоПослВерсии = ЗапросПоПослВерсии.Выполнить().Выбрать();
		Если РезультатЗапросаПоПослВерсии.Следующий() Тогда
			НовыйАктуальныйЭД  = РезультатЗапросаПоПослВерсии.Ссылка;
		Иначе	
			НовыйАктуальныйЭД = Неопределено;
		КонецЕсли;
		ЭлектронныеДокументыСлужебныйВызовСервера.УстановитьНовуюВерсиюЭД(СтарыйВладелец, НовыйАктуальныйЭД)
	КонецЕсли;
	
	Результат2 = Выборка[1].Выбрать();
	Если НЕ Результат2.Следующий() Тогда
		ЭлектронныеДокументыСлужебныйВызовСервера.УстановитьНовуюВерсиюЭД(НовыйВладелец, ЭД);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьСтатус(КартаСтатусовЭД, Статус, Значение = Ложь)
	
	НовСтрока = КартаСтатусовЭД.Добавить();
	НовСтрока.Статус = Статус;
	НовСтрока.Пройден = Значение;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьКартуСтатусовЭД(КартаСтатусовЭД, ЭД)
	
	ПараметрыЭД = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ЭД.Ссылка,
		"ВидЭД, НаправлениеЭД, ВерсияРегламентаЭДО, Организация, Контрагент, СоглашениеЭД");
	
	НастройкиСтатусов = Новый ТаблицаЗначений;
	НастройкиСтатусов.Колонки.Добавить("СпособОбмена");
	НастройкиСтатусов.Колонки.Добавить("Направление");
	НастройкиСтатусов.Колонки.Добавить("ВидЭД");
	НастройкиСтатусов.Колонки.Добавить("ИспользоватьПодпись");
	НастройкиСтатусов.Колонки.Добавить("ИспользоватьКвитанции");
	НастройкиСтатусов.Колонки.Добавить("ИспользуетсяНесколькоПодписей");
	НастройкиСтатусов.Колонки.Добавить("ВерсияРегламентаЭДО");
	НастройкиСтатусов.Колонки.Добавить("ПрограммаБанка");
	ИспользуетсяЭЦП = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьЗначениеФункциональнойОпции(
															"ИспользоватьЭлектронныеЦифровыеПодписи");
	
	Если ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.ИзвещениеОПолучении
		ИЛИ ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.Подтверждение
		ИЛИ ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.ПлатежноеПоручение
		ИЛИ ((ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.ТОРГ12Покупатель
			ИЛИ ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.ТОРГ12Продавец
			ИЛИ ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.АктИсполнитель
			ИЛИ ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.АктЗаказчик
			ИЛИ ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиОтправитель
			ИЛИ ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиПолучатель)
			И ПараметрыЭД.НаправлениеЭД = Перечисления.НаправленияЭД.Входящий)
		ИЛИ ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.УведомлениеОбУточнении Тогда
		
		РеквизитыСоглашения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
														ПараметрыЭД.СоглашениеЭД,
														"СпособОбменаЭД, ПрограммаБанка");
		
		НоваяСтрока = НастройкиСтатусов.Добавить();
		НоваяСтрока.СпособОбмена          = РеквизитыСоглашения.СпособОбменаЭД;
		НоваяСтрока.Направление           = ПараметрыЭД.НаправлениеЭД;
		НоваяСтрока.ВидЭД                 = ПараметрыЭД.ВидЭД;
		НоваяСтрока.ИспользоватьПодпись   = ИспользуетсяЭЦП;
		НоваяСтрока.ИспользоватьКвитанции = Ложь;
		НоваяСтрока.ИспользуетсяНесколькоПодписей = ПараметрыЭД.СоглашениеЭД.СертификатыПодписейОрганизации.Количество() > 1;
		НоваяСтрока.ВерсияРегламентаЭДО   = ПараметрыЭД.ВерсияРегламентаЭДО;
		НоваяСтрока.ПрограммаБанка        = РеквизитыСоглашения.ПрограммаБанка
	Иначе
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ВидЭД",           ПараметрыЭД.ВидЭД);
		Запрос.УстановитьПараметр("НаправлениеЭД",   ПараметрыЭД.НаправлениеЭД);
		Запрос.УстановитьПараметр("Контрагент",      ПараметрыЭД.Контрагент);
		Запрос.УстановитьПараметр("Организация",     ПараметрыЭД.Организация);
		Запрос.УстановитьПараметр("ИспользуетсяЭЦП", ИспользуетсяЭЦП);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА &ИспользуетсяЭЦП
		|			ТОГДА Соглашение.ИспользоватьПодпись
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ИспользоватьПодпись,
		|	Соглашение.ИспользоватьКвитанции,
		|	Соглашение.ВидЭД,
		|	Соглашение.Направление,
		|	Соглашение.СпособОбмена,
		|	Соглашение.ПрограммаБанка
		|ИЗ
		|	(ВЫБРАТЬ
		|		СоглашенияОбИспользованииЭДВходящиеДокументы.ИспользоватьЭЦП КАК ИспользоватьПодпись,
		|		СоглашенияОбИспользованииЭДВходящиеДокументы.ОжидатьКвитанциюОДоставке КАК ИспользоватьКвитанции,
		|		ЗНАЧЕНИЕ(Перечисление.НаправленияЭД.Входящий) КАК Направление,
		|		СоглашенияОбИспользованииЭДВходящиеДокументы.ВходящийДокумент КАК ВидЭД,
		|		СоглашенияОбИспользованииЭДВходящиеДокументы.Ссылка.СпособОбменаЭД КАК СпособОбмена,
		|		0 КАК Приоритет,
		|		СоглашенияОбИспользованииЭДВходящиеДокументы.Ссылка.ПрограммаБанка КАК ПрограммаБанка
		|	ИЗ
		|		Справочник.СоглашенияОбИспользованииЭД.ВходящиеДокументы КАК СоглашенияОбИспользованииЭДВходящиеДокументы
		|	ГДЕ
		|		СоглашенияОбИспользованииЭДВходящиеДокументы.ВходящийДокумент = &ВидЭД
		|		И СоглашенияОбИспользованииЭДВходящиеДокументы.Ссылка.Организация = &Организация
		|		И СоглашенияОбИспользованииЭДВходящиеДокументы.Ссылка.Контрагент = &Контрагент
		|		И &НаправлениеЭД = ЗНАЧЕНИЕ(Перечисление.НаправленияЭД.Входящий)
		|		И СоглашенияОбИспользованииЭДВходящиеДокументы.Ссылка.СтатусСоглашения = ЗНАЧЕНИЕ(Перечисление.статусыСоглашенийЭД.Действует)
		|		И СоглашенияОбИспользованииЭДВходящиеДокументы.Формировать
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		СоглашенияОбИспользованииЭДИсходящиеДокументы.ИспользоватьЭЦП,
		|		СоглашенияОбИспользованииЭДИсходящиеДокументы.ОжидатьКвитанциюОДоставке,
		|		ВЫБОР
		|			КОГДА &НаправлениеЭД = ЗНАЧЕНИЕ(Перечисление.НаправленияЭД.Интеркампани)
		|				ТОГДА ЗНАЧЕНИЕ(Перечисление.НаправленияЭД.Интеркампани)
		|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НаправленияЭД.Исходящий)
		|		КОНЕЦ,
		|		СоглашенияОбИспользованииЭДИсходящиеДокументы.ИсходящийДокумент,
		|		СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка.СпособОбменаЭД,
		|		0,
		|		СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка.ПрограммаБанка
		|	ИЗ
		|		Справочник.СоглашенияОбИспользованииЭД.ИсходящиеДокументы КАК СоглашенияОбИспользованииЭДИсходящиеДокументы
		|	ГДЕ
		|		СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка.Организация = &Организация
		|		И СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка.Контрагент = &Контрагент
		|		И СоглашенияОбИспользованииЭДИсходящиеДокументы.ИсходящийДокумент = &ВидЭД
		|		И СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка.СтатусСоглашения = ЗНАЧЕНИЕ(Перечисление.статусыСоглашенийЭД.Действует)
		|		И СоглашенияОбИспользованииЭДИсходящиеДокументы.Формировать
		|		И (&НаправлениеЭД = ЗНАЧЕНИЕ(перечисление.НаправленияЭД.Исходящий)
		|				ИЛИ &НаправлениеЭД = ЗНАЧЕНИЕ(перечисление.НаправленияЭД.Интеркампани))
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ИСТИНА,
		|		NULL,
		|		&НаправлениеЭД,
		|		&ВидЭД,
		|		СоглашениеЧерезОЭДО.Ссылка.СпособОбменаЭД,
		|		1,
		|		NULL
		|	ИЗ
		|		РегистрСведений.УчастникиОбменовЭДЧерезОператоровЭДО КАК УчастникиОбменовЭДЧерезОператоровЭДО
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СоглашенияОбИспользованииЭД КАК СоглашениеЧерезОЭДО
		|			ПО УчастникиОбменовЭДЧерезОператоровЭДО.СоглашениеОбИспользованииЭД = СоглашениеЧерезОЭДО.Ссылка
		|	ГДЕ
		|		НЕ СоглашениеЧерезОЭДО.Ссылка.ПометкаУдаления
		|		И СоглашениеЧерезОЭДО.Ссылка.СтатусСоглашения = ЗНАЧЕНИЕ(Перечисление.СтатусыСоглашенийЭД.Действует)
		|		И СоглашениеЧерезОЭДО.Ссылка.СпособОбменаЭД = ЗНАЧЕНИЕ(Перечисление.СпособыОбменаЭД.ЧерезОператораЭДОТакском)
		|		И СоглашениеЧерезОЭДО.Ссылка.Организация = &Организация
		|		И УчастникиОбменовЭДЧерезОператоровЭДО.Участник = &Контрагент
		|		И УчастникиОбменовЭДЧерезОператоровЭДО.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыУчастниковОбменаЭД.Присоединен)
		|		И НЕ &НаправлениеЭД = ЗНАЧЕНИЕ(перечисление.НаправленияЭД.Интеркампани)) КАК Соглашение
		|
		|УПОРЯДОЧИТЬ ПО
		|	Соглашение.Приоритет";
		Результат = Запрос.Выполнить().Выбрать();
		
		Если Результат.Следующий() Тогда
			НоваяСтрока = НастройкиСтатусов.Добавить();
			НоваяСтрока.СпособОбмена          = Результат.СпособОбмена;
			НоваяСтрока.Направление           = Результат.Направление;
			НоваяСтрока.ВидЭД                 = Результат.ВидЭД;
			НоваяСтрока.ИспользоватьПодпись   = Результат.ИспользоватьПодпись;
			НоваяСтрока.ИспользоватьКвитанции = Результат.ИспользоватьКвитанции;

			НоваяСтрока.ВерсияРегламентаЭДО   = ПараметрыЭД.ВерсияРегламентаЭДО;
			НоваяСтрока.ПрограммаБанка        = Результат.ПрограммаБанка;
		КонецЕсли;
	КонецЕсли;
	
	Если НастройкиСтатусов.Количество() > 0 Тогда
		УстановитьСтатусы(КартаСтатусовЭД, НастройкиСтатусов[0]);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСтатусы(КартаСтатусовЭД, НастройкиСтатусов)
	
	МассивСтатусов = ЭлектронныеДокументыСлужебный.ВернутьМассивСтатусовЭД(НастройкиСтатусов);
	Для Каждого Элемент Из МассивСтатусов Цикл
		ДобавитьСтатус(КартаСтатусовЭД, Элемент);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьТаблицуСтатусовЭД(ЭД)
	
	КартаСтатусовЭД = Новый ТаблицаЗначений;
	КартаСтатусовЭД.Колонки.Добавить("Статус");
	КартаСтатусовЭД.Колонки.Добавить("Пройден");
	
	Если ЭД.СтатусЭД = Перечисления.СтатусыЭД.Отклонен ИЛИ ЭД.СтатусЭД = Перечисления.СтатусыЭД.ОтклоненПолучателем Тогда
		
		Если ЭД.НаправлениеЭД = Перечисления.НаправленияЭД.Входящий Тогда
		
			Стр = КартаСтатусовЭД.Добавить();
			Стр.Статус = Перечисления.СтатусыЭД.Получен;
			
			Стр = КартаСтатусовЭД.Добавить();
			Стр.Статус = Перечисления.СтатусыЭД.Отклонен;
		Иначе
			
			Стр = КартаСтатусовЭД.Добавить();
			Стр.Статус = Перечисления.СтатусыЭД.Сформирован;
			
			Стр = КартаСтатусовЭД.Добавить();
			Стр.Статус = ЭД.СтатусЭД;
		КонецЕсли;
		
		КартаСтатусовЭД.ЗаполнитьЗначения(Истина, "Пройден");
		
	Иначе
		
		ЗаполнитьКартуСтатусовЭД(КартаСтатусовЭД, ЭД);
		ПризнакПройден = Истина;
		Для Каждого ТекСтрока Из КартаСтатусовЭД Цикл 
			ТекСтрока.Пройден = ПризнакПройден;
			Если ТекСтрока.Статус = Перечисления.СтатусыЭД.Утвержден
				И (ЭД.СтатусЭД = Перечисления.СтатусыЭД.Отклонен ИЛИ ЭД.СтатусЭД = Перечисления.СтатусыЭД.ОтклоненПолучателем) Тогда
				ТекСтрока.Статус = Перечисления.СтатусыЭД.Отклонен;
				Прервать;
			КонецЕсли;
			Если ЭД.СтатусЭД = ТекСтрока.Статус Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат КартаСтатусовЭД;
	
КонецФункции

&НаСервере
Функция ПечатнаяФормаСЭЦП()
	
	Если Объект.Ссылка.ЭлектронныеЦифровыеПодписи.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ТабДок = Новый ТабличныйДокумент;
	Макет = Справочники.ЭДПрисоединенныеФайлы.ПолучитьМакет("МакетЭЦП");
	
	Область = Макет.ПолучитьОбласть("Шапка");
	Область.Параметры.Документ = ИмяДокумента();
	Область.Параметры.Дата     = Формат(Объект.Ссылка.ДатаДокументаОтправителя, "ДЛФ=Д");
	Область.Параметры.Номер    = Объект.Ссылка.НомерДокументаОтправителя;
	ТабДок.Вывести(Область);
	
	Для Каждого ЭлектроннаяПодпись Из Объект.Ссылка.ЭлектронныеЦифровыеПодписи Цикл
		Область = Макет.ПолучитьОбласть("СтрокаСертификата");
		Область.Параметры.Заполнить(ЭлектроннаяПодпись);
		Область.Параметры.СтатусПодписи = ?(ЭлектроннаяПодпись.ПодписьВерна, "Верна", "Неверна")
			+" (" + ЭлектроннаяПодпись.ДатаПроверкиПодписи + ")";
		ТабДок.Вывести(Область);
	КонецЦикла;
	Область = Макет.ПолучитьОбласть("Подвал");
	Область.Параметры.ИдентификаторДокумента = Объект.Ссылка;
	ТабДок.Вывести(Область);
	ТабДок.ТолькоПросмотр  = Истина;
	ТабДок.ОтображатьСетку = Ложь;
	
	Возврат ТабДок;
	
КонецФункции

&НаСервере
Функция ИмяДокумента()
	
	СсылкаНаЭД = Объект.Ссылка;
	
	Если СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.ЗаказТовара Тогда
		
		Возврат "Заказ поставщику";
	ИначеЕсли СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.КаталогТоваров Тогда
		
		Возврат "Каталог товаров";
	ИначеЕсли СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.ТОРГ12
		ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.ТОРГ12Покупатель
		ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.ТОРГ12Продавец Тогда
		
		Возврат "Товарная накладная";
	ИначеЕсли СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.ОтветНаЗаказ Тогда
		
		Возврат "Заказ клиента";
	ИначеЕсли СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.ПрайсЛист Тогда
		
		Возврат "Прайс-лист"
	ИначеЕсли СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.СчетНаОплату Тогда
		
		Возврат "Счет";
	ИначеЕсли СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.СчетФактура Тогда
		
		Возврат "Счет-фактура";
	ИначеЕсли СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.КорректировочныйСчетФактура Тогда
		
		Возврат "Корректировочный счет-фактура";
	ИначеЕсли СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.АктВыполненныхРабот
		ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.АктЗаказчик
		ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.АктИсполнитель Тогда
		
		Возврат "Акт выполненных работ";
	ИначеЕсли СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.ОтчетОПродажахКомиссионногоТовара Тогда
		
		Возврат "Отчет о продажах комиссионного товара";
	ИначеЕсли СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.ОтчетОСписанииКомиссионногоТовара Тогда
		
		Возврат "Отчет о списании комиссионного товара";
	ИначеЕсли СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.ПередачаТоваровМеждуОрганизациями Тогда
		
		Возврат "Передача товаров между организациями";
	ИначеЕсли СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.ВозвратТоваровМеждуОрганизациями Тогда
		
		Возврат "Возврат товаров между организациями";
	КонецЕсли;
	Возврат "Документ";
	
КонецФункции

&НаКлиенте
Процедура ПоказатьСертификат(НомерСтроки, Отпечаток)
	
	АдресДанныхСертификата = АдресДанныхСертификата(НомерСтроки);
	ДвоичныеДанныеСертификата = ПолучитьИзВременногоХранилища(АдресДанныхСертификата);
	
	ВыбранныйСертификат = Новый СертификатКриптографии(ДвоичныеДанныеСертификата);
	Если ВыбранныйСертификат=Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Сертификат не найден");
		Возврат;
	КонецЕсли;
	
	СтруктураСертификата = ЭлектроннаяЦифроваяПодписьКлиентСервер.ЗаполнитьСтруктуруСертификата(ВыбранныйСертификат);
	Если СтруктураСертификата <> Неопределено Тогда
		ПараметрыФормы = Новый Структура("СтруктураСертификата, Отпечаток, АдресСертификата",
			СтруктураСертификата, Отпечаток, АдресДанныхСертификата);
		СтруктураВозврата = ОткрытьФормуМодально("ОбщаяФорма.СертификатЭЦП", ПараметрыФормы);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция АдресДанныхСертификата(НомерСтроки)
	
	ДвоичныеДанныеСертификата = Объект.Ссылка.ЭлектронныеЦифровыеПодписи[НомерСтроки-1].Сертификат.Получить();
	СсылкаНаХранилищеДанныхСертификата = ПоместитьВоВременноеХранилище(ДвоичныеДанныеСертификата, ЭтаФорма.УникальныйИдентификатор);
	
	Возврат СсылкаНаХранилищеДанныхСертификата;
	
КонецФункции

&НаСервере
Процедура ДобавитьСертификатПодписиВСоглашение(Отпечаток, СертификатДобавлен)
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка.СоглашениеЭД) Тогда
		Возврат ;
	КонецЕсли;
	
	ОбъектЭД = РеквизитФормыВЗначение("Объект");
	НайденнаяСтрока = ОбъектЭД.ЭлектронныеЦифровыеПодписи.Найти(Отпечаток, "Отпечаток");
	Если НЕ НайденнаяСтрока = Неопределено Тогда
		СоглашениеОбъект = Объект.Ссылка.СоглашениеЭД.ПолучитьОбъект();
		
		НоваяСтрока = СоглашениеОбъект.СертификатыПодписейКонтрагента.Добавить();
		НоваяСтрока.Сертификат = НайденнаяСтрока.Сертификат;
		НоваяСтрока.Отпечаток  = Отпечаток;
		СоглашениеОбъект.Записать();
		
		СертификатДобавлен = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСертификатВДоверенные(ДанныеПодписи)
	
	Если ДанныеПодписи <> Неопределено И ДанныеПодписи.ОтсутствуетВСписке Тогда 
		Текст = НСтр("ru = 'Добавить сертификат %1 в список ожидаемых сертификатов контрагента?'");
		ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Текст, ДанныеПодписи.КомуВыданСертификат);
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			ПоказатьСертификат(Элементы.ЭЦП.ТекущиеДанные.НомерСтроки,Элементы.ЭЦП.ТекущиеДанные.Отпечаток);
			Возврат;
		КонецЕсли;
		
		// Добавим сертификат в Соглашение.
		СертификатДобавлен = Ложь;
		ДобавитьСертификатПодписиВСоглашение(ДанныеПодписи.Отпечаток, СертификатДобавлен);
		Если НЕ СертификатДобавлен Тогда 
			ТекстСообщения = НСтр("ru = 'Ошибка добавления сертификата подписи в список ожидаемых сертификатов!'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Иначе
			ЗаполнитьТаблицуЭЦП();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПоместитьТекстУточненияВОбъект(ТекстУточнения)
	
	ЭлектронныйДокумент = Объект.Ссылка.ПолучитьОбъект();
	ЭлектронныйДокумент.ПричинаОтклонения = ТекстУточнения;
	ЭлектронныйДокумент.Записать();
	ЗначениеВРеквизитФормы(ЭлектронныйДокумент, "Объект");
	
КонецПроцедуры

&НаКлиенте
Функция ОбработатьОтклонениеЭД()
	
	ВозвращаемоеЗначение = Ложь;
	Если НЕ ЭДОтклонен И НЕ ЭлектронныеДокументыСлужебныйВызовСервера.ЭтоСлужебныйДокумент(Объект.Ссылка)
		И МожноОтклонитьЭтотЭД() Тогда
		// Нельзя снять отклонение документа.
		ТекстВопроса = НСтр("ru = 'Для отклонения документа необходимо указать причину отклонения.
		|Отменить отклонение документа в дальнейшем будет невозможно.
		|Отклонить документ?'");
		СписокКнопок = Новый СписокЗначений;
		СписокКнопок.Добавить(КодВозвратаДиалога.Да, "Отклонить");
		СписокКнопок.Добавить(КодВозвратаДиалога.Нет, "Не отклонять");
		Ответ = Вопрос(ТекстВопроса, СписокКнопок, 60,
			КодВозвратаДиалога.Нет, НСтр("ru = 'Отклонение электронного документа...'"));
		Если Ответ = КодВозвратаДиалога.Да Тогда
			ТекстУточнения = "";
			УточнениеВведено = ВвестиСтроку(ТекстУточнения, НСтр("ru = 'Укажите причины отклонения документа'"), , Истина);
			
			Если УточнениеВведено И ЗначениеЗаполнено(ТекстУточнения) Тогда
				Текст = НСтр("ru = '%1, %2:
				|%3'");
				ТекстУточнения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Текст, Объект.Организация,
					ПользователиКлиентСервер.ТекущийПользователь(), ТекстУточнения);
				Если Объект.НаправлениеЭД = ПредопределенноеЗначение("Перечисление.НаправленияЭД.Исходящий")
					// Для входящего документа, необходимо сформировать ЭД Уведомление об уточнении.
					ИЛИ ЭтоПрямойОбмен() // ограничение на прямой обмен
					ИЛИ СформироватьПодписатьЭДУведомлениеОбУточнении(Объект.Ссылка, ТекстУточнения) Тогда
					
					ВозвращаемоеЗначение = Истина;
					ПоместитьТекстУточненияВОбъект(ТекстУточнения);
					Если ЗначениеЗаполнено(СсылкаНаМассивСертификатов) Тогда
						ИзменитьВидимостьДоступность();
					Иначе
						УстановитьВидимостьДоступностьНаКлиенте();
					КонецЕсли;
				КонецЕсли;
			Иначе
				ТекстСообщения = НСтр("ru = 'Для отклонения документа необходимо указать причину отклонения!'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

&НаСервере
Функция ЭтоПрямойОбмен()
	
	СпособОбменаЭД = ОбщегоНазначения.ПолучитьЗначениеРеквизита(Объект.Ссылка.СоглашениеЭД, "СпособОбменаЭД");
	Возврат СпособОбменаЭД <> Перечисления.СпособыОбменаЭД.ЧерезОператораЭДОТакском;
	
КонецФункции

&НаКлиенте
Функция СформироватьПодписатьЭДУведомлениеОбУточнении(СсылкаНаЭД, ТекстУточнения)
	
	МассивЭД = Новый Массив;
	МассивЭД.Добавить(СсылкаНаЭД);
	ВидЭД = ПредопределенноеЗначение("Перечисление.ВидыЭД.УведомлениеОбУточнении");
	
	ЭДСформирован = ЭлектронныеДокументыСлужебныйКлиент.СформироватьПодписатьИОтправитьСлужебныеЭД(МассивЭД, ВидЭД,
		ТекстУточнения);
	
	Возврат ЭДСформирован;
	
КонецФункции

&НаСервере
Процедура УдалитьФайлыНаСервере()
	
	Если ЗначениеЗаполнено(Объект.Ссылка.НомерЭД) Тогда
		УдалитьФайлы(КаталогВременныхФайлов() + Объект.Ссылка.НомерЭД);
	КонецЕсли;
	УдалитьФайлы(ИмяФайла);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьКоличествоНеустановленныхПодписей()
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ЭДПрисоединенныеФайлыЭлектронныеЦифровыеПодписи.Отпечаток
	               |ПОМЕСТИТЬ УстановленныеПодписи
	               |ИЗ
	               |	Справочник.ЭДПрисоединенныеФайлы.ЭлектронныеЦифровыеПодписи КАК ЭДПрисоединенныеФайлыЭлектронныеЦифровыеПодписи
	               |ГДЕ
	               |	ЭДПрисоединенныеФайлыЭлектронныеЦифровыеПодписи.Ссылка = &СсылкаНаЭД
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СоглашенияОбИспользованииЭДСертификатыПодписейОрганизации.Сертификат) КАК КоличествоНеобходимыхПодписей
	               |ИЗ
	               |	Справочник.СоглашенияОбИспользованииЭД.СертификатыПодписейОрганизации КАК СоглашенияОбИспользованииЭДСертификатыПодписейОрганизации
	               |ГДЕ
	               |	НЕ СоглашенияОбИспользованииЭДСертификатыПодписейОрганизации.Сертификат.Отпечаток В
	               |				(ВЫБРАТЬ
	               |					УстановленныеПодписи.Отпечаток
	               |				ИЗ
	               |					УстановленныеПодписи КАК УстановленныеПодписи)
	               |	И СоглашенияОбИспользованииЭДСертификатыПодписейОрганизации.Ссылка = &СоглашениеЭД";
	Запрос.УстановитьПараметр("СсылкаНаЭД", Объект.Ссылка);
	Запрос.УстановитьПараметр("СоглашениеЭД", Объект.Ссылка.СоглашениеЭД);
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	РезультатЗапроса.Следующий();
	
	Возврат РезультатЗапроса.КоличествоНеобходимыхПодписей;
	
КонецФункции

&НаСервере
Функция ЭДОтклонен()
	
	ЭДОтклонен = (Объект.Ссылка.СтатусЭД = Перечисления.СтатусыЭД.Отклонен
					ИЛИ Объект.Ссылка.СтатусЭД = Перечисления.СтатусыЭД.ОтклоненПолучателем
					ИЛИ Объект.Ссылка.СтатусЭД = Перечисления.СтатусыЭД.ОтклоненБанком
					ИЛИ Объект.Ссылка.СтатусЭД = Перечисления.СтатусыЭД.ОшибкаПередачи
					ИЛИ Объект.Ссылка.СтатусЭД = Перечисления.СтатусыЭД.ОтказанАБС
					ИЛИ Объект.Ссылка.СтатусЭД = Перечисления.СтатусыЭД.ОтвергнутБанком
					ИЛИ Объект.Ссылка.СтатусЭД = Перечисления.СтатусыЭД.ЭЦПНеВерна
					ИЛИ Объект.Ссылка.СтатусЭД = Перечисления.СтатусыЭД.ОшибкаЭкспорта
					ИЛИ Объект.Ссылка.СтатусЭД = Перечисления.СтатусыЭД.ОшибкаРеквизитов);
	Возврат ЭДОтклонен
	
КонецФункции

&НаСервере
Функция СкрытьОбластьТабличногоДокумента(ИмяОбласти)
	
	// Находим область дополнительных данных и скрываем ее.
	// Если область ДД имеет левую и/или правую границы, то это вертикальная область, иначе горизонтальная область.
	// Горизонтальная область выводится внизу таблицы, поэтому находим шапку горизонтальной области ДД и скрываем строки
	// от шапки ДД до конца всей таблицы.
	// Вертикальная область присоединяется к основной таблице данных, при этом, если таблица не помещается на одной странице,
	// то именуемая область будет присутствовать только на последней странице. В этом случае алгоритм определения скрываемой
	// области следующий:
	// 1. находим именованную область ДД (например ОбластьДД).
	// 2. вычисляем верхнюю границу области ДД: находим область шапки, следующая строка за областью шапки - будет верхней
	//  строкой области ДД, поэтому берем низ области шапки + 1.
	// 3. т.к. доп.данные присоединяются к основным данным справа, то в качестве нижней границы можно взять высоту табличного документа.
	//
	// Если область "Шапка" не существует, то область ДД может скрываться некорректно/неполностью.
	ЕстьОбластьДД = ТабличныйДокументФормы.Области.Найти(ИмяОбласти);
	Если ЕстьОбластьДД <> Неопределено Тогда
		ОбластьДДТаблицы = ТабличныйДокументФормы.Область(ИмяОбласти);
		ОбластьШапки = ТабличныйДокументФормы.Область("Шапка");
		Верх = ?(ОбластьДДТаблицы.Лево = 0 И ОбластьДДТаблицы.Право = 0 ИЛИ ОбластьШапки = Неопределено,
			ОбластьДДТаблицы.Верх, ОбластьШапки.Низ + 1);
		УдаляемаяОбласть = ТабличныйДокументФормы.Область(Верх, ОбластьДДТаблицы.Лево,
			ТабличныйДокументФормы.ВысотаТаблицы, ОбластьДДТаблицы.Право);
		ТабличныйДокументФормы.УдалитьОбласть(УдаляемаяОбласть);
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура СкрытьДопДанные(Скрыть)
	
	Если Скрыть Тогда
		СкрытьОбластьТабличногоДокумента("ОбластьДД");
		СкрытьОбластьТабличногоДокумента("ОбластьДДСЭЦП");
		СкрытьОбластьТабличногоДокумента("ОбластьДДБезЭЦП");
		СкрытьОбластьТабличногоДокумента("ДопДанныеШапки_Шапка");
	Иначе
		ТабличныйДокументФормы = ИсходныйТабличныйДокумент.ПолучитьОбласть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОпределитьНаличиеДопДанныхСкрытьФлаг()
	
	СсылкаНаЭД = Объект.Ссылка;
	
	Если СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.АктЗаказчик 
		ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.АктИсполнитель
		ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.ТОРГ12Покупатель
		ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.ТОРГ12Продавец
		ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.СчетФактура
		ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.КорректировочныйСчетФактура
		ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиОтправитель
		ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиПолучатель Тогда
		
		ЕстьДопДанные = Истина;
	Иначе
		
		ЕстьДопДанные = Ложь;
	КонецЕсли;
	Элементы.ОтключитьВыводДопДанных.Видимость = ЕстьДопДанные;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеФормы(ЗначениеВозврата)
	
	Если НЕ ЭтаФорма.Объект = Неопределено И ЗначениеЗаполнено(ЗначениеВозврата) Тогда

		Если ТипЗнч(ЭтаФорма) = Тип("УправляемаяФорма") Тогда
			ДанныеФормы = ЭтаФорма.Объект;
		Иначе
			ДанныеФормы = ЭтаФорма.Объект.ДокументОбъект;
		КонецЕсли;
		
		ЭлектронныеДокументыСлужебныйВызовСервера.ЗаполнитьИсточник(ДанныеФормы, ЗначениеВозврата);
		
		Если ТипЗнч(ЭтаФорма) = Тип("УправляемаяФорма") Тогда
			КопироватьДанныеФормы(ДанныеФормы, ЭтаФорма.Объект);
		Иначе
			ЭтаФорма.Объект.ДокументОбъект = ДанныеФормы;
		КонецЕсли;

	КонецЕсли;
	
	ТекстСостоянияВывод = НСтр("ru = 'Документ перезаполнен.'");
	ТекстЗаголовка      = НСтр("ru = 'Обмен электронными документами'");
	ПоказатьОповещениеПользователя(ТекстЗаголовка, , ТекстСостоянияВывод);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьБанковскиеДокументыСбербанка(ЭД)
	
	КоличествоВыгруженныхДокументов = 0;
	
	МассивФайловСбербанка = МассивФайловСбербанка(ЭД);
	
	Для Каждого Элемент Из МассивФайловСбербанка Цикл
		ДанныеФайлаДляСохранения = Новый Структура;
		ДанныеФайлаДляСохранения.Вставить("Расширение", "");
		ДанныеФайлаДляСохранения.Вставить("ПолноеНаименование", Элемент.ИмяФайла);
		ДанныеФайлаДляСохранения.Вставить("АдресХранилища", Элемент.СсылкаНаФайл);
		
		ЭлектронныеДокументыКлиент.СохранитьКак(ДанныеФайлаДляСохранения);
		КоличествоВыгруженныхДокументов = КоличествоВыгруженныхДокументов + 1;
	КонецЦикла;
	
	ТекстОповещения = Нстр ("ru = 'Выгружено файлов: (" + КоличествоВыгруженныхДокументов + ")'");
	ПоказатьОповещениеПользователя(ТекстОповещения);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ПерезаполнитьДокумент(Команда)
	
	СопоставлятьНоменклатуруПередЗаполнениемДокумента = Ложь;
	ЭлектронныеДокументыКлиентПереопределяемый.СопоставлятьНоменклатуруПередЗаполнениемДокумента(
													СопоставлятьНоменклатуруПередЗаполнениемДокумента);
	
	Если СопоставлятьНоменклатуруПередЗаполнениемДокумента Тогда
		СтруктураПараметров = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьПараметрыФормыСопоставленияНоменклатуры(
																										Объект.Ссылка);
		Если ЗначениеЗаполнено(СтруктураПараметров) Тогда
			ОткрытьФорму(СтруктураПараметров.ИмяФормы, СтруктураПараметров.ПараметрыОткрытияФормы, ЭтаФорма);
			Возврат;
		КонецЕсли;
	Иначе
		ДокументЗагружен = Ложь;
		ПерезаполнитьДокументПоЭД(ДокументЗагружен);
		
		Если ДокументЗагружен Тогда
			Оповестить("ОбновитьСостояниеЭД");
			МассивОповещения = Новый Массив;
			МассивОповещения.Добавить(Объект.ВладелецФайла);
			Оповестить("ОбновитьДокументИБПослеЗаполнения", МассивОповещения);
			
			Если Не СопоставлятьНоменклатуруПередЗаполнениемДокумента Тогда
				СтруктураПараметров = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьПараметрыФормыСопоставленияНоменклатуры(
																												Объект.Ссылка);
				Если ЗначениеЗаполнено(СтруктураПараметров) Тогда
					ЗначениеВозврата = ОткрытьФормуМодально(СтруктураПараметров.ИмяФормы,
															СтруктураПараметров.ПараметрыОткрытияФормы,
															ЭтаФорма);
					ОбновитьДанныеФормы(ЗначениеВозврата);
				КонецЕсли;
			КонецЕсли;
			ТекстСостоянияВывод = НСтр("ru = 'Документ перезаполнен.'");
			ТекстЗаголовка = НСтр("ru = 'Обмен электронными документами'");
			ПоказатьОповещениеПользователя(ТекстЗаголовка, , ТекстСостоянияВывод);
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отклонить(Команда)
	
	Если ОбработатьОтклонениеЭД() Тогда
		ЭДОтклонен = Истина;
		ИзменитьСтатусОтклонить();
		Оповестить("ОбновитьСостояниеЭД");
	КонецЕсли;
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьДокумент(Команда)
	
	ЭтаФорма.Модифицированность = Ложь;
	ТекстПредупреждения = НСтр("ru = 'Внимание! Не рекомендуется выбирать документ отражения в учете вручную. Продолжить?'");
	Ответ = Вопрос(ТекстПредупреждения, РежимДиалогаВопрос.ДаНет, ,КодВозвратаДиалога.Нет);
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	ВыбЗнач = Объект.ВладелецФайла;
	ОписаниеТипов = Новый ОписаниеТипов();
	
	Если ВвестиЗначение(ВыбЗнач, "Укажите документ отражения в учете") Тогда
		ПерепривязатьЭлектронныйДокумент(ВыбЗнач);
		Оповестить("ОбновитьСостояниеЭД");
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьЭДНаДиск(Команда)
	
	Если ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн")
		И (Объект.ВидЭД = ПредопределенноеЗначение("Перечисление.ВидыЭД.ПлатежноеПоручение")
			ИЛИ Объект.ВидЭД = ПредопределенноеЗначение("Перечисление.ВидыЭД.ЗапросВыписки")
			ИЛИ Объект.ВидЭД = ПредопределенноеЗначение("Перечисление.ВидыЭД.ВыпискаБанка")) Тогда
		СохранитьБанковскиеДокументыСбербанка(Объект.Ссылка);
		Возврат;
	КонецЕсли;
	
	ПрисоединенныйФайл = ЭтаФорма.Объект.Ссылка;
	
	ДанныеФайла = ПрисоединенныеФайлыСлужебныйВызовСервера.ПолучитьДанныеФайла(ПрисоединенныйФайл, УникальныйИдентификатор);
	ПолноеИмяФайла = ДанныеФайла.ИмяФайла;
	
	Если НЕ ЭтоПрямойОбмен() И Найти(ДанныеФайла.Расширение, "xml") > 0 И ПолучитьДанныеФайлаОператора(ДанныеФайла) Тогда
		Возврат;
	КонецЕсли;
	

	ДанныеФайлаДляСохранения = Новый Структура;

	ДанныеФайлаДляСохранения.Вставить("Расширение", ДанныеФайла.Расширение);
	ДанныеФайлаДляСохранения.Вставить("ПолноеНаименование", ДанныеФайла.Наименование);
	ДанныеФайлаДляСохранения.Вставить("АдресХранилища", ДанныеФайла.СсылкаНаДвоичныеДанныеФайла);
	
	ЭлектронныеДокументыКлиент.СохранитьКак(ДанныеФайлаДляСохранения);
	
	Настройка = ЭлектроннаяЦифроваяПодписьКлиент.ПолучитьПерсональныеНастройкиРаботыСЭЦП().ДействияПриСохраненииСЭЦП;
	Если Настройка = "Спрашивать" Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Объект",                  ПрисоединенныйФайл);
		ПараметрыФормы.Вставить("УникальныйИдентификатор", УникальныйИдентификатор);
		
		МассивСтруктурПодписей = ОткрытьФормуМодально("ОбщаяФорма.ВыборПодписей", ПараметрыФормы);
		
	ИначеЕсли Настройка = "СохранятьВсеПодписи" Тогда
		МассивСтруктурПодписей = ПрисоединенныеФайлыСлужебныйВызовСервера.ПолучитьВсеПодписи(ПрисоединенныйФайл,
			УникальныйИдентификатор);
	КонецЕсли;
	
	Если ТипЗнч(МассивСтруктурПодписей) = Тип("Массив") И МассивСтруктурПодписей.Количество() > 0 Тогда
		
		Для Каждого СтруктураПодписи Из МассивСтруктурПодписей Цикл
			ИмяФайлаПодписи = СтруктураПодписи.ИмяФайлаПодписи;
			Если НЕ ЗначениеЗаполнено(ИмяФайлаПодписи) Тогда
				ИмяФайлаПодписи = ДанныеФайла.Наименование + ".p7s";
			КонецЕсли;
			// Сохраним Файл из БД на диск

			ДанныеФайлаДляСохранения = Новый Структура;
			ДанныеФайлаДляСохранения.Вставить("Расширение", "");
			ДанныеФайлаДляСохранения.Вставить("ПолноеНаименование", ИмяФайлаПодписи);
			ДанныеФайлаДляСохранения.Вставить("АдресХранилища", СтруктураПодписи.АдресПодписи);
			
			ЭлектронныеДокументыКлиент.СохранитьКак(ДанныеФайлаДляСохранения);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьДанныеФайлаОператора(ДанныеФайла)
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла(ДанныеФайла.Расширение);
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(ДанныеФайла.СсылкаНаДвоичныеДанныеФайла);
	ДвоичныеДанные.Записать(ИмяВременногоФайла);
	
	ЧтениеХМЛ = Новый ЧтениеXML;
	ЧтениеХМЛ.ОткрытьФайл(ИмяВременногоФайла);
	
	Результат = Ложь;
	Пока ЧтениеХМЛ.Прочитать() Цикл
		Если ЧтениеХМЛ.ТипУзла = ТипУзлаXML.НачалоЭлемента И ЧтениеХМЛ.ЛокальноеИмя = "СвОЭДОтпр" Тогда
			Идентификатор = ЧтениеХМЛ.ПолучитьАтрибут("ИдЭДО");
			
			Если Не Найти(Идентификатор, "2AL") > 0 Тогда
				
				Результат = Истина;
			Иначе
				Прервать;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ПечатьСЭлектроннымиПодписями(Команда)
	
	ТабДок = ТабличныйДокументФормы;
	ТабДок.Напечатать();
	Если ТабДок.ВысотаТаблицы > 0 Тогда
		ТабДокЭЦП = ПечатнаяФормаСЭЦП();
		Если НЕ ТабДокЭЦП = Неопределено Тогда
			ТабДокЭЦП.Напечатать();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДоверятьЭтомуСертификату(Команда)
	
	ДобавитьСертификатВДоверенные(Элементы.ЭЦП.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКЖурналуСобытийЭД(Команда)
	
	ПараметрыФормы = Новый Структура;
	
	Отбор = Новый Структура;
	Отбор.Вставить("ПрисоединенныйФайл", Объект.Ссылка);
	
	ПараметрыФормы.Вставить("Отбор", Отбор);
	ПараметрыФормы.Вставить("РежимОткрытияОкна", РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	ОткрытьФорму("РегистрСведений.ЖурналСобытийЭД.ФормаСписка", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСертификат(Команда)
	
	Если Элементы.ЭЦП.ТекущиеДанные <> Неопределено Тогда
		ПоказатьСертификат(Элементы.ЭЦП.ТекущиеДанные.НомерСтроки, Элементы.ЭЦП.ТекущиеДанные.Отпечаток);
	Иначе
		ОчиститьСообщения();
		ТекстОшибки = НСтр("ru = 'Выберите сертификат в списке установленных подписей.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПодписи(Команда)
	
	ОчиститьСообщения();
	
	Если ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн")
		И (Объект.ВидЭД = ПредопределенноеЗначение("Перечисление.ВидыЭД.ПлатежноеПоручение")
			ИЛИ Объект.ВидЭД = ПредопределенноеЗначение("Перечисление.ВидыЭД.ЗапросВыписки")
			ИЛИ Объект.ВидЭД = ПредопределенноеЗначение("Перечисление.ВидыЭД.ВыпискаБанка")) Тогда
		#Если ВебКлиент Тогда
			ТекстСообщения = НСтр("ru = 'Невозможна проверка подписей из WEB-браузера'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		#Иначе
			ЭлектронныеДокументыСлужебныйКлиент.ОпределитьСтатусыПодписейСбербанка(Объект.Ссылка);
		#КонецЕсли
	Иначе
		Если ЭлектронныеДокументыСлужебныйВызовСервера.ВыполнятьКриптооперацииНаСервере() Тогда
			ЭлектронныеДокументыСлужебныйВызовСервера.ОпределитьСтатусыПодписей(Объект.Ссылка, Истина);
		Иначе
			ЭлектронныеДокументыСлужебныйКлиент.ОпределитьСтатусыПодписей(Объект.Ссылка);
		КонецЕсли;
	КонецЕсли;
	
	ЭтаФорма.ОбновитьОтображениеДанных();
	ЗаполнитьТаблицуЭЦП();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьПодтверждениеЭД(Команда)
	
	ЭлектронныеДокументыСлужебныйКлиент.ОтправитьПодтверждениеЭД(Объект.ВладелецФайла, Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура УтвердитьЭД(Команда)
	
	НовыйЭД = Неопределено;
	ЭлектронныеДокументыСлужебныйКлиент.УтвердитьЭД(Объект.ВладелецФайла, Объект.Ссылка, , НовыйЭД);
	ЭтаФорма.ОбновитьОтображениеДанных();

	Если НовыйЭД <> Неопределено Тогда
		
		ОткрытьФорму("Справочник.ЭДПрисоединенныеФайлы.Форма.ФормаПросмотраЭД", Новый Структура("СсылкаНаОбъект", НовыйЭД),
			ЭтаФорма.ВладелецФормы, ЭтаФорма.УникальныйИдентификатор, ЭтаФорма.Окно);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЭД(Команда)
	
	ОчиститьСообщения();
	
	ЭлектронныеДокументыКлиент.СформироватьПодписатьОтправитьЭД(Объект.ВладелецФайла, Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьПовторно(Команда)
	
	ЭлектронныеДокументыКлиент.ОтправитьПовторноЭД(Объект.ВладелецФайла, Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодписатьЭД(Команда)
	
	ЭлектронныеДокументыКлиент.СформироватьПодписатьОтправитьЭД(Объект.ВладелецФайла, Объект.Ссылка);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ПОЛЕЙ ФОРМЫ

&НаКлиенте
Процедура ТекстДокументИБНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьЗначение(Объект.ВладелецФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура ПричиныОтклоненияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОткрытьЗначение(Элемент.ТекущиеДанные.ПричинаОтклонения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтключитьВыводДопДанныхПриИзменении(Элемент)
	
	СкрытьДопДанные(ОтключитьВыводДопДанных);
	ЭтаФорма.ОбновитьОтображениеДанных();
	
КонецПроцедуры

&НаКлиенте
Процедура КвитанцияНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ТипЗнч(Квитанция) = Тип("СправочникСсылка.ЭДПрисоединенныеФайлы") Тогда
		ПараметрыФормы = Новый Структура("СсылкаНаОбъект", Квитанция);
		ОткрытьФорму("Справочник.ЭДПрисоединенныеФайлы.Форма.ФормаПросмотраЭД", ПараметрыФормы);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ПОЛЕЙ ТАБЛИЦЫ ЭЛЕКТРОННЫЕ ЦИФРОВЫЕ ПОДПИСИ

&НаКлиенте
Процедура ЭЦППриАктивизацииСтроки(Элемент)
	
	Если Элементы.ЭЦП.ТекущиеДанные <> Неопределено Тогда
		Элементы.ДоверятьСертификату.Доступность = Элементы.ЭЦП.ТекущиеДанные.ОтсутствуетВСписке;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЭЦПВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Найти(Поле.Имя, "ЭЦПКомуВыданСертификат")>0 Тогда
		ДобавитьСертификатВДоверенные(Элемент.ТекущиеДанные);
		Если Элемент.ТекущиеДанные <> Неопределено И НЕ Элемент.ТекущиеДанные.ОтсутствуетВСписке Тогда
			ПоказатьСертификат(Элемент.ТекущиеДанные.НомерСтроки, Элемент.ТекущиеДанные.Отпечаток);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
		
	Если Параметры.Свойство("СсылкаНаОбъект") Тогда
		ОбъектСправочника = Параметры.СсылкаНаОбъект.ПолучитьОбъект();
		ЗначениеВРеквизитФормы(ОбъектСправочника, "Объект");
	КонецЕсли;
	
	Если Параметры.Свойство("СсылкаНаМассивСертификатов") Тогда
		СсылкаНаМассивСертификатов = Параметры.СсылкаНаМассивСертификатов;
	КонецЕсли;
	
	ПрограммаБанка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.СоглашениеЭД, "ПрограммаБанка");
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ЭДОтклонен = ЭДОтклонен();
		ТребуетсяПодпись = НеобходимоПодписать();
		ОбновитьСтатусЭД();
		ЭтаФорма.Заголовок = ЭлектронныеДокументыСлужебный.ПолучитьПредставлениеЭД(Объект.Ссылка);
		ЗаполнитьТаблицуЭЦП();
		
		Если НЕ Отказ Тогда
			ВыполнитьПросмотрЭДИзБДСервер(Отказ);
		КонецЕсли;
	КонецЕсли;
	
	ИзменитьВидимостьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если НЕ Отказ И ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Если ЭтоАдресВременногоХранилища(АдресФайлаВХранилище) Тогда
			#Если ВебКлиент Тогда
				ПутьКФайлуПросмотра = АдресФайлаВХранилище;
			#Иначе
				ПутьКФайлуПросмотра = ПолучитьИмяВременногоФайла(РасширениеФайла);
				ДДФайла = ПолучитьИзВременногоХранилища(АдресФайлаВХранилище);
				ДДФайла.Записать(ПутьКФайлуПросмотра);
			#КонецЕсли
			Если Найти("HTML PDF DOCX XLSX", ВРег(РасширениеФайла)) > 0 Тогда
				Элементы.ГруппаСодержимоеДокумента.ТекущаяСтраница = Элементы.СтраницаДругойФормат;
			Иначе
				#Если НЕ ВебКлиент Тогда
					ЗапуститьПриложение(ПутьКФайлуПросмотра);
				#КонецЕсли
				Отказ = Истина;
				Возврат;
			КонецЕсли;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(СсылкаНаМассивСертификатов) Тогда
			УстановитьВидимостьДоступностьНаКлиенте();
		КонецЕсли;
	Иначе
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	УдалитьФайлыНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновитьСостояниеЭД" Тогда
		Если Не ЗначениеЗаполнено(СсылкаНаМассивСертификатов) Тогда
			Попытка
				Если ЭлектронныеДокументыСлужебныйВызовСервера.ВыполнятьКриптооперацииНаСервере() Тогда
					МассивСтруктурСертификатов = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьМассивСтруктурСертификатов(Истина);
				Иначе
					МассивСтруктурСертификатов = ЭлектроннаяЦифроваяПодписьКлиент.ПолучитьМассивСтруктурСертификатов(Истина);
				КонецЕсли;
				СсылкаНаМассивСертификатов = ПоместитьВоВременноеХранилище(МассивСтруктурСертификатов, УникальныйИдентификатор);
			Исключение
			КонецПопытки;
		КонецЕсли;
		ВыполнитьОбработкуОповещенияНаСервере();
		ЭтаФорма.ОбновитьОтображениеДанных();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	СкрытьДопДанные(ОтключитьВыводДопДанных);
	
КонецПроцедуры

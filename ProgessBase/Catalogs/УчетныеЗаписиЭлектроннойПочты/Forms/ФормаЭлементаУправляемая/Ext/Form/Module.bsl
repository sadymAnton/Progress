////////////////////////////////////////////////////////////////////////////////
// СЕКЦИЯ ОБРАБОТЧИКОВ СОБЫТИЙ ФОРМЫ И ЭЛЕМЕНТОВ ФОРМЫ
//

// Обработчик события "при создании на сервере" формы
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Объект.Пароль) Тогда
		ЗапомнитьПароль = Истина;
	КонецЕсли;
	
	Если Объект.Ссылка.Пустая() Тогда
		Объект.ИспользоватьДляОтправки = Истина;
		Объект.ИспользоватьДляПолучения = Истина;
	КонецЕсли;
	
КонецПроцедуры

// Обработчик события "перед записью на сервере" формы
//
&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект)
	
	Если НЕ ЗапомнитьПароль Тогда
		ТекущийОбъект.Пароль = "";
	КонецЕсли;
	
	Если НЕ Объект.ТребуетсяSMTPАутентификация Тогда
		Объект.ЛогинSMTP = "";
		Объект.ПарольSMTP = "";
	КонецЕсли;
	
КонецПроцедуры

// Обработчик события нажатия на кнопку "Дополнительно" формы
//
&НаКлиенте
Процедура ДополнительныеНастройкиВыполнить()
	
	УчетнаяЗаписьСтруктура = Новый Структура;
	УчетнаяЗаписьСтруктура.Вставить("ВремяОжиданияСервера",					Объект.ВремяОжиданияСервера);
	УчетнаяЗаписьСтруктура.Вставить("ОставлятьКопииСообщенийНаСервере",		Объект.ОставлятьКопииСообщенийНаСервере);
	УчетнаяЗаписьСтруктура.Вставить("УдалятьПисьмаССервераЧерез",			Объект.УдалятьПисьмаССервераЧерез);
	УчетнаяЗаписьСтруктура.Вставить("КоличествоДнейУдаленияПисемССервера",	Объект.КоличествоДнейУдаленияПисемССервера);
	УчетнаяЗаписьСтруктура.Вставить("ЛогинSMTP",							Объект.ЛогинSMTP);
	УчетнаяЗаписьСтруктура.Вставить("ПарольSMTP",							Объект.ПарольSMTP);
	УчетнаяЗаписьСтруктура.Вставить("ПортPOP3",								Объект.ПортPOP3);
	УчетнаяЗаписьСтруктура.Вставить("ПортSMTP",								Объект.ПортSMTP);
	УчетнаяЗаписьСтруктура.Вставить("ТребуетсяSMTPАутентификация",			Объект.ТребуетсяSMTPАутентификация);
	
	ПараметрыВызова = Новый Структура("Ссылка, УчетнаяЗаписьСтруктура, ТолькоПросмотр", Объект.Ссылка, УчетнаяЗаписьСтруктура, ТолькоПросмотр);
	
	ОткрытьФорму("Справочник.УчетныеЗаписиЭлектроннойПочты.Форма.ДополнительныеСвойстваУчетнойЗаписиУправляемая", ПараметрыВызова);
	
КонецПроцедуры

// Обрабатывает оповещение, от формы настройки дополнительных свойств учетной записи,
// содержащее некоторые настройки учетной записи.
//
&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "УстановкаДополнительныхПараметровУчетнойЗаписи" И Источник = Объект.Ссылка Тогда
		Объект.ВремяОжиданияСервера					= Параметр.ВремяОжиданияСервера;
		Объект.ОставлятьКопииСообщенийНаСервере 	= Параметр.ОставлятьКопииСообщенийНаСервере;
		Объект.УдалятьПисьмаССервераЧерез			= Параметр.УдалятьПисьмаССервераЧерез;
		Объект.КоличествоДнейУдаленияПисемССервера	= Параметр.КоличествоДнейУдаленияПисемССервера;
		Объект.ЛогинSMTP							= Параметр.ЛогинSMTP;
		Объект.ПарольSMTP							= Параметр.ПарольSMTP;
		Объект.ПортPOP3								= Параметр.ПортPOP3;
		Объект.ПортSMTP								= Параметр.ПортSMTP;
		Объект.ТребуетсяSMTPАутентификация			= Параметр.ТребуетсяSMTPАутентификация;
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

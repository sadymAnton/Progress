
Процедура ПередЗаписью(Отказ)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	//Проверим чтобы были заполнены контрагенты, и чтобы контрагент был использован только в одном назначении
	Если Контрагенты.Количество() = 0 Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Заполните данные по контрагентам", Отказ);
	Иначе
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	АП_НазначениеРезервовКонтрагенты.Ссылка
		               |ИЗ
		               |	Справочник.АП_НазначениеРезервов.Контрагенты КАК АП_НазначениеРезервовКонтрагенты
		               |ГДЕ
		               |	АП_НазначениеРезервовКонтрагенты.Ссылка.Отделение = &Отделение
		               |	И НЕ АП_НазначениеРезервовКонтрагенты.Ссылка = &Ссылка
		               |	И АП_НазначениеРезервовКонтрагенты.Контрагент = &Контрагент";
		Запрос.УстановитьПараметр("Отделение", Отделение);
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		Для каждого СтрКонтрагент Из Контрагенты Цикл
		
			Запрос.УстановитьПараметр("Контрагент", СтрКонтрагент.Контрагент);	
			
			Результат = Запрос.Выполнить().Выбрать();
			Если Результат.Следующий() Тогда
				//начало изменений Ожиганов 15.04.2015 37867 
				//ОбщегоНазначения.СообщитьОбОшибке("Контрагент/Склад: " + СокрЛП(СтрКонтрагент.Контрагент) + " уже указан в назначении резерва " + СокрЛП(Результат.Ссылка), Отказ);
				Сообщить("Контрагент/Склад: " + СокрЛП(СтрКонтрагент.Контрагент) + " уже указан в назначении резерва " + СокрЛП(Результат.Ссылка));
				//конец изменений 
			КонецЕсли;
		
		КонецЦикла; 
	КонецЕсли;
	
	Если Не Отказ И Не ЭтоНовый() Тогда
		Если Не ФиксированныйРезерв = Ссылка.ФиксированныйРезерв Тогда
			Отказ = Не МожноМенятьВидНазначения();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Функция МожноМенятьВидНазначения(ВыдаватьСообщение = Истина) Экспорт
	
	Если ЭтоНовый() Тогда
		Возврат Истина;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	               |	КП_ФиксированныеРезервыПоНазначениюРезервов.НазначениеРезерва
	               |ИЗ
	               |	РегистрСведений.КП_ФиксированныеРезервыПоНазначениюРезервов КАК КП_ФиксированныеРезервыПоНазначениюРезервов
	               |ГДЕ
	               |	КП_ФиксированныеРезервыПоНазначениюРезервов.НазначениеРезерва = &НазначениеРезерва
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ ПЕРВЫЕ 1
	               |	ТоварыВРезервеНаСкладах.АП_НазначениеРезерва
	               |ИЗ
	               |	РегистрНакопления.ТоварыВРезервеНаСкладах КАК ТоварыВРезервеНаСкладах
	               |ГДЕ
	               |	ТоварыВРезервеНаСкладах.АП_НазначениеРезерва = &НазначениеРезерва";
				   
     Запрос.УстановитьПараметр("НазначениеРезерва", Ссылка);
	 
	 Результат = Запрос.Выполнить().Пустой();
	 Если Результат Тогда
		 Возврат Истина;
	 Иначе
		 Если ВыдаватьСообщение Тогда
			 ОбщегоНазначения.СообщитьОбОшибке("Нельзя менять вид назначения резерва, потому что по нему уже выполнялось резервирование");
		 КонецЕсли;
		 
		 Если РольДоступна("АП_Администратор") Тогда
			 Возврат Истина;
		 Иначе
			 Возврат Ложь;
		 КонецЕсли;
	 Конецесли;
КонецФункции
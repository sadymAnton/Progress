
//++ Spl_Апроф 06.02.2015 (k.russkih@a-prof.ru) Добавил ресурс "КоличествоПоЕдиницеИзмеренияКонтрагента" для загрузки заказов в разных единицах

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого Запись Из ЭтотОбъект Цикл
		
		Если Запись.Номенклатура.Пустая() Тогда
			Отказ = Истина;
			Сообщить("Нельзя записывать с незаполненным значением номенклатуры.");
			Продолжить;
		КонецЕсли; 
		
		Если Запись.Номенклатура.ЭтоГруппа Тогда
			Отказ = Истина;
			Сообщить("Нельзя выбирать группу номенклатуры в качестве номенклатуры контрагента.");
			Продолжить;
		КонецЕсли;
		
	КонецЦикла; 
	
КонецПроцедуры


Процедура ПриЗаписи(Отказ, Замещение)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(1) КАК Количество
	|ИЗ
	|	РегистрСведений.НоменклатураКонтрагентов КАК НоменклатураКонтрагентов
	|ГДЕ
	|	НоменклатураКонтрагентов.Контрагент = &Контрагент
	|	И НоменклатураКонтрагентов.КодНоменклатурыКонтрагента = &Код
	|	И НоменклатураКонтрагентов.АртикулНоменклатурыКонтрагента = &Артикул
	|	И НоменклатураКонтрагентов.ШтрихКодНоменклатурыКонтрагента = &Штрихкод
	|	И НоменклатураКонтрагентов.Отделение = &Отделение
	|	И НоменклатураКонтрагентов.Приоритет = &Приоритет");
	
	Для Каждого Запись Из ЭтотОбъект Цикл
		
		Запрос.УстановитьПараметр("Контрагент",	Запись.Контрагент);
		Запрос.УстановитьПараметр("Код", 		Запись.КодНоменклатурыКонтрагента);
		Запрос.УстановитьПараметр("Артикул", 	Запись.АртикулНоменклатурыКонтрагента);
		Запрос.УстановитьПараметр("Штрихкод", 	Запись.ШтрихКодНоменклатурыКонтрагента);
		Запрос.УстановитьПараметр("Приоритет", 	Запись.Приоритет);
		Запрос.УстановитьПараметр("Отделение",	Запись.Отделение);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Выборка.Следующий();
		
		Если Выборка.Количество > 1 Тогда
			#Если Клиент Тогда
			Сообщить("Нельзя регистрировать больше одной записи по сочетанию <код контрагента> : <артикул контрагента> : <штрихкод контрагента> : <отделение> : <приоритет>!");
			#КонецЕсли
			Отказ = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры


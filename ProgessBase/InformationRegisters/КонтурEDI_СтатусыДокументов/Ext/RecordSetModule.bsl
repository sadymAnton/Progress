
Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	//для нового интерфейса необходима правильная установка флага "Архив" в сообщениях.
	//в случае с RECADV сообщение по логике не может считаться архивным до тех пор, пока не приняты расхождения приемки, если они были.
	//поэтому пока что обработаем случай "RECADV в статусе Загружен, а документ в статусе Загружен частично"
	
	НЗ_Проверка = РегистрыСведений.КонтурEDI_СтатусыДокументов.СоздатьНаборЗаписей();
	Если ЭтотОбъект.Отбор.Документ.Использование Тогда
		НЗ_Проверка.Отбор.Документ.Установить(ЭтотОбъект.Отбор.Документ.Значение);
	КонецЕсли;	
	Если ЭтотОбъект.Отбор.ИмяСтатуса.Использование Тогда
		НЗ_Проверка.Отбор.ИмяСтатуса.Установить(ЭтотОбъект.Отбор.ИмяСтатуса.Значение);
	КонецЕсли;	
	
	НЗ_Проверка.Прочитать();
	
	Для Каждого Стр Из НЗ_Проверка Цикл
		Если НЕ ЗначениеЗаполнено(Стр.Документ) Тогда
			Продолжить;
		КонецЕсли;
		
		ТипСообщения = Неопределено;
		
		Если Стр.ИмяСтатуса = "Приемка" Тогда
			ТипСообщения = "RECADV"
		ИначеЕсли (Стр.ИмяСтатуса = "Заказ" И Стр.Статус = "Уточнен, принят") Тогда
			ТипСообщения = "ORDRSP";
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ТипСообщения) Тогда
			Запрос = Новый Запрос(
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	КонтурEDI_Сообщения.Ссылка
			|ИЗ
			|	Справочник.КонтурEDI_Сообщения КАК КонтурEDI_Сообщения
			|ГДЕ
			|	КонтурEDI_Сообщения.Документ = &Документ
			|	И КонтурEDI_Сообщения.Направление = ""Входящее""
			|	И КонтурEDI_Сообщения.ТипСообщения = &ТипСообщения"
			);
			Запрос.УстановитьПараметр("Документ",Стр.Документ);
			Запрос.УстановитьПараметр("ТипСообщения",ТипСообщения);
			Выб = Запрос.Выполнить().Выбрать();
			Если Выб.Следующий() Тогда
				Выб.Ссылка.ПолучитьОбъект().Записать();
				//в этом случае у сообщения снимется/установится статус "Архив"
			КонецЕсли;	
		КонецЕсли;	
	КонецЦикла;

	
КонецПроцедуры

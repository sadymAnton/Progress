////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ
//

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	СписокКодовВидовДеятельностиФизЛиц = КонтролируемыеСделкиПовтИсп.ПолучитьКодыВидовДеятельностиФизЛиц();
	Элементы.КодВидаДеятельностиФизическогоЛица.СписокВыбора.Очистить();
	Для Каждого Код Из СписокКодовВидовДеятельностиФизЛиц Цикл
		НовыйКод = Элементы.КодВидаДеятельностиФизическогоЛица.СписокВыбора.Добавить();	
		ЗаполнитьЗначенияСвойств(НовыйКод, Код);
	КонецЦикла;
	
	ЮрЛицо = ОбщегоНазначения.ПолучитьЗначениеРеквизита(Запись.Контрагент, "ЮрФизЛицо") = Перечисления.ЮрФизЛицо.ЮрЛицо;
	
	ДоступностьКлючевыхЗаписей = Истина;
	Если Параметры.Свойство("ДоступностьКлючевыхЗаписей") Тогда
		ДоступностьКлючевыхЗаписей = Параметры.ДоступностьКлючевыхЗаписей;
	КонецЕсли;
	
	ЭтаФорма.Элементы.Контрагент.ТолькоПросмотр = НЕ ДоступностьКлючевыхЗаписей;
	
	КонтролируемыеСделки.ЗаполнитьТаблицуОфшоров(СписокОфшоров);

	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Контрагент = Форма.Запись.Контрагент;
	Если Не ЗначениеЗаполнено(Контрагент) Тогда 
		Форма.Элементы.ГруппаСтраницы.Видимость = Ложь;
	Иначе
		Форма.Элементы.ГруппаСтраницы.Видимость = Истина;
	КонецЕсли;
	
	Форма.Элементы.ЗарегистрированВСтранеСЛьготнымНалогообложением.Доступность = (Форма.Запись.СтранаРегистрации <> ПредопределенноеЗначение("Справочник.КлассификаторСтранМира.Россия"));
	
	Если Форма.ЮрЛицо Тогда
		Если Форма.Запись.СтранаРегистрации = ПредопределенноеЗначение("Справочник.КлассификаторСтранМира.Россия") Тогда 
			Форма.Элементы.ГруппаСтраницы.ТекущаяСтраница = Форма.Элементы.ГруппаЮрЛицоРФ;
		Иначе
			Форма.Элементы.ГруппаСтраницы.ТекущаяСтраница = Форма.Элементы.ГруппаЮрЛицоНеРФ;
		КонецЕсли;
	Иначе 
		Форма.Элементы.ГруппаСтраницы.ТекущаяСтраница = Форма.Элементы.ГруппаФизЛицо;
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция ПриИзмененииКонтрагентаНаСервере(Контрагент)
	
	ЮрЛицо = ОбщегоНазначения.ПолучитьЗначениеРеквизита(Контрагент, "ЮрФизЛицо") = Перечисления.ЮрФизЛицо.ЮрЛицо;
	Возврат Новый Структура("ЮрЛицо", ЮрЛицо);
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ
//

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма, ПриИзмененииКонтрагентаНаСервере(Запись.Контрагент));
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СтранаРегистрацииПриИзменении(Элемент)
	
	Если Запись.СтранаРегистрации = ПредопределенноеЗначение("Справочник.КлассификаторСтранМира.Россия") Тогда
		Запись.ЗарегистрированВСтранеСЛьготнымНалогообложением = Ложь;
		Запись.РегистрационныйНомерВСтранеРегистрации = "";
		Запись.РегистрационныйНомерВСтранеРегистрации = "";
	ИначеЕсли ЗначениеЗаполнено(Запись.СтранаРегистрации) Тогда
		Запись.ЗарегистрированВСтранеСЛьготнымНалогообложением = СписокОфшоров.НайтиСтроки(Новый Структура("СтранаРегистрации", Запись.СтранаРегистрации)).Количество()>0;
	КонецЕсли;
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
//

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Ключ.Пустой()
		И ЗначениеЗаполнено(Параметры.ЗначенияЗаполнения) Тогда
		ЗаполнитьЗначенияСвойств(Запись, Параметры.ЗначенияЗаполнения);
	КонецЕсли;
	
	Если Параметры.Ключ.Пустой() Тогда
		ПодготовитьФормуНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПодготовитьФормуНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("УчастникКонтролируемойСделкиЗаписан", Новый Структура("Контрагент, СтранаРегистрации, ЗарегистрированВСтранеСЛьготнымНалогообложением,
		|РегистрационныйНомерВСтранеРегистрации, КодНалогоплательщикаВСтранеРегистрации, КодВидаДеятельностиФизическогоЛица, ФизическоеЛицо, ЮрЛицо",
		Запись.Контрагент, Запись.СтранаРегистрации, Запись.ЗарегистрированВСтранеСЛьготнымНалогообложением, Запись.РегистрационныйНомерВСтранеРегистрации,
		Запись.КодНалогоплательщикаВСтранеРегистрации, Запись.КодВидаДеятельностиФизическогоЛица, Запись.ФизическоеЛицо, ЮрЛицо));
	
КонецПроцедуры


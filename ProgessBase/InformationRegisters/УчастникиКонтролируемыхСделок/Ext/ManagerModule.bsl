
//Получает параметры формы для отрытия записи регистра
//
Функция ПолучитьПараметрыОткрытияФормыЗаписиКонтрагента(Контрагент) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Контрагент) Тогда
		Возврат(Неопределено);
	КонецЕсли;
	
	ОбособленноеПодразделениеКонтрагента = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(Контрагент, "ОбособленноеПодразделение, ГоловнойКонтрагент");
	
	Если ОбособленноеПодразделениеКонтрагента.ОбособленноеПодразделение = Истина Тогда
		ГоловнойКонтрагент = ОбособленноеПодразделениеКонтрагента.ГоловнойКонтрагент;
	Иначе
		ГоловнойКонтрагент = Контрагент;
	КонецЕсли;
	
	ПараметрыКонтрагента = РегистрыСведений.УчастникиКонтролируемыхСделок.СоздатьНаборЗаписей();
	ПараметрыКонтрагента.Отбор.Контрагент.Значение 		= ГоловнойКонтрагент;
	ПараметрыКонтрагента.Отбор.Контрагент.Использование = Истина;
	ПараметрыКонтрагента.Прочитать();
	
	Если ПараметрыКонтрагента.Количество()>0 Тогда
		ПараметрыФормы = Новый Структура("Ключ",
			РегистрыСведений.УчастникиКонтролируемыхСделок.СоздатьКлючЗаписи(Новый Структура("Контрагент", ГоловнойКонтрагент)));
	Иначе
		ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", Новый Структура("Контрагент", ГоловнойКонтрагент));
	КонецЕсли;
	
	ПараметрыФормы.Вставить("ДоступностьКлючевыхЗаписей",Ложь);
	
	Возврат(ПараметрыФормы);
	
КонецФункции

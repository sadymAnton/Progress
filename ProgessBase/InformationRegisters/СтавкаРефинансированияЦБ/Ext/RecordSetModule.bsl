Функция СтавкиРефинансированияУстановленныеЦБ()
	
	ТаблицаСтавок = Новый ТаблицаЗначений();
	// Имена колонок - как в регистре сведений СтавкаРефинансированияЦБ
	ТаблицаСтавок.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата", , , Новый КвалификаторыДаты(ЧастиДаты.Дата)));
	ТаблицаСтавок.Колонки.Добавить("Размер", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(5, 2, ДопустимыйЗнак.Неотрицательный)));
	
	// Данные о ставках - в макете, в виде XML
	УказанияЦБ = РегистрыСведений.СтавкаРефинансированияЦБ.ПолучитьМакет("УказанияЦБ");
	
	// Прочитаем данные XML в таблицу значений
	Чтение = Новый ЧтениеXML;
	Чтение.УстановитьСтроку(УказанияЦБ.ПолучитьТекст());
	
	// Прочитаем первый узел и проверим его
	Если Не Чтение.Прочитать() Тогда
		// Нет никаких данных
		Возврат ТаблицаСтавок;
	ИначеЕсли Чтение.Имя <> "СтавкаРефинансирования" Тогда
		// Нет нужных данных
		Возврат ТаблицаСтавок;
	КонецЕсли;
	
	// Заполним значения в таблице
	Пока Чтение.Прочитать() Цикл
		
		// Читаем элемент Указание
		Если Чтение.ТипУзла <> ТипУзлаXML.НачалоЭлемента Тогда
			Продолжить;
		ИначеЕсли Чтение.Имя <> "Указание" Тогда
			// Ошибка в структуре XML
			Возврат ТаблицаСтавок;
		КонецЕсли;
		
		НоваяСтрока = ТаблицаСтавок.Добавить();
		
		// Читаем аттрибуты Указания
		// Используем неявное преобразование типов
		НоваяСтрока.Период = СтрЗаменить(Чтение.ПолучитьАтрибут("ДатаНачалаПрименения"), "-", "");
		НоваяСтрока.Размер = Чтение.ПолучитьАтрибут("Ставка");
		
	КонецЦикла;
	
	Возврат ТаблицаСтавок;
	
КонецФункции

Процедура ЗагрузитьСтавкиРефинансированияУстановленныеЦБ() Экспорт
	
	// Не используем отбор
	Для Каждого ЭлементОтбора Из Отбор Цикл
		ЭлементОтбора.Использование = Ложь;
	КонецЦикла;
	
	// Запишем значения ставки, установленные ЦБ
	Загрузить(СтавкиРефинансированияУстановленныеЦБ());
	Записать();

КонецПроцедуры

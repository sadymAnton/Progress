////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБЩЕГО НАЗНАЧЕНИЯ

&НаСервереБезКонтекста
Функция ПолучитьПараметрыОткрытияФормыЗаписиКонтрагента(Контрагент)
	
	Возврат(РегистрыСведений.УчастникиКонтролируемыхСделок.ПолучитьПараметрыОткрытияФормыЗаписиКонтрагента(Контрагент));
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьДополнительныеСведения(Организация, Контрагент, Период)
	
	ДополнительныеСведения = Новый Структура("СведенияДействуютПо, ИнформацияОРегистрации");
	
	Запрос = Новый Запрос();
	Запрос.Параметры.Вставить("Организация", Организация);
	Запрос.Параметры.Вставить("Контрагент", Контрагент);
	Запрос.Параметры.Вставить("Период", Период);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	МАКСИМУМ(ВзаимозависимыеЛица.Период) КАК Период
	|ИЗ
	|	РегистрСведений.ВзаимозависимыеЛица КАК ВзаимозависимыеЛица
	|ГДЕ
	|	ВзаимозависимыеЛица.Организация = &Организация
	|	И ВзаимозависимыеЛица.Контрагент = &Контрагент
	|	И ВзаимозависимыеЛица.Период > &Период";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();

	Если Выборка.Период = NULL Тогда
		СведенияДействуютПо = НСтр("ru = ''");
	Иначе
		СведенияДействуютПо = НСтр("ru = 'по %ДатаОкончанияДействия%'");
		СведенияДействуютПо = СтрЗаменить(СведенияДействуютПо, "%ДатаОкончанияДействия%", Формат(НачалоДня(Выборка.Период - 1),"ДФ=dd.MM.yyyy"));
		СведенияДействуютПо = СведенияДействуютПо;
	КонецЕсли;
	ДополнительныеСведения.СведенияДействуютПо = СведенияДействуютПо;
	
	Запрос = Новый Запрос();
	Запрос.Параметры.Вставить("Контрагент", Контрагент);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА УчастникиКонтролируемыхСделок.ЗарегистрированВСтранеСЛьготнымНалогообложением
	|			ТОГДА ""(офшор)""
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК Офшор,
	|	ЕстьNULL(УчастникиКонтролируемыхСделок.СтранаРегистрации.Наименование, ""не указана"") КАК НаименованиеСтраны
	|ИЗ
	|	РегистрСведений.УчастникиКонтролируемыхСделок КАК УчастникиКонтролируемыхСделок
	|ГДЕ
	|	УчастникиКонтролируемыхСделок.Контрагент = &Контрагент";
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		ИнформацияОРегистрации = НСтр("ru = 'Сведения о регистрации не указаны'");
	Иначе
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ИнформацияОРегистрации = НСтр("ru = 'Страна регистрации: %НаименованиеСтраны% %Офшор%'");
		ИнформацияОРегистрации = СтрЗаменить(ИнформацияОРегистрации, "%НаименованиеСтраны%", Выборка.НаименованиеСтраны);
		ИнформацияОРегистрации = СтрЗаменить(ИнформацияОРегистрации, "%Офшор%", Выборка.Офшор);
	КонецЕсли;
	ДополнительныеСведения.ИнформацияОРегистрации = ИнформацияОРегистрации;
	
	Возврат(ДополнительныеСведения);
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма, 
		ПолучитьДополнительныеСведения(Запись.Организация, Запись.Контрагент, Запись.Период));
	
КонецПроцедуры

&НаКлиенте
Процедура ИнформацияОРегистрацииНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ЗначениеЗаполнено(Запись.Контрагент) Тогда
		ПараметрыОткрытияФормы = ПолучитьПараметрыОткрытияФормыЗаписиКонтрагента(Запись.Контрагент);
		Если ПараметрыОткрытияФормы<>Неопределено Тогда
			ОткрытьФорму("РегистрСведений.УчастникиКонтролируемыхСделок.ФормаЗаписи", ПараметрыОткрытияФормы, Элемент, Запись.Контрагент);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма, 
		ПолучитьДополнительныеСведения(Запись.Организация, Запись.Контрагент, Запись.Период));
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма, 
		ПолучитьДополнительныеСведения(Запись.Организация, Запись.Контрагент, Запись.Период));
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(Запись, Параметры.ЗначенияЗаполнения);
	
	Если Параметры.ЗначенияЗаполнения.Свойство("Организация") Тогда 
		Элементы.Организация.ТолькоПросмотр = Истина;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма, 
		ПолучитьДополнительныеСведения(Запись.Организация, Запись.Контрагент, Запись.Период));
		
	ДоступностьКлючевыхЗаписей = Истина;
	Если Параметры.Свойство("ДоступностьКлючевыхЗаписей") Тогда
		ДоступностьКлючевыхЗаписей = Параметры.ДоступностьКлючевыхЗаписей;
	КонецЕсли;
	
	ЭтаФорма.Элементы.Организация.ТолькоПросмотр 	= НЕ ДоступностьКлючевыхЗаписей;
	ЭтаФорма.Элементы.Контрагент.ТолькоПросмотр 	= НЕ ДоступностьКлючевыхЗаписей;
	ЭтаФорма.Элементы.Период.ТолькоПросмотр 		= НЕ ДоступностьКлючевыхЗаписей;
	
	ЭтаФорма.Элементы.ТипВзаимозависимости.АвтоОтметкаНезаполненного = ДоступностьКлючевыхЗаписей;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "УчастникКонтролируемойСделкиЗаписан" Тогда
		ЗаполнитьЗначенияСвойств(ЭтаФорма, 
			ПолучитьДополнительныеСведения(Запись.Организация, Запись.Контрагент, Запись.Период));
	КонецЕсли;
	
КонецПроцедуры








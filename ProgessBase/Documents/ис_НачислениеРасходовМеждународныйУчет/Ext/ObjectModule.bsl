
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ОбщегоНазначения.УдалитьДвиженияРегистратора(ЭтотОбъект, Отказ);
	
	ВыполнитьДвижения(ВидДвиженияНакопления.Приход, Дата);
	Если РеверсированиеНаДату Тогда 
		ВыполнитьДвижения(ВидДвиженияНакопления.Расход, ДатаРеверсирования);
	КонецЕсли;
	
	Если Движения.НезавершенноеПроизводствоМеждународныйУчет.Модифицированность() Тогда 
		Движения.НезавершенноеПроизводствоМеждународныйУчет.Записать();
	КонецЕсли;
	Если Движения.ЗатратыМеждународныйУчет.Модифицированность() Тогда 
		Движения.ЗатратыМеждународныйУчет.Записать();
	КонецЕсли;
	Если Движения.Международный.Модифицированность() Тогда 
		Движения.Международный.Записать();
	КонецЕсли;
	
КонецПроцедуры


Процедура ВыполнитьДвижения(ВидДвижения, Период)
	
	Для Каждого лСтрокаЗатрат Из Затраты Цикл 
		
		// Движение по регистру накопления
		лИмяРегистра = ис_Международный.ПолучитьРегистрПоСчетуЗатрат(лСтрокаЗатрат.СчетЗатрат);
		Если лИмяРегистра = "НезавершенноеПроизводствоМеждународныйУчет" Тогда 
			
			лДвижение = Движения.НезавершенноеПроизводствоМеждународныйУчет.Добавить();
			лДвижение.ВидДвижения = ВидДвижения;
			лДвижение.Период = Период;
			лДвижение.Организация = Организация;
			лДвижение.Подразделение = лСтрокаЗатрат.Подразделение;
			лДвижение.НоменклатурнаяГруппа = лСтрокаЗатрат.НоменклатурнаяГруппа;
			лДвижение.СтатьяЗатрат = лСтрокаЗатрат.СтатьяЗатрат;
			лДвижение.СчетУчета = лСтрокаЗатрат.СчетЗатрат;
			лДвижение.Заказ = лСтрокаЗатрат.Заказ;
			лДвижение.Стоимость = лСтрокаЗатрат.Сумма;
			
		ИначеЕсли лИмяРегистра = "ЗатратыМеждународныйУчет" Тогда 
			
			лДвижение = Движения.ЗатратыМеждународныйУчет.Добавить();
			лДвижение.ВидДвижения = ВидДвижения;
			лДвижение.Период = Период;
			лДвижение.Организация = Организация;
			лДвижение.Подразделение = лСтрокаЗатрат.Подразделение;
			лДвижение.НоменклатурнаяГруппа = лСтрокаЗатрат.НоменклатурнаяГруппа;
			лДвижение.СтатьяЗатрат = лСтрокаЗатрат.СтатьяЗатрат;
			лДвижение.СчетУчета = лСтрокаЗатрат.СчетЗатрат;
			лДвижение.Заказ = лСтрокаЗатрат.Заказ;
			лДвижение.Сумма = лСтрокаЗатрат.Сумма;
			
		КонецЕсли;
		
		// Проводка
		лПроводка = Движения.Международный.Добавить();
		лПроводка.Период = Период;
		лПроводка.Организация = Организация;
		лПроводка.СчетДт = лСтрокаЗатрат.СчетЗатрат;
		Для лНомерСубконто=1 По лПроводка.СчетДт.ВидыСубконто.Количество() Цикл 
			БухгалтерскийУчет.УстановитьСубконто(лПроводка.СчетДт, лПроводка.СубконтоДт, лНомерСубконто, лСтрокаЗатрат["Субконто"+лНомерСубконто]);
		КонецЦикла;
		лПроводка.СчетКт = лСтрокаЗатрат.КорСчет;
		Для лНомерСубконто=1 По лПроводка.СчетКт.ВидыСубконто.Количество() Цикл 
			БухгалтерскийУчет.УстановитьСубконто(лПроводка.СчетКт, лПроводка.СубконтоКт, лНомерСубконто, лСтрокаЗатрат["КорСубконто"+лНомерСубконто]);
		КонецЦикла;
		Если ВидДвижения = ВидДвиженияНакопления.Приход Тогда 
			лПроводка.Сумма = лСтрокаЗатрат.Сумма;
			лПроводка.Содержание = "Начисление расходов";
		Иначе
			лПроводка.Сумма = -лСтрокаЗатрат.Сумма;
			лПроводка.Содержание = "Начисление расходов (сторно)";
		КонецЕсли;
			
	КонецЦикла;
	
КонецПроцедуры
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		ДокументОбъект = РеквизитФормыВЗначение("Объект");
		НастройкаПравДоступа.ОпределитьДоступностьВозможностьИзмененияДокументаПоДатеЗапрета(ДокументОбъект, ЭтаФорма);
		
		УстановитьПривилегированныйРежим(Истина);
		Если НЕ РольДоступна("ПолныеПрава")
			И Объект.ГраницаСмены <= РегистрыСведений.ЗавершенныеСмены.Получить(Новый Структура("Подразделение", Объект.Подразделение)).ГраницаСмены Тогда
			
			ТолькоПросмотр = Истина;
		КонецЕсли;
		УстановитьПривилегированныйРежим(Ложь);
		
	КонецЕсли;
	
КонецПроцедуры

// СОХРАНЕНИЕ И ВОССТАНОВЛЕНИЕ ДАННЫХ ИЗ НАСТРОЕК

&НаСервере
Процедура ПриСохраненииДанныхВНастройкахНаСервере(Настройки)
	
	ХранилищаНастроек.ДанныеФорм.ДобавитьДополнительныеДанныеВНастройку(Объект, Настройки, Документы.ОтчетОСоставеСмены.СтруктураДополнительныхДанныхФормы());
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	ХранилищаНастроек.ДанныеФорм.ЗаполнитьОбъектДополнительнымиДанными(Объект, Настройки, Документы.ОтчетОСоставеСмены.СтруктураДополнительныхДанныхФормы());
	Модифицированность = Истина;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Управляет доступностью реквизитов в строке табличного поля СоставСмены.
//
// Табличное поле имеет две группы реквизитов - для явки и для неявки.
//
&НаКлиенте
Процедура УправлениеДоступностьюРеквизитовВСтрокеСоставСмены()
	
	ДанныеСтроки = Элементы.СоставСмены.ТекущиеДанные;
	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	//Запрещаем редактировать поля, имеющие смысл только при явке или только при неявке.
	Элементы.СоставСменыВремяЯвки.ТолькоПросмотр 			= НЕ ДанныеСтроки.Явка;
	Элементы.СоставСменыОтработанноеВремя.ТолькоПросмотр 	= НЕ ДанныеСтроки.Явка;
	Элементы.СоставСменыЗамещающийСотрудник.ТолькоПросмотр	= ДанныеСтроки.Явка;
	
	//Запрещаем редактировать сотрудника, имеющегося в графике
	Элементы.СоставСменыСотрудник.ТолькоПросмотр = ДанныеСтроки.ПоГрафику;
	
КонецПроцедуры

// Выполняет общие действия при изменении поля ЗамещающийСотрудник в строке табличной части СоставСмены
//
&НаКлиенте
Процедура ЗамещающийСотрудник_Изменение()
	
	//Обработаем действия при изменении значения реквизита.
	ДанныеСтроки = Элементы.СоставСмены.ТекущиеДанные;
	
	//Замещающий сотрудник должен входить в состав смены.
	Если ЗначениеЗаполнено(ДанныеСтроки.ЗамещающийСотрудник) 
	   И Объект.СоставСмены.НайтиСтроки(Новый Структура("Сотрудник",ДанныеСтроки.ЗамещающийСотрудник)).Количество() = 0 Тогда
	   
	   	НоваяСтрокаТабличнойЧастиСФизЛицом(ДанныеСтроки.ЗамещающийСотрудник);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьФизЛицо(Сотрудник)
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
	Запрос.Текст = "ВЫБРАТЬ
	               |	СотрудникиОрганизаций.Физлицо
	               |ИЗ
	               |	Справочник.СотрудникиОрганизаций КАК СотрудникиОрганизаций
	               |ГДЕ
	               |	СотрудникиОрганизаций.Ссылка = &Сотрудник";
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ФизЛицо;
	Иначе
		Возврат "";
	КонецЕсли;
КонецФункции //ПолучитьФизЛицо()

&НаКлиенте
Процедура НоваяСтрокаТабличнойЧастиСФизЛицом(ФизЛицо)
	
	НоваяСтрока 			= Объект.СоставСмены.Добавить();
	НоваяСтрока.Сотрудник 	= ФизЛицо;
	ДобавлениеСтроки(НоваяСтрока);
	
КонецПроцедуры	

///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - СОСТАВ СМЕНЫ

&НаСервере
Процедура ЗаполнитьСоставомТекущейСмены()

	Объект.СоставСмены.Очистить();
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	ДокументОбъект.ЗаполнитьСоставомТекущейСмены();
	ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");
	
КонецПроцедуры
 
&НаКлиенте
Процедура КомандаЗаполнитьСоставомТекущейСмены(Команда)
	
	Если Объект.СоставСмены.Количество() > 0 Тогда
		Ответ = Вопрос(НСтр("ru = 'Перед заполнением табличная часть будет очищена. Заполнить?'"), РежимДиалогаВопрос.ДаНет, , , НСтр("ru='Заполнение списка сотрудников'"));
		Если Ответ <> КодВозвратаДиалога.Да Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ЗаполнитьСоставомТекущейСмены();
	
	// Выведем сообщение, чтобы избежать ситуации "нажал на кнопку - ничего не произошло"
	Если Объект.СоставСмены.Количество() = 0 Тогда
		Предупреждение(НСтр("ru = 'Данные для заполнения отсутствуют'"),,НСтр("ru='Заполнение списка сотрудников'"));
	КонецЕсли;
	
КонецПроцедуры

// Выполняет необходимые действия после добавления новой строки
//
// Параметры
//  ДанныеСтроки - данные добавленной строки
//
&НаКлиенте
Процедура ДобавлениеСтроки(ДанныеСтроки)
	
	ДанныеСтроки.Явка 		= Истина;
	ДанныеСтроки.ПоГрафику 	= Ложь;
	ИзменениеЯвки(ДанныеСтроки);
	
КонецПроцедуры

// Выполняет необходимые действия при изменении флага явки в строке
//
// Параметры
//  ДанныеСтроки - данные измененной строки
//
&НаКлиенте
Процедура ИзменениеЯвки(ДанныеСтроки)
	
	Если НЕ ДанныеСтроки.Явка Тогда
		ДанныеСтроки.ВремяЯвки 			= Неопределено;
		ДанныеСтроки.ОтработанноеВремя 	= Неопределено;
	Иначе
		ДанныеСтроки.ЗамещающийСотрудник= Неопределено;
	КонецЕсли;
	
	//Если это текущая редактируемая строка, то настроим доступность полей
	Если ДанныеСтроки = Элементы.СоставСмены.ТекущиеДанные Тогда
		УправлениеДоступностьюРеквизитовВСтрокеСоставСмены();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоставСменыЯвкаПриИзменении(Элемент)
	
	ДанныеСтроки = Элементы.СоставСмены.ТекущиеДанные;
	ИзменениеЯвки(ДанныеСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура СоставСменыПередУдалением(Элемент, Отказ)
	
	ДанныеСтроки = Элементы.СоставСмены.ТекущиеДанные;
	Если ДанныеСтроки <> Неопределено Тогда
		
		Если ДанныеСтроки.ПоГрафику Тогда
			//Нельзя удалить из списка сотрудника, работающего по графику.
			//Воспримем это как намерение поставить неявку.
			//Если уже стоит неявка - то сообщим.
			Отказ = Истина;
			
			Если ДанныеСтроки.Явка Тогда
				ДанныеСтроки.Явка 	= Ложь;
				ИзменениеЯвки(ДанныеСтроки);
			Иначе
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					НСтр("ru = 'Нельзя удалить из списка сотрудника, который согласно графику должен был выйти на работу.'"),,
					"СоставСмены");
			КонецЕсли;
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоставСменыПриАктивизацииСтроки(Элемент)
	
	УправлениеДоступностьюРеквизитовВСтрокеСоставСмены();
	
КонецПроцедуры

&НаКлиенте
Процедура СоставСменыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ДанныеСтроки = Элементы.СоставСмены.ТекущиеДанные;
	
	Если НоваяСтрока Тогда
		ДобавлениеСтроки(ДанныеСтроки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоставСменыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	УправлениеДоступностьюРеквизитовВСтрокеСоставСмены();
	
КонецПроцедуры

&НаКлиенте
Процедура СоставСменыОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) <> Тип("СправочникСсылка.СотрудникиОрганизаций") Тогда
		Возврат;
	КонецЕсли;
	
	//Если выбор произошел в форме подбора и этого физлица в документе пока нет,
	//добавим новую строку в таблицу
	СтруктураПоиска = Новый Структура("Сотрудник", ВыбранноеЗначение.Физлицо);
	
	Если Объект.СоставСмены.НайтиСтроки(СтруктураПоиска).Количество() = 0 Тогда
		
	   	НоваяСтрокаТабличнойЧастиСФизЛицом(ПолучитьФизЛицо(ВыбранноеЗначение.ФизЛицо));
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ТЧ СОСТАВ СМЕНЫ

//Поле ввода в колонке Сотрудник

// Обработчик события ПриИзменении
//
&НаКлиенте
Процедура СоставСменыСотрудникПриИзменении(Элемент)
	
	ДанныеСтроки = Элементы.СоставСмены.ТекущиеДанные;
	
	//Этот обработчик будет вызываться только при подборе текста 
	//и не будет при выборе из справочника, так как заполнение реквизита сделано через
	//справочник "СотрудникиОрганизаций" и в обработке выбора стандартная обработка отключена.
	//См. СоставСменыСотрудникОбработкаВыбора()
	
КонецПроцедуры

// Обработчик события НачалоВыбора
// Для выбора используется форма выбора справочника СотрудникиОрганизаций
&НаКлиенте
Процедура СоставСменыСотрудникНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация", "");
	ОткрытьФорму("Справочник.СотрудникиОрганизаций.Форма.ФормаВыбораУправляемая", ПараметрыФормы, Элемент);
	
КонецПроцедуры

// Обработчик события ОбработкаВыбора
// Для выбора используется форма выбора справочника СотрудникиОрганизаций
&НаКлиенте
Процедура СоставСменыСотрудникОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.СотрудникиОрганизаций") Тогда
		
		СтандартнаяОбработка = Ложь;
		ДанныеСтроки = Элементы.СоставСмены.ТекущиеДанные;
		ДанныеСтроки.Сотрудник = ПолучитьФизЛицо(ВыбранноеЗначение);

	КонецЕсли;
	
КонецПроцедуры

//Поле ввода в колонке ЗамещающийСотрудник

// Обработчик события ПриИзменении
//
&НаКлиенте
Процедура СоставСменыЗамещающийСотрудникПриИзменении(Элемент)
	
	ДанныеСтроки = Элементы.СоставСмены.ТекущиеДанные;
	
	//Этот обработчик будет вызываться только при подборе текста 
	//и не будет при выборе из справочника, так как заполнение реквизита сделано через
	//справочник "СотрудникиОрганизаций" и в обработке выбора стандартная обработка отключена.
	//См. СоставСменыЗамещающийСотрудникОбработкаВыбора()
	ЗамещающийСотрудник_Изменение();
	
КонецПроцедуры

// Обработчик события НачалоВыбора
// Для выбора используется форма выбора справочника СотрудникиОрганизаций
&НаКлиенте
Процедура СоставСменыЗамещающийСотрудникНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация", "");
	ОткрытьФорму("Справочник.СотрудникиОрганизаций.Форма.ФормаВыбораУправляемая", ПараметрыФормы, Элемент);
	
КонецПроцедуры

// Обработчик события ОбработкаВыбора
// Для выбора используется форма выбора справочника СотрудникиОрганизаций
&НаКлиенте
Процедура СоставСменыЗамещающийСотрудникОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.СотрудникиОрганизаций") Тогда
		
		СтандартнаяОбработка = Ложь;
		ДанныеСтроки = Элементы.СоставСмены.ТекущиеДанные;
		ДанныеСтроки.ЗамещающийСотрудник = ПолучитьФизЛицо(ВыбранноеЗначение);
		
		ЗамещающийСотрудник_Изменение();
	
	КонецЕсли;
	
КонецПроцедуры

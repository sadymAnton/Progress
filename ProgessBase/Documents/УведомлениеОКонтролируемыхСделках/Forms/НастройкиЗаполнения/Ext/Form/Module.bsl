////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
//

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Уведомление") Тогда
		СвойстваУведомления = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Параметры.Уведомление, "ГруппироватьСделкиСОдинаковойЦеной, КодМестаПредставления");
		ГруппироватьСделкиСОдинаковойЦеной = УстановитьЗначенияГруппировкиСделок(СвойстваУведомления.ГруппироватьСделкиСОдинаковойЦеной = Истина);
		КодМестаПредставления = СвойстваУведомления.КодМестаПредставления;
		Уведомление = Параметры.Уведомление; 
	Иначе
		ГруппироватьСделкиСОдинаковойЦеной = УстановитьЗначенияГруппировкиСделок(
				Параметры.Свойство("ГруппироватьСделкиСОдинаковойЦеной") И Параметры.ГруппироватьСделкиСОдинаковойЦеной = Истина);
		
		Если Параметры.Свойство("КодМестаПредставления") Тогда
			КодМестаПредставления = Параметры.КодМестаПредставления;
		КонецЕсли;
	КонецЕсли;
	
	СписокКодов = КонтролируемыеСделкиПовтИсп.ПолучитьКодыКатегорийНалогоплательщика();
	Элементы.КодМестаПредставления.СписокВыбора.Очистить();
	Для Каждого Код Из СписокКодов Цикл
		НовыйКод = Элементы.КодМестаПредставления.СписокВыбора.Добавить();
		ЗаполнитьЗначенияСвойств(НовыйКод, Код);
	КонецЦикла;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура Сохранить(Команда)
	
	Если ПроверитьЗаполнение() Тогда
	
		ЗаписатьСвойстваУведомления();
	
		НастройкиУведомления = Новый Структура();
		НастройкиУведомления.Вставить("ГруппироватьСделкиСОдинаковойЦеной", ?(ГруппироватьСделкиСОдинаковойЦеной = "ГруппироватьСделки", Истина, Ложь));
		НастройкиУведомления.Вставить("КодМестаПредставления", КодМестаПредставления);
		ОповеститьОВыборе(НастройкиУведомления);
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиентеНаСервереБезКонтекста
Функция УстановитьЗначенияГруппировкиСделок(ГруппироватьСделки)
	
	Если ГруппироватьСделки = Истина Тогда
		Возврат "ГруппироватьСделки";
	Иначе
		Возврат "НеГруппироватьСделки";
	КонецЕсли;
КонецФункции

&НаСервере
Процедура ЗаписатьСвойстваУведомления()
	
	Если ЗначениеЗаполнено(Уведомление) Тогда
		УведомлениеОбъект = Уведомление.ПолучитьОбъект();
		УведомлениеОбъект.ГруппироватьСделкиСОдинаковойЦеной = ?(ГруппироватьСделкиСОдинаковойЦеной = "ГруппироватьСделки", Истина, Ложь);
		УведомлениеОбъект.КодМестаПредставления = КодМестаПредставления;
		УведомлениеОбъект.Записать(РежимЗаписиДокумента.Запись);
	КонецЕсли;
	
КонецПроцедуры
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Процедура Автозаполнение() Экспорт
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("парамОрганизация",			Организация);
	Запрос.УстановитьПараметр("парамДата",					Макс(КонецДня(Дата)+1, КонецМесяца(ПериодРегистрации) + 1));
	Запрос.УстановитьПараметр("парамМесяц",					ПериодРегистрации);
	
	Если ПодразделениеОрганизации.Пустая() Тогда
		
		ТекстЗапроса = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВзаиморасчетыСРаботникамиОрганизацииОстатки.Физлицо,
		|	ВзаиморасчетыСРаботникамиОрганизацииОстатки.ПериодВзаиморасчетов КАК ПериодВозникновения,";
		
		ПереносЗадолженностиРаботниковОрганизацийПереопределяемый.ДополнитьСоставПолейТаблицыЗапроса(ТекстЗапроса, "ВзаиморасчетыСРаботникамиОрганизацииОстатки");
		
		ТекстЗапроса = ТекстЗапроса + "
		|	-ВзаиморасчетыСРаботникамиОрганизацииОстатки.СуммаВзаиморасчетовОстаток КАК Результат
		|ИЗ
		|	РегистрНакопления.ВзаиморасчетыСРаботникамиОрганизаций.Остатки(
		|			&парамДата,
		|			Организация = &парамОрганизация
		|				И ПериодВзаиморасчетов < &парамМесяц) КАК ВзаиморасчетыСРаботникамиОрганизацииОстатки
		|ГДЕ
		|	ВзаиморасчетыСРаботникамиОрганизацииОстатки.СуммаВзаиморасчетовОстаток < 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВзаиморасчетыСРаботникамиОрганизацииОстатки.Физлицо.Наименование,
		|	ПериодВозникновения";
		
	Иначе
		
		Запрос.УстановитьПараметр("ВнутреннееСовместительство",	Перечисления.ВидыЗанятостиВОрганизации.ВнутреннееСовместительство);
		Запрос.УстановитьПараметр("Прием",						Перечисления.ПричиныИзмененияСостояния.ПриемНаРаботу);
		Запрос.УстановитьПараметр("парамПодразделениеОрганизации",ПодразделениеОрганизации);
		Запрос.УстановитьПараметр("парамГоловнаяОрганизация",	ОбщегоНазначенияЗК.ГоловнаяОрганизация(Организация));
		Запрос.УстановитьПараметр("ПустаяДата",					'00010101');

		ТекстЗапроса = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВзаиморасчетыСРаботникамиОрганизацииОстатки.Физлицо,
		|	ВзаиморасчетыСРаботникамиОрганизацииОстатки.ПериодВзаиморасчетов КАК ПериодВозникновения,";
		
		ПереносЗадолженностиРаботниковОрганизацийПереопределяемый.ДополнитьСоставПолейТаблицыЗапроса(ТекстЗапроса, "ВзаиморасчетыСРаботникамиОрганизацииОстатки");
		
		ТекстЗапроса = ТекстЗапроса + "
		|	-ВзаиморасчетыСРаботникамиОрганизацииОстатки.СуммаВзаиморасчетовОстаток КАК Результат
		|ИЗ
		|	РегистрНакопления.ВзаиморасчетыСРаботникамиОрганизаций.Остатки(
		|			&парамДата,
		|			Организация = &парамОрганизация
		|				И ПериодВзаиморасчетов < &парамМесяц) КАК ВзаиморасчетыСРаботникамиОрганизацииОстатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			РаботникиОрганизаций.Сотрудник КАК Сотрудник,
		|			ДатыДвижений.Физлицо КАК Физлицо
		|		ИЗ
		|			(ВЫБРАТЬ
		|				МАКСИМУМ(РаботникиОрганизаций.Период) КАК Период,
		|				РаботникиОрганизаций.Сотрудник.Физлицо КАК Физлицо,
		|				РаботникиОрганизаций.Организация КАК Организация
		|			ИЗ
		|				РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизаций
		|			ГДЕ
		|				РаботникиОрганизаций.Организация = &парамГоловнаяОрганизация
		|				И РаботникиОрганизаций.Период <= &парамДата
		|				И РаботникиОрганизаций.Сотрудник.ВидЗанятости <> &ВнутреннееСовместительство
		|				И РаботникиОрганизаций.ПричинаИзмененияСостояния = &Прием
		|			
		|			СГРУППИРОВАТЬ ПО
		|				РаботникиОрганизаций.Сотрудник.Физлицо,
		|				РаботникиОрганизаций.Организация) КАК ДатыДвижений
		|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизаций
		|				ПО ДатыДвижений.Период = РаботникиОрганизаций.Период
		|					И ДатыДвижений.Организация = РаботникиОрганизаций.Организация
		|					И ДатыДвижений.Физлицо = РаботникиОрганизаций.Сотрудник.Физлицо
		|					И (РаботникиОрганизаций.Сотрудник.ВидЗанятости <> &ВнутреннееСовместительство)
		|					И (РаботникиОрганизаций.ПричинаИзмененияСостояния = &Прием)) КАК Сотрудники
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций.СрезПоследних(&парамДата, Организация = &парамГоловнаяОрганизация) КАК РаботникиОрганизацийСрезПоследних
		|			ПО Сотрудники.Сотрудник = РаботникиОрганизацийСрезПоследних.Сотрудник
		|				И (ВЫБОР
		|					КОГДА РаботникиОрганизацийСрезПоследних.ПериодЗавершения <= &парамДата
		|							И РаботникиОрганизацийСрезПоследних.ПериодЗавершения <> &ПустаяДата
		|						ТОГДА РаботникиОрганизацийСрезПоследних.ПодразделениеОрганизацииЗавершения
		|					ИНАЧЕ РаботникиОрганизацийСрезПоследних.ПодразделениеОрганизации
		|				КОНЕЦ В ИЕРАРХИИ (&парамПодразделениеОрганизации))
		|		ПО ВзаиморасчетыСРаботникамиОрганизацииОстатки.Физлицо = Сотрудники.Физлицо
		|ГДЕ
		|	ВзаиморасчетыСРаботникамиОрганизацииОстатки.СуммаВзаиморасчетовОстаток < 0
		|	И РаботникиОрганизацийСрезПоследних.Сотрудник ЕСТЬ НЕ NULL 
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВзаиморасчетыСРаботникамиОрганизацииОстатки.Физлицо.Наименование,
		|	ПериодВозникновения";
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	
	Задолженность.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры //Автозаполнение()

Процедура Рассчитать() Экспорт
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("парамОрганизация",	Организация);
	Запрос.УстановитьПараметр("парамДата",			Макс(КонецДня(Дата)+1,КонецМесяца(ПериодРегистрации) + 1));
	Запрос.УстановитьПараметр("парамМесяц",			ПериодРегистрации);
	Запрос.УстановитьПараметр("Ссылка",				Ссылка);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ПереносЗадолженностиРаботниковОрганизацийЗадолженность.Физлицо,
	|	ПереносЗадолженностиРаботниковОрганизацийЗадолженность.ПериодВозникновения,";
	
	ПереносЗадолженностиРаботниковОрганизацийПереопределяемый.ДополнитьСоставПолейТаблицыЗапроса(ТекстЗапроса, "ВзаиморасчетыСРаботникамиОрганизацииОстатки");
	
	ТекстЗапроса = ТекстЗапроса + "
	|	-ВзаиморасчетыСРаботникамиОрганизацииОстатки.СуммаВзаиморасчетовОстаток КАК Результат
	|ИЗ
	|	Документ.ПереносЗадолженностиРаботниковОрганизаций.Задолженность КАК ПереносЗадолженностиРаботниковОрганизацийЗадолженность
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ВзаиморасчетыСРаботникамиОрганизаций.Остатки(
	|				&парамДата,
	|				Организация = &парамОрганизация
	|					И ПериодВзаиморасчетов < &парамМесяц
	|					И Физлицо В
	|						(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|							ПереносЗадолженностиРаботниковОрганизацийЗадолженность.Физлицо
	|						ИЗ
	|							Документ.ПереносЗадолженностиРаботниковОрганизаций.Задолженность КАК ПереносЗадолженностиРаботниковОрганизацийЗадолженность
	|						ГДЕ
	|							ПереносЗадолженностиРаботниковОрганизацийЗадолженность.Ссылка = &Ссылка)) КАК ВзаиморасчетыСРаботникамиОрганизацииОстатки
	|		ПО ПереносЗадолженностиРаботниковОрганизацийЗадолженность.Физлицо = ВзаиморасчетыСРаботникамиОрганизацииОстатки.Физлицо
	|			И ПереносЗадолженностиРаботниковОрганизацийЗадолженность.ПериодВозникновения = ВзаиморасчетыСРаботникамиОрганизацииОстатки.ПериодВзаиморасчетов
	|			И (ВзаиморасчетыСРаботникамиОрганизацииОстатки.СуммаВзаиморасчетовОстаток < 0)
	|ГДЕ
	|	ПереносЗадолженностиРаботниковОрганизацийЗадолженность.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПереносЗадолженностиРаботниковОрганизацийЗадолженность.НомерСтроки";
	
	Запрос.Текст = ТекстЗапроса;
	
	Задолженность.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры
	
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

// Формирует запрос по шапке документа
//
// Параметры: 
//	Режим - режим проведения
//
// Возвращаемое значение:
//	Результат запроса
//
Функция СформироватьЗапросПоШапке()

	Запрос = Новый Запрос;

	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка",			Ссылка);
	Запрос.УстановитьПараметр("парамПустаяОрганизация",	Справочники.Организации.ПустаяСсылка());

	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПереносЗадолженностиРаботниковОрганизаций.Дата,
	|	ПереносЗадолженностиРаботниковОрганизаций.Организация,
	|	ПереносЗадолженностиРаботниковОрганизаций.ПериодРегистрации,
	|	ПереносЗадолженностиРаботниковОрганизаций.Ответственный,
	|	ПереносЗадолженностиРаботниковОрганизаций.Ссылка
	|ИЗ
	|	Документ.ПереносЗадолженностиРаботниковОрганизаций КАК ПереносЗадолженностиРаботниковОрганизаций
	|ГДЕ
	|	ПереносЗадолженностиРаботниковОрганизаций.Ссылка = &ДокументСсылка";
	Возврат Запрос.Выполнить();

КонецФункции // СформироватьЗапросПоШапке()

// Формирует запрос по таблице "Задолженность" документа
//
// Параметры:
//	нет
//
// Возвращаемое значение:
//	Результат запроса.
//
Функция СформироватьЗапросПоЗадолженность()

	Запрос = Новый Запрос;
	
	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка",			Ссылка);

	// Описание текста запроса:
	// 1. Выборка "ТЧНачисления":
	//		Выбираются строки документа.
	// 2. Выборка "ПересекающиесяСтроки":
	//		Среди остальных строк документа ищем строки c совпадающими физлицом и периодом возникновения задолженности
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗадолженностьРаботниковОрганизаций.НомерСтроки КАК НомерСтроки,
	|	ЗадолженностьРаботниковОрганизаций.Физлицо КАК Физлицо,
	|	ЗадолженностьРаботниковОрганизаций.ПериодВозникновения,";
	
	ПереносЗадолженностиРаботниковОрганизацийПереопределяемый.ДополнитьСоставПолейТаблицыЗапроса(ТекстЗапроса, "ЗадолженностьРаботниковОрганизаций");
	
	ТекстЗапроса = ТекстЗапроса + "
	|	ЗадолженностьРаботниковОрганизаций.Результат КАК Результат,
	|	МИНИМУМ(ПересекающиесяСтроки.НомерСтроки) КАК КонфликтнаяСтрокаНомер
	|ИЗ
	|	Документ.ПереносЗадолженностиРаботниковОрганизаций.Задолженность КАК ЗадолженностьРаботниковОрганизаций
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПереносЗадолженностиРаботниковОрганизаций.Задолженность КАК ПересекающиесяСтроки
	|		ПО ЗадолженностьРаботниковОрганизаций.Ссылка = ПересекающиесяСтроки.Ссылка
	|			И ЗадолженностьРаботниковОрганизаций.НомерСтроки < ПересекающиесяСтроки.НомерСтроки
	|			И ЗадолженностьРаботниковОрганизаций.Физлицо = ПересекающиесяСтроки.Физлицо
	|			И ЗадолженностьРаботниковОрганизаций.ПериодВозникновения = ПересекающиесяСтроки.ПериодВозникновения";
	
	ПереносЗадолженностиРаботниковОрганизацийПереопределяемый.ДополнитьСоставУсловийСоединенияТаблицЗапроса(ТекстЗапроса, "ЗадолженностьРаботниковОрганизаций", "ПересекающиесяСтроки");
	
	ТекстЗапроса = ТекстЗапроса + "
	|ГДЕ
	|	ЗадолженностьРаботниковОрганизаций.Ссылка = &ДокументСсылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗадолженностьРаботниковОрганизаций.НомерСтроки,
	|	ЗадолженностьРаботниковОрганизаций.Физлицо,
	|	ЗадолженностьРаботниковОрганизаций.ПериодВозникновения,";
	
	ПереносЗадолженностиРаботниковОрганизацийПереопределяемый.ДополнитьСоставПолейТаблицыЗапроса(ТекстЗапроса, "ЗадолженностьРаботниковОрганизаций");
	
	ТекстЗапроса = ТекстЗапроса + "
	|	ЗадолженностьРаботниковОрганизаций.Результат
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Запрос.Текст = ТекстЗапроса;
	
	Возврат Запрос.Выполнить();

КонецФункции // СформироватьЗапросПоЗадолженность()

// Проверяет правильность заполнения шапки документа.
// Если какой-то из реквизитов шапки, влияющий на проведение не заполнен или
// заполнен не корректно, то выставляется флаг отказа в проведении.
// Проверка выполняется по выборке из результата запроса по шапке,
// все проверяемые реквизиты должны быть включены в выборку по шапке.
//
// Параметры: 
//	ВыборкаПоШапкеДокумента	- выборка из результата запроса по шапке документа,
//	Отказ					- флаг отказа в проведении,
//	Заголовок				- Заголовок для сообщений об ошибках проведения.
//
Процедура ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ, Заголовок)

	Если НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.Организация) Тогда
		ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке(ОбщегоНазначенияЗК.ПреобразоватьСтрокуИнтерфейса("Не указана организация!"), Отказ, Заголовок);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.ПериодРегистрации) Тогда
		ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке("Не указан период (месяц) в который переносится задолженность!", Отказ, Заголовок);
	КонецЕсли;
	
КонецПроцедуры // ПроверитьЗаполнениеШапки()

// Проверяет правильность заполнения реквизитов в строке ТЧ "Задолженность" документа.
// Если какой-то из реквизитов, влияющий на проведение не заполнен или
// заполнен не корректно, то выставляется флаг отказа в проведении.
// Проверка выполняется по выборке из результата запроса по строке ТЧ документа,
// все проверяемые реквизиты должны быть включены в выборку.
//
// Параметры: 
//	ВыборкаПоШапкеДокумента		- выборка из результата запроса по шапке документа,
//	ВыборкаПоСтрокамДокумента	- спозиционированная на определеной строке выборка 
//								  из результата запроса по строкам документа, 
//	Отказ 						- флаг отказа в проведении,
//	Заголовок					- Заголовок для сообщений об ошибках проведения.
//
Процедура ПроверитьЗаполнениеСтрокиЗадолженность(ВыборкаПоШапкеДокумента, ВыборкаПоСтрокамДокумента, Отказ, Заголовок)
	
	СтрокаНачалаСообщенияОбОшибке = "В строке номер """+ СокрЛП(ВыборкаПоСтрокамДокумента.НомерСтроки) +
	""" табл. части ""Задолженность"": ";

	// Физлицо
	НетФизлицо = НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.Физлицо);
	Если НетФизлицо Тогда
		ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не выбран сотрудник!", Отказ, Заголовок);
	КонецЕсли;
	
	// ПериодВозникновения
	НетПериодВозникновения = НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ПериодВозникновения);
	Если НетПериодВозникновения Тогда
		ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не указан период возникновения задолженности!", Отказ, Заголовок);
	КонецЕсли;
	
	ПереносЗадолженностиРаботниковОрганизацийПереопределяемый.ПроверитьЗаполнениеДополнительныхПолей(ВыборкаПоСтрокамДокумента, СтрокаНачалаСообщенияОбОшибке, Отказ, Заголовок);
	
	// Результат
	НетРезультата = НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.Результат);
	Если НетРезультата Тогда
		ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не указан размер переносимой задолженности!", Отказ, Заголовок);
	КонецЕсли;

	Если Отказ Тогда
		Возврат; // Дальше не проверяем
	КонецЕсли;
	
	Если ВыборкаПоСтрокамДокумента.ПериодВозникновения >= ВыборкаПоШапкеДокумента.ПериодРегистрации Тогда
		ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке(СтрокаНачалаСообщенияОбОшибке + "период возникновения задолженности должен предшествовать периоду, в который она переносится!", Отказ, Заголовок);
	КонецЕсли;
	
	// Проверка: противоречие другой строке документа
	Если ВыборкаПоСтрокамДокумента.КонфликтнаяСтрокаНомер <> NULL Тогда
		СтрокаСообщениеОбОшибке = "указана повторяющаяся строка (см. строку  № " + ВыборкаПоСтрокамДокумента.КонфликтнаяСтрокаНомер + ")!"; 
		ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке(СтрокаНачалаСообщенияОбОшибке + СтрокаСообщениеОбОшибке, Отказ, Заголовок);
	КонецЕсли;

КонецПроцедуры // ПроверитьЗаполнениеСтрокиЗадолженность()

// По строке выборки результата запроса по документу формируем движения по регистрам
//
// Параметры:
//	ВыборкаПоШапкеДокумента			- выборка из результата запроса по шапке документа
//	ВыборкаПоТЧ						- выборка из результата запроса по табличной части документа.
//
// Возвращаемое значение:
//	Нет.
//
Процедура ДобавитьСтрокуВДвиженияПоРегистрамНакопления(ВыборкаПоШапкеДокумента, ВыборкаПоТЧ, ОбрабатываетсяПрошлыйПериод)
	
	////////////////////////////////////////
	/// ВзаиморасчетыСРаботникамиОрганизаций
	
	Движение = Движения.ВзаиморасчетыСРаботникамиОрганизаций.ДобавитьПриход();
	
	// Свойства
	Движение.Период					= КонецДня(Дата);
	
	// Измерения
	Движение.Организация			= ВыборкаПоШапкеДокумента.Организация;
	Движение.ФизЛицо				= ВыборкаПоТЧ.ФизЛицо;
	
	ПереносЗадолженностиРаботниковОрганизацийПереопределяемый.ДополнитьСтрокуДвиженияПоВзаиморасчетам(Движение, ВыборкаПоТЧ);
	
	Если ОбрабатываетсяПрошлыйПериод Тогда // списано задолженности прошлых периодов за работником
		
		// т.к. процедура вызывается только когда учет задолженности ведется в разрезе периодов, то ПериодВзаиморасчетов заполняется безусловно
		Движение.ПериодВзаиморасчетов	= ВыборкаПоТЧ.ПериодВозникновения;
		
		// Ресурсы
		Движение.СуммаВзаиморасчетов	= ВыборкаПоТЧ.Результат;
		
	Иначе // перенесено задолженности за работником в текущий период
		
		// т.к. процедура вызывается только когда учет задолженности ведется в разрезе периодов, то ПериодВзаиморасчетов заполняется безусловно
		Движение.ПериодВзаиморасчетов	= ПериодРегистрации;
		
		// Ресурсы
		Движение.СуммаВзаиморасчетов	= -ВыборкаПоТЧ.Результат;
		
	КонецЕсли;
	
	ПереносЗадолженностиРаботниковОрганизацийПереопределяемый.ДополнитьДвиженияПоРегистрамНакопления(Движения, ВыборкаПоШапкеДокумента, ВыборкаПоТЧ, ОбрабатываетсяПрошлыйПериод);
	
КонецПроцедуры // ДобавитьСтрокуВДвиженияПоРегистрамНакопления()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПроведения(Отказ, Режим)

	ОбработкаКомментариев = глЗначениеПеременной("глОбработкаСообщений");
	ОбработкаКомментариев.УдалитьСообщения();
	
	// Заголовок для сообщений об ошибках проведения.
	Заголовок = ОбщегоНазначенияЗК.ПредставлениеДокументаПриПроведении(Ссылка);
		
	// Получение учетной политики по персоналу организации
	УчетЗадолженностиПоМесяцам	= ПроцедурыУправленияПерсоналом.ЗначениеУчетнойПолитикиПоПерсоналуОрганизации(глЗначениеПеременной("глУчетнаяПолитикаПоПерсоналуОрганизации"), Организация, "УчетЗадолженностиПоМесяцам");
	Если НЕ УчетЗадолженностиПоМесяцам Тогда
		ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке("Учет задолженности сотрудников в разрезе месяцев ее образования не ведется!", Отказ, Заголовок);
		
	Иначе
		РезультатЗапросаПоШапке = СформироватьЗапросПоШапке();
		
		// Получим реквизиты шапки из запроса
		ВыборкаПоШапкеДокумента = РезультатЗапросаПоШапке.Выбрать();
		
		Если ВыборкаПоШапкеДокумента.Следующий() Тогда
			
			//Надо позвать проверку заполнения реквизитов шапки
			ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ, Заголовок);
			
			// Движения стоит добавлять, если в проведении еще не отказано (отказ =ложь)
			Если НЕ Отказ Тогда
				
				// получим реквизиты табличной части
				ВыборкаПоЗадолженности = СформироватьЗапросПоЗадолженность().Выбрать();
				
				Пока ВыборкаПоЗадолженности.Следующий() Цикл 
					
					// проверим очередную строку табличной части
					ПроверитьЗаполнениеСтрокиЗадолженность(ВыборкаПоШапкеДокумента, ВыборкаПоЗадолженности, Отказ, Заголовок);
						
					Если НЕ Отказ Тогда
						
						// Заполним записи в наборах записей регистров
						ДобавитьСтрокуВДвиженияПоРегистрамНакопления(ВыборкаПоШапкеДокумента, ВыборкаПоЗадолженности, Истина);
						ДобавитьСтрокуВДвиженияПоРегистрамНакопления(ВыборкаПоШапкеДокумента, ВыборкаПоЗадолженности, Ложь);
					КонецЕсли;
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;

	ОбработкаКомментариев.ПоказатьСообщения();
	
КонецПроцедуры // ОбработкаПроведения()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// Посчитать суммы документа и записать ее в соответствующий реквизит шапки для показа в журналах
	СуммаДокумента = Задолженность.Итог("Результат");
	
	МассивТЧ = Новый Массив;
	МассивТЧ.Добавить(Задолженность);
	КраткийСоставДокумента = ПроцедурыУправленияПерсоналом.ЗаполнитьКраткийСоставДокумента(МассивТЧ, "Физлицо");
	
КонецПроцедуры // ПередЗаписью()

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

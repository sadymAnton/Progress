
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОКУМЕНТА

#Если ТолстыйКлиентОбычноеПриложение Тогда

// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходимое количество копий.
//
//  Название макета печати передается в качестве параметра,
// по переданному названию находим имя макета в соответствии.
//
// Параметры:
//  НазваниеМакета - строка, название макета.
//
Функция Печать(ИмяМакета, КоличествоЭкземпляров = 1, НаПринтер = Ложь) Экспорт
	
	// Получить экземпляр документа на печать
	Если ИмяМакета = "ПечатьСЗВ_КОРР" Тогда
		
		ОбработкаКомментариев = глЗначениеПеременной("глОбработкаСообщений");
		ОбработкаКомментариев.УдалитьСообщения();
		
		Заголовок = "Печать документа: " + СокрЛП(ЭтотОбъект);
		
		Отказ = Ложь;
		
		//Проверка корректности заполнения документа
		ПроверитьПравильностьЗаполненияДокумента(Отказ, Ложь);
		
		Если Отказ Тогда
			ОбработкаКомментариев.ПоказатьСообщения();
			Возврат Неопределено;
		КонецЕсли;	 
		
		ТабДокумент = ПроцедурыПерсонифицированногоУчета.ПечатнаяФормаОДВ_1(Ссылка);
		УниверсальныеМеханизмы.НапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер, "Опись ОДВ-1 " + ОбщегоНазначенияЗК.СформироватьЗаголовокДокумента(ЭтотОбъект, "по документу"), , ТабДокумент);
		
		ТабДокумент = ПечатнаяФормаСЗВ_КОРР();
		Возврат УниверсальныеМеханизмы.НапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер, "Форма СЗВ-КОРР " + ОбщегоНазначенияЗК.СформироватьЗаголовокДокумента(ЭтотОбъект, "по документу"), , ТабДокумент);
		
	КонецЕсли;
	
КонецФункции // Печать

Функция ПечатнаяФормаСЗВ_КОРР()
	
	Макет = ПолучитьМакет("ФормаСЗВ_КОРР");
	
	ДокументРезультат = Новый ТабличныйДокумент;
	ДокументРезультат.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СЗВ_КОРР";
	ДокументРезультат.ПолеСлева = 0;
	ДокументРезультат.ПолеСправа = 0;
	ДокументРезультат.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьСтрокаЗаработка = Макет.ПолучитьОбласть("СтрокаЗаработка");
	ОбластьВредныеИТяжелыеРаботы = Макет.ПолучитьОбласть("ВредныеИТяжелыеРаботы");
	ОбластьСтрокаВредныхИТяжелыхРабот = Макет.ПолучитьОбласть("СтрокаВредныхИТяжелыхРабот");
	ОбластьСтаж = Макет.ПолучитьОбласть("Стаж");
	ОбластьСтрокаСтажа = Макет.ПолучитьОбласть("СтрокаСтажа");
	ОбластьДвустрочнаяСтрокаСтажа = Макет.ПолучитьОбласть("ДвустрочнаяСтрокаСтажа");
	ОбластьСтрокаКодПозицииСписка = Макет.ПолучитьОбласть("СтрокаКодПозицииСписка");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	ВыборкаПоШапкеДокумента = ЗапросПоШапкеДляПечати().Выбрать();
	ВыборкаПоРаботникам = ЗапросПоСотрудникамДляПечати().Выбрать();
	ВыборкаПоДоходамВзносам = ЗапросПоЗаработкуИВзносамДляПечати().Выбрать();
	ВыборкаПоДоходамНаВредныхИТяжелыхРаботах = ЗапросПоДоходамЗанятыхНаВредныхИТяжелыхРаботахДляПечати().Выбрать();
	
	Если ВыборкаПоШапкеДокумента.Следующий() Тогда
		
		СписокМесяцев = Новый Массив;
		КодыМесяцев = КодыМесяцевСЗВ_КОРР();
		ПервыйМесяц = Месяц(ВыборкаПоШапкеДокумента.КорректируемыйПериод);
		ПоследнийМесяц = Месяц(ПроцедурыПерсонифицированногоУчета.ОкончаниеОтчетногоПериодаПерсучета(ВыборкаПоШапкеДокумента.КорректируемыйПериод));
		Для Сч = ПервыйМесяц По ПоследнийМесяц Цикл 
			СписокМесяцев.Добавить(КодыМесяцев[Сч]);
		КонецЦикла;
		
		ВыборкаПоРаботникам.Сбросить();
		Если ВыборкаПоРаботникам.НайтиСледующий(Новый Структура("Ссылка", ВыборкаПоШапкеДокумента.Ссылка)) Тогда
			
			ВыборкаПоРаботникам.СледующийПоЗначениюПоля("Ссылка");
			Пока ВыборкаПоРаботникам.СледующийПоЗначениюПоля("НомерСтроки") Цикл
				
				ТекущийЛист = Новый ТабличныйДокумент;
				ТекущийЛист.ПолеСлева = ДокументРезультат.ПолеСлева;
				ТекущийЛист.ПолеСправа = ДокументРезультат.ПолеСправа;
				ТекущийЛист.ОриентацияСтраницы = ДокументРезультат.ОриентацияСтраницы;
				
				РегистрационныйНомерПФР = СтрЗаменить(ВыборкаПоШапкеДокумента.РегистрационныйНомерПФР," ","");
				РегистрационныйНомерПФР = СтрЗаменить(РегистрационныйНомерПФР,"-","");
				ФормированиеПечатныхФормЗК.ВывестиДанныеПоБуквенно(РегистрационныйНомерПФР, ОбластьШапка, "НомРег", 12, 13);
				
				КоррРегистрационныйНомерПФР = СтрЗаменить(ВыборкаПоШапкеДокумента.РегистрационныйНомерПФРвКорректируемыйПериод," ","");
				КоррРегистрационныйНомерПФР = СтрЗаменить(КоррРегистрационныйНомерПФР,"-","");
				ФормированиеПечатныхФормЗК.ВывестиДанныеПоБуквенно(КоррРегистрационныйНомерПФР, ОбластьШапка, "НомРег", 12, 25);
				
				ИНН = СтроковыеФункцииКлиентСервер.ДополнитьСтроку(ВыборкаПоШапкеДокумента.ИНН, 12, "-", "Справа");
				ФормированиеПечатныхФормЗК.ВывестиДанныеПоБуквенно(ИНН, ОбластьШапка, "ИНН", 12);
				ФормированиеПечатныхФормЗК.ВывестиДанныеПоБуквенно(ИНН, ОбластьШапка, "ИНН", 12, 13);
				
				КоррИНН = СтроковыеФункцииКлиентСервер.ДополнитьСтроку(ВыборкаПоШапкеДокумента.ИННвКорректируемыйПериод, 12, "-", "Справа");
				ФормированиеПечатныхФормЗК.ВывестиДанныеПоБуквенно(КоррИНН, ОбластьШапка, "ИНН", 12, 25);
				
				ФормированиеПечатныхФормЗК.ВывестиДанныеПоБуквенно(Формат(ВыборкаПоШапкеДокумента.КПП,"ЧГ=0"), ОбластьШапка, "КПП", 9);
				ФормированиеПечатныхФормЗК.ВывестиДанныеПоБуквенно(Формат(ВыборкаПоШапкеДокумента.КПП,"ЧГ=0"), ОбластьШапка, "КПП", 9, 10);
				
				ФормированиеПечатныхФормЗК.ВывестиДанныеПоБуквенно(Формат(ВыборкаПоШапкеДокумента.КППвКорректируемыйПериод,"ЧГ=0"), ОбластьШапка, "КПП", 9, 19);
				
				НомерСтраницы = 1;
				НомерДляПечати = СтроковыеФункцииКлиентСервер.ДополнитьСтроку(НомерСтраницы, 3);
				ФормированиеПечатныхФормЗК.ВывестиДанныеПоБуквенно(НомерДляПечати, ОбластьШапка, "НомСтр", 3);
				
				ОбластьШапка.Параметры.КодПериода = КодОтчетногоПериода(ВыборкаПоШапкеДокумента.ОтчетныйПериод);
				ОбластьШапка.Параметры.КодПериодаКорр = КодОтчетногоПериода(ВыборкаПоШапкеДокумента.КорректируемыйПериод);
				
				ОбластьШапка.Параметры.Год = Год(ВыборкаПоШапкеДокумента.ОтчетныйПериод);
				ОбластьШапка.Параметры.ГодКорр = Год(ВыборкаПоШапкеДокумента.КорректируемыйПериод);
				
				Если ВыборкаПоШапкеДокумента.ТипСведений = Перечисления.ТипыСведенийСЗВ_КОРР.Корректирующая Тогда 
					ОбластьШапка.Параметры.ТипСведений = "КОРР";
				ИначеЕсли ВыборкаПоШапкеДокумента.ТипСведений = Перечисления.ТипыСведенийСЗВ_КОРР.Отменяющая Тогда
					ОбластьШапка.Параметры.ТипСведений = "ОТМН";
				ИначеЕсли ВыборкаПоШапкеДокумента.ТипСведений = Перечисления.ТипыСведенийСЗВ_КОРР.Особая Тогда
					ОбластьШапка.Параметры.ТипСведений = "ОСОБ";
				КонецЕсли;
				
				ОбластьШапка.Параметры.НаименованиеОрганизации = ВыборкаПоШапкеДокумента.НаименованиеОрганизации;
				ОбластьШапка.Параметры.Организация = ВыборкаПоШапкеДокумента.Организация;
				
				ЗаполнитьЗначенияСвойств(ОбластьШапка.Параметры, ВыборкаПоРаботникам);
				ОбластьШапка.Параметры.ТипДоговора = ?(ЗначениеЗаполнено(ВыборкаПоРаботникам.ТипДоговора), ?(ВыборкаПоРаботникам.ТипДоговора = 1, "трудовой", "гражданско-правовой"), "");
				ОбластьШапка.Параметры.КодДТ = ?(ВыборкаПоРаботникам.ДополнительныйТариф, "АВИА", "");
				
				ТекущийЛист.Вывести(ОбластьШапка);
			
				СтруктураПоиска = Новый Структура("Ссылка, Сотрудник, ИдентификаторСтроки, КодМесяца");
				ЗаполнитьЗначенияСвойств(СтруктураПоиска, ВыборкаПоРаботникам);
				
				ЕстьСведенияОЗаработке = Ложь;
				Для Каждого КодМесяца Из СписокМесяцев Цикл 
					СтруктураПоиска.КодМесяца = КодМесяца;
					ОчиститьОбласть(ОбластьСтрокаЗаработка);
					ВыводитьСтроку = Ложь;
					Если ВыборкаПоДоходамВзносам.НайтиСледующий(СтруктураПоиска) Тогда
						ЗаполнитьЗначенияСвойств(ОбластьСтрокаЗаработка.Параметры, ВыборкаПоДоходамВзносам);
						ВыводитьСтроку = Истина;
					КонецЕсли;
					Если ВыводитьСтроку Тогда 
						ТекущийЛист.Вывести(ОбластьСтрокаЗаработка);
						ЕстьСведенияОЗаработке = Истина;
					КонецЕсли;
				КонецЦикла;
				
				Если Не ЕстьСведенияОЗаработке Тогда 
					ТекущийЛист.Вывести(ОбластьСтрокаЗаработка);
				КонецЕсли;
				
				ВывестиОбласть(ОбластьВредныеИТяжелыеРаботы, ВыборкаПоШапкеДокумента, ДокументРезультат, ТекущийЛист, Макет, НомерСтраницы);
				
				ЕстьСведенияОЗаработкеНаВредныхИТяжелыхРаботах = Ложь;
				Для Каждого КодМесяца Из СписокМесяцев Цикл 
					СтруктураПоиска.КодМесяца = КодМесяца;
					ОчиститьОбласть(ОбластьСтрокаВредныхИТяжелыхРабот);
					ВыводитьСтроку = Ложь;
					Если ВыборкаПоДоходамНаВредныхИТяжелыхРаботах.НайтиСледующий(СтруктураПоиска) Тогда
						ЗаполнитьЗначенияСвойств(ОбластьСтрокаВредныхИТяжелыхРабот.Параметры, ВыборкаПоДоходамНаВредныхИТяжелыхРаботах);
						ВыводитьСтроку = Истина;
					КонецЕсли;
					Если ВыводитьСтроку Тогда 
						ВывестиОбласть(ОбластьСтрокаВредныхИТяжелыхРабот, ВыборкаПоШапкеДокумента, ДокументРезультат, ТекущийЛист, Макет, НомерСтраницы);
						ЕстьСведенияОЗаработкеНаВредныхИТяжелыхРаботах = Истина;
					КонецЕсли;
				КонецЦикла;
				
				Если Не ЕстьСведенияОЗаработкеНаВредныхИТяжелыхРаботах Тогда 
					ТекущийЛист.Вывести(ОбластьСтрокаВредныхИТяжелыхРабот);
				КонецЕсли;
				
				ВывестиОбласть(ОбластьСтаж, ВыборкаПоШапкеДокумента, ДокументРезультат, ТекущийЛист, Макет, НомерСтраницы);
				
				ОчиститьОбласть(ОбластьСтрокаСтажа);
				ОчиститьОбласть(ОбластьДвустрочнаяСтрокаСтажа);
				ЗаполнитьЗначенияСвойств(ОбластьСтрокаСтажа.Параметры, ВыборкаПоРаботникам);
				ЗаполнитьЗначенияСвойств(ОбластьДвустрочнаяСтрокаСтажа.Параметры, ВыборкаПоРаботникам);
				
				Если ВыборкаПоРаботникам.ЗаполненСтаж Тогда
					Пока ВыборкаПоРаботникам.СледующийПоЗначениюПоля("НомерОсновнойЗаписи") Цикл
						Если ЗначениеЗаполнено(ВыборкаПоРаботникам.КодПозицииСпискаКод) Или ЗначениеЗаполнено(ВыборкаПоРаботникам.ТерриториальныеУсловияСтавка) Тогда
							ЗаполнитьДвустрочнуюОбластьСтаж(ВыборкаПоРаботникам, ОбластьДвустрочнаяСтрокаСтажа);
							ВывестиОбласть(ОбластьДвустрочнаяСтрокаСтажа, ВыборкаПоШапкеДокумента, ДокументРезультат, ТекущийЛист, Макет, НомерСтраницы);
						Иначе
							ЗаполнитьОбластьСтаж(ВыборкаПоРаботникам, ОбластьСтрокаСтажа);
							ВывестиОбласть(ОбластьСтрокаСтажа, ВыборкаПоШапкеДокумента, ДокументРезультат, ТекущийЛист, Макет, НомерСтраницы);
						КонецЕсли;
						Пока ВыборкаПоРаботникам.СледующийПоЗначениюПоля("НомерДополнительнойЗаписи") Цикл
							Если ВыборкаПоРаботникам.НомерДополнительнойЗаписи = 0 Тогда
								Продолжить;
							КонецЕсли;
							Если ЗначениеЗаполнено(ВыборкаПоРаботникам.КодПозицииСпискаКод) Или ЗначениеЗаполнено(ВыборкаПоРаботникам.ТерриториальныеУсловияСтавка) Тогда
								ЗаполнитьДвустрочнуюОбластьСтаж(ВыборкаПоРаботникам, ОбластьДвустрочнаяСтрокаСтажа);
								ОбластьДвустрочнаяСтрокаСтажа.Параметры.ДатаНачалаПериода = "";
								ОбластьДвустрочнаяСтрокаСтажа.Параметры.ДатаОкончанияПериода = "";
								ВывестиОбласть(ОбластьДвустрочнаяСтрокаСтажа, ВыборкаПоШапкеДокумента, ДокументРезультат, ТекущийЛист, Макет, НомерСтраницы);
							Иначе
								ЗаполнитьОбластьСтаж(ВыборкаПоРаботникам, ОбластьСтрокаСтажа);
								ОбластьСтрокаСтажа.Параметры.ДатаНачалаПериода = "";
								ОбластьСтрокаСтажа.Параметры.ДатаОкончанияПериода = "";
								ВывестиОбласть(ОбластьСтрокаСтажа, ВыборкаПоШапкеДокумента, ДокументРезультат, ТекущийЛист, Макет, НомерСтраницы);
							КонецЕсли;
						КонецЦикла;
					КонецЦикла;
				Иначе
					ВывестиОбласть(ОбластьСтрокаСтажа, ВыборкаПоШапкеДокумента, ДокументРезультат, ТекущийЛист, Макет, НомерСтраницы);
				КонецЕсли;
				
				ЗаполнитьЗначенияСвойств(ОбластьПодвал.Параметры, ВыборкаПоШапкеДокумента);
				ВывестиОбласть(ОбластьПодвал, ВыборкаПоШапкеДокумента, ДокументРезультат, ТекущийЛист, Макет, НомерСтраницы);
				ДокументРезультат.Вывести(ТекущийЛист);
				
			КонецЦикла;
			
		КонецЕсли;

	КонецЕсли;
	
	Возврат ДокументРезультат;

КонецФункции

#КонецЕсли

Функция ЗапросПоШапкеДляПечати()
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("СписокДокументов", Ссылка);
	Запрос.УстановитьПараметр("Руководитель", Руководитель);
	Запрос.УстановитьПараметр("ДатаФИО", Дата);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВЫБОР
	|		КОГДА ФИОФизЛицСрезПоследних.ФизЛицо ЕСТЬ NULL 
	|			ТОГДА ФизическиеЛица.Наименование
	|		ИНАЧЕ ФИОФизЛицСрезПоследних.Фамилия + "" "" + ФИОФизЛицСрезПоследних.Имя + "" "" + ФИОФизЛицСрезПоследних.Отчество
	|	КОНЕЦ КАК ФИОРуководителя
	|ПОМЕСТИТЬ ВТФИОРуководителя
	|ИЗ
	|	Справочник.ФизическиеЛица КАК ФизическиеЛица
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(&ДатаФИО, ФизЛицо = &Руководитель) КАК ФИОФизЛицСрезПоследних
	|		ПО ФизическиеЛица.Ссылка = ФИОФизЛицСрезПоследних.ФизЛицо
	|ГДЕ
	|	ФизическиеЛица.Ссылка = &Руководитель
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеОКорректировкеСведений.Ссылка КАК Ссылка,
	|	ДанныеОКорректировкеСведений.Организация,
	|	ДанныеОКорректировкеСведений.Организация.НаименованиеСокращенное КАК НаименованиеОрганизации,
	|	ДанныеОКорректировкеСведений.Организация.ИНН КАК ИНН,
	|	ДанныеОКорректировкеСведений.Организация.КПП КАК КПП,
	|	ДанныеОКорректировкеСведений.Организация.ЮрФизЛицо КАК ЮридическоеФизическоеЛицо,
	|	ДанныеОКорректировкеСведений.Год,
	|	ДанныеОКорректировкеСведений.ОтчетныйПериод,
	|	ДанныеОКорректировкеСведений.ТипСведенийСЗВ КАК ТипСведений,
	|	ДанныеОКорректировкеСведений.КорректируемыйПериод,
	|	ДанныеОКорректировкеСведений.Руководитель КАК Руководитель,
	|	ДанныеОКорректировкеСведений.ДолжностьРуководителя.Наименование КАК ДолжностьРуководителя,
	|	ДанныеОКорректировкеСведений.Дата,
	|	ДанныеОКорректировкеСведений.Организация.КодПоОКПО КАК КодПоОКПО,
	|	ДанныеОКорректировкеСведений.Организация.ОГРН КАК ОГРН,
	|	ДанныеОКорректировкеСведений.Организация.НаименованиеПолное КАК НаименованиеПолное,
	|	ДанныеОКорректировкеСведений.Организация.НаименованиеСокращенное КАК НаименованиеСокращенное,
	|	ДанныеОКорректировкеСведений.Организация.РегистрационныйНомерПФР КАК РегистрационныйНомерПФР,
	|	ДанныеОКорректировкеСведений.РегистрационныйНомерПФРвКорректируемыйПериод,
	|	ДанныеОКорректировкеСведений.КППвКорректируемыйПериод,
	|	ДанныеОКорректировкеСведений.ИННвКорректируемыйПериод,
	|	ДанныеОКорректировкеСведений.ИмяФайла
	|ПОМЕСТИТЬ ВТДанныеДокументов
	|ИЗ
	|	Документ.ДанныеОКорректировкеСведенийЗастрахованныхЛицСЗВ_КОРР КАК ДанныеОКорректировкеСведений
	|ГДЕ
	|	ДанныеОКорректировкеСведений.Ссылка В(&СписокДокументов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	Руководитель
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка КАК Ссылка,
	|	ДанныеДокументов.РегистрационныйНомерПФР КАК РегистрационныйНомерПФР,
	|	ДанныеДокументов.НаименованиеОрганизации КАК НаименованиеОрганизации,
	|	ДанныеДокументов.ЮридическоеФизическоеЛицо,
	|	ДанныеДокументов.ИНН КАК ИНН,
	|	ДанныеДокументов.КПП КАК КПП,
	|	ДанныеДокументов.ОГРН КАК ОГРН,
	|	ДанныеДокументов.КодПоОКПО КАК ОКПО,
	|	ДанныеДокументов.Организация,
	|	ДанныеДокументов.НаименованиеПолное КАК НаименованиеПолное,
	|	ДанныеДокументов.НаименованиеСокращенное КАК НаименованиеСокращенное,
	|	ДанныеДокументов.Год,
	|	ДанныеДокументов.ОтчетныйПериод,
	|	ДанныеДокументов.ТипСведений КАК ТипСведений,
	|	ДанныеДокументов.КорректируемыйПериод КАК КорректируемыйПериод,
	|	ЕСТЬNULL(ВТФИОПоследние.ФИОРуководителя, """") КАК Руководитель,
	|	ДанныеДокументов.ДолжностьРуководителя КАК ДолжностьРуководителя,
	|	ДанныеДокументов.Дата,
	|	ДанныеДокументов.РегистрационныйНомерПФРвКорректируемыйПериод,
	|	ДанныеДокументов.КППвКорректируемыйПериод,
	|	ДанныеДокументов.ИННвКорректируемыйПериод,
	|	ДанныеДокументов.ИмяФайла КАК ИмяФайлаДляПФР
	|ИЗ
	|	ВТДанныеДокументов КАК ДанныеДокументов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТФИОРуководителя КАК ВТФИОПоследние
	|		ПО (ИСТИНА)
	|
	|УПОРЯДОЧИТЬ ПО
	|	КорректируемыйПериод УБЫВ,
	|	ТипСведений,
	|	Ссылка";
	
	Возврат Запрос.Выполнить();
	
КонецФункции

Функция ЗапросПоСотрудникамДляПечати() 
	
	ТаблицаКодовКатегорий = КодыКатегорий();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СписокДокументов", Ссылка);
	Запрос.УстановитьПараметр("ТаблицаКодовКатегорий",	ТаблицаКодовКатегорий);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаКодовКатегорий.Код КАК КодКатегории,
	|	ТаблицаКодовКатегорий.Категория
	|ПОМЕСТИТЬ ВТКодыКатегорий
	|ИЗ
	|	&ТаблицаКодовКатегорий КАК ТаблицаКодовКатегорий
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Сотрудники.НомерСтроки КАК НомерСтроки,
	|	Сотрудники.ФизЛицо КАК Сотрудник,
	|	Сотрудники.ИдентификаторСтроки,
	|	Сотрудники.СтраховойНомерПФР,
	|	Сотрудники.Фамилия,
	|	Сотрудники.Имя,
	|	Сотрудники.Отчество,
	|	Сотрудники.Ссылка КАК Ссылка,
	|	Сотрудники.ТипДоговора,
	|	Сотрудники.КатегорияЗастрахованныхЛиц,
	|	Сотрудники.НомерДоговора,
	|	Сотрудники.ДатаДоговора,
	|	Сотрудники.ДополнительныйТариф
	|ПОМЕСТИТЬ ВТСотрудникиДокумента
	|ИЗ
	|	Документ.ДанныеОКорректировкеСведенийЗастрахованныхЛицСЗВ_КОРР.РаботникиОрганизации КАК Сотрудники
	|ГДЕ
	|	Сотрудники.Ссылка В(&СписокДокументов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаписиОСтаже.НомерОсновнойЗаписи КАК НомерОсновнойЗаписи,
	|	ЗаписиОСтаже.НомерДополнительнойЗаписи КАК НомерДополнительнойЗаписи,
	|	ЗаписиОСтаже.ДатаНачалаПериода,
	|	ЗаписиОСтаже.ДатаОкончанияПериода,
	|	ЗаписиОСтаже.ТерриториальныеУсловия,
	|	ЗаписиОСтаже.ТерриториальныеУсловия.Код КАК ТерриториальныеУсловияКод,
	|	ЗаписиОСтаже.ПараметрТерриториальныхУсловий КАК ПараметрТерриториальныхУсловий,
	|	ЗаписиОСтаже.ПараметрТерриториальныхУсловий КАК ТерриториальныеУсловияСтавка,
	|	ЗаписиОСтаже.ОсобыеУсловияТруда,
	|	ВЫБОР
	|		КОГДА ЗаписиОСтаже.Ссылка.ОтчетныйПериод < ДАТАВРЕМЯ(2009, 1, 1)
	|			ТОГДА ЗаписиОСтаже.ОсобыеУсловияТруда.Код
	|		ИНАЧЕ ЗаписиОСтаже.ОсобыеУсловияТруда.КодДляОтчетности2010
	|	КОНЕЦ КАК ОсобыеУсловияТрудаКод,
	|	ЗаписиОСтаже.КодПозицииСписка.Код КАК КодПозицииСпискаКод,
	|	ЗаписиОСтаже.ОснованиеИсчисляемогоСтажа,
	|	ЗаписиОСтаже.ОснованиеИсчисляемогоСтажа.Код КАК ОснованиеИсчисляемогоСтажаКод,
	|	ЗаписиОСтаже.ПервыйПараметрИсчисляемогоСтажа,
	|	ЗаписиОСтаже.ВторойПараметрИсчисляемогоСтажа,
	|	ЗаписиОСтаже.ТретийПараметрИсчисляемогоСтажа КАК ТретийПараметрИсчисляемогоСтажа,
	|	ЗаписиОСтаже.ТретийПараметрИсчисляемогоСтажа.Код КАК ТретийПараметрИсчисляемогоСтажаКод,
	|	ЗаписиОСтаже.ОснованиеВыслугиЛет,
	|	ВЫБОР
	|		КОГДА ЗаписиОСтаже.Ссылка.ОтчетныйПериод < ДАТАВРЕМЯ(2009, 1, 1)
	|			ТОГДА ЗаписиОСтаже.ОснованиеВыслугиЛет.Код
	|		ИНАЧЕ ЗаписиОСтаже.ОснованиеВыслугиЛет.КодДляОтчетности2010
	|	КОНЕЦ КАК ОснованиеВыслугиЛетКод,
	|	ЗаписиОСтаже.ПервыйПараметрВыслугиЛет,
	|	ЗаписиОСтаже.ВторойПараметрВыслугиЛет,
	|	ЗаписиОСтаже.ТретийПараметрВыслугиЛет,
	|	СотрудникиДокумента.НомерСтроки КАК НомерСтроки,
	|	СотрудникиДокумента.Сотрудник,
	|	СотрудникиДокумента.ИдентификаторСтроки,
	|	СотрудникиДокумента.СтраховойНомерПФР,
	|	СотрудникиДокумента.Фамилия,
	|	СотрудникиДокумента.Имя,
	|	СотрудникиДокумента.Отчество,
	|	СотрудникиДокумента.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА ЗаписиОСтаже.НомерОсновнойЗаписи ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЗаполненСтаж,
	|	ВЫБОР
	|		КОГДА СотрудникиДокумента.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипДоговораСЗВ63.Трудовой)
	|			ТОГДА 1
	|		КОГДА СотрудникиДокумента.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипДоговораСЗВ63.ГражданскоПравовой)
	|			ТОГДА 2
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ТипДоговора,
	|	СотрудникиДокумента.КатегорияЗастрахованныхЛиц,
	|	КодыКатегорий.КодКатегории,
	|	СотрудникиДокумента.НомерДоговора,
	|	СотрудникиДокумента.ДатаДоговора,
	|	СотрудникиДокумента.ДополнительныйТариф
	|ИЗ
	|	ВТСотрудникиДокумента КАК СотрудникиДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ДанныеОКорректировкеСведенийЗастрахованныхЛицСЗВ_КОРР.ЗаписиОСтаже КАК ЗаписиОСтаже
	|		ПО СотрудникиДокумента.Сотрудник = ЗаписиОСтаже.ФизЛицо
	|			И СотрудникиДокумента.ИдентификаторСтроки = ЗаписиОСтаже.ИдентификаторСтроки
	|			И СотрудникиДокумента.Ссылка = ЗаписиОСтаже.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКодыКатегорий КАК КодыКатегорий
	|		ПО СотрудникиДокумента.КатегорияЗастрахованныхЛиц = КодыКатегорий.Категория
	|
	|УПОРЯДОЧИТЬ ПО
	|	СотрудникиДокумента.Ссылка.КорректируемыйПериод УБЫВ,
	|	СотрудникиДокумента.Ссылка.ТипСведенийСЗВ,
	|	Ссылка,
	|	НомерСтроки,
	|	НомерОсновнойЗаписи,
	|	НомерДополнительнойЗаписи";
	
	Возврат Запрос.Выполнить();
	
КонецФункции

Функция ЗапросПоЗаработкуИВзносамДляПечати()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СписокДокументов", Ссылка);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Сотрудники.Ссылка,
	|	Сотрудники.ФизЛицо КАК Сотрудник,
	|	Сотрудники.ИдентификаторСтроки,
	|	Сотрудники.НомерСтроки
	|ПОМЕСТИТЬ ВТСотрудники
	|ИЗ
	|	Документ.ДанныеОКорректировкеСведенийЗастрахованныхЛицСЗВ_КОРР.РаботникиОрганизации КАК Сотрудники
	|ГДЕ
	|	Сотрудники.Ссылка В(&СписокДокументов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Сотрудники.Ссылка КАК Ссылка,
	|	Сотрудники.Сотрудник КАК Сотрудник,
	|	Сотрудники.ИдентификаторСтроки,
	|	СведенияОЗаработке.КодМесяца КАК КодМесяца,
	|	Сотрудники.НомерСтроки КАК НомерСтрокиСотрудник,
	|	ВЫБОР
	|		КОГДА СведенияОЗаработке.КодМесяца ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЗаработокЗаполнен,
	|	СведенияОЗаработке.Заработок,
	|	СведенияОЗаработке.ОблагаетсяВзносамиДоПредельнойВеличины,
	|	СведенияОЗаработке.ПоДоговорамГПХДоПредельнойВеличины,
	|	СведенияОЗаработке.ОблагаетсяВзносамиСвышеПредельнойВеличины,
	|	СведенияОЗаработке.ПоДоговорамГПХСвышеПредельнойВеличины,
	|	СведенияОЗаработке.ДоначисленоНаОПС,
	|	СведенияОЗаработке.ДоначисленоСтраховая,
	|	СведенияОЗаработке.ДоначисленоНакопительная,
	|	СведенияОЗаработке.УплачиваемыеСтрахователем,
	|	СведенияОЗаработке.УплачиваемыеИзЗаработкаЗЛ,
	|	СведенияОЗаработке.УплаченоСтраховая,
	|	СведенияОЗаработке.УплаченоНакопительная
	|ИЗ
	|	ВТСотрудники КАК Сотрудники
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ДанныеОКорректировкеСведенийЗастрахованныхЛицСЗВ_КОРР.СведенияОЗаработке КАК СведенияОЗаработке
	|		ПО Сотрудники.Ссылка = СведенияОЗаработке.Ссылка
	|			И Сотрудники.Сотрудник = СведенияОЗаработке.ФизЛицо
	|			И Сотрудники.ИдентификаторСтроки = СведенияОЗаработке.ИдентификаторСтроки
	|
	|УПОРЯДОЧИТЬ ПО
	|	Сотрудники.Ссылка.КорректируемыйПериод УБЫВ,
	|	Сотрудники.Ссылка.ТипСведенийСЗВ,
	|	Ссылка,
	|	НомерСтрокиСотрудник,
	|	СведенияОЗаработке.НомерСтроки";
	
	Возврат Запрос.Выполнить();
	
КонецФункции

Функция ЗапросПоДоходамЗанятыхНаВредныхИТяжелыхРаботахДляПечати()

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СписокДокументов", Ссылка);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Сотрудники.Ссылка,
	|	Сотрудники.ФизЛицо КАК Сотрудник,
	|	Сотрудники.ИдентификаторСтроки,
	|	Сотрудники.НомерСтроки
	|ПОМЕСТИТЬ ВТСотрудники
	|ИЗ
	|	Документ.ДанныеОКорректировкеСведенийЗастрахованныхЛицСЗВ_КОРР.РаботникиОрганизации КАК Сотрудники
	|ГДЕ
	|	Сотрудники.Ссылка В(&СписокДокументов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Сотрудники.Ссылка КАК Ссылка,
	|	Сотрудники.Сотрудник КАК Сотрудник,
	|	Сотрудники.ИдентификаторСтроки,
	|	СведенияОЗаработке.КодМесяца КАК КодМесяца,
	|	ВЫБОР
	|		КОГДА СведенияОЗаработке.КлассУсловийТруда = ЗНАЧЕНИЕ(Перечисление.КлассыУсловийТрудаПоРезультатамСпециальнойОценки.Опасный)
	|			ТОГДА ""О4""
	|		КОГДА СведенияОЗаработке.КлассУсловийТруда = ЗНАЧЕНИЕ(Перечисление.КлассыУсловийТрудаПоРезультатамСпециальнойОценки.Вредный4)
	|			ТОГДА ""В3.4""
	|		КОГДА СведенияОЗаработке.КлассУсловийТруда = ЗНАЧЕНИЕ(Перечисление.КлассыУсловийТрудаПоРезультатамСпециальнойОценки.Вредный3)
	|			ТОГДА ""В3.3""
	|		КОГДА СведенияОЗаработке.КлассУсловийТруда = ЗНАЧЕНИЕ(Перечисление.КлассыУсловийТрудаПоРезультатамСпециальнойОценки.Вредный2)
	|			ТОГДА ""В3.2""
	|		КОГДА СведенияОЗаработке.КлассУсловийТруда = ЗНАЧЕНИЕ(Перечисление.КлассыУсловийТрудаПоРезультатамСпециальнойОценки.Вредный1)
	|			ТОГДА ""В3.1""
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК КодКлассаУсловийТруда,
	|	Сотрудники.НомерСтроки КАК НомерСтрокиСотрудник,
	|	ВЫБОР
	|		КОГДА СведенияОЗаработке.КодМесяца ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЗаработокЗаполнен,
	|	СведенияОЗаработке.КлассУсловийТруда,
	|	СведенияОЗаработке.ОблагаетсяВзносамиЗаЗанятыхНаПодземныхИВредныхРаботах,
	|	СведенияОЗаработке.ОблагаетсяВзносамиЗаЗанятыхНаТяжелыхИПрочихРаботах
	|ИЗ
	|	ВТСотрудники КАК Сотрудники
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ДанныеОКорректировкеСведенийЗастрахованныхЛицСЗВ_КОРР.СведенияОЗаработкеНаВредныхИТяжелыхРаботах КАК СведенияОЗаработке
	|		ПО Сотрудники.Ссылка = СведенияОЗаработке.Ссылка
	|			И Сотрудники.Сотрудник = СведенияОЗаработке.ФизЛицо
	|			И Сотрудники.ИдентификаторСтроки = СведенияОЗаработке.ИдентификаторСтроки
	|
	|УПОРЯДОЧИТЬ ПО
	|	Сотрудники.Ссылка.КорректируемыйПериод УБЫВ,
	|	Сотрудники.Ссылка.ТипСведенийСЗВ,
	|	Ссылка,
	|	НомерСтрокиСотрудник,
	|	СведенияОЗаработке.КлассУсловийТруда,
	|	СведенияОЗаработке.НомерСтроки";
	
	Возврат Запрос.Выполнить();

КонецФункции

Процедура ЗаполнитьОбластьСтаж(ВыборкаЗаписейСтажа, ОбластьСтаж)
	
	ОбластьСтаж.Параметры.ДатаНачалаПериода = Формат(ВыборкаЗаписейСтажа.ДатаНачалаПериода, "ДФ=""дд.ММ.гггг""");
	ОбластьСтаж.Параметры.ДатаОкончанияПериода = Формат(ВыборкаЗаписейСтажа.ДатаОкончанияПериода, "ДФ=""дд.ММ.гггг""");
	
	ОбластьСтаж.Параметры.ТерриториальныеУсловияКод = ВыборкаЗаписейСтажа.ТерриториальныеУсловияКод;
	Если ЗначениеЗаполнено(ВыборкаЗаписейСтажа.ТерриториальныеУсловияСтавка) Тогда 
		ОбластьСтаж.Параметры.ТерриториальныеУсловияКод = Строка(ВыборкаЗаписейСтажа.ТерриториальныеУсловияКод) + " " + ВыборкаЗаписейСтажа.ТерриториальныеУсловияСтавка;
	КонецЕсли;
	ОбластьСтаж.Параметры.ОсобыеУсловияТрудаКод = ВыборкаЗаписейСтажа.ОсобыеУсловияТрудаКод;
	ОбластьСтаж.Параметры.ОснованиеИсчисляемогоСтажаКод = ВыборкаЗаписейСтажа.ОснованиеИсчисляемогоСтажаКод;
	ОбластьСтаж.Параметры.ОснованиеВыслугиЛетКод = ВыборкаЗаписейСтажа.ОснованиеВыслугиЛетКод;
	ОбластьСтаж.Параметры.ДопИТС = ПроцедурыПерсонифицированногоУчета.ПредставлениеПараметровИсчисляемогоТрудовогоСтажа(ВыборкаЗаписейСтажа);
	
	ПроцедурыПерсонифицированногоУчета.ПредставлениеПараметровПенсииЗаВыслугуЛет(ВыборкаЗаписейСтажа, ОбластьСтаж.Параметры.ДопВЛ, ОбластьСтаж.Параметры.ДопВЛСтавка);
	
КонецПроцедуры

Процедура ЗаполнитьДвустрочнуюОбластьСтаж(ВыборкаЗаписейСтажа, ОбластьСтаж)
	
	ОбластьСтаж.Параметры.ДатаНачалаПериода = Формат(ВыборкаЗаписейСтажа.ДатаНачалаПериода, "ДФ=""дд.ММ.гггг""");
	ОбластьСтаж.Параметры.ДатаОкончанияПериода = Формат(ВыборкаЗаписейСтажа.ДатаОкончанияПериода, "ДФ=""дд.ММ.гггг""");
	
	ОбластьСтаж.Параметры.ТерриториальныеУсловияКод = ВыборкаЗаписейСтажа.ТерриториальныеУсловияКод;
	ОбластьСтаж.Параметры.ОсобыеУсловияТрудаКод = ВыборкаЗаписейСтажа.ОсобыеУсловияТрудаКод;
	ОбластьСтаж.Параметры.КодПозицииСпискаКод = ВыборкаЗаписейСтажа.КодПозицииСпискаКод;
	ОбластьСтаж.Параметры.ОснованиеИсчисляемогоСтажаКод = ВыборкаЗаписейСтажа.ОснованиеИсчисляемогоСтажаКод;
	ОбластьСтаж.Параметры.ОснованиеВыслугиЛетКод = ВыборкаЗаписейСтажа.ОснованиеВыслугиЛетКод;
	ОбластьСтаж.Параметры.ДопИТС = ПроцедурыПерсонифицированногоУчета.ПредставлениеПараметровИсчисляемогоТрудовогоСтажа(ВыборкаЗаписейСтажа);
	ЗаписьТУ = Новый Структура("ПервыйПараметрИсчисляемогоСтажа,ВторойПараметрИсчисляемогоСтажа");
	ЗаполнитьЗначенияСвойств(ЗаписьТУ, ВыборкаЗаписейСтажа);
	ЗаписьТУ.Вставить("Коэффициент", ВыборкаЗаписейСтажа.ТерриториальныеУсловияСтавка);
	ОбластьСтаж.Параметры.ДопТУ = ПроцедурыПерсонифицированногоУчета.ПредставлениеПараметровТерриториальныхУсловий(ЗаписьТУ);
	
	ПроцедурыПерсонифицированногоУчета.ПредставлениеПараметровПенсииЗаВыслугуЛет(ВыборкаЗаписейСтажа, ОбластьСтаж.Параметры.ДопВЛ, ОбластьСтаж.Параметры.ДопВЛСтавка);
	
КонецПроцедуры

Процедура ВывестиОбласть(ВыводимаяОбласть, ВыборкаПоШапкеДокумента, ДокументРезультат, ТекущийЛист, Макет, НомерСтраницы)
	
	ВыводимыеОбласти = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ВыводимаяОбласть);
	Если ТекущийЛист.ПроверитьВывод(ВыводимыеОбласти) Тогда 
		ТекущийЛист.Вывести(ВыводимаяОбласть);
	Иначе 
		ДокументРезультат.Вывести(ТекущийЛист);
		ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
		ТекущийЛист = НовыйПечатныйЛист(ВыборкаПоШапкеДокумента, ДокументРезультат, Макет, НомерСтраницы);
		ТекущийЛист.Вывести(ВыводимаяОбласть);
	КонецЕсли;	
	
КонецПроцедуры 

Функция НовыйПечатныйЛист(ВыборкаПоШапкеДокумента, ДокументРезультат, Макет, НомерСтраницы)
	
	ТекущийЛист = Новый ТабличныйДокумент;
	ТекущийЛист.ПолеСлева = 0;
	ТекущийЛист.ПолеСправа = 0;
	ТекущийЛист.ОриентацияСтраницы = ДокументРезультат.ОриентацияСтраницы;
	
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьПустаяСтрока = Макет.ПолучитьОбласть("ПустаяСтрока");
	
	РегистрационныйНомерПФР = СтрЗаменить(ВыборкаПоШапкеДокумента.РегистрационныйНомерПФР," ","");
	РегистрационныйНомерПФР = СтрЗаменить(РегистрационныйНомерПФР,"-","");
	ФормированиеПечатныхФормЗК.ВывестиДанныеПоБуквенно(РегистрационныйНомерПФР, ОбластьШапка, "НомРег", 12);
	
	НомерСтраницы = НомерСтраницы + 1;
	НомерДляПечати = СтроковыеФункцииКлиентСервер.ДополнитьСтроку(НомерСтраницы, 3);
	ФормированиеПечатныхФормЗК.ВывестиДанныеПоБуквенно(НомерДляПечати, ОбластьШапка, "НомСтр", 3);
	
	ИНН = СтроковыеФункцииКлиентСервер.ДополнитьСтроку(ВыборкаПоШапкеДокумента.ИНН, 12, "-", "Справа");
	ФормированиеПечатныхФормЗК.ВывестиДанныеПоБуквенно(ИНН, ОбластьШапка, "ИНН", 12);
	
	ФормированиеПечатныхФормЗК.ВывестиДанныеПоБуквенно(Формат(ВыборкаПоШапкеДокумента.КПП,"ЧГ=0"), ОбластьШапка, "КПП", 9);
	
	ТекущийЛист.Вывести(ОбластьШапка);
	ТекущийЛист.Вывести(ОбластьПустаяСтрока);
	
	Возврат ТекущийЛист;
	
КонецФункции

Процедура ОчиститьОбласть(ОбластьМакета)
	
	Для Счетчик = 0 По ОбластьМакета.Параметры.Количество() - 1 Цикл
		ОбластьМакета.Параметры[Счетчик] = ""
	КонецЦикла;
	
КонецПроцедуры

// Возвращает доступные варианты печати документа
//
// Возвращаемое значение:
//  Структура, каждая строка которой соответствует одному из вариантов печати
//  
Функция ПолучитьСтруктуруПечатныхФорм() Экспорт
	
	Возврат Новый Структура("ПечатьСЗВ_КОРР","СЗВ-КОРР");

КонецФункции // ПолучитьСтруктуруПечатныхФорм()

Процедура ПроверитьПравильностьЗаполненияДокумента(ЕстьОшибки, СообщатьОбУспехе = Истина, ВыборкаПоШапкеДокумента = Неопределено, ВыборкаПоРаботникам = Неопределено) Экспорт
	
	Обработка = глЗначениеПеременной("глОбработкаСообщений");
	
	Если ВыборкаПоШапкеДокумента = Неопределено Тогда
		ВыборкаПоШапкеДокумента = СформироватьЗапросПоШапкеДокументов().Выбрать();
	КонецЕсли;
	Если ВыборкаПоРаботникам = Неопределено Тогда
		ВыборкаПоРаботникам = СформироватьЗапросПоРаботникам().Выбрать();
	КонецЕсли;
	
	Заработки = СведенияОЗаработке.Выгрузить();
	Заработки.Индексы.Добавить("ФизЛицо,ИдентификаторСтроки,КодМесяца");
	Заработки.Сортировать("ФизЛицо,ИдентификаторСтроки,КодМесяца");
	
	ЗаработкиВредныеТяжелые = СведенияОЗаработкеНаВредныхИТяжелыхРаботах.Выгрузить();
	ЗаработкиВредныеТяжелые.Индексы.Добавить("ФизЛицо,ИдентификаторСтроки,КодМесяца");
	ЗаработкиВредныеТяжелые.Сортировать("ФизЛицо,ИдентификаторСтроки,КодМесяца");
	
	Отказ = Ложь;
	Если ВыборкаПоШапкеДокумента.Следующий() Тогда
		
		ТекущийДокумент = ВыборкаПоШапкеДокумента.Ссылка;
		
		// Заголовок для сообщений об ошибках проведения.
		Заголовок =  "Проверка документа " + СокрЛП(ВыборкаПоШапкеДокумента.Ссылка);
		СообщенияРазделПроверки = Обработка.ДобавитьСообщение(Заголовок, Перечисления.ВидыСообщений.Информация);
		
		КоличествоДокументов = ВыборкаПоШапкеДокумента.Количество;
		Если КоличествоДокументов = 0 Тогда
			ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке("Список застрахованных лиц пуст!", Отказ, Заголовок,,, СообщенияРазделПроверки);
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.Организация) Тогда
			ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке(ОбщегоНазначенияЗК.ПреобразоватьСтрокуИнтерфейса("Не указана организация!"), Отказ, Заголовок,,, СообщенияРазделПроверки);
		ИначеЕсли СтрДлина(СокрЛП(ВыборкаПоШапкеДокумента.ОрганизацияРегистрационныйНомерПФР)) <> 14 Тогда
			ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке("Предупреждение: Регистрационный номер организации в ПФР скорее всего неправильный",,Заголовок,,, СообщенияРазделПроверки);
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.Год) Тогда
			ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке("Не указан отчетный год!", Отказ, Заголовок,,, СообщенияРазделПроверки);
		ИначеЕсли ВыборкаПоШапкеДокумента.Год < Год(РасчетСтраховыхВзносов.ДатаПередачиАдминистрированияВзносовФНС()) Тогда
			ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке("Форма СЗВ-КОРР представляется начиная с 2017 года!", Отказ,,,, СообщенияРазделПроверки);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.ТипСведенийСЗВ) Тогда
			ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке("Не указан тип сведений!", Отказ,,,, СообщенияРазделПроверки);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.ИмяФайла) Тогда
			ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке("Не указано имя файла!", Отказ, Заголовок);
		КонецЕсли;
		
		// проверим данные руководителя
		Если Не ЗначениеЗаполнено(Руководитель) Тогда
			ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке("Не указан руководитель!", Отказ, Заголовок);
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ДолжностьРуководителя) Тогда
			ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке("Не указана должность руководителя!", Отказ, Заголовок);
		КонецЕсли;
		
		ВыборкаПоРаботникам.Сбросить();
		Если ВыборкаПоРаботникам.НайтиСледующий(ВыборкаПоШапкеДокумента.Ссылка, "Ссылка") Тогда
			
			Пока ВыборкаПоРаботникам.СледующийПоЗначениюПоля("НомерСтроки") цикл
				
				Если ВыборкаПоРаботникам.Ссылка <> ТекущийДокумент Тогда
					Прервать;
				КонецЕсли;
				
				СтрокаНачалаСообщенияОбОшибке = "В строке № "+ СокрЛП(ВыборкаПоРаботникам.НомерСтроки);
				
				Если Не ЗначениеЗаполнено(ВыборкаПоРаботникам.ФизЛицо) Тогда
					ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке(СтрокаНачалаСообщенияОбОшибке + ": Не выбран сотрудник!", Отказ, Заголовок,,, СообщенияРазделПроверки);
				Иначе
					
					СтрокаНачалаСообщенияОбОшибке = СтрокаНачалаСообщенияОбОшибке + " по сотруднику " + ВыборкаПоРаботникам.ФизЛицоНаименование + ": ";
					
					Если Не ВыборкаПоРаботникам.ФизЛицоРаботаетВОрганизации тогда
						ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке(СтрокаНачалаСообщенияОбОшибке + "- не зарегистрирован(а) как сотрудник организации " + ВыборкаПоШапкеДокумента.ОрганизацияНаименование, Отказ, Заголовок,,, СообщенияРазделПроверки);
					КонецЕсли;
					
					СтраховойНомерПФР = ВыборкаПоРаботникам.СтраховойНомерПФР;
					Если Не ЗначениеЗаполнено(СтраховойНомерПФР) Тогда
						ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке(СтрокаНачалаСообщенияОбОшибке + "- Не задан страховой номер!", Отказ, Заголовок,,, СообщенияРазделПроверки);
					ИначеЕсли Не РегламентированнаяОтчетность.СтраховойНомерПФРСоответствуетТребованиям(СтраховойНомерПФР) тогда
						ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке(СтрокаНачалаСообщенияОбОшибке + "- Задан неверный страховой номер!", Отказ, Заголовок,,, СообщенияРазделПроверки);
					КонецЕсли;
					
					Если Не ЗначениеЗаполнено(ВыборкаПоРаботникам.Фамилия) И Не ЗначениеЗаполнено(ВыборкаПоРаботникам.Имя) Тогда
						ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке(СтрокаНачалаСообщенияОбОшибке + "- Не заданы фамилия и имя!", Отказ ,Заголовок,,, СообщенияРазделПроверки);
					КонецЕсли;	
					
					//Контроль дубля строк
					Если ЗначениеЗаполнено(ВыборкаПоРаботникам.НомерПовторяющейсяСтроки) Тогда
						ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке("Строка №" + ВыборкаПоРаботникам.НомерПовторяющейсяСтроки + ": Сотрудник " + ВыборкаПоРаботникам.ФизЛицоНаименование + " указан в документе дважды!", Отказ, Заголовок,,, СообщенияРазделПроверки);
					КонецЕсли;
					
					// Контроль дубля строк заработков
					МесяцыДохода = Новый Соответствие;
					Для Каждого СтрокаЗаработка Из Заработки.НайтиСтроки(Новый Структура("ФизЛицо, ИдентификаторСтроки", ВыборкаПоРаботникам.ФизЛицо, ВыборкаПоРаботникам.ИдентификаторСтроки)) Цикл
						МесяцыДохода.Вставить(СтрокаЗаработка.КодМесяца, ?(МесяцыДохода[СтрокаЗаработка.КодМесяца] = Неопределено, 0, МесяцыДохода[СтрокаЗаработка.КодМесяца]) + 1);
					КонецЦикла;
					Для Каждого КлючИЗначение Из МесяцыДохода Цикл
						Если КлючИЗначение.Значение > 1 Тогда
							ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке(СтрокаНачалаСообщенияОбОшибке + "- В строках заработка месяц " + КлючИЗначение.Ключ + " указан более одного раза!", Отказ, Заголовок,,, СообщенияРазделПроверки);
						КонецЕсли;
					КонецЦикла;
					
					МесяцыДохода = Новый Соответствие;
					Для Каждого СтрокаЗаработка Из ЗаработкиВредныеТяжелые.НайтиСтроки(Новый Структура("ФизЛицо, ИдентификаторСтроки", ВыборкаПоРаботникам.ФизЛицо, ВыборкаПоРаботникам.ИдентификаторСтроки)) Цикл
						Если МесяцыДохода[СтрокаЗаработка.КодМесяца] = Неопределено Тогда
							МесяцыДохода.Вставить(СтрокаЗаработка.КодМесяца, Новый Соответствие());
						КонецЕсли;
						
						Если МесяцыДохода[СтрокаЗаработка.КодМесяца][СтрокаЗаработка.КлассУсловийТруда] = Неопределено Тогда
							МесяцыДохода[СтрокаЗаработка.КодМесяца].Вставить(СтрокаЗаработка.КлассУсловийТруда, 1);
						Иначе
							МесяцыДохода[СтрокаЗаработка.КодМесяца].Вставить(СтрокаЗаработка.КлассУсловийТруда, МесяцыДохода[СтрокаЗаработка.КодМесяца][СтрокаЗаработка.КлассУсловийТруда] + 1);
						КонецЕсли;
					КонецЦикла;
					Для Каждого МесяцДохода Из МесяцыДохода Цикл
						Для Каждого КлючИЗначение Из МесяцДохода.Значение Цикл
							Если КлючИЗначение.Значение > 1 Тогда
								ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке(СтрокаНачалаСообщенияОбОшибке + "- В строках заработка на вредных и тяжелых работах месяц " + МесяцДохода.Ключ + " и класс условий " + КлючИЗначение.Ключ+ " указан более одного раза!", Отказ, Заголовок,,, СообщенияРазделПроверки);
							КонецЕсли;
						КонецЦикла;
					КонецЦикла;
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Отказ = Отказ Или Не ПроцедурыПерсонифицированногоУчета.НаборЗаписейОСтажеСоответствуетТребованиям(Ссылка, Истина, СообщенияРазделПроверки);
	
	Если Отказ Тогда
		ЕстьОшибки = Истина;
	КонецЕсли;	 
	
	Если СообщатьОбУспехе И Не ЕстьОшибки Тогда
		Обработка.ДобавитьСообщение("Ошибок не обнаружено.", Перечисления.ВидыСообщений.Информация, , СообщенияРазделПроверки);		
	КонецЕсли;
	
КонецПроцедуры

Функция СформироватьЗапросПоШапкеДокументов()

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументСсылка", Ссылка);

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(РаботникиОрганизации.НомерСтроки) КАК Количество,
	|	РаботникиОрганизации.Ссылка
	|ПОМЕСТИТЬ ВТИтоги
	|ИЗ
	|	Документ.ДанныеОКорректировкеСведенийЗастрахованныхЛицСЗВ_КОРР.РаботникиОрганизации КАК РаботникиОрганизации
	|ГДЕ
	|	РаботникиОрганизации.Ссылка В(&ДокументСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	РаботникиОрганизации.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Документы.Ссылка,
	|	Документы.Номер,
	|	Документы.Дата,
	|	Документы.Проведен,
	|	Документы.Организация,
	|	Документы.Год,
	|	Документы.ТипСведенийСЗВ,
	|	Документы.ПринятоВПФР,
	|	Документы.Ответственный,
	|	ЗНАЧЕНИЕ(Перечисление.ФорматФайлаПФР.Версия07) КАК ФорматФайла,
	|	Документы.Организация.Наименование,
	|	Документы.Организация.ЮрФизЛицо,
	|	Документы.Организация.НаименованиеОКОПФ,
	|	Документы.Организация.НаименованиеПолное,
	|	Документы.Организация.НаименованиеСокращенное,
	|	Документы.Организация.РегистрационныйНомерПФР,
	|	Документы.Организация.ИНН,
	|	Документы.Организация.КПП,
	|	ВЫБОР
	|		КОГДА Документы.Организация.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА Документы.Организация
	|		ИНАЧЕ Документы.Организация.ГоловнаяОрганизация
	|	КОНЕЦ КАК ГоловнаяОрганизация,
	|	Документы.Организация.КодПоОКПО КАК ОКПО,
	|	ЕСТЬNULL(Итоги.Количество, 0) КАК Количество,
	|	Документы.ИмяФайла
	|ИЗ
	|	Документ.ДанныеОКорректировкеСведенийЗастрахованныхЛицСЗВ_КОРР КАК Документы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТИтоги КАК Итоги
	|		ПО Документы.Ссылка = Итоги.Ссылка
	|ГДЕ
	|	Документы.Ссылка В(&ДокументСсылка)";
	
	Возврат Запрос.Выполнить();

КонецФункции 

Функция СформироватьЗапросПоРаботникам()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументСсылка", Ссылка);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РаботникиОрганизации.Ссылка КАК Ссылка,
	|	РаботникиОрганизации.ФизЛицо КАК ФизЛицо,
	|	РаботникиОрганизации.НомерСтроки КАК НомерСтроки,
	|	РаботникиОрганизации.ИдентификаторСтроки,
	|	РаботникиОрганизации.СтраховойНомерПФР,
	|	РаботникиОрганизации.Фамилия,
	|	РаботникиОрганизации.Имя,
	|	РаботникиОрганизации.Отчество,
	|	РаботникиОрганизации.СотрудникУволен,
	|	ВЫБОР
	|		КОГДА РаботникиОрганизации.Ссылка.Организация.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА РаботникиОрганизации.Ссылка.Организация
	|		ИНАЧЕ РаботникиОрганизации.Ссылка.Организация.ГоловнаяОрганизация
	|	КОНЕЦ КАК ГоловнаяОрганизация,
	|	РаботникиОрганизации.КатегорияЗастрахованныхЛиц,
	|	РаботникиОрганизации.ТипДоговора,
	|	РаботникиОрганизации.ДополнительныйТариф
	|ПОМЕСТИТЬ ВТЗастрахованныеЛица
	|ИЗ
	|	Документ.ДанныеОКорректировкеСведенийЗастрахованныхЛицСЗВ_КОРР.РаботникиОрганизации КАК РаботникиОрганизации
	|ГДЕ
	|	РаботникиОрганизации.Ссылка В(&ДокументСсылка)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерСтроки,
	|	ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТЧРаботникиОрг.НомерСтроки КАК НомерСтроки,
	|	ТЧРаботникиОрг.Ссылка
	|ПОМЕСТИТЬ ВТЗарегистрированныеСотрудниками
	|ИЗ
	|	ВТЗастрахованныеЛица КАК ТЧРаботникиОрг
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СотрудникиОрганизаций КАК СотрудникиОрганизаций
	|		ПО ТЧРаботникиОрг.ФизЛицо = СотрудникиОрганизаций.Физлицо
	|			И ТЧРаботникиОрг.ГоловнаяОрганизация = СотрудникиОрганизаций.Организация
	|ГДЕ
	|	СотрудникиОрганизаций.Ссылка ЕСТЬ НЕ NULL 
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ВтораяТаблица.НомерСтроки) КАК НомерПовторяющейсяСтроки,
	|	ПерваяТаблица.Ссылка КАК Ссылка,
	|	ПерваяТаблица.ФизЛицо,
	|	ПерваяТаблица.НомерСтроки КАК НомерСтроки,
	|	ПерваяТаблица.ИдентификаторСтроки,
	|	ПерваяТаблица.СтраховойНомерПФР,
	|	ПерваяТаблица.Фамилия,
	|	ПерваяТаблица.Имя,
	|	ПерваяТаблица.Отчество,
	|	ПерваяТаблица.СотрудникУволен,
	|	ПерваяТаблица.ФизЛицо.Наименование,
	|	ВЫБОР
	|		КОГДА ЗарегистрированныеСотрудниками.НомерСтроки ЕСТЬ НЕ NULL 
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ФизЛицоРаботаетВОрганизации
	|ИЗ
	|	ВТЗастрахованныеЛица КАК ПерваяТаблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТЗарегистрированныеСотрудниками КАК ЗарегистрированныеСотрудниками
	|		ПО ПерваяТаблица.НомерСтроки = ЗарегистрированныеСотрудниками.НомерСтроки
	|			И ПерваяТаблица.Ссылка = ЗарегистрированныеСотрудниками.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТЗастрахованныеЛица КАК ВтораяТаблица
	|		ПО ПерваяТаблица.ФизЛицо = ВтораяТаблица.ФизЛицо
	|			И ПерваяТаблица.НомерСтроки < ВтораяТаблица.НомерСтроки
	|			И ПерваяТаблица.Ссылка = ВтораяТаблица.Ссылка
	|			И ПерваяТаблица.КатегорияЗастрахованныхЛиц = ВтораяТаблица.КатегорияЗастрахованныхЛиц
	|			И ПерваяТаблица.ТипДоговора = ВтораяТаблица.ТипДоговора
	|			И ПерваяТаблица.ДополнительныйТариф = ВтораяТаблица.ДополнительныйТариф
	|
	|СГРУППИРОВАТЬ ПО
	|	ПерваяТаблица.Ссылка,
	|	ПерваяТаблица.ФизЛицо,
	|	ПерваяТаблица.НомерСтроки,
	|	ПерваяТаблица.ИдентификаторСтроки,
	|	ПерваяТаблица.СтраховойНомерПФР,
	|	ПерваяТаблица.Фамилия,
	|	ПерваяТаблица.Имя,
	|	ПерваяТаблица.Отчество,
	|	ПерваяТаблица.ФизЛицо.Наименование,
	|	ПерваяТаблица.СотрудникУволен,
	|	ВЫБОР
	|		КОГДА ЗарегистрированныеСотрудниками.НомерСтроки ЕСТЬ НЕ NULL 
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки";
	
	Возврат Запрос.Выполнить();
	
КонецФункции // СформироватьЗапросПоРаботникам()

Функция КодыМесяцевСЗВ_КОРР() Экспорт 
	
	КодыМесяцев = Новый Соответствие;
	КодыМесяцев.Вставить(1, "Янв");
	КодыМесяцев.Вставить(2, "Фев");
	КодыМесяцев.Вставить(3, "Мрт");
	КодыМесяцев.Вставить(4, "Апр");
	КодыМесяцев.Вставить(5, "Май");
	КодыМесяцев.Вставить(6, "Июн");
	КодыМесяцев.Вставить(7, "Июл");
	КодыМесяцев.Вставить(8, "Авг");
	КодыМесяцев.Вставить(9, "Сен");
	КодыМесяцев.Вставить(10, "Окт");
	КодыМесяцев.Вставить(11, "Нбр");
	КодыМесяцев.Вставить(12, "Дек");
	
	Возврат КодыМесяцев;
	
КонецФункции

Функция КодыКатегорий()
	
	ТаблицаКодовКатегорий = Новый ТаблицаЗначений;
	ТаблицаКодовКатегорий.Колонки.Добавить("Код", Новый ОписаниеТипов("Строка", ,Новый КвалификаторыСтроки(10)));
	ТаблицаКодовКатегорий.Колонки.Добавить("Категория", Новый ОписаниеТипов("ПеречислениеСсылка.КатегорииЗастрахованныхЛицПФР"));
	
	МетаданныхПеречисления = Метаданные.Перечисления.КатегорииЗастрахованныхЛицПФР;
	
	Для каждого Элемент Из Перечисления.КатегорииЗастрахованныхЛицПФР Цикл
		ИндексЗначения = Перечисления.КатегорииЗастрахованныхЛицПФР.Индекс(Элемент);
		
		НоваяСтрока = ТаблицаКодовКатегорий.Добавить();
		НоваяСтрока.Код = МетаданныхПеречисления.ЗначенияПеречисления[ИндексЗначения].Имя;
		НоваяСтрока.Категория = Элемент;
	КонецЦикла;
	
	Возврат ТаблицаКодовКатегорий;
	
КонецФункции

Процедура ОтчетныйПериодРегулирование(ПолеОтчетныйПериод, ПредставлениеОтчетногоПериода, Направление, МинимальныйПериод = '00010101', МаксимальныйПериод = '00010101', Отказ = Ложь) Экспорт 
	
	Если ЗначениеЗаполнено(МинимальныйПериод) 
		И ПолеОтчетныйПериод = МинимальныйПериод
		И Направление = -1 Тогда
		
		Отказ = Истина;
		Возврат;
		
	ИначеЕсли  ЗначениеЗаполнено(МаксимальныйПериод) 
		И ПолеОтчетныйПериод = МаксимальныйПериод
		И Направление = 1 Тогда
		
		Отказ = Истина;
		Возврат;
		
	КонецЕсли;	
	
	Если Не (ПолеОтчетныйПериод = '19960101' И Направление = -1) Тогда
		
		Если ПолеОтчетныйПериод > '20170101' Или (ПолеОтчетныйПериод = '20170101' И Направление = 1) Тогда  
			Шаг = 12;
		ИначеЕсли ПолеОтчетныйПериод > '20110101' Или (ПолеОтчетныйПериод = '20110101' И Направление = 1) Тогда 
			Шаг = 3;
		ИначеЕсли ПолеОтчетныйПериод > '20100101' Или (ПолеОтчетныйПериод = '20100101' И Направление = 1) Тогда
			Шаг = 6;
		Иначе 
			Шаг = 12;
		КонецЕсли;	
		
		ПолеОтчетныйПериод = ДобавитьМесяц(ПолеОтчетныйПериод, Направление * Шаг);
		
		ПредставлениеОтчетногоПериода = ПредставлениеПериода(НачалоГода(ПолеОтчетныйПериод), ПроцедурыПерсонифицированногоУчета.ОкончаниеОтчетногоПериодаПерсучета(ПолеОтчетныйПериод), "ФП = Истина");
		
	КонецЕсли;
	
КонецПроцедуры

Функция КодОтчетногоПериода(ОтчетныйПериод) Экспорт 
	
	Если ОтчетныйПериод >= '20170101' Тогда
		Возврат 0;
	ИначеЕсли ОтчетныйПериод >= '20140101' Тогда
		Если Месяц(ОтчетныйПериод) <= 3 Тогда 
			Возврат 3;
		ИначеЕсли Месяц(ОтчетныйПериод) <= 6 Тогда 
			Возврат 6;
		ИначеЕсли Месяц(ОтчетныйПериод) <= 9 Тогда 
			Возврат 9;
		Иначе 
			Возврат 0;
		КонецЕсли;
	ИначеЕсли ОтчетныйПериод >= '20110101' Тогда
		Если Месяц(ОтчетныйПериод) <= 3 Тогда 
			Возврат 1;
		ИначеЕсли Месяц(ОтчетныйПериод) <= 6 Тогда 
			Возврат 2;
		ИначеЕсли Месяц(ОтчетныйПериод) <= 9 Тогда 
			Возврат 3;
		Иначе 
			Возврат 4;
		КонецЕсли;
	ИначеЕсли ОтчетныйПериод >= '20100101' Тогда
		Если  Месяц(ОтчетныйПериод) <= 6 Тогда
			Возврат 1; 
		Иначе
			Возврат 2;
		КонецЕсли;
	ИначеЕсли ОтчетныйПериод >= '20020101' Тогда
		Возврат 0;
	ИначеЕсли ОтчетныйПериод >= '20010101' Тогда
		Если Месяц(ОтчетныйПериод) <= 3 Тогда 
			Возврат 5;
		ИначеЕсли Месяц(ОтчетныйПериод) <= 6 Тогда 
			Возврат 6;
		ИначеЕсли Месяц(ОтчетныйПериод) <= 9 Тогда 
			Возврат 7;
		Иначе 
			Возврат 8;
		КонецЕсли;
	ИначеЕсли ОтчетныйПериод >= '19970101' Тогда
		Если  Месяц(ОтчетныйПериод) <= 6 Тогда
			Возврат 2; 
		Иначе
			Возврат 3;
		КонецЕсли;
	ИначеЕсли ОтчетныйПериод = '19960101' Тогда 
		Возврат 3;
	Иначе 
		Возврат 4;
	КонецЕсли;
	
КонецФункции

Процедура ВыделитьСтрокиДоговоров(СтруктураПоискаДоговоров, ЗаписиФизлица, ВременнаяТаблицаФизлица)
	
	СтрокиДоговоров = ЗаписиФизлица.НайтиСтроки(СтруктураПоискаДоговоров);
	Для Каждого СтрокаТЗ Из СтрокиДоговоров Цикл
		ЗаполнитьЗначенияСвойств(ВременнаяТаблицаФизлица.Добавить(),СтрокаТЗ);
		ЗаписиФизлица.Удалить(СтрокаТЗ);
	КонецЦикла;
	
КонецПроцедуры

Процедура ВыполнитьНумерациюЗаписейОСтаже(СообщатьОПроблемах) Экспорт
	
	Отказ = Ложь;
	Если СообщатьОПроблемах Тогда
		ОбработкаКомментариев = глЗначениеПеременной("глОбработкаСообщений");
		ОбработкаКомментариев.УдалитьСообщения();
	КонецЕсли;
	
	СтруктураПоиска = Новый Структура("ИдентификаторСтроки");
	СтруктураПоискаДоговоров = Новый Структура("ИдентификаторСтроки,ТретийПараметрИсчисляемогоСтажа");
	ВременнаяТаблица = ЗаписиОСтаже.ВыгрузитьКолонки();
	ВременнаяТаблицаФизлица = ЗаписиОСтаже.ВыгрузитьКолонки();
	
	ТЗЗаписиОСтаже = ЗаписиОСтаже.Выгрузить();
	ТЗЗаписиОСтаже.Сортировать("ФизЛицо, ИдентификаторСтроки, НомерСтроки");
	
	Для Каждого СтрокаТЧ Из РаботникиОрганизации Цикл
		
		СтруктураПоиска.ИдентификаторСтроки = СтрокаТЧ.ИдентификаторСтроки;
		СтруктураПоискаДоговоров.ИдентификаторСтроки = СтрокаТЧ.ИдентификаторСтроки;
		
		ВременнаяТаблицаФизлица.Очистить();
		
		ЗаписиФизлица = ТЗЗаписиОСтаже.Скопировать(СтруктураПоиска);
		
		СтруктураПоискаДоговоров.ТретийПараметрИсчисляемогоСтажа = Справочники.ПараметрыИсчисляемогоСтраховогоСтажа2014.ДОГОВОР;
		ВыделитьСтрокиДоговоров(СтруктураПоискаДоговоров, ЗаписиФизлица, ВременнаяТаблицаФизлица);
		СтруктураПоискаДоговоров.ТретийПараметрИсчисляемогоСтажа = Справочники.ПараметрыИсчисляемогоСтраховогоСтажа2014.НЕОПЛАВТ;
		ВыделитьСтрокиДоговоров(СтруктураПоискаДоговоров, ЗаписиФизлица, ВременнаяТаблицаФизлица);
		СтруктураПоискаДоговоров.ТретийПараметрИсчисляемогоСтажа = Справочники.ПараметрыИсчисляемогоСтраховогоСтажа2014.НЕОПЛДОГ;
		ВыделитьСтрокиДоговоров(СтруктураПоискаДоговоров, ЗаписиФизлица, ВременнаяТаблицаФизлица);
		
		ВременнаяТаблицаФизлица.Сортировать("ДатаНачалаПериода");
		ОбщегоНазначенияЗК.ЗагрузитьВТаблицуЗначений(ЗаписиФизлица, ВременнаяТаблицаФизлица);
		
		//Нумерация записей стажа 
		НомерОсновнойЗаписи = 0; 
		НомерСтроки = 0;
		Для Каждого СтрокаСтажа Из ВременнаяТаблицаФизлица цикл
			
			НомерСтроки = НомерСтроки + 1;
			ТекстОшибкиСНомеромСтроки = "В строке стажа " + НомерСтроки + ": ";
			
			//Контролируем смену основной записи
			Если ЗначениеЗаполнено(СтрокаСтажа.ДатаНачалаПериода) И ЗначениеЗаполнено(СтрокаСтажа.ДатаОкончанияПериода) тогда
				//Встретили основную запись
				НомерОсновнойЗаписи = НомерОсновнойЗаписи + 1; 
				НомерДополнительнойЗаписи = 0;
			ИначеЕсли Не ЗначениеЗаполнено(СтрокаСтажа.ДатаНачалаПериода) И Не ЗначениеЗаполнено(СтрокаСтажа.ДатаОкончанияПериода) тогда	
				Если НомерОсновнойЗаписи <> 0 Тогда
					НомерДополнительнойЗаписи = НомерДополнительнойЗаписи + 1; 
				ИначеЕсли СообщатьОПроблемах Тогда
					ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "не указаны даты начала и окончания периода!", Отказ);
				КонецЕсли;
			ИначеЕсли СообщатьОПроблемах Тогда
				Если ЗначениеЗаполнено(СтрокаСтажа.ДатаНачалаПериода) Тогда
					ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "не указана дата окончания периода!", Отказ);
				Иначе
					ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "не указана начала окончания периода!", Отказ);
				КонецЕсли;
			КонецЕсли;	
			
			СтрокаСтажа.НомерОсновнойЗаписи = НомерОсновнойЗаписи;
			СтрокаСтажа.НомерДополнительнойЗаписи = НомерДополнительнойЗаписи;
			
		КонецЦикла;	
		
		ОбщегоНазначенияЗК.ЗагрузитьВТаблицуЗначений(ВременнаяТаблицаФизлица, ВременнаяТаблица);
		
	КонецЦикла;
	
	ВременнаяТаблица.Сортировать("ФизЛицо, ИдентификаторСтроки, НомерОсновнойЗаписи, НомерДополнительнойЗаписи");
	ЗаписиОСтаже.Загрузить(ВременнаяТаблица);
	
	Если СообщатьОПроблемах И Отказ Тогда
		ОбработкаКомментариев.ПоказатьСообщения();
	КонецЕсли;
	
КонецПроцедуры

Процедура ОчиститьЗначенияКолонок(ИмяТабличнойЧасти, ИменаКолонок)
	
	Если ИменаКолонок.Количество() И ЭтотОбъект[ИмяТабличнойЧасти].Количество() Тогда
		ВременнаяТаблица = ЭтотОбъект[ИмяТабличнойЧасти].Выгрузить();
		Для Каждого ИмяКолонки Из ИменаКолонок Цикл
			ВременнаяТаблица.Колонки.Удалить(ИмяКолонки);
		КонецЦикла;
		ЭтотОбъект[ИмяТабличнойЧасти].Очистить();
		ЭтотОбъект[ИмяТабличнойЧасти].Загрузить(ВременнаяТаблица);
	КонецЕсли;
	
КонецПроцедуры

Функция ВидимостьКолонокТабличныхЧастей(КорректируемыйГод) Экспорт
	
	ВидимостьКолонок = Новый Соответствие();
	ВидимостьКолонок.Вставить("РаботникиОрганизации", Новый Структура);
	ВидимостьКолонок.Вставить("СведенияОЗаработке", Новый Структура);
	ВидимостьКолонок.Вставить("УплаченныеВзносыРасшифровка", Новый Структура);
	
	ВидимостьКолонокТЧ = ВидимостьКолонок["РаботникиОрганизации"];
	ВидимостьКолонокТЧ.Вставить("НомерДоговора", КорректируемыйГод <= 2001);
	ВидимостьКолонокТЧ.Вставить("ДатаДоговора", КорректируемыйГод <= 2001);
	ВидимостьКолонокТЧ.Вставить("ДополнительныйТариф", КорректируемыйГод >= 2000 И КорректируемыйГод <= 2009);
	ВидимостьКолонокТЧ.Вставить("ТипДоговора", КорректируемыйГод >= 2010 И КорректируемыйГод <= 2013);
	
	
	ВидимостьКолонокТЧ = ВидимостьКолонок["СведенияОЗаработке"];
	ВидимостьКолонокТЧ.Вставить("Заработок", КорректируемыйГод <= 2001 Или КорректируемыйГод >= 2010);
	ВидимостьКолонокТЧ.Вставить("ОблагаетсяВзносамиДоПредельнойВеличины", КорректируемыйГод <= 2001 Или КорректируемыйГод >= 2010);
	ВидимостьКолонокТЧ.Вставить("ПоДоговорамГПХДоПредельнойВеличины", КорректируемыйГод >= 2010);
	ВидимостьКолонокТЧ.Вставить("ОблагаетсяВзносамиСвышеПредельнойВеличины", КорректируемыйГод >= 2010);
	ВидимостьКолонокТЧ.Вставить("ПоДоговорамГПХСвышеПредельнойВеличины", КорректируемыйГод >= 2010);
	
	ВидимостьКолонокТЧ.Вставить("УплачиваемыеСтрахователем", КорректируемыйГод <= 2001);
	ВидимостьКолонокТЧ.Вставить("УплачиваемыеИзЗаработкаЗЛ", КорректируемыйГод <= 2000);
	ВидимостьКолонокТЧ.Вставить("ДоначисленоНаОПС", КорректируемыйГод >= 2014);
	ВидимостьКолонокТЧ.Вставить("ДоначисленоСтраховая", КорректируемыйГод >= 2002);
	ВидимостьКолонокТЧ.Вставить("ДоначисленоНакопительная", КорректируемыйГод >= 2002 И КорректируемыйГод <= 2013);
	
	ВидимостьКолонокТЧ.Вставить("УплаченоСтраховая", КорректируемыйГод >= 2010 И КорректируемыйГод <= 2013);
	ВидимостьКолонокТЧ.Вставить("УплаченоНакопительная", КорректируемыйГод >= 2010 И КорректируемыйГод <= 2013);
	
	ВидимостьКолонокТЧ = ВидимостьКолонок["УплаченныеВзносыРасшифровка"];
	ВидимостьКолонокТЧ.Вставить("НаСтраховуюПенсию", КорректируемыйГод >= 2002 И КорректируемыйГод <= 2013);
	ВидимостьКолонокТЧ.Вставить("НаНакопительнуюПенсию", КорректируемыйГод >= 2002 И КорректируемыйГод <= 2013);
	ВидимостьКолонокТЧ.Вставить("ПоТарифуСтраховыхВзносов", КорректируемыйГод >= 2014);

	Возврат ВидимостьКолонок;
	
КонецФункции

// Формирует файл, который можно будет записать на дискетку
//
// Параметры: 
//  Нет
//
// Возвращаемое значение:
//  Строка - содержимое файла
//
Функция СформироватьВыходнойФайл(Отказ) Экспорт
	
	ОбработкаКомментариев = глЗначениеПеременной("глОбработкаСообщений");
	ОбработкаКомментариев.УдалитьСообщения();
	Отказ = Ложь;
	
	//Проверка корректности заполнения документа
	ПроверитьПравильностьЗаполненияДокумента(Отказ, Ложь);
	
	Если Отказ Тогда
		ОбработкаКомментариев.ПоказатьСообщения();
		Возврат "";
	КонецЕсли;
	
	ВыборкаПоШапкеДокумента = ПроцедурыПерсонифицированногоУчета.ЗапросПоШапкеДляПечатиОДВ_1(Ссылка, Дата).Выбрать();
	ВыборкаПоШапкеДокумента.Следующий();
	
	ВыборкаПоДаннымВзносов = ПроцедурыПерсонифицированногоУчета.ЗапросПоДаннымНачисленныхУплаченныхВзносовОДВ_1(Ссылка).Выбрать();
	ВыборкаУплаченныхВзносов = ПроцедурыПерсонифицированногоУчета.ЗапросПоДаннымРасшифровкиУплаченныхВзносовОДВ_1(Ссылка).Выбрать();
	
	// Загружаем формат файла сведений
	МакетФормата = ПолучитьОбщийМакет("ФорматПФР_2017");
	
	ФорматОДВ_1 = ПроцедурыПерсонифицированногоУчета.ЗагрузитьФорматНабораЗаписейдляПФР(МакетФормата, "ОДВ_1");
	
	НаборЗаписейДатаЗаполнения = Новый Структура("ДатаЗаполнения", ПроцедурыПерсонифицированногоУчета.СкопироватьСтруктуруДанных(ФорматОДВ_1.ДатаЗаполнения));
	ФорматОДВ_1.Удалить("ДатаЗаполнения");
	НаборЗаписейРуководитель = Новый Структура("Руководитель", ПроцедурыПерсонифицированногоУчета.СкопироватьСтруктуруДанных(ФорматОДВ_1.Руководитель));
	ФорматОДВ_1.Удалить("Руководитель");
	ФорматОснованияДНП = ПроцедурыПерсонифицированногоУчета.СкопироватьСтруктуруДанных(ФорматОДВ_1.ОснованияДНП.Значение);
	ФорматОДВ_1.Удалить("ОснованияДНП");
	ФорматУплата = ПроцедурыПерсонифицированногоУчета.СкопироватьСтруктуруДанных(ФорматОДВ_1.Уплата.Значение);
	ФорматОДВ_1.Удалить("Уплата");
	
	// Создаем начальное дерево
	ДеревоВыгрузки = ПроцедурыПерсонифицированногоУчета.СоздатьДеревоXML();
	
	Атрибуты = Новый Соответствие;
	Атрибуты.Вставить("xmlns", "http://пф.рф/ВС/СЗВ-КОРР/2017-01-01");
	Атрибуты.Вставить("xmlns:АФ", "http://пф.рф/АФ");
	Атрибуты.Вставить("xmlns:АФ2", "http://пф.рф/АФ/2016-06-21");
	Атрибуты.Вставить("xmlns:УТ", "http://пф.рф/унифицированныеТипы/2014-01-01");
	Атрибуты.Вставить("xmlns:ВС", "http://пф.рф/ВС/типы/2014-01-01");
	Атрибуты.Вставить("xmlns:РВ", "http://пф.рф/ВС/типыРВ/2014-01-01");
	Атрибуты.Вставить("xmlns:ИС", "http://пф.рф/ВС/типыИС/2017-01-01");
	
	УзелПФР = ПроцедурыПерсонифицированногоУчета.ДобавитьУзелВДеревоXML(ДеревоВыгрузки, "ЭДПФР", "", Атрибуты);
	
	// Служебная информация о файле
	НаборЗаписейСлужебнаяИнформация = ПроцедурыПерсонифицированногоУчета.ЗагрузитьФорматНабораЗаписейдляПФР(МакетФормата, "СлужебнаяИнформация");
	Если ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.ИмяФайлаДляПФР) Тогда
		ЧастиИмениФайла = ОбщегоНазначенияЗК.РазложитьСтрокуВМассивПодстрок(СокрЛП(ВыборкаПоШапкеДокумента.ИмяФайлаДляПФР),"_");
		НаборЗаписейСлужебнаяИнформация.АФ__GUID.Значение = ЧастиИмениФайла[ЧастиИмениФайла.ВГраница()];
	КонецЕсли;
	НаборЗаписейСлужебнаяИнформация.АФ__ДатаВремя.Значение = ПроцедурыПерсонифицированногоУчета.ПредставлениеДатыВФорматеdateTimeСЧасовымПоясом(ВыборкаПоШапкеДокумента.Дата);
	НаборЗаписейСлужебнаяИнформация.АФ2__ПрограммаПодготовки.Значение =  ИсправленныйСимвольныйЭлемент("1С:" + РегламентированнаяОтчетность.ИДКонфигурации()) + " " + РегламентированнаяОтчетность.РедакцияКонфигурации();
	ПроцедурыПерсонифицированногоУчета.ДобавитьИнформациюВДерево(ПроцедурыПерсонифицированногоУчета.ДобавитьУзелВДеревоXML(УзелПФР, "СлужебнаяИнформация",""), НаборЗаписейСлужебнаяИнформация);
	
	// Данные формы и страхователя
	ФорматОДВ_1.Тип.Значение = 0;
	НаборЗаписейСтрахователь = ФорматОДВ_1.Страхователь.Значение;
	НаборЗаписейСтрахователь.ИС__Наименование = ИсправленныйСимвольныйЭлемент(ВыборкаПоШапкеДокумента.НаименованиеСокращенное);
	НаборЗаписейСтрахователь.УТ__РегНомер = ВыборкаПоШапкеДокумента.РегистрационныйНомерПФР;
	НаборЗаписейСтрахователь.УТ__ИНН = ВыборкаПоШапкеДокумента.ИНН;
	Если ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.КПП) Тогда
		НаборЗаписейСтрахователь.УТ__КПП = ВыборкаПоШапкеДокумента.КПП;
	Иначе
		НаборЗаписейСтрахователь.Удалить("УТ__КПП");
		СтрокаПоляКПП = ФорматОДВ_1.Страхователь.Поля.Найти("УТ__КПП", "ИмяПоля");
		Если СтрокаПоляКПП <> Неопределено Тогда
			ФорматОДВ_1.Страхователь.Поля.Удалить(СтрокаПоляКПП);
		КонецЕсли;
	КонецЕсли;
	НаборЗаписейОтчетныйПериод = ФорматОДВ_1.ОтчетныйПериод.Значение;
	НаборЗаписейОтчетныйПериод.Код = 0;
	НаборЗаписейОтчетныйПериод.Год = ВыборкаПоШапкеДокумента.Год;
	
	ФорматОДВ_1.КоличествоЗЛ.Значение = ВыборкаПоШапкеДокумента.КоличествоЗастрахованныхЛиц;
	
	Если ВыборкаПоДаннымВзносов.Следующий()  
		И (ВыборкаПоДаннымВзносов.ЕстьСтраховая Или ВыборкаПоДаннымВзносов.ЕстьНакопительная Или ВыборкаПоДаннымВзносов.ЕстьТарифСВ) Тогда 
		
		ФорматОДВ_1.Страховая.Значение.ИС__ЗадолженностьНаНачало = ВыборкаПоДаннымВзносов.ЗадолженностьНаНачалоПериодаСтраховая;
		ФорматОДВ_1.Страховая.Значение.ИС__Начислено = ВыборкаПоДаннымВзносов.НачисленоСтраховая;
		ФорматОДВ_1.Страховая.Значение.ИС__Уплачено = ВыборкаПоДаннымВзносов.УплаченоСтраховая;
		ФорматОДВ_1.Страховая.Значение.ИС__ЗадолженностьНаКонец = ВыборкаПоДаннымВзносов.ЗадолженностьНаКонецПериодаСтраховая;
		
		ФорматОДВ_1.Накопительная.Значение.ИС__ЗадолженностьНаНачало = ВыборкаПоДаннымВзносов.ЗадолженностьНаНачалоПериодаНакопительная;
		ФорматОДВ_1.Накопительная.Значение.ИС__Начислено = ВыборкаПоДаннымВзносов.НачисленоНакопительная;
		ФорматОДВ_1.Накопительная.Значение.ИС__Уплачено = ВыборкаПоДаннымВзносов.УплаченоНакопительная;
		ФорматОДВ_1.Накопительная.Значение.ИС__ЗадолженностьНаКонец = ВыборкаПоДаннымВзносов.ЗадолженностьНаКонецПериодаНакопительная;
		
		ФорматОДВ_1.ТарифСВ.Значение.ИС__ЗадолженностьНаНачало = ВыборкаПоДаннымВзносов.ЗадолженностьНаНачалоПериодаТарифСВ;
		ФорматОДВ_1.ТарифСВ.Значение.ИС__Начислено = ВыборкаПоДаннымВзносов.НачисленоТарифСВ;
		ФорматОДВ_1.ТарифСВ.Значение.ИС__Уплачено = ВыборкаПоДаннымВзносов.УплаченоТарифСВ;
		ФорматОДВ_1.ТарифСВ.Значение.ИС__ЗадолженностьНаКонец = ВыборкаПоДаннымВзносов.ЗадолженностьНаКонецПериодаТарифСВ;
		
	Иначе
		ФорматОДВ_1.Удалить("Страховая");
		ФорматОДВ_1.Удалить("Накопительная");
		ФорматОДВ_1.Удалить("ТарифСВ");
	КонецЕсли;
	
	УзелФормы = ПроцедурыПерсонифицированногоУчета.ДобавитьУзелВДеревоXML(УзелПФР, "ОДВ-1", "");
	ПроцедурыПерсонифицированногоУчета.ДобавитьИнформациюВДерево(УзелФормы, ФорматОДВ_1);
	
	Пока ВыборкаУплаченныхВзносов.Следующий() Цикл 
		НаборЗаписейТипУплата = ПроцедурыПерсонифицированногоУчета.СкопироватьСтруктуруДанных(ФорматУплата);
		НаборЗаписейТипУплата.ИС__Год.Значение = ВыборкаУплаченныхВзносов.УплатаВзносовГод;
		НаборЗаписейТипУплата.ИС__Страховая.Значение = ВыборкаУплаченныхВзносов.НаСтраховуюПенсию;
		НаборЗаписейТипУплата.ИС__Накопительная.Значение = ВыборкаУплаченныхВзносов.НаНакопительнуюПенсию;
		НаборЗаписейТипУплата.ИС__ТарифСВ.Значение = ВыборкаУплаченныхВзносов.ПоТарифуСтраховыхВзносов;
		УзелУплата = ПроцедурыПерсонифицированногоУчета.ДобавитьУзелВДеревоXML(УзелФормы, "Уплата", "");
		ПроцедурыПерсонифицированногоУчета.ДобавитьИнформациюВДерево(УзелУплата, НаборЗаписейТипУплата);
	КонецЦикла;
	
	НаборЗаписейРуководитель.Руководитель.Значение.Должность.Значение = ВРег(ВыборкаПоШапкеДокумента.ДолжностьРуководителя);
	НаборЗаписейФИО = НаборЗаписейРуководитель.Руководитель.Значение.ФИО.Значение;
	НаборЗаписейФИО.УТ__Фамилия = ВРег(ВыборкаПоШапкеДокумента.ФамилияРуководителя);
	НаборЗаписейФИО.УТ__Имя = ВРег(ВыборкаПоШапкеДокумента.ИмяРуководителя);
	НаборЗаписейФИО.УТ__Отчество = ВРег(ВыборкаПоШапкеДокумента.ОтчествоРуководителя);
	ПроцедурыПерсонифицированногоУчета.ДобавитьИнформациюВДерево(УзелФормы, НаборЗаписейРуководитель);
	
	НаборЗаписейДатаЗаполнения.ДатаЗаполнения.Значение = Формат(ВыборкаПоШапкеДокумента.Дата, "ДФ=yyyy-MM-dd");
	ПроцедурыПерсонифицированногоУчета.ДобавитьИнформациюВДерево(УзелФормы, НаборЗаписейДатаЗаполнения);
	
	ВыборкаПоШапкеДокумента = ЗапросПоШапкеДляПечати().Выбрать();
	ВыборкаПоШапкеДокумента.Следующий();
	
	ВыборкаПоРаботникам = ЗапросПоСотрудникамДляПечати().Выбрать();
	ВыборкаПоДоходамВзносам = ЗапросПоЗаработкуИВзносамДляПечати().Выбрать();
	ВыборкаПоДоходамНаВредныхИТяжелыхРаботах = ЗапросПоДоходамЗанятыхНаВредныхИТяжелыхРаботахДляПечати().Выбрать();
	
	ФорматСЗВ_КОРР = ПроцедурыПерсонифицированногоУчета.ЗагрузитьФорматНабораЗаписейдляПФР(МакетФормата, "СЗВ_КОРР");
	
	Если Не ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.КПП) Тогда
		ФорматСЗВ_КОРР.Страхователь.Значение.Удалить("УТ__КПП");
		СтрокаПоляКПП = ФорматСЗВ_КОРР.Страхователь.Поля.Найти("УТ__КПП", "ИмяПоля");
		Если СтрокаПоляКПП <> Неопределено Тогда
			ФорматСЗВ_КОРР.Страхователь.Поля.Удалить(СтрокаПоляКПП);
		КонецЕсли;		
	КонецЕсли;
	
	СтрокаПоляНаименование = ФорматСЗВ_КОРР.КорректируемыйПериод.Значение.Страхователь.Поля.Найти("ИС__Наименование", "ИмяПоля");
	Если СтрокаПоляНаименование <> Неопределено Тогда
		ФорматСЗВ_КОРР.КорректируемыйПериод.Значение.Страхователь.Поля.Удалить(СтрокаПоляНаименование);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.КППвКорректируемыйПериод) Тогда
		ФорматСЗВ_КОРР.КорректируемыйПериод.Значение.Страхователь.Значение.Удалить("УТ__КПП");
		СтрокаПоляКПП = ФорматСЗВ_КОРР.КорректируемыйПериод.Значение.Страхователь.Поля.Найти("УТ__КПП", "ИмяПоля");
		Если СтрокаПоляКПП <> Неопределено Тогда
			ФорматСЗВ_КОРР.КорректируемыйПериод.Значение.Страхователь.Поля.Удалить(СтрокаПоляКПП);
		КонецЕсли;		
	КонецЕсли;
	
	ФорматЗЛ = Новый Структура("ЗЛ", ПроцедурыПерсонифицированногоУчета.СкопироватьСтруктуруДанных(ФорматСЗВ_КОРР.ЗЛ));
	ФорматЗЛ.ЗЛ.Значение.Удалить("СтажевыйПериод");
	ФорматЗЛ.ЗЛ.Значение.Удалить("ДатаУвольнения");
	ФорматСЗВ_КОРР.Удалить("ЗЛ");
	
	ФорматДанныеЗЛ = Новый Структура("ДанныеЗЛ", ПроцедурыПерсонифицированногоУчета.СкопироватьСтруктуруДанных(ФорматСЗВ_КОРР.ДанныеЗЛ));
	ФорматСЗВ_КОРР.Удалить("ДанныеЗЛ");
	
	ФорматСуммы = Новый Структура("Суммы", ПроцедурыПерсонифицированногоУчета.СкопироватьСтруктуруДанных(ФорматСЗВ_КОРР.Суммы));
	ФорматСЗВ_КОРР.Удалить("Суммы");
	
	ФорматВыплатыДТ = Новый Структура("ВыплатыДТ", ПроцедурыПерсонифицированногоУчета.СкопироватьСтруктуруДанных(ФорматСЗВ_КОРР.ВыплатыДТ));
	ФорматСЗВ_КОРР.Удалить("ВыплатыДТ");
	
	ФорматСтажевыйПериод = ПроцедурыПерсонифицированногоУчета.СкопироватьСтруктуруДанных(ФорматСЗВ_КОРР.СтажевыйПериод.Значение);
	ФорматСЗВ_КОРР.Удалить("СтажевыйПериод");
	
	Пока ВыборкаПоРаботникам.СледующийПоЗначениюПоля("НомерСтроки") Цикл
		
		// Данные формы и страхователя
		НаборЗаписейСтрахователь = ФорматСЗВ_КОРР.Страхователь.Значение;
		НаборЗаписейСтрахователь.ИС__Наименование = ВРег(ВыборкаПоШапкеДокумента.НаименованиеСокращенное);
		НаборЗаписейСтрахователь.УТ__РегНомер = ВыборкаПоШапкеДокумента.РегистрационныйНомерПФР;
		НаборЗаписейСтрахователь.УТ__ИНН = ВыборкаПоШапкеДокумента.ИНН;
		Если ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.КПП) Тогда
			НаборЗаписейСтрахователь.УТ__КПП = ВыборкаПоШапкеДокумента.КПП;
		КонецЕсли;
		
		НаборЗаписейОтчетныйПериод = ФорматСЗВ_КОРР.ОтчетныйПериод.Значение;
		НаборЗаписейОтчетныйПериод.Код = КодОтчетногоПериода(ВыборкаПоШапкеДокумента.ОтчетныйПериод);
		НаборЗаписейОтчетныйПериод.Год = Год(ВыборкаПоШапкеДокумента.ОтчетныйПериод);
		
		Если ВыборкаПоШапкеДокумента.ТипСведений = Перечисления.ТипыСведенийСЗВ_КОРР.Корректирующая Тогда
			ФорматСЗВ_КОРР.Тип.Значение = 0;
		ИначеЕсли ВыборкаПоШапкеДокумента.ТипСведений = Перечисления.ТипыСведенийСЗВ_КОРР.Отменяющая Тогда
			ФорматСЗВ_КОРР.Тип.Значение = 1;
		ИначеЕсли ВыборкаПоШапкеДокумента.ТипСведений = Перечисления.ТипыСведенийСЗВ_КОРР.Особая Тогда
			ФорматСЗВ_КОРР.Тип.Значение = 2;
		КонецЕсли;
		
		НаборЗаписейКорректируемыйПериод = ФорматСЗВ_КОРР.КорректируемыйПериод.Значение;
		НаборЗаписейКорректируемыйПериод.ОтчетныйПериод.Значение.Код = КодОтчетногоПериода(ВыборкаПоШапкеДокумента.КорректируемыйПериод);
		НаборЗаписейКорректируемыйПериод.ОтчетныйПериод.Значение.Год = Год(ВыборкаПоШапкеДокумента.КорректируемыйПериод);
		
		НаборЗаписейКорректируемыйПериод.Страхователь.Значение.УТ__РегНомер = ВыборкаПоШапкеДокумента.РегистрационныйНомерПФРвКорректируемыйПериод;
		НаборЗаписейКорректируемыйПериод.Страхователь.Значение.УТ__ИНН = ВыборкаПоШапкеДокумента.ИННвКорректируемыйПериод;
		НаборЗаписейКорректируемыйПериод.Страхователь.Значение.Удалить("ИС__Наименование");
		Если ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.КППвКорректируемыйПериод) Тогда
			НаборЗаписейКорректируемыйПериод.Страхователь.Значение.УТ__КПП = ВыборкаПоШапкеДокумента.КППвКорректируемыйПериод;
		КонецЕсли;
		
		УзелФормы = ПроцедурыПерсонифицированногоУчета.ДобавитьУзелВДеревоXML(УзелПФР, "СЗВ-КОРР", "");
		ПроцедурыПерсонифицированногоУчета.ДобавитьИнформациюВДерево(УзелФормы, ФорматСЗВ_КОРР);
		
		// Данные по физлицу
		НаборЗаписейФИО = ФорматЗЛ.ЗЛ.Значение.ФИО.Значение;
		НаборЗаписейФИО.УТ__Фамилия = ВРег(СокрЛП(ВыборкаПоРаботникам.Фамилия));
		НаборЗаписейФИО.УТ__Имя = ВРег(СокрЛП(ВыборкаПоРаботникам.Имя));
		НаборЗаписейФИО.УТ__Отчество = ВРег(СокрЛП(ВыборкаПоРаботникам.Отчество));
		
		ФорматЗЛ.ЗЛ.Значение.СНИЛС.Значение = ВыборкаПоРаботникам.СтраховойНомерПФР;
		ПроцедурыПерсонифицированногоУчета.ДобавитьИнформациюВДерево(УзелФормы, ФорматЗЛ);
		
		// Данные ЗЛ
		НаборЗаписейДанныеЗЛ = ПроцедурыПерсонифицированногоУчета.СкопироватьСтруктуруДанных(ФорматДанныеЗЛ);
		
		Если ЗначениеЗаполнено(ВыборкаПоРаботникам.КодКатегории) Тогда
			НаборЗаписейДанныеЗЛ.ДанныеЗЛ.Значение.Категория.Значение = ВыборкаПоРаботникам.КодКатегории;
		Иначе
			НаборЗаписейДанныеЗЛ.ДанныеЗЛ.Значение.Удалить("Категория");
		КонецЕсли;
		
		Если ВыборкаПоРаботникам.ДополнительныйТариф Тогда
			НаборЗаписейДанныеЗЛ.ДанныеЗЛ.Значение.КодДТ.Значение = "АВИА";
		Иначе 
			НаборЗаписейДанныеЗЛ.ДанныеЗЛ.Значение.Удалить("КодДТ");
		КонецЕсли;
		
		НаборЗаписейДоговор = НаборЗаписейДанныеЗЛ.ДанныеЗЛ.Значение.Договор.Значение;
		Если ЗначениеЗаполнено(ВыборкаПоРаботникам.ТипДоговора) Тогда
			НаборЗаписейДоговор.Тип.Значение = ВыборкаПоРаботникам.ТипДоговора;
		Иначе
			НаборЗаписейДоговор.Удалить("Тип");
		КонецЕсли;
		Если ЗначениеЗаполнено(ВыборкаПоРаботникам.НомерДоговора) Тогда 
			НаборЗаписейДоговор.Реквизиты.Значение.УТ__Дата = ВыборкаПоРаботникам.ДатаДоговора;
			НаборЗаписейДоговор.Реквизиты.Значение.УТ__Номер = ВыборкаПоРаботникам.НомерДоговора;
		Иначе 
			НаборЗаписейДоговор.Удалить("Реквизиты");
		КонецЕсли;
		
		Если Не (ЗначениеЗаполнено(ВыборкаПоРаботникам.ТипДоговора) Или ЗначениеЗаполнено(ВыборкаПоРаботникам.НомерДоговора)) Тогда
			НаборЗаписейДанныеЗЛ.ДанныеЗЛ.Значение.Удалить("Договор");
		КонецЕсли;
		
		ПроцедурыПерсонифицированногоУчета.ДобавитьИнформациюВДерево(УзелФормы, НаборЗаписейДанныеЗЛ);
		
		СтруктураПоиска = Новый Структура("Ссылка, Сотрудник, ИдентификаторСтроки, КодМесяца");
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, ВыборкаПоРаботникам);
		
		СписокМесяцев = Новый Массив;
		КодыМесяцев = КодыМесяцевСЗВ_КОРР();
		ПервыйМесяц = Месяц(ВыборкаПоШапкеДокумента.КорректируемыйПериод);
		ПоследнийМесяц = Месяц(ПроцедурыПерсонифицированногоУчета.ОкончаниеОтчетногоПериодаПерсучета(ВыборкаПоШапкеДокумента.КорректируемыйПериод));
		Для Сч = ПервыйМесяц По ПоследнийМесяц Цикл 
			СписокМесяцев.Добавить(КодыМесяцев[Сч]);
		КонецЦикла;
		
		// Суммы
		Для Каждого КодМесяца Из СписокМесяцев Цикл 
			СтруктураПоиска.КодМесяца = КодМесяца;
			НаборЗаписейСуммы = ПроцедурыПерсонифицированногоУчета.СкопироватьСтруктуруДанных(ФорматСуммы);
			НаборЗаписейСуммы.Суммы.Значение.Месяц.Значение = КодМесяца;
			ВыводитьВФайл = Ложь;
			
			Если ВыборкаПоДоходамВзносам.НайтиСледующий(СтруктураПоиска) Тогда
				Если ВыборкаПоДоходамВзносам.Заработок <> 0
					Или ВыборкаПоДоходамВзносам.ОблагаетсяВзносамиДоПредельнойВеличины <> 0
					Или ВыборкаПоДоходамВзносам.ПоДоговорамГПХДоПредельнойВеличины <> 0
					Или ВыборкаПоДоходамВзносам.ОблагаетсяВзносамиСвышеПредельнойВеличины <> 0
					Или ВыборкаПоДоходамВзносам.ПоДоговорамГПХСвышеПредельнойВеличины <> 0 Тогда
					НаборЗаписейВыплаты = НаборЗаписейСуммы.Суммы.Значение.Выплаты.Значение;
					НаборЗаписейВыплаты.СуммаВыплат.Значение = ВыборкаПоДоходамВзносам.Заработок;
					НаборЗаписейВыплаты.НеПревышающие.Значение.Всего = ВыборкаПоДоходамВзносам.ОблагаетсяВзносамиДоПредельнойВеличины;
					НаборЗаписейВыплаты.НеПревышающие.Значение.ПоГПД = ВыборкаПоДоходамВзносам.ПоДоговорамГПХДоПредельнойВеличины;
					НаборЗаписейВыплаты.Превышающие.Значение.Всего = ВыборкаПоДоходамВзносам.ОблагаетсяВзносамиСвышеПредельнойВеличины;
					НаборЗаписейВыплаты.Превышающие.Значение.ПоГПД = ВыборкаПоДоходамВзносам.ПоДоговорамГПХСвышеПредельнойВеличины;
					Если ВыборкаПоДоходамВзносам.ОблагаетсяВзносамиДоПредельнойВеличины = 0 Тогда 
						НаборЗаписейВыплаты.Удалить("НеПревышающие");
					КонецЕсли;
					Если ВыборкаПоДоходамВзносам.ОблагаетсяВзносамиСвышеПредельнойВеличины = 0 Тогда 
						НаборЗаписейВыплаты.Удалить("Превышающие");
					КонецЕсли;
					ВыводитьВФайл = Истина;
				Иначе
					НаборЗаписейСуммы.Суммы.Значение.Удалить("Выплаты");
				КонецЕсли;
				
				Если ВыборкаПоДоходамВзносам.УплачиваемыеСтрахователем <> 0
					Или ВыборкаПоДоходамВзносам.УплачиваемыеИзЗаработкаЗЛ <> 0
					Или ВыборкаПоДоходамВзносам.ДоначисленоСтраховая <> 0
					Или ВыборкаПоДоходамВзносам.ДоначисленоНакопительная <> 0
					Или ВыборкаПоДоходамВзносам.ДоначисленоНаОПС <> 0 Тогда
					НаборЗаписейДоначисленоСВ = НаборЗаписейСуммы.Суммы.Значение.ДоначисленоСВ.Значение;
					Если ВыборкаПоШапкеДокумента.КорректируемыйПериод < '20020101' Тогда 
						НаборЗаписейДоначисленоСВ.СВстрахователя.Значение = ВыборкаПоДоходамВзносам.УплачиваемыеСтрахователем;
						НаборЗаписейДоначисленоСВ.СВизЗаработка.Значение = ВыборкаПоДоходамВзносам.УплачиваемыеИзЗаработкаЗЛ;
						НаборЗаписейДоначисленоСВ.Удалить("Страховая");
						НаборЗаписейДоначисленоСВ.Удалить("Накопительная");
						НаборЗаписейДоначисленоСВ.Удалить("СВпоТарифу");
					ИначеЕсли ВыборкаПоШапкеДокумента.КорректируемыйПериод < '20140101'Тогда 
						НаборЗаписейДоначисленоСВ.Страховая.Значение = ВыборкаПоДоходамВзносам.ДоначисленоСтраховая;
						НаборЗаписейДоначисленоСВ.Накопительная.Значение = ВыборкаПоДоходамВзносам.ДоначисленоНакопительная;
						НаборЗаписейДоначисленоСВ.Удалить("СВстрахователя");
						НаборЗаписейДоначисленоСВ.Удалить("СВизЗаработка");
						НаборЗаписейДоначисленоСВ.Удалить("СВпоТарифу");
					Иначе
						НаборЗаписейДоначисленоСВ.СВпоТарифу.Значение = ВыборкаПоДоходамВзносам.ДоначисленоНаОПС;
						НаборЗаписейДоначисленоСВ.Удалить("СВстрахователя");
						НаборЗаписейДоначисленоСВ.Удалить("СВизЗаработка");
						НаборЗаписейДоначисленоСВ.Удалить("Страховая");
						НаборЗаписейДоначисленоСВ.Удалить("Накопительная");
					КонецЕсли;
					ВыводитьВФайл = Истина;
				Иначе
					НаборЗаписейСуммы.Суммы.Значение.Удалить("ДоначисленоСВ");
				КонецЕсли;
				
				Если ВыборкаПоДоходамВзносам.УплаченоСтраховая <> 0 
					Или ВыборкаПоДоходамВзносам.УплаченоНакопительная <> 0 Тогда
					НаборЗаписейУплата = НаборЗаписейСуммы.Суммы.Значение.Уплата.Значение;
					НаборЗаписейУплата.Страховая = ВыборкаПоДоходамВзносам.УплаченоСтраховая;
					НаборЗаписейУплата.Накопительная = ВыборкаПоДоходамВзносам.УплаченоНакопительная;
					ВыводитьВФайл = Истина;
				Иначе
					НаборЗаписейСуммы.Суммы.Значение.Удалить("Уплата");
				КонецЕсли;
				
			КонецЕсли;
			
			Если ВыводитьВФайл Тогда 
				ПроцедурыПерсонифицированногоУчета.ДобавитьИнформациюВДерево(УзелФормы, НаборЗаписейСуммы);
			КонецЕсли;
		КонецЦикла;
		
		// ВыплатыДТ
		Для Каждого КодМесяца Из СписокМесяцев Цикл 
			СтруктураПоиска.КодМесяца = КодМесяца;
			НаборЗаписейВыплатыДТ = ПроцедурыПерсонифицированногоУчета.СкопироватьСтруктуруДанных(ФорматВыплатыДТ);
			НаборЗаписейВыплатыДТ.ВыплатыДТ.Значение.Месяц = КодМесяца;
			ВыводитьВФайл = Ложь;
			Если ВыборкаПоДоходамНаВредныхИТяжелыхРаботах.НайтиСледующий(СтруктураПоиска) Тогда
				НаборЗаписейВыплатыДТ.ВыплатыДТ.Значение.КодСОУТ = ВыборкаПоДоходамНаВредныхИТяжелыхРаботах.КодКлассаУсловийТруда;
				НаборЗаписейВыплатыДТ.ВыплатыДТ.Значение.ИС__ДопТарифП1 = ВыборкаПоДоходамНаВредныхИТяжелыхРаботах.ОблагаетсяВзносамиЗаЗанятыхНаПодземныхИВредныхРаботах;
				НаборЗаписейВыплатыДТ.ВыплатыДТ.Значение.ИС__ДопТарифП2_18 = ВыборкаПоДоходамНаВредныхИТяжелыхРаботах.ОблагаетсяВзносамиЗаЗанятыхНаТяжелыхИПрочихРаботах;
				ВыводитьВФайл = Истина;
			КонецЕсли;
			Если ВыводитьВФайл Тогда 
				ПроцедурыПерсонифицированногоУчета.ДобавитьИнформациюВДерево(УзелФормы, НаборЗаписейВыплатыДТ);
			КонецЕсли;
		КонецЦикла;
		
		ВписатьЗаписиОСтажеВНаборДанных(УзелФормы, ПроцедурыПерсонифицированногоУчета.СкопироватьСтруктуруДанных(ФорматСтажевыйПериод), ВыборкаПоРаботникам);
		
	КонецЦикла;
	
	// Преобразуем дерево в строковое описание XML
	ПотокВыгрузкиXML = ПроцедурыПерсонифицированногоУчета.СоздатьПотокXML(, "UTF-8");
	СправкиПоНДФЛ.ЗаписатьУзелДереваXMLВXML(ДеревоВыгрузки, ПотокВыгрузкиXML, "xsi", "http://www.w3.org/2001/XMLSchema-instance");
	ТекстФайла = ПроцедурыПерсонифицированногоУчета.ЗаписатьПотокXML(ПотокВыгрузкиXML);
	
	Возврат СтрЗаменить(ТекстФайла, "__", ":");
	
КонецФункции

Процедура ВписатьЗаписиОСтажеВНаборДанных(УзелСотрудника, ФорматСтажевыйПериод, ВыборкаПоРаботникам) 
	
	ФорматЛьготныйСтаж = ФорматСтажевыйПериод.ИС__ЛьготныйСтаж.Значение;
	ФорматСтажевыйПериод.Удалить("ИС__ЛьготныйСтаж");
	ПараметрыСтажаСПравомНаДосрочнуюПенсию = ПроцедурыПерсонифицированногоУчета.ПараметрыИсчисляемогоСтажаПредусматривающиеПравоНаДосрочнуюПенсию();
	
	Если ЗначениеЗаполнено(ВыборкаПоРаботникам.НомерОсновнойЗаписи) Тогда
		
		Пока ВыборкаПоРаботникам.СледующийПоЗначениюПоля("НомерОсновнойЗаписи") Цикл
			
			УзелСтажевыйПериод = ПроцедурыПерсонифицированногоУчета.ДобавитьУзелВДеревоXML(УзелСотрудника, "СтажевыйПериод", "");
			ПроцедурыПерсонифицированногоУчета.ДобавитьИнформациюВДерево(УзелСтажевыйПериод, ФорматСтажевыйПериод);
			
			УзелСтажевыйПериод.Строки.Найти("ИС__Период").Строки.Найти("УТ__С").Значение = Формат(ВыборкаПоРаботникам.ДатаНачалаПериода, "ДФ=yyyy-MM-dd");
			УзелСтажевыйПериод.Строки.Найти("ИС__Период").Строки.Найти("УТ__По").Значение = Формат(ВыборкаПоРаботникам.ДатаОкончанияПериода, "ДФ=yyyy-MM-dd");
			
			НаборЗаписейЛьготныйСтаж = ПроцедурыПерсонифицированногоУчета.СкопироватьСтруктуруДанных(ФорматЛьготныйСтаж);
			
			ВыводитьЛьготныйСтаж = Ложь;
			НеВыводитьВФайлТУ = Ложь;
			НеВыводитьВФайлОУТ = Ложь;
			НеВыводитьВФайлИС = Ложь;
			НеВыводитьВФайлДопСведенияИС = Ложь;
			НеВыводитьВФайлВЛ = Ложь;
			
			// ДопСведенияИС
			Если ЗначениеЗаполнено(ВыборкаПоРаботникам.ТретийПараметрИсчисляемогоСтажа) Тогда
				НаборЗаписейЛьготныйСтаж.ИС__ДопСведенияИС.Значение = ВыборкаПоРаботникам.ТретийПараметрИсчисляемогоСтажаКод;
				ВыводитьЛьготныйСтаж = Истина;
			Иначе
				НеВыводитьВФайлДопСведенияИС = Истина;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ВыборкаПоРаботникам.ТретийПараметрИсчисляемогоСтажа)
				И ПараметрыСтажаСПравомНаДосрочнуюПенсию.Найти(ВыборкаПоРаботникам.ТретийПараметрИсчисляемогоСтажа) = Неопределено Тогда 
				
				НеВыводитьВФайлВЛ = Истина;
				НеВыводитьВФайлИС = Истина;
				НеВыводитьВФайлОУТ = Истина;
				НеВыводитьВФайлТУ = Истина;
				
			Иначе
				
				// Территориальные условия
				НаборЗаписейТерриториальныеУсловия = НаборЗаписейЛьготныйСтаж.ИС__ТУ.Значение;
				Если ЗначениеЗаполнено(ВыборкаПоРаботникам.ТерриториальныеУсловияКод) Тогда
					НаборЗаписейТерриториальныеУсловия.ИС__Основание = СокрЛП(ВыборкаПоРаботникам.ТерриториальныеУсловияКод);
					НаборЗаписейТерриториальныеУсловия.ИС__Коэффициент = ВыборкаПоРаботникам.ТерриториальныеУсловияСтавка;
					Если ВыборкаПоРаботникам.ТерриториальныеУсловия = Справочники.ТерриториальныеУсловия.МКС 
						Или ВыборкаПоРаботникам.ТерриториальныеУсловия = Справочники.ТерриториальныеУсловия.РКС 
						Или ВыборкаПоРаботникам.ТерриториальныеУсловия = Справочники.ТерриториальныеУсловия.РКСМ 
						Или ВыборкаПоРаботникам.ТерриториальныеУсловия = Справочники.ТерриториальныеУсловия.МКСР Тогда
						НаборЗаписейТерриториальныеУсловия.ИС__Коэффициент = ВыборкаПоРаботникам.ТерриториальныеУсловияСтавка;
					Иначе
						ОписаниеПолей = НаборЗаписейЛьготныйСтаж.ИС__ТУ.Поля;
						СтрокаКУдалению = ОписаниеПолей.Найти("ИС__Коэффициент", "ИмяПоля");
						ОписаниеПолей.Удалить(СтрокаКУдалению);
					КонецЕсли;
				Иначе
					НеВыводитьВФайлТУ = Истина;	
				КонецЕсли;	
				
				ВыводитьЛьготныйСтаж = ВыводитьЛьготныйСтаж Или ЗначениеЗаполнено(НаборЗаписейТерриториальныеУсловия.ИС__Основание);
				
				// Особые условия труда
				НаборЗаписейОсобыеУсловияТруда = НаборЗаписейЛьготныйСтаж.ИС__ОУТ.Значение;
				Если ЗначениеЗаполнено(ВыборкаПоРаботникам.ОсобыеУсловияТрудаКод) Тогда
					НаборЗаписейОсобыеУсловияТруда.ИС__Код = СокрЛП(ВыборкаПоРаботникам.ОсобыеУсловияТрудаКод);
					НаборЗаписейОсобыеУсловияТруда.ИС__ПозицияСписка = ИсправленныйСимвольныйЭлемент(СокрЛП(ВыборкаПоРаботникам.КодПозицииСпискаКод));
					ВыводитьЛьготныйСтаж = Истина;
				Иначе
					НеВыводитьВФайлОУТ = Истина;
				КонецЕсли;	
				
				// Исчисляемый стаж
				НаборЗаписейИсчисляемыйСтаж = НаборЗаписейЛьготныйСтаж.ИС__ИС.Значение;
				НаборЗаписейИсчисляемыйСтаж.ИС__Основание.Значение = СокрЛП(ВыборкаПоРаботникам.ОснованиеИсчисляемогоСтажаКод);
				Если Не ЗначениеЗаполнено(ВыборкаПоРаботникам.ОснованиеИсчисляемогоСтажа) И ЗначениеЗаполнено(ВыборкаПоРаботникам.ТретийПараметрИсчисляемогоСтажа) Тогда
					НеВыводитьВФайлИС = Истина;
				ИначеЕсли ЗначениеЗаполнено(ВыборкаПоРаботникам.ОснованиеИсчисляемогоСтажа) Или ЗначениеЗаполнено(ВыборкаПоРаботникам.ПервыйПараметрИсчисляемогоСтажа) Или ЗначениеЗаполнено(ВыборкаПоРаботникам.ВторойПараметрИсчисляемогоСтажа) Тогда		
					НеВыводитьВФайлВыработкаВЧасахИС = Ложь;
					НеВыводитьВФайлВыработкаКалендарнаяИС = Ложь;
					Если ЗначениеЗаполнено(ВыборкаПоРаботникам.ТретийПараметрИсчисляемогоСтажа) Тогда
						НеВыводитьВФайлВыработкаВЧасахИС = Истина;
					ИначеЕсли ВыборкаПоРаботникам.ОснованиеИсчисляемогоСтажа = Справочники.ОснованияИсчисляемогоТрудовогоСтажа.ВОДОЛАЗ Тогда
						НаборЗаписейВыработкаВЧасах = НаборЗаписейИсчисляемыйСтаж.ВС__ВыработкаВЧасах.Значение;
						НаборЗаписейВыработкаВЧасах.ВС__Часы = ВыборкаПоРаботникам.ПервыйПараметрИсчисляемогоСтажа;
						НаборЗаписейВыработкаВЧасах.ВС__Минуты = ВыборкаПоРаботникам.ВторойПараметрИсчисляемогоСтажа;
						НеВыводитьВФайлВыработкаКалендарнаяИС = Истина;
					ИначеЕсли ВыборкаПоРаботникам.ПервыйПараметрИсчисляемогоСтажа > 0 Или ВыборкаПоРаботникам.ВторойПараметрИсчисляемогоСтажа > 0 Тогда
						НеВыводитьВФайлВыработкаВЧасахИС = Истина;
						НаборЗаписейВыработкаКалендарная = НаборЗаписейИсчисляемыйСтаж.ВС__ВыработкаКалендарная.Значение;
						НаборЗаписейВыработкаКалендарная.ВС__ВсеМесяцы = ВыборкаПоРаботникам.ПервыйПараметрИсчисляемогоСтажа;
						НаборЗаписейВыработкаКалендарная.ВС__ВсеДни = ВыборкаПоРаботникам.ВторойПараметрИсчисляемогоСтажа;
					Иначе
						НеВыводитьВФайлВыработкаВЧасахИС = Истина;
						НеВыводитьВФайлВыработкаКалендарнаяИС = Истина;
					КонецЕсли;
					Если НеВыводитьВФайлВыработкаВЧасахИС Тогда
						НаборЗаписейИсчисляемыйСтаж.Удалить("ВС__ВыработкаВЧасах")
					КонецЕсли;
					Если НеВыводитьВФайлВыработкаКалендарнаяИС Тогда
						НаборЗаписейИсчисляемыйСтаж.Удалить("ВС__ВыработкаКалендарная")
					КонецЕсли;
					ВыводитьЛьготныйСтаж = Истина;
					Если Не ЗначениеЗаполнено(ВыборкаПоРаботникам.ОснованиеИсчисляемогоСтажа) Тогда
						НаборЗаписейИсчисляемыйСтаж.Удалить("ИС__Основание")
					КонецЕсли;
				Иначе
					НеВыводитьВФайлИС = Истина;
				КонецЕсли;	

				ВыводитьЛьготныйСтаж = ВыводитьЛьготныйСтаж Или ЗначениеЗаполнено(НаборЗаписейИсчисляемыйСтаж.ИС__Основание.Значение);
				
				// Выслуга лет
				НаборЗаписейВыслугаЛет = НаборЗаписейЛьготныйСтаж.ИС__ВЛ.Значение;
				Если ЗначениеЗаполнено(ВыборкаПоРаботникам.ОснованиеВыслугиЛетКод) Тогда
					НеВыводитьВФайлВыработкаВЧасахВЛ = Ложь;
					НеВыводитьВФайлВыработкаКалендарнаяВЛ = Ложь;
					НаборЗаписейВыслугаЛет.ИС__Основание.Значение = ИсправленныйСимвольныйЭлемент(СокрЛП(ВыборкаПоРаботникам.ОснованиеВыслугиЛетКод));
					НаборЗаписейВыслугаЛет.ИС__ДоляСтавки.Значение = ВыборкаПоРаботникам.ТретийПараметрВыслугиЛет;
					Если ЗначениеЗаполнено(ВыборкаПоРаботникам.ТретийПараметрИсчисляемогоСтажа) Тогда	
						НеВыводитьВФайлВыработкаВЧасахВЛ = Истина;
						НеВыводитьВФайлВыработкаКалендарнаяВЛ = Истина;
					ИначеЕсли Не ЗначениеЗаполнено(ВыборкаПоРаботникам.ПервыйПараметрВыслугиЛет) И Не ЗначениеЗаполнено(ВыборкаПоРаботникам.ВторойПараметрВыслугиЛет) Тогда
						НеВыводитьВФайлВыработкаВЧасахВЛ = Истина;
						НеВыводитьВФайлВыработкаКалендарнаяВЛ = Истина;
					ИначеЕсли ВыборкаПоРаботникам.ОснованиеВыслугиЛет = Справочники.ОснованияВыслугиЛет.Ст27_15 Тогда
						НеВыводитьВФайлВыработкаВЧасахВЛ = Истина;
						НаборЗаписейВыработкаКалендарная = НаборЗаписейВыслугаЛет.ВС__ВыработкаКалендарная.Значение;
						НаборЗаписейВыработкаКалендарная.ВС__ВсеМесяцы = ВыборкаПоРаботникам.ПервыйПараметрВыслугиЛет;
						НаборЗаписейВыработкаКалендарная.ВС__ВсеДни = ВыборкаПоРаботникам.ВторойПараметрВыслугиЛет;
					Иначе
						НеВыводитьВФайлВыработкаКалендарнаяВЛ = Истина;
						НаборЗаписейВыработкаВЧасах = НаборЗаписейВыслугаЛет.ВС__ВыработкаВЧасах.Значение;
						НаборЗаписейВыработкаВЧасах.ВС__Часы = ВыборкаПоРаботникам.ПервыйПараметрВыслугиЛет;
						НаборЗаписейВыработкаВЧасах.ВС__Минуты = ВыборкаПоРаботникам.ВторойПараметрВыслугиЛет;
					КонецЕсли;
					Если НеВыводитьВФайлВыработкаВЧасахВЛ Тогда
						НаборЗаписейВыслугаЛет.Удалить("ВС__ВыработкаВЧасах")
					КонецЕсли;
					Если НеВыводитьВФайлВыработкаКалендарнаяВЛ Тогда
						НаборЗаписейВыслугаЛет.Удалить("ВС__ВыработкаКалендарная")
					КонецЕсли;
					ВыводитьЛьготныйСтаж = Истина;
				Иначе
					НеВыводитьВФайлВЛ = Истина;	
				КонецЕсли;	
					
			КонецЕсли;
			
			Если ВыводитьЛьготныйСтаж Тогда
				
				Если НеВыводитьВФайлТУ Тогда
					НаборЗаписейЛьготныйСтаж.Удалить("ИС__ТУ")
				КонецЕсли;
				Если НеВыводитьВФайлОУТ Тогда
					НаборЗаписейЛьготныйСтаж.Удалить("ИС__ОУТ")
				КонецЕсли;
				Если НеВыводитьВФайлИС Тогда
					НаборЗаписейЛьготныйСтаж.Удалить("ИС__ИС")
				КонецЕсли;
				Если НеВыводитьВФайлДопСведенияИС Тогда
					НаборЗаписейЛьготныйСтаж.Удалить("ИС__ДопСведенияИС")
				КонецЕсли;
				Если НеВыводитьВФайлВЛ Тогда
					НаборЗаписейЛьготныйСтаж.Удалить("ИС__ВЛ")
				КонецЕсли;
				
				УзелЛьготныйСтаж = ПроцедурыПерсонифицированногоУчета.ДобавитьУзелВДеревоXML(УзелСтажевыйПериод, "ИС__ЛьготныйСтаж","");
				ПроцедурыПерсонифицированногоУчета.ДобавитьИнформациюВДерево(УзелЛьготныйСтаж, НаборЗаписейЛьготныйСтаж);
			КонецЕсли;
			
			Пока ВыборкаПоРаботникам.СледующийПоЗначениюПоля("НомерДополнительнойЗаписи")	Цикл
				
				//В дополнительной записи с номером 0 находится информация об основной записи
				Если ВыборкаПоРаботникам.НомерДополнительнойЗаписи = 0 Тогда
					Продолжить;
				КонецЕсли;
				
				НаборЗаписейЛьготныйСтаж = ПроцедурыПерсонифицированногоУчета.СкопироватьСтруктуруДанных(ФорматЛьготныйСтаж);
				
				// Территориальные условия
				Если Не ЗначениеЗаполнено(ВыборкаПоРаботникам.ТерриториальныеУсловия) Тогда
					НаборЗаписейЛьготныйСтаж.Удалить("ИС__ТУ")
				Иначе
					НаборЗаписейДопТерриториальныеУсловия = НаборЗаписейЛьготныйСтаж.ИС__ТУ.Значение;
					НаборЗаписейДопТерриториальныеУсловия.ИС__Основание = СокрЛП(ВыборкаПоРаботникам.ТерриториальныеУсловияКод);
					НаборЗаписейДопТерриториальныеУсловия.ИС__Коэффициент = ВыборкаПоРаботникам.ТерриториальныеУсловияСтавка;
					Если ВыборкаПоРаботникам.ТерриториальныеУсловия = Справочники.ТерриториальныеУсловия.МКС
						Или ВыборкаПоРаботникам.ТерриториальныеУсловия = Справочники.ТерриториальныеУсловия.РКС
						Или ВыборкаПоРаботникам.ТерриториальныеУсловия = Справочники.ТерриториальныеУсловия.РКСМ 
						Или ВыборкаПоРаботникам.ТерриториальныеУсловия = Справочники.ТерриториальныеУсловия.МКСР Тогда
						
						НаборЗаписейДопТерриториальныеУсловия.ИС__Коэффициент = ВыборкаПоРаботникам.ТерриториальныеУсловияСтавка;
					Иначе
						ОписаниеПолей = НаборЗаписейЛьготныйСтаж.ИС__ТУ.Поля;
						СтрокаКУдалению = ОписаниеПолей.Найти("ИС__Коэффициент","ИмяПоля");
						ОписаниеПолей.Удалить(СтрокаКУдалению);
					КонецЕсли;
				КонецЕсли;
				
				// Особые условия труда
				Если Не ЗначениеЗаполнено(ВыборкаПоРаботникам.ОсобыеУсловияТруда) И Не ЗначениеЗаполнено(ВыборкаПоРаботникам.КодПозицииСписка) Тогда
					НаборЗаписейЛьготныйСтаж.Удалить("ИС__ОУТ")
				Иначе
					НаборЗаписейДопОсобыеУсловияТруда = НаборЗаписейЛьготныйСтаж.ИС__ОУТ.Значение;
					НаборЗаписейДопОсобыеУсловияТруда.ИС__Код = СокрЛП(ВыборкаПоРаботникам.ОсобыеУсловияТрудаКод);
					НаборЗаписейДопОсобыеУсловияТруда.ИС__ПозицияСписка = ИсправленныйСимвольныйЭлемент(СокрЛП(ВыборкаПоРаботникам.КодПозицииСпискаКод));
				КонецЕсли;
				
				// Исчисляемый стаж
				НаборЗаписейДопИсчисляемыйСтаж = НаборЗаписейЛьготныйСтаж.ИС__ИС.Значение;
				НеВыводитьВФайлВыработкаВЧасахИС = Ложь;
				НеВыводитьВФайлВыработкаКалендарнаяИС = Ложь;
				Если ВыборкаПоРаботникам.ОснованиеИсчисляемогоСтажа = Справочники.ОснованияИсчисляемогоТрудовогоСтажа.ВОДОЛАЗ Тогда
					НаборЗаписейВыработкаВЧасах = НаборЗаписейДопИсчисляемыйСтаж.ВС__ВыработкаВЧасах.Значение;
					НаборЗаписейВыработкаВЧасах.ВС__Часы = ВыборкаПоРаботникам.ПервыйПараметрИсчисляемогоСтажа;
					НаборЗаписейВыработкаВЧасах.ВС__Минуты = ВыборкаПоРаботникам.ВторойПараметрИсчисляемогоСтажа;
					НеВыводитьВФайлВыработкаКалендарнаяИС = Истина;
				ИначеЕсли ВыборкаПоРаботникам.ПервыйПараметрИсчисляемогоСтажа > 0 
					ИЛИ ВыборкаПоРаботникам.ВторойПараметрИсчисляемогоСтажа > 0 Тогда
					
					НаборЗаписейДопВыработкаКалендарная = НаборЗаписейДопИсчисляемыйСтаж.ВС__ВыработкаКалендарная.Значение;
					НаборЗаписейДопВыработкаКалендарная.ВС__ВсеМесяцы = ВыборкаПоРаботникам.ПервыйПараметрИсчисляемогоСтажа;
					НаборЗаписейДопВыработкаКалендарная.ВС__ВсеДни = ВыборкаПоРаботникам.ВторойПараметрИсчисляемогоСтажа;
					НеВыводитьВФайлВыработкаВЧасахИС = Истина;
				Иначе
					НеВыводитьВФайлВыработкаВЧасахИС = Истина;
					НеВыводитьВФайлВыработкаКалендарнаяИС = Истина;
				КонецЕсли;
				Если НеВыводитьВФайлВыработкаВЧасахИС Тогда
					НаборЗаписейДопИсчисляемыйСтаж.Удалить("ВС__ВыработкаВЧасах")
				КонецЕсли;
				Если НеВыводитьВФайлВыработкаКалендарнаяИС Тогда
					НаборЗаписейДопИсчисляемыйСтаж.Удалить("ВС__ВыработкаКалендарная")
				КонецЕсли;
				Если ЗначениеЗаполнено(ВыборкаПоРаботникам.ОснованиеИсчисляемогоСтажа) Тогда
					НаборЗаписейДопИсчисляемыйСтаж.ИС__Основание.Значение = ИсправленныйСимвольныйЭлемент(СокрЛП(ВыборкаПоРаботникам.ОснованиеИсчисляемогоСтажаКод));
				ИначеЕсли НеВыводитьВФайлВыработкаВЧасахИС И НеВыводитьВФайлВыработкаКалендарнаяИС Тогда
					НаборЗаписейЛьготныйСтаж.Удалить("ИС__ИС")
				Иначе
					НаборЗаписейДопИсчисляемыйСтаж.Удалить("ИС__Основание")
				КонецЕсли;
				
				// Выслуга лет
				НаборЗаписейДопВыслугаЛет = НаборЗаписейЛьготныйСтаж.ИС__ВЛ.Значение;
				Если ЗначениеЗаполнено(ВыборкаПоРаботникам.ОснованиеВыслугиЛет) Тогда
					НаборЗаписейДопВыслугаЛет.ИС__Основание.Значение = ИсправленныйСимвольныйЭлемент(СокрЛП(ВыборкаПоРаботникам.ОснованиеВыслугиЛетКод));
					НаборЗаписейДопВыслугаЛет.ИС__ДоляСтавки.Значение = ВыборкаПоРаботникам.ТретийПараметрВыслугиЛет;
					НеВыводитьВФайлВыработкаВЧасахВЛ = Ложь;
					НеВыводитьВФайлВыработкаКалендарнаяВЛ = Ложь;
					Если ВыборкаПоРаботникам.ОснованиеВыслугиЛет = Справочники.ОснованияВыслугиЛет.Ст27_15 Тогда
						НеВыводитьВФайлВыработкаВЧасахВЛ = Истина;
						НаборЗаписейВыработкаКалендарная = НаборЗаписейДопВыслугаЛет.ВС__ВыработкаКалендарная.Значение;
						НаборЗаписейВыработкаКалендарная.ВС__ВсеМесяцы = ВыборкаПоРаботникам.ПервыйПараметрВыслугиЛет;
						НаборЗаписейВыработкаКалендарная.ВС__ВсеДни = ВыборкаПоРаботникам.ВторойПараметрВыслугиЛет;
					Иначе
						НеВыводитьВФайлВыработкаКалендарнаяВЛ = Истина;
						НаборЗаписейДопВыработкаВЧасах = НаборЗаписейДопВыслугаЛет.ВС__ВыработкаВЧасах.Значение;
						НаборЗаписейДопВыработкаВЧасах.ВС__Часы = ВыборкаПоРаботникам.ПервыйПараметрВыслугиЛет;
						НаборЗаписейДопВыработкаВЧасах.ВС__Минуты = ВыборкаПоРаботникам.ВторойПараметрВыслугиЛет;
					КонецЕсли;
					Если НеВыводитьВФайлВыработкаВЧасахВЛ Тогда
						НаборЗаписейДопВыслугаЛет.Удалить("ВС__ВыработкаВЧасах")
					КонецЕсли;
					Если НеВыводитьВФайлВыработкаКалендарнаяВЛ Тогда
						НаборЗаписейДопВыслугаЛет.Удалить("ВС__ВыработкаКалендарная")
					КонецЕсли;
				Иначе
					НаборЗаписейЛьготныйСтаж.Удалить("ИС__ВЛ")
				КонецЕсли;
				
				// ДопСведенияИС
				Если ЗначениеЗаполнено(ВыборкаПоРаботникам.ТретийПараметрИсчисляемогоСтажа) Тогда
					НаборЗаписейЛьготныйСтаж.ИС__ДопСведенияИС.Значение = Строка(ВыборкаПоРаботникам.ТретийПараметрИсчисляемогоСтажаКод);
				Иначе	
					НаборЗаписейЛьготныйСтаж.Удалить("ИС__ДопСведенияИС")
				КонецЕсли;
				
				УзелЛьготныйСтаж = ПроцедурыПерсонифицированногоУчета.ДобавитьУзелВДеревоXML(УзелСтажевыйПериод, "ИС__ЛьготныйСтаж", "");
				ПроцедурыПерсонифицированногоУчета.ДобавитьИнформациюВДерево(УзелЛьготныйСтаж, НаборЗаписейЛьготныйСтаж);
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЕсли;
		
КонецПроцедуры

Функция ИсправленныйСимвольныйЭлемент(СтроковоеЗначение, ИспользоватьВРег = Истина) 
	
	НовоеЗначение = ?(ИспользоватьВРег, ВРег(СтроковоеЗначение), СтроковоеЗначение); // традиционно в файле строки передаются в верхнем регистре
	// правило ВС.ОИС2017.1.2: Лидирующие пробелы в символьных элементах, за исключением элементов блока ФИО, не допускаются
	НовоеЗначение = СокрЛП(НовоеЗначение);
	// правило ВС.ОИС2017.1.3: Если в символьном элементе, за исключением элементов блока ФИО, использован символ '-', то рядом с этим символом пробелы недопустимы
	Пока Найти(НовоеЗначение," -") > 0 Или Найти(НовоеЗначение,"- ") > 0 Цикл
		НовоеЗначение = СтрЗаменить(НовоеЗначение," -","-");
		НовоеЗначение = СтрЗаменить(НовоеЗначение,"- ","-");
	КонецЦикла;
	// правило ВС.ОИС2017.1.4: Наличие двух и более пробелов подряд внутри символьных элементов, за исключением элементов блока ФИО, не допускается (только один)
	Пока Найти(НовоеЗначение,"  ") > 0 Цикл
		НовоеЗначение = СтрЗаменить(НовоеЗначение,"  "," ");
	КонецЦикла;
	
	Возврат НовоеЗначение
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Если Проведен И ПринятоВПФР Тогда
		Возврат
	КонецЕсли;
	
	Для Каждого Набор Из Движения Цикл
		Набор.Очистить();
	КонецЦикла;
	
	ОбработкаКомментариев = глЗначениеПеременной("глОбработкаСообщений");
	ОбработкаКомментариев.УдалитьСообщения();
	
	ПроверитьПравильностьЗаполненияДокумента(Отказ, Ложь);
	
	Если Отказ тогда
		ОбработкаКомментариев.ПоказатьСообщения();
		Возврат;
	КонецЕсли;
	
	ТекстФайла = СформироватьВыходнойФайл(Отказ);
	
	//Сохраним сформированный файл сведений в регистре сведений
	Запись = Движения.АрхивДанныхРегламентированнойОтчетности.Добавить();
	Запись.Объект = Ссылка;
	Запись.ОписаниеДанных = "Файл формы СЗВ-КОРР";
	Запись.Данные = ТекстФайла;

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОтчетныйПериод = Дата(Год, 1, 1);
	
	МассивТЧ = Новый Массив();
	МассивТЧ.Добавить(РаботникиОрганизации);
	
	КраткийСоставДокумента = ПроцедурыУправленияПерсоналом.ЗаполнитьКраткийСоставДокумента(МассивТЧ, "Физлицо");
	
	Соответствие = Новый Соответствие;
	Для каждого СтрокаТЧ Из РаботникиОрганизации Цикл
		Соответствие.Вставить(СтрокаТЧ.ИдентификаторСтроки, Истина)
	КонецЦикла;
	
	Если ТипСведенийСЗВ = Перечисления.ТипыСведенийСЗВ_КОРР.Отменяющая Тогда
		СведенияОЗаработке.Очистить();
		СведенияОЗаработкеНаВредныхИТяжелыхРаботах.Очистить();
		ЗаписиОСтаже.Очистить();
		НачисленныеУплаченныеВзносы.Очистить();
		УплаченныеВзносыРасшифровка.Очистить();
	ИначеЕсли ТипСведенийСЗВ = Перечисления.ТипыСведенийСЗВ_КОРР.Корректирующая Тогда
		НачисленныеУплаченныеВзносы.Очистить();
		УплаченныеВзносыРасшифровка.Очистить();
	КонецЕсли;
	
	Если ТипСведенийСЗВ <> Перечисления.ТипыСведенийСЗВ_КОРР.Отменяющая Тогда
		
		КорректируемыйГод = Год(КорректируемыйПериод);
		
		ВидимостьКолонок = ВидимостьКолонокТабличныхЧастей(КорректируемыйГод);
		Для Каждого ТабличнаяЧастьДокумента Из ВидимостьКолонок Цикл
			ИменаКолонок = Новый Массив;
			Для Каждого Колонка Из ТабличнаяЧастьДокумента.Значение Цикл
				Если Колонка.Значение = Ложь Тогда
					ИменаКолонок.Добавить(Колонка.Ключ);
				КонецЕсли;
			КонецЦикла;
			ОчиститьЗначенияКолонок(ТабличнаяЧастьДокумента.Ключ, ИменаКолонок);
		КонецЦикла;
		
		Если Не ПроцедурыУправленияПерсоналом.ЗначениеУчетнойПолитикиПоПерсоналуВсехОрганизаций(глЗначениеПеременной("глУчетнаяПолитикаПоПерсоналуОрганизации"), "ИспользуютсяРаботыСДосрочнойПенсией") 
			Или КорректируемыйГод < 2013 Тогда
			СведенияОЗаработкеНаВредныхИТяжелыхРаботах.Очистить();
		КонецЕсли;
		
		// если указан признак АВИА, то категорию не заполняем
		Если КорректируемыйГод >= 2000 И КорректируемыйГод <= 2009 Тогда
			Для Каждого СтрокаРаботника Из РаботникиОрганизации Цикл
				Если СтрокаРаботника.ДополнительныйТариф Тогда
					СтрокаРаботника.КатегорияЗастрахованныхЛиц = Перечисления.КатегорииЗастрахованныхЛицПФР.ПустаяСсылка();
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	
	КонецЕсли;
	
	ЗависимыеТЧ = Новый Массив;
	ЗависимыеТЧ.Добавить("СведенияОЗаработке");
	ЗависимыеТЧ.Добавить("СведенияОЗаработкеНаВредныхИТяжелыхРаботах");
	ЗависимыеТЧ.Добавить("ЗаписиОСтаже");
	
	Для Каждого ИмяТЧ Из ЗависимыеТЧ Цикл
		ТабличнаяЧасть = ЭтотОбъект[ИмяТЧ];
		ВсегоСтрок = ТабличнаяЧасть.Количество();
		Для Сч = 1 По ВсегоСтрок Цикл
			СтрокаТЧ = ТабличнаяЧасть[ВсегоСтрок - Сч];
			Если Соответствие[СтрокаТЧ.ИдентификаторСтроки] = Неопределено Тогда
				ТабличнаяЧасть.Удалить(СтрокаТЧ)
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	ВыполнитьНумерациюЗаписейОСтаже(РежимЗаписи = РежимЗаписиДокумента.Проведение);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
		
	Отказ = ПринятоВПФР;
	
	Если Не Отказ Тогда
		Для Каждого Набор Из Движения Цикл
			Набор.Очистить();
			Набор.Записать();
		КонецЦикла
	КонецЕсли;
	
КонецПроцедуры
 
////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Функция ТекущиеОстатки(Организация, Дата, ВидОстатка, УчитыватьДанныеДокумента = Ложь, Ссылка = Неопределено, ДатаПлатежа = Неопределено, УчитыватьРасходыПоСтрахованию = Ложь, РасчетБезКопеек = Ложь) Экспорт
	
	СтруктураОстатков = Новый Структура("ПФРПоСуммарномуТарифу,ПФРСтраховая,ПФРНакопительная,ПФРПоДополнительномуТарифу,ПФРНаДоплатуКПенсииШахтерам,ПФРЗаЗанятыхНаПодземныхИВредныхРаботах,ПФРЗаЗанятыхНаТяжелыхИПрочихРаботах,ФФОМС,ТФОМС,ФСС,ФССНесчастныеСлучаи",0,0,0,0,0,0,0,0,0,0);
	
	ДатаОстатка = КонецДня(Дата);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ДатаОстатка",			ДатаОстатка);
	Запрос.УстановитьПараметр("ВидПлатежа",				ВидОстатка);
	Запрос.УстановитьПараметр("ДатаПереходаНаВзносы",	ПроведениеРасчетов.ДатаЗаменыЕСНСтраховымиВзносами());
	Запрос.УстановитьПараметр("Организация",			Организация);

	Если УчитыватьДанныеДокумента И ЗначениеЗаполнено(ДатаПлатежа) И ДатаПлатежа < ДатаОстатка Тогда
		Запрос.УстановитьПараметр("Ссылка",	Ссылка);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасчетыПоСтраховымВзносам.ПФРПоСуммарномуТарифу + РасчетыПоСтраховымВзносам.ПФРСПревышения ПФРПоСуммарномуТарифу,
		|	РасчетыПоСтраховымВзносам.ПФРСтраховая,
		|	РасчетыПоСтраховымВзносам.ПФРНакопительная,
		|	РасчетыПоСтраховымВзносам.ФСС,
		|	РасчетыПоСтраховымВзносам.ФФОМС,
		|	РасчетыПоСтраховымВзносам.ТФОМС,
		|	РасчетыПоСтраховымВзносам.ФССНесчастныеСлучаи,
		|	РасчетыПоСтраховымВзносам.ПФРПоДополнительномуТарифу,
		|	РасчетыПоСтраховымВзносам.ПФРНаДоплатуКПенсииШахтерам,
		|	РасчетыПоСтраховымВзносам.ПФРЗаЗанятыхНаПодземныхИВредныхРаботах,
		|	РасчетыПоСтраховымВзносам.ПФРЗаЗанятыхНаТяжелыхИПрочихРаботах
		|ПОМЕСТИТЬ ВТДанныеДокумента
		|ИЗ
		|	РегистрНакопления.РасчетыПоСтраховымВзносам КАК РасчетыПоСтраховымВзносам
		|ГДЕ
		|	РасчетыПоСтраховымВзносам.Регистратор = &Ссылка
		|	И РасчетыПоСтраховымВзносам.ВидПлатежа = &ВидПлатежа
		|	И РасчетыПоСтраховымВзносам.Организация = &Организация
		|	И РасчетыПоСтраховымВзносам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	И РасчетыПоСтраховымВзносам.МесяцРасчетногоПериода >= &ДатаПереходаНаВзносы";	
	Иначе 
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	0 КАК ПФРПоСуммарномуТарифу,
		|	0 КАК ПФРСтраховая,
		|	0 КАК ПФРНакопительная,
		|	0 КАК ФСС,
		|	0 КАК ФФОМС,
		|	0 КАК ТФОМС,
		|	0 КАК ФССНесчастныеСлучаи,
		|	0 КАК ПФРПоДополнительномуТарифу,
		|	0 КАК ПФРНаДоплатуКПенсииШахтерам,
		|	0 КАК ПФРЗаЗанятыхНаПодземныхИВредныхРаботах,
		|	0 КАК ПФРЗаЗанятыхНаТяжелыхИПрочихРаботах
		|ПОМЕСТИТЬ ВТДанныеДокумента";	
	КонецЕсли;
	Запрос.Выполнить();
	
	Если УчитыватьРасходыПоСтрахованию Тогда
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасчетыПоСтраховымВзносам.ФССОстаток КАК ФСС,
		|	РасчетыПоСтраховымВзносам.ФССНесчастныеСлучаиОстаток КАК ФССНесчастныеСлучаи
		|ПОМЕСТИТЬ ВТРасходыПоСоцСтрахованию
		|ИЗ
		|	РегистрНакопления.РасчетыПоСтраховымВзносам.Остатки(
		|			&ДатаОстатка,
		|			ВидПлатежа В (ЗНАЧЕНИЕ(Перечисление.ВидыПлатежейВГосБюджет.РасходыПоСтрахованию), ЗНАЧЕНИЕ(Перечисление.ВидыПлатежейВГосБюджет.НеПринято), ЗНАЧЕНИЕ(Перечисление.ВидыПлатежейВГосБюджет.ПолученоИзФонда))
		|				И МесяцРасчетногоПериода >= &ДатаПереходаНаВзносы
		|				И Организация = &Организация) КАК РасчетыПоСтраховымВзносам";	
	Иначе 
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	0 КАК ФСС,
		|	0 КАК ФССНесчастныеСлучаи
		|ПОМЕСТИТЬ ВТРасходыПоСоцСтрахованию";	
	КонецЕсли;
	Запрос.Выполнить();
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА (ВЫРАЗИТЬ(СУММА(ДанныеДляСуммирования.ПФРПоСуммарномуТарифу) КАК ЧИСЛО(15, 2))) > 0
	|			ТОГДА ВЫРАЗИТЬ(СУММА(ДанныеДляСуммирования.ПФРПоСуммарномуТарифу) КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПФРПоСуммарномуТарифу,
	|	ВЫБОР
	|		КОГДА (ВЫРАЗИТЬ(СУММА(ДанныеДляСуммирования.ПФРСтраховая) КАК ЧИСЛО(15, 2))) > 0
	|			ТОГДА ВЫРАЗИТЬ(СУММА(ДанныеДляСуммирования.ПФРСтраховая) КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПФРСтраховая,
	|	ВЫБОР
	|		КОГДА (ВЫРАЗИТЬ(СУММА(ДанныеДляСуммирования.ПФРНакопительная) КАК ЧИСЛО(15, 2))) > 0
	|			ТОГДА ВЫРАЗИТЬ(СУММА(ДанныеДляСуммирования.ПФРНакопительная) КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПФРНакопительная,
	|	ВЫБОР
	|		КОГДА (ВЫРАЗИТЬ(СУММА(ДанныеДляСуммирования.ФСС) КАК ЧИСЛО(15, 2))) > 0
	|			ТОГДА ВЫРАЗИТЬ(СУММА(ДанныеДляСуммирования.ФСС) КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ФСС,
	|	ВЫБОР
	|		КОГДА (ВЫРАЗИТЬ(СУММА(ДанныеДляСуммирования.ФФОМС) КАК ЧИСЛО(15, 2))) > 0
	|			ТОГДА ВЫРАЗИТЬ(СУММА(ДанныеДляСуммирования.ФФОМС) КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ФФОМС,
	|	ВЫБОР
	|		КОГДА (ВЫРАЗИТЬ(СУММА(ДанныеДляСуммирования.ТФОМС) КАК ЧИСЛО(15, 2))) > 0
	|			ТОГДА ВЫРАЗИТЬ(СУММА(ДанныеДляСуммирования.ТФОМС) КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ТФОМС,
	|	ВЫБОР
	|		КОГДА СУММА(ДанныеДляСуммирования.ФССНесчастныеСлучаи) > 0
	|			ТОГДА СУММА(ДанныеДляСуммирования.ФССНесчастныеСлучаи)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ФССНесчастныеСлучаи,
	|	ВЫБОР
	|		КОГДА (ВЫРАЗИТЬ(СУММА(ДанныеДляСуммирования.ПФРПоДополнительномуТарифу) КАК ЧИСЛО(15, 2))) > 0
	|			ТОГДА ВЫРАЗИТЬ(СУММА(ДанныеДляСуммирования.ПФРПоДополнительномуТарифу) КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПФРПоДополнительномуТарифу,
	|	ВЫБОР
	|		КОГДА (ВЫРАЗИТЬ(СУММА(ДанныеДляСуммирования.ПФРНаДоплатуКПенсииШахтерам) КАК ЧИСЛО(15, 2))) > 0
	|			ТОГДА ВЫРАЗИТЬ(СУММА(ДанныеДляСуммирования.ПФРНаДоплатуКПенсииШахтерам) КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПФРНаДоплатуКПенсииШахтерам,
	|	ВЫБОР
	|		КОГДА (ВЫРАЗИТЬ(СУММА(ДанныеДляСуммирования.ПФРЗаЗанятыхНаПодземныхИВредныхРаботахОстаток) КАК ЧИСЛО(15, 2))) > 0
	|			ТОГДА ВЫРАЗИТЬ(СУММА(ДанныеДляСуммирования.ПФРЗаЗанятыхНаПодземныхИВредныхРаботахОстаток) КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПФРЗаЗанятыхНаПодземныхИВредныхРаботах,
	|	ВЫБОР
	|		КОГДА (ВЫРАЗИТЬ(СУММА(ДанныеДляСуммирования.ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахОстаток) КАК ЧИСЛО(15, 2))) > 0
	|			ТОГДА ВЫРАЗИТЬ(СУММА(ДанныеДляСуммирования.ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахОстаток) КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПФРЗаЗанятыхНаТяжелыхИПрочихРаботах
	|ИЗ
	|	(ВЫБРАТЬ
	|		РасчетыПоСтраховымВзносамОстатки.ПФРПоСуммарномуТарифуОстаток + РасчетыПоСтраховымВзносамОстатки.ПФРСПревышенияОстаток КАК ПФРПоСуммарномуТарифу,
	|		РасчетыПоСтраховымВзносамОстатки.ПФРСтраховаяОстаток КАК ПФРСтраховая,
	|		РасчетыПоСтраховымВзносамОстатки.ПФРНакопительнаяОстаток КАК ПФРНакопительная,
	|		РасчетыПоСтраховымВзносамОстатки.ФССОстаток КАК ФСС,
	|		РасчетыПоСтраховымВзносамОстатки.ФФОМСОстаток КАК ФФОМС,
	|		РасчетыПоСтраховымВзносамОстатки.ТФОМСОстаток КАК ТФОМС,
	|		РасчетыПоСтраховымВзносамОстатки.ФССНесчастныеСлучаиОстаток КАК ФССНесчастныеСлучаи,
	|		РасчетыПоСтраховымВзносамОстатки.ПФРПоДополнительномуТарифуОстаток КАК ПФРПоДополнительномуТарифу,
	|		РасчетыПоСтраховымВзносамОстатки.ПФРНаДоплатуКПенсииШахтерамОстаток КАК ПФРНаДоплатуКПенсииШахтерам,
	|		РасчетыПоСтраховымВзносамОстатки.ПФРЗаЗанятыхНаПодземныхИВредныхРаботахОстаток КАК ПФРЗаЗанятыхНаПодземныхИВредныхРаботахОстаток,
	|		РасчетыПоСтраховымВзносамОстатки.ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахОстаток КАК ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахОстаток
	|	ИЗ
	|		РегистрНакопления.РасчетыПоСтраховымВзносам.Остатки(
	|				&ДатаОстатка,
	|				ВидПлатежа = &ВидПлатежа
	|					И МесяцРасчетногоПериода >= &ДатаПереходаНаВзносы
	|					И Организация = &Организация) КАК РасчетыПоСтраховымВзносамОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДанныеДокумента.ПФРПоСуммарномуТарифу,
	|		ДанныеДокумента.ПФРСтраховая,
	|		ДанныеДокумента.ПФРНакопительная,
	|		ДанныеДокумента.ФСС,
	|		ДанныеДокумента.ФФОМС,
	|		ДанныеДокумента.ТФОМС,
	|		ДанныеДокумента.ФССНесчастныеСлучаи,
	|		ДанныеДокумента.ПФРПоДополнительномуТарифу,
	|		ДанныеДокумента.ПФРНаДоплатуКПенсииШахтерам,
	|		ДанныеДокумента.ПФРЗаЗанятыхНаПодземныхИВредныхРаботах,
	|		ДанныеДокумента.ПФРЗаЗанятыхНаТяжелыхИПрочихРаботах
	|	ИЗ
	|		ВТДанныеДокумента КАК ДанныеДокумента
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		0,
	|		0,
	|		0,
	|		РасходыПоСоцСтрахованию.ФСС,
	|		0,
	|		0,
	|		РасходыПоСоцСтрахованию.ФССНесчастныеСлучаи,
	|		0,
	|		0,
	|		0,
	|		0
	|	ИЗ
	|		ВТРасходыПоСоцСтрахованию КАК РасходыПоСоцСтрахованию) КАК ДанныеДляСуммирования";
	
	Если РасчетБезКопеек Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"ЧИСЛО(15, 2)","ЧИСЛО(15, 0)")
	КонецЕсли;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(СтруктураОстатков,Выборка);
	КонецЦикла;
	
	Возврат СтруктураОстатков
	
КонецФункции // ТекущиеОстатки()

Процедура Автозаполнение() Экспорт

	Если ВидОперации = Перечисления.ВидыОперацийРасчетыПоСтраховымВзносам.УплатаПФР Тогда
		СтрокаЗаполняемыхПолей = "ПФРПоСуммарномуТарифу,ПФРСтраховая,ПФРНакопительная,ПФРПоДополнительномуТарифу,ПФРНаДоплатуКПенсииШахтерам,ПФРЗаЗанятыхНаПодземныхИВредныхРаботах,ПФРЗаЗанятыхНаТяжелыхИПрочихРаботах";
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийРасчетыПоСтраховымВзносам.УплатаФОМС Тогда
		СтрокаЗаполняемыхПолей = "ФФОМС,ТФОМС";
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийРасчетыПоСтраховымВзносам.УплатаФСС Тогда
		СтрокаЗаполняемыхПолей = "ФСС,ФССНесчастныеСлучаи";
	Иначе	
		СтрокаЗаполняемыхПолей = "ПФРПоСуммарномуТарифу,ПФРСтраховая,ПФРНакопительная,ПФРПоДополнительномуТарифу,ПФРНаДоплатуКПенсииШахтерам,ФФОМС,ТФОМС,ФСС,ФССНесчастныеСлучаи";
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект,ТекущиеОстатки(Организация, Дата, ВидПлатежа, Проведен, Ссылка, ДатаПлатежа, ВидПлатежа = Перечисления.ВидыПлатежейВГосБюджет.Налог И ВидОперации = Перечисления.ВидыОперацийРасчетыПоСтраховымВзносам.УплатаФСС, Дата < '20150101'),СтрокаЗаполняемыхПолей);
	
КонецПроцедуры

// Возвращает доступные варианты печати документа
//
// Возвращаемое значение:
//  Структура, каждая строка которой соответствует одному из вариантов печати
//
Функция ПолучитьСтруктуруПечатныхФорм() Экспорт
	Возврат Новый Структура();
КонецФункции // ПолучитьСтруктуруПечатныхФорм()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

// Проверяет правильность заполнения шапки документа.
// Если какой-то из реквизитов шапки, влияющий на проведение не заполнен или
// заполнен не корректно, то выставляется флаг отказа в проведении.
// Проверка выполняется по выборке из результата запроса по шапке,
// все проверяемые реквизиты должны быть включены в выборку по шапке.
//
// Параметры: 
//	ВыборкаПоШапкеДокумента	- выборка из результата запроса по шапке документа,
//	Отказ					- флаг отказа в проведении,
//	Заголовок				- Заголовок для сообщений об ошибках проведения.
//
Процедура ПроверитьЗаполнениеШапки(Отказ, Заголовок = "")

	Если Не ЗначениеЗаполнено(Организация) Тогда
		СтрокаСообщения = ОбщегоНазначенияЗК.ПреобразоватьСтрокуИнтерфейса("Не указана организация!");
		ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке(СтрокаСообщения, Отказ, Заголовок);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(МесяцРасчетногоПериода) Тогда
		ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке("Не указан расчетный период (год)!", Отказ, Заголовок);
	Иначе
		ГодУплаты = Год(МесяцРасчетногоПериода);
		Если ВидОперации = Перечисления.ВидыОперацийРасчетыПоСтраховымВзносам.УплатаЕСН И (ГодУплаты < 2001 Или ГодУплаты > 2009) Тогда
			ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке("Неверно указан год, за который уплачивается ЕСН (действовал в 2001-2009 годах)!", Отказ, Заголовок);
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДатаПлатежа) Тогда
		ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке("Не указана дата операции!", Отказ, Заголовок);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВидПлатежа) Тогда
		ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке("Не указан вид платежа!", Отказ, Заголовок);
	КонецЕсли;
	
КонецПроцедуры // ПроверитьЗаполнениеШапки()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПроведения(Отказ, Режим)
	
	ОбработкаКомментариев = глЗначениеПеременной("глОбработкаСообщений");
	ОбработкаКомментариев.УдалитьСообщения();
	
	// Заголовок для сообщений об ошибках проведения.
	Заголовок = ОбщегоНазначенияЗК.ПредставлениеДокументаПриПроведении(Ссылка);
	
	ПроверитьЗаполнениеШапки(Отказ, Заголовок);
	
	Если Отказ Тогда
		ОбработкаКомментариев.ПоказатьСообщения();
		Возврат;
	КонецЕсли;
	
	Если ВидОперации = Перечисления.ВидыОперацийРасчетыПоСтраховымВзносам.ПособияПоСтрахованию Тогда
		Если ФСС <> 0 Или ФССНесчастныеСлучаи <> 0 Тогда
			Движение = Движения.РасчетыПоСтраховымВзносам.Добавить();
			ЗаполнитьЗначенияСвойств(Движение,ЭтотОбъект);
			Движение.Период = ДатаПлатежа;
			Движение.ВидДвижения = ?(ВидПлатежа = Перечисления.ВидыПлатежейВГосБюджет.РасходыПоСтрахованию,ВидДвиженияНакопления.Расход,ВидДвиженияНакопления.Приход);
		КонецЕсли;
		Если ФССЕНВД <> 0 И МесяцРасчетногоПериода < ПроведениеРасчетов.ДатаРасширенияПеречняЛьготныхТарифовСтраховыхВзносов()Тогда
			Движение = Движения.РасчетыПоСтраховымВзносам.Добавить();
			ЗаполнитьЗначенияСвойств(Движение,ЭтотОбъект, ,"ФСС, ФССНесчастныеСлучаи");
			Движение.ФСС = ФССЕНВД;
			Движение.ОблагаетсяЕНВД = Истина;
			Движение.Период = ДатаПлатежа;	
			Движение.ВидДвижения = ?(ВидПлатежа = Перечисления.ВидыПлатежейВГосБюджет.РасходыПоСтрахованию,ВидДвиженияНакопления.Расход,ВидДвиженияНакопления.Приход);
		КонецЕсли;
	Иначе
		
		Если ВидОперации = Перечисления.ВидыОперацийРасчетыПоСтраховымВзносам.Начисление Или ВидОперации = Перечисления.ВидыОперацийРасчетыПоСтраховымВзносам.ДоначислениеВзносов Тогда
			Движение = Движения.РасчетыПоСтраховымВзносам.ДобавитьПриход()
		Иначе
			Движение = Движения.РасчетыПоСтраховымВзносам.ДобавитьРасход()
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(Движение,ЭтотОбъект);
		Движение.Период = ДатаПлатежа;
		Если ДатаПлатежа >= РасчетСтраховыхВзносов.ДатаПередачиАдминистрированияВзносовФНС() Тогда
			 Движение.ПФРЗаЗанятыхНаПодземныхИВредныхРаботах = 0;
			 Движение.ПФРЗаЗанятыхНаТяжелыхИПрочихРаботах = 0;
			 Движение.ПФРНаДоплатуКПенсииШахтерам = 0;
			 Движение.ПФРНакопительная = 0;
			 Движение.ПФРПоДополнительномуТарифу = 0;
			 Движение.ПФРПоСуммарномуТарифу = 0;
			 Движение.ПФРСПревышения = 0;
			 Движение.ПФРСтраховая = 0;
			 Движение.ТФОМС = 0;
			 Движение.ФФОМС = 0;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ОбработкаПроведения()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПФРСПревышения = 0;
	Если ВидОперации = Перечисления.ВидыОперацийРасчетыПоСтраховымВзносам.УплатаПФР Тогда
		ФФОМС = 0;
		ТФОМС = 0;
		ФСС = 0; ПлатежноеПоручениеФССДата = ""; ПлатежноеПоручениеФССНомер = "";
		ФССНесчастныеСлучаи = 0; ПлатежноеПоручениеФСС_НС_ПЗНомер = ""; ПлатежноеПоручениеФСС_НС_ПЗДата = "";
		МесяцРасчетногоПериода = ПроведениеРасчетов.ДатаЗаменыЕСНСтраховымиВзносами();
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийРасчетыПоСтраховымВзносам.УплатаФОМС Тогда
		ПФРПоСуммарномуТарифу = 0;
		ПФРСтраховая = 0;
		ПФРНакопительная = 0;
		ПФРПоДополнительномуТарифу = 0;
		ПФРНаДоплатуКПенсииШахтерам = 0;
		ПФРЗаЗанятыхНаПодземныхИВредныхРаботах = 0;
		ПФРЗаЗанятыхНаТяжелыхИПрочихРаботах = 0;
		ФСС = 0; ПлатежноеПоручениеФССДата = ""; ПлатежноеПоручениеФССНомер = "";
		ФССНесчастныеСлучаи = 0; ПлатежноеПоручениеФСС_НС_ПЗНомер = ""; ПлатежноеПоручениеФСС_НС_ПЗДата = "";
		МесяцРасчетногоПериода = ПроведениеРасчетов.ДатаЗаменыЕСНСтраховымиВзносами();
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийРасчетыПоСтраховымВзносам.УплатаФСС Тогда
		ПФРПоСуммарномуТарифу = 0;
		ПФРСтраховая = 0;
		ПФРНакопительная = 0;
		ПФРПоДополнительномуТарифу = 0;
		ПФРНаДоплатуКПенсииШахтерам = 0;
		ПФРЗаЗанятыхНаПодземныхИВредныхРаботах = 0;
		ПФРЗаЗанятыхНаТяжелыхИПрочихРаботах = 0;
		ФФОМС = 0;
		ТФОМС = 0;
		МесяцРасчетногоПериода = ПроведениеРасчетов.ДатаЗаменыЕСНСтраховымиВзносами();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Если ВидОперации = Перечисления.ВидыОперацийРасчетыПоСтраховымВзносам.УдалитьУплата Тогда
		ВидОперации = Перечисления.ВидыОперацийРасчетыПоСтраховымВзносам.УплатаПФР
	КонецЕсли;
КонецПроцедуры


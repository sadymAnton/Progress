////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

Перем мГоловнаяОрганизация;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ РАСЧЕТА

// Получаем таблицу сотрудников для сбора данных и расчета
//
// Возвращаемое значение:
//   ТаблицаЗначений - перечень сотрудников, или Неопределено, если по всем
//						поля таблицы: Сотрудник и Физлицо
//
Функция ПолучитьОграничениеНаСотрудников(Отказ)

	МассивПодразделений = Новый Массив;
	
	ТекстЗапроса = "";
	
	Если Не ЗначениеЗаполнено(Ответственный) и Не ЗначениеЗаполнено(ПодразделениеОрганизации) Тогда
		// нет условий по подразделению	
	ИначеЕсли Не ЗначениеЗаполнено(ПодразделениеОрганизации) Тогда
		//применяем условие на подразделение по ответственному
		Если ПроцедурыУправленияПерсоналом.ЗначениеУчетнойПолитикиПоПерсоналуОрганизации(глЗначениеПеременной("глУчетнаяПолитикаПоПерсоналуОрганизации"), Организация, "РасчетЗарплатыОрганизацииПоОтветственным") Тогда
			//расчет зарплаты по ответственным включен, получим список подразделений
			ТекстЗапроса = 
			"ВЫБРАТЬ 
			|	ПодразделенияОрганизаций.Ссылка
			|ИЗ
			|	Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
			|ГДЕ
			|	ПодразделенияОрганизаций.Ссылка В ИЕРАРХИИ
			|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
			|				Расчетчики.ПодразделениеОрганизации
			|			ИЗ
			|				РегистрСведений.РасчетчикиЗарплатыОрганизаций.СрезПоследних(&парамПериодРегистрации, ) КАК Расчетчики
			|			ГДЕ
			|				Расчетчики.Пользователь = &парамОтветственный)
			|	И ПодразделенияОрганизаций.Владелец = &парамОрганизация";
	
		КонецЕсли;
		
	Иначе // отбор по подразделению заданному в форме документа	
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ПодразделенияОрганизаций.Ссылка
		|ИЗ
		|	Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
		|ГДЕ
		|	ПодразделенияОрганизаций.Ссылка В ИЕРАРХИИ(&парамПодразделениеОрганизации)
		|	И ПодразделенияОрганизаций.Владелец = &парамОрганизация";
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекстЗапроса) Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("парамОрганизация",				Организация);
		Запрос.УстановитьПараметр("парамПодразделениеОрганизации",	ПодразделениеОрганизации);
		Запрос.УстановитьПараметр("парамПериодРегистрации",			ПериодРегистрации);
		Запрос.УстановитьПараметр("парамОтветственный",				Ответственный);
		
		Запрос.Текст = ТекстЗапроса;
		МассивПодразделений = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
		
		Если МассивПодразделений.Количество() = 0 Тогда
			//список может быть пустым когда ответственному не назначены подразделения для расчета
			ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке("Для ответственного не задан список подразделений для расчета!", Отказ, "Расчет страховых взносов");
		КонецЕсли;
	
	КонецЕсли;
	
	Если Отказ Или МассивПодразделений.Количество() = 0 Тогда
		//ошибка или нет отбора по подразделениям, возвращаем пустой список
		Возврат Неопределено; 	
	КонецЕсли;	
	
	//необходимо собрать список сотрудников, которые работали по состоянию на конец месяца
	//в подразделении из списка МассивПодразделений
	
	Запрос = Новый Запрос;

	Запрос.УстановитьПараметр("парамМассивПодразделений",	МассивПодразделений);
	Запрос.УстановитьПараметр("парамГоловнаяОрганизация",	мГоловнаяОрганизация);
	Запрос.УстановитьПараметр("парамОрганизация",			Организация);
	Запрос.УстановитьПараметр("НачалоПериода",				ПериодРегистрации);
	Запрос.УстановитьПараметр("КонецПериода",				КонецМесяца(ПериодРегистрации));
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВТСписокВсехФизлиц.ФизЛицо,
	|	МИНИМУМ(ВТСписокВсехФизлиц.Договорник) КАК ТолькоДоговорПодряда
	|ПОМЕСТИТЬ ВТСписокВсехФизлиц
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ОсновныеНачисления.ФизЛицо КАК ФизЛицо,
	|		ВЫБОР
	|			КОГДА ОсновныеНачисления.Сотрудник.ВидДоговора В (ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровСФизЛицами.Подряда), ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровСФизЛицами.Авторский))
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК Договорник
	|	ИЗ
	|		РегистрРасчета.ОсновныеНачисленияРаботниковОрганизаций КАК ОсновныеНачисления
	|	ГДЕ
	|		ОсновныеНачисления.ПериодРегистрации = &НачалоПериода
	|		И ОсновныеНачисления.ОбособленноеПодразделение = &парамОрганизация
	|	
	|	ОБЪЕДИНИТЬ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ДополнительныеНачисления.ФизЛицо,
	|		ВЫБОР
	|			КОГДА ДополнительныеНачисления.Сотрудник.ВидДоговора В (ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровСФизЛицами.Подряда), ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровСФизЛицами.Авторский))
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	ИЗ
	|		РегистрРасчета.ДополнительныеНачисленияРаботниковОрганизаций КАК ДополнительныеНачисления
	|	ГДЕ
	|		НАЧАЛОПЕРИОДА(ДополнительныеНачисления.ПериодРегистрации, МЕСЯЦ) = &НачалоПериода
	|		И ДополнительныеНачисления.ОбособленноеПодразделение = &парамОрганизация) КАК ВТСписокВсехФизлиц
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТСписокВсехФизлиц.ФизЛицо
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВТСписокВсехФизлиц.ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписокВсехФизлиц.ФизЛицо КАК Физлицо,
	|	МАКСИМУМ(РаботникиОрганизаций.Период) КАК Период
	|ПОМЕСТИТЬ ВТФизлицаПоследниеДвижения
	|ИЗ
	|	ВТСписокВсехФизлиц КАК СписокВсехФизлиц
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизаций
	|		ПО (РаботникиОрганизаций.Организация = &парамГоловнаяОрганизация)
	|			И (РаботникиОрганизаций.Период <= &КонецПериода)
	|			И (РаботникиОрганизаций.Сотрудник.ВидЗанятости <> ЗНАЧЕНИЕ(Перечисление.ВидыЗанятостиВОрганизации.ВнутреннееСовместительство))
	|			И (ВЫБОР
	|				КОГДА РаботникиОрганизаций.ПериодЗавершения <= &КонецПериода
	|						И РаботникиОрганизаций.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|					ТОГДА РаботникиОрганизаций.ОбособленноеПодразделениеЗавершения
	|				ИНАЧЕ РаботникиОрганизаций.ОбособленноеПодразделение
	|			КОНЕЦ = &парамОрганизация)
	|			И СписокВсехФизлиц.ФизЛицо = РаботникиОрганизаций.Сотрудник.Физлицо
	|ГДЕ
	|	РаботникиОрганизаций.НомерСтроки ЕСТЬ НЕ NULL 
	|	И СписокВсехФизлиц.ТолькоДоговорПодряда = 0
	|
	|СГРУППИРОВАТЬ ПО
	|	СписокВсехФизлиц.ФизЛицо
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Физлицо,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ФизлицаПоследниеДвижения.Физлицо,
	|	ВЫБОР
	|		КОГДА РаботникиОрганизаций.ПериодЗавершения <= &КонецПериода
	|				И РаботникиОрганизаций.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА РаботникиОрганизаций.ПодразделениеОрганизацииЗавершения
	|		ИНАЧЕ РаботникиОрганизаций.ПодразделениеОрганизации
	|	КОНЕЦ КАК ПодразделениеОрганизации,
	|	РаботникиОрганизаций.ПричинаИзмененияСостояния
	|ПОМЕСТИТЬ ВТСтрокиФизлиц
	|ИЗ
	|	ВТФизлицаПоследниеДвижения КАК ФизлицаПоследниеДвижения
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизаций
	|		ПО ФизлицаПоследниеДвижения.Физлицо = РаботникиОрганизаций.Сотрудник.Физлицо
	|			И ФизлицаПоследниеДвижения.Период = РаботникиОрганизаций.Период
	|			И (РаботникиОрганизаций.Организация = &парамГоловнаяОрганизация)
	|			И (РаботникиОрганизаций.Сотрудник.ВидЗанятости <> ЗНАЧЕНИЕ(Перечисление.ВидыЗанятостиВОрганизации.ВнутреннееСовместительство))
	|			И (ВЫБОР
	|				КОГДА РаботникиОрганизаций.ПериодЗавершения <= &КонецПериода
	|						И РаботникиОрганизаций.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|					ТОГДА РаботникиОрганизаций.ОбособленноеПодразделениеЗавершения
	|				ИНАЧЕ РаботникиОрганизаций.ОбособленноеПодразделение
	|			КОНЕЦ = &парамОрганизация)
	|ГДЕ
	|	РаботникиОрганизаций.НомерСтроки ЕСТЬ НЕ NULL 
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПодразделениеОрганизации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ФизлицаПоследниеДвижения.Физлицо,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ФизлицаПоследниеДвижения.ПодразделениеОрганизации) КАК РазныхСтрок
	|ПОМЕСТИТЬ ВТКоличествоСтрок
	|ИЗ
	|	ВТСтрокиФизлиц КАК ФизлицаПоследниеДвижения
	|
	|СГРУППИРОВАТЬ ПО
	|	ФизлицаПоследниеДвижения.Физлицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтрокиФизлиц.Физлицо КАК Физлицо,
	|	СтрокиФизлиц.ПодразделениеОрганизации
	|ПОМЕСТИТЬ ВТФизлицаВсеРаботники
	|ИЗ
	|	ВТСтрокиФизлиц КАК СтрокиФизлиц
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКоличествоСтрок КАК КоличествоСтрок
	|		ПО СтрокиФизлиц.Физлицо = КоличествоСтрок.Физлицо
	|ГДЕ
	|	КоличествоСтрок.РазныхСтрок = 1
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СтрокиФизлиц.Физлицо,
	|	СтрокиФизлиц.ПодразделениеОрганизации
	|ИЗ
	|	ВТСтрокиФизлиц КАК СтрокиФизлиц
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКоличествоСтрок КАК КоличествоСтрок
	|		ПО СтрокиФизлиц.Физлицо = КоличествоСтрок.Физлицо
	|ГДЕ
	|	КоличествоСтрок.РазныхСтрок > 1
	|	И СтрокиФизлиц.ПричинаИзмененияСостояния <> ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.Увольнение)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Физлицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТФизлицаВсеРаботники.Физлицо КАК Физлицо
	|ПОМЕСТИТЬ ВТФизлицаРаботникиПодразделения
	|ИЗ
	|	ВТФизлицаВсеРаботники КАК ВТФизлицаВсеРаботники
	|ГДЕ
	|	ВТФизлицаВсеРаботники.ПодразделениеОрганизации В(&парамМассивПодразделений)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Физлицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТФизлицаВсеРаботники.Физлицо КАК Физлицо
	|ПОМЕСТИТЬ ВТФизлицаРаботникиДругихПодразделений
	|ИЗ
	|	ВТФизлицаВсеРаботники КАК ВТФизлицаВсеРаботники
	|ГДЕ
	|	(НЕ ВТФизлицаВсеРаботники.ПодразделениеОрганизации В (&парамМассивПодразделений))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Физлицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДополнительныеНачисления.ФизЛицо,
	|	МАКСИМУМ(ДополнительныеНачисления.ПодразделениеОрганизации) КАК ПодразделениеОрганизации
	|ПОМЕСТИТЬ ВТСписокВсехДоговорников
	|ИЗ
	|	РегистрРасчета.ДополнительныеНачисленияРаботниковОрганизаций КАК ДополнительныеНачисления
	|ГДЕ
	|	НАЧАЛОПЕРИОДА(ДополнительныеНачисления.ПериодРегистрации, МЕСЯЦ) = &НачалоПериода
	|	И ДополнительныеНачисления.ДокументОснование ССЫЛКА Документ.ДоговорНаВыполнениеРаботСФизЛицом
	|	И ДополнительныеНачисления.ОбособленноеПодразделение = &парамОрганизация
	|
	|СГРУППИРОВАТЬ ПО
	|	ДополнительныеНачисления.ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТСписокВсехДоговорников.ФизЛицо КАК ФизЛицо
	|ПОМЕСТИТЬ ВТСписокФизлицПодразделения
	|ИЗ
	|	ВТСписокВсехДоговорников КАК ВТСписокВсехДоговорников
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТФизлицаРаботникиДругихПодразделений КАК ВТФизлицаРаботникиДругихПодразделений
	|		ПО ВТСписокВсехДоговорников.ФизЛицо = ВТФизлицаРаботникиДругихПодразделений.Физлицо
	|ГДЕ
	|	ВТФизлицаРаботникиДругихПодразделений.Физлицо ЕСТЬ NULL 
	|	И ВТСписокВсехДоговорников.ПодразделениеОрганизации В(&парамМассивПодразделений)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ВТФизлицаРаботникиПодразделения.Физлицо
	|ИЗ
	|	ВТФизлицаРаботникиПодразделения КАК ВТФизлицаРаботникиПодразделения
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СотрудникиОрганизаций.Ссылка КАК Сотрудник,
	|	ВТСписокВсехФизлиц.ФизЛицо
	|ИЗ
	|	ВТСписокФизлицПодразделения КАК ВТСписокВсехФизлиц
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СотрудникиОрганизаций КАК СотрудникиОрганизаций
	|		ПО ВТСписокВсехФизлиц.ФизЛицо = СотрудникиОрганизаций.Физлицо
	|			И (СотрудникиОрганизаций.Организация = &парамГоловнаяОрганизация)";
	
	Запрос.Текст = ТекстЗапроса;
	
    Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции // ПолучитьОграничениеНаСотрудников()

// расчет документа
//
Процедура РассчитатьВзносы(ВыборкаПоШапкеДокумента, ОграничениеНаСотрудников = Неопределено)
	
	НаборЗаписейОДоходах = РегистрыНакопления.СтраховыеВзносыСведенияОДоходах.СоздатьНаборЗаписей().ВыгрузитьКолонки();
	Отказ = Ложь;
	РасчетСтраховыхВзносовПереопределяемый.СформироватьДвиженияПоДоходам(ЭтотОбъект, ВыборкаПоШапкеДокумента, "", Отказ, НаборЗаписейОДоходах);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если ОграничениеНаСотрудников = Неопределено Тогда
		ОграничениеНаСотрудников = ПолучитьОграничениеНаСотрудников(Отказ);	
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	СформироватьДвиженияПоСкидкам(ВыборкаПоШапкеДокумента, НаборЗаписейОДоходах);
	
	ИсчисленныеСтраховыеВзносы.Загрузить(РасчетСтраховыхВзносов.РассчитатьВзносы(ВыборкаПоШапкеДокумента, ОграничениеНаСотрудников, НаборЗаписейОДоходах));
	
КонецПроцедуры // РассчитатьВзносы()

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// заполнение доходов и расчет документа
Процедура ЗаполнитьИРассчитать(Скидки = Истина, Взносы = Истина) Экспорт

	Если Не Скидки И Не Взносы Тогда  // делать ничего не надо
		Возврат
	КонецЕсли;
	
	Отказ = Ложь;
	
	мГоловнаяОрганизация = ОбщегоНазначенияЗК.ГоловнаяОрганизация(Организация);
	ОписаниеДокумента = Новый Структура;
	ОписаниеДокумента.Вставить("Организация",				мГоловнаяОрганизация);
	ОписаниеДокумента.Вставить("ОрганизацияПредставление",	Строка(мГоловнаяОрганизация));
	ОписаниеДокумента.Вставить("ОбособленноеПодразделение",	Организация);
	ОписаниеДокумента.Вставить("Ссылка",					Ссылка);
	ОписаниеДокумента.Вставить("Год",						Год(ПериодРегистрации));
	ОписаниеДокумента.Вставить("Период",					ПериодРегистрации);
	ОписаниеДокумента.Вставить("ПериодРегистрации",			ПериодРегистрации);
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ГоловнаяОрганизация",мГоловнаяОрганизация);
	Запрос.УстановитьПараметр("ПериодРегистрации",ПериодРегистрации);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА УчетнаяПолитикаНалоговыйУчетСрезПоследних.ВидТарифаСтраховыхВзносов = ЗНАЧЕНИЕ(Перечисление.ТарифыСтраховыхВзносов.ПустаяСсылка)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НеУказанВидТарифа
	|ИЗ
	|	РегистрСведений.УчетнаяПолитикаНалоговыйУчет.СрезПоследних(&ПериодРегистрации, Организация = &ГоловнаяОрганизация) КАК УчетнаяПолитикаНалоговыйУчетСрезПоследних";
	Запрос.Текст = СтрЗаменить(Запрос.Текст,"РегистрСведений.УчетнаяПолитикаНалоговыйУчет", ЗаполнениеРегламентированнойОтчетностиПереопределяемый.ИмяУчетнойПолитики());
	Выборка = Запрос.Выполнить().Выбрать();
	НеУказанВидТарифа = Ложь;
	Если Выборка.Следующий() Тогда
		НеУказанВидТарифа = Выборка.НеУказанВидТарифа
	КонецЕсли;
	ОписаниеДокумента.Вставить("НеУказанВидТарифа", НеУказанВидТарифа);
	
	ПроверитьЗаполнениеШапки(ОписаниеДокумента, Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если Взносы Тогда // выберем сотрудников для последующей обработки
		ОграничениеНаСотрудников = ПолучитьОграничениеНаСотрудников(Отказ);	
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если Скидки И Взносы Тогда // надо полностью перезаполнить данные о доходах
		РасчетСтраховыхВзносовПереопределяемый.Автозаполнение(ЭтотОбъект, ОписаниеДокумента, ОграничениеНаСотрудников, Отказ);
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если Скидки и Не Взносы Тогда // только пересчет скидок к доходам
		РасчетСкидок(ОписаниеДокумента);
	КонецЕсли;
	
	Если Взносы Тогда // полное заполнение или пересчет взносов
		РассчитатьВзносы(ОписаниеДокумента, ОграничениеНаСотрудников);
	КонецЕсли;

КонецПроцедуры

Процедура РасчетСкидок(ВыборкаПоШапкеДокумента) Экспорт
	
	Если НеоблагаемыеСуммыДоходов.Количество() = 0 Тогда  // считать оказалось нечего
		Возврат
	КонецЕсли;
	
	Отказ = Ложь;
	ДанныеОДоходах = РегистрыНакопления.СтраховыеВзносыСведенияОДоходах.СоздатьНаборЗаписей().ВыгрузитьКолонки();
	РасчетСтраховыхВзносовПереопределяемый.СформироватьДвиженияПоДоходам(ЭтотОбъект, ВыборкаПоШапкеДокумента, "", Отказ, ДанныеОДоходах);
	Если Отказ Тогда
		Возврат	
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("парамДанныеОДоходах",				ДанныеОДоходах);
	Запрос.УстановитьПараметр("парамФизлица",						НеоблагаемыеСуммыДоходов.ВыгрузитьКолонку("ФизЛицо"));
	Запрос.УстановитьПараметр("парамРегистратор",					ВыборкаПоШапкеДокумента.Ссылка);
	Запрос.УстановитьПараметр("парамКонецМесяцаПериодаРегистрации",	КонецМесяца(ВыборкаПоШапкеДокумента.ПериодРегистрации));
	Запрос.УстановитьПараметр("парамГоловнаяОрганизация",			ВыборкаПоШапкеДокумента.Организация);
	
	МодифицируемаяТаблица = НеоблагаемыеСуммыДоходов.Выгрузить();
	
	Если ВыборкаПоШапкеДокумента.ПериодРегистрации < РасчетСтраховыхВзносов.ДатаОбъединенияСтраховойИНакопительнойЧастейВзносовПФР() Тогда
		
		Запрос.УстановитьПараметр("парамНачалоГода", НачалоГода(ВыборкаПоШапкеДокумента.ПериодРегистрации));
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	СведенияОДоходах.ФизЛицо КАК ФизЛицо,
		|	СведенияОДоходах.ВидДохода КАК ВидДохода,
		|	СведенияОДоходах.Результат
		|ПОМЕСТИТЬ ВТДанныеДокумента
		|ИЗ
		|	&парамДанныеОДоходах КАК СведенияОДоходах
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ВидДохода,
		|	ФизЛицо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДоходыПоМесяцам.ВидДохода КАК ВидДохода,
		|	ДоходыПоМесяцам.ФизЛицо,
		|	СУММА(ДоходыПоМесяцам.Результат) КАК РезультатЗаГод,
		|	СУММА(ДоходыПоМесяцам.Скидка) КАК ПредоставленнаяСкидкаЗаГод
		|ИЗ
		|	(ВЫБРАТЬ
		|		СведенияОДоходах.ФизЛицо КАК ФизЛицо,
		|		СведенияОДоходах.РезультатОборот КАК Результат,
		|		СведенияОДоходах.СкидкаОборот КАК Скидка,
		|		СведенияОДоходах.ВидДохода КАК ВидДохода
		|	ИЗ
		|		РегистрНакопления.СтраховыеВзносыСведенияОДоходах.Обороты(
		|				&парамНачалоГода,
		|				&парамКонецМесяцаПериодаРегистрации,
		|				Месяц,
		|				Организация = &парамГоловнаяОрганизация
		|					И ВидДохода В (ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.МатпомощьПрокуроров), ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.Матпомощь))
		|					И Физлицо В (&парамФизлица)) КАК СведенияОДоходах
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		СтраховыеВзносыСведенияОДоходах.ФизЛицо,
		|		-СтраховыеВзносыСведенияОДоходах.Результат,
		|		-СтраховыеВзносыСведенияОДоходах.Скидка,
		|		СтраховыеВзносыСведенияОДоходах.ВидДохода
		|	ИЗ
		|		РегистрНакопления.СтраховыеВзносыСведенияОДоходах КАК СтраховыеВзносыСведенияОДоходах
		|	ГДЕ
		|		СтраховыеВзносыСведенияОДоходах.Регистратор = &парамРегистратор
		|		И СтраховыеВзносыСведенияОДоходах.ФизЛицо В(&парамФизлица)
		|		И СтраховыеВзносыСведенияОДоходах.ВидДохода В (ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.МатпомощьПрокуроров), ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.Матпомощь))
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		СтраховыеВзносыСведенияОДоходах.ФизЛицо,
		|		СтраховыеВзносыСведенияОДоходах.Результат,
		|		0,
		|		СтраховыеВзносыСведенияОДоходах.ВидДохода
		|	ИЗ
		|		ВТДанныеДокумента КАК СтраховыеВзносыСведенияОДоходах
		|	ГДЕ
		|		СтраховыеВзносыСведенияОДоходах.ФизЛицо В(&парамФизлица)
		|		И СтраховыеВзносыСведенияОДоходах.ВидДохода В (ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.МатпомощьПрокуроров), ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.Матпомощь))) КАК ДоходыПоМесяцам
		|
		|СГРУППИРОВАТЬ ПО
		|	ДоходыПоМесяцам.ФизЛицо,
		|	ДоходыПоМесяцам.ВидДохода";
		
		СтруктураПоиска = Новый Структура("Физлицо, ВидДохода");
		ПравоНаСкидку = РегистрыСведений.СтраховыеВзносыСкидкиКДоходам.ПолучитьПоследнее(НачалоГода(ВыборкаПоШапкеДокумента.ПериодРегистрации), Новый Структура("ВидДохода", Справочники.ДоходыПоСтраховымВзносам.Матпомощь)).ГодоваяСкидка;
		Если Не ЗначениеЗаполнено(ПравоНаСкидку) Тогда
			ПравоНаСкидку = 0
		КонецЕсли;
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			
			// Распределим скидку между строками о доходах
			ЗаполнитьЗначенияСвойств(СтруктураПоиска, Выборка); // Физлицо, ВидДохода
			
			НайденныеСтроки = МодифицируемаяТаблица.НайтиСтроки(СтруктураПоиска);
			Если НайденныеСтроки.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли; 
			
			// В общем случае предоставляемая скидка есть
			НайденныеСтроки[0].Скидка = Мин(Выборка.РезультатЗаГод, ПравоНаСкидку) - Выборка.ПредоставленнаяСкидкаЗаГод;
			Если НайденныеСтроки.Количество() > 1 Тогда
				Для Сч = 1  По НайденныеСтроки.Количество() - 1 Цикл
					НайденныеСтроки[Сч].Скидка = 0
				КонецЦикла;
			КонецЕсли;
			
		КонецЦикла; 
		
	Иначе
		
		ТаблицаДат = МодифицируемаяТаблица.Скопировать(, "ДатаПолученияДохода");
		ТаблицаДат.Сортировать("ДатаПолученияДохода");
		НачалоПериода = НачалоГода(Мин(ВыборкаПоШапкеДокумента.ПериодРегистрации, ТаблицаДат[0].ДатаПолученияДохода));
		ТаблицаДат =  Неопределено;
		
		Запрос.УстановитьПараметр("парамНачалоГода", НачалоПериода);
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	СведенияОДоходах.ФизЛицо КАК ФизЛицо,
		|	СведенияОДоходах.ВидДохода КАК ВидДохода,
		|	СведенияОДоходах.ДатаПолученияДохода,
		|	СведенияОДоходах.Результат
		|ПОМЕСТИТЬ ВТДанныеДокумента
		|ИЗ
		|	&парамДанныеОДоходах КАК СведенияОДоходах
		|ГДЕ
		|	СведенияОДоходах.ФизЛицо В(&парамФизлица)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ВидДохода,
		|	ФизЛицо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СведенияОДоходах.ФизЛицо КАК ФизЛицо,
		|	СведенияОДоходах.ВидДохода КАК ВидДохода,
		|	СведенияОДоходах.ДатаПолученияДохода КАК ДатаПолученияДохода,
		|	СведенияОДоходах.РезультатОборот КАК Результат,
		|	СведенияОДоходах.СкидкаОборот КАК Скидка
		|ПОМЕСТИТЬ ВТДанныеУчета
		|ИЗ
		|	РегистрНакопления.СтраховыеВзносыСведенияОДоходах.Обороты(
		|			&парамНачалоГода,
		|			&парамКонецМесяцаПериодаРегистрации,
		|			,
		|			Организация = &парамГоловнаяОрганизация
		|				И Физлицо В (&парамФизлица)) КАК СведенияОДоходах
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СтраховыеВзносыСведенияОДоходах.ФизЛицо,
		|	СтраховыеВзносыСведенияОДоходах.ВидДохода,
		|	СтраховыеВзносыСведенияОДоходах.ДатаПолученияДохода,
		|	-СтраховыеВзносыСведенияОДоходах.Результат,
		|	-СтраховыеВзносыСведенияОДоходах.Скидка
		|ИЗ
		|	РегистрНакопления.СтраховыеВзносыСведенияОДоходах КАК СтраховыеВзносыСведенияОДоходах
		|ГДЕ
		|	СтраховыеВзносыСведенияОДоходах.Регистратор = &парамРегистратор
		|	И СтраховыеВзносыСведенияОДоходах.ФизЛицо В(&парамФизлица)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СтраховыеВзносыСведенияОДоходах.ФизЛицо,
		|	СтраховыеВзносыСведенияОДоходах.ВидДохода,
		|	СтраховыеВзносыСведенияОДоходах.ДатаПолученияДохода,
		|	СтраховыеВзносыСведенияОДоходах.Результат,
		|	0
		|ИЗ
		|	ВТДанныеДокумента КАК СтраховыеВзносыСведенияОДоходах
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТДанныеДокумента";
		
		Запрос.Выполнить();
		
		РасчетСтраховыхВзносов.РасчетСкидок(НачалоГода(ВыборкаПоШапкеДокумента.ПериодРегистрации), МодифицируемаяТаблица, Запрос.МенеджерВременныхТаблиц);
		
	КонецЕсли;
	
	НеоблагаемыеСуммыДоходов.Загрузить(МодифицируемаяТаблица);
	
КонецПроцедуры // РасчетСкидок()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

/// Формирует запрос по шапке документа
//
// Параметры: 
//	нет
//
// Возвращаемое значение:
//	Результат запроса.
//
Функция СформироватьЗапросПоШапке()

	Запрос = Новый Запрос;

	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка" , Ссылка);
	Запрос.УстановитьПараметр("ПериодРегистрации" , ПериодРегистрации);

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РасчетСтраховыхВзносов.Ссылка,
	|	РасчетСтраховыхВзносов.Дата,
	|	РасчетСтраховыхВзносов.Организация КАК ОбособленноеПодразделение,
	|	РасчетСтраховыхВзносов.Ответственный,
	|	РасчетСтраховыхВзносов.ПериодРегистрации КАК ПериодРегистрации,
	|	РасчетСтраховыхВзносов.ПериодРегистрации КАК Период,
	|	ГОД(РасчетСтраховыхВзносов.ПериодРегистрации) КАК Год,
	|	ВЫБОР
	|		КОГДА РасчетСтраховыхВзносов.Организация.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА РасчетСтраховыхВзносов.Организация
	|		ИНАЧЕ РасчетСтраховыхВзносов.Организация.ГоловнаяОрганизация
	|	КОНЕЦ КАК Организация,
	|	ПРЕДСТАВЛЕНИЕ(ВЫБОР
	|			КОГДА РасчетСтраховыхВзносов.Организация.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|				ТОГДА РасчетСтраховыхВзносов.Организация
	|			ИНАЧЕ РасчетСтраховыхВзносов.Организация.ГоловнаяОрганизация
	|		КОНЕЦ) КАК ОрганизацияПредставление,
	|	ВЫБОР
	|		КОГДА УчетнаяПолитикаНалоговыйУчетСрезПоследних.ВидТарифаСтраховыхВзносов = ЗНАЧЕНИЕ(Перечисление.ТарифыСтраховыхВзносов.ПустаяСсылка) 
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НеУказанВидТарифа
	|ИЗ
	|	Документ.РасчетСтраховыхВзносов КАК РасчетСтраховыхВзносов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаНалоговыйУчет.СрезПоследних(&ПериодРегистрации, ) КАК УчетнаяПолитикаНалоговыйУчетСрезПоследних
	|		ПО (ВЫБОР
	|				КОГДА РасчетСтраховыхВзносов.Организация.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|					ТОГДА РасчетСтраховыхВзносов.Организация
	|				ИНАЧЕ РасчетСтраховыхВзносов.Организация.ГоловнаяОрганизация
	|			КОНЕЦ = УчетнаяПолитикаНалоговыйУчетСрезПоследних.Организация)
	|ГДЕ
	|	РасчетСтраховыхВзносов.Ссылка = &ДокументСсылка";
	Запрос.Текст = СтрЗаменить(Запрос.Текст,"РегистрСведений.УчетнаяПолитикаНалоговыйУчет", ЗаполнениеРегламентированнойОтчетностиПереопределяемый.ИмяУчетнойПолитики());

	Возврат Запрос.Выполнить();

КонецФункции // СформироватьЗапросПоШапке()

// Проверяет правильность заполнения шапки документа.
// Если какой-то из реквизитов шапки, влияющий на проведение не заполнен или
// заполнен не корректно, то выставляется флаг отказа в проведении.
// Проверка выполняется по выборке из результата запроса по шапке,
// все проверяемые реквизиты должны быть включены в выборку по шапке.
//
// Параметры: 
//	ВыборкаПоШапкеДокумента	- выборка из результата запроса по шапке документа,
//	Отказ					- флаг отказа в проведении,
//	Заголовок				- Заголовок для сообщений об ошибках проведения.
//
Процедура ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ, Заголовок = "")

	Если Не ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.ОбособленноеПодразделение) Тогда
		СтрокаСообщения = ОбщегоНазначенияЗК.ПреобразоватьСтрокуИнтерфейса("Не указана организация!");
		ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке(СтрокаСообщения, Отказ, Заголовок);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.Период) Тогда
		ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке("Не указан период регистрации!", Отказ, Заголовок);
	ИначеЕсли ВыборкаПоШапкеДокумента.ПериодРегистрации < ПроведениеРасчетов.ДатаЗаменыЕСНСтраховымиВзносами() Тогда
		ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке("Уплата страховых взносов вводится с 1 января 2010 года!", Отказ);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.ОбособленноеПодразделение) и ПустаяСтрока(Заголовок) Тогда

		Если ВыборкаПоШапкеДокумента.НеУказанВидТарифа Тогда
			
			Расшифровки = Новый Массив;
			// расшифровка сообщений представляет собой массив структур "Представление+Расшифровка"
			ОткрытьСведенияОСтавках = Новый Массив;
			ОткрытьСведенияОСтавках.Добавить("ПроцедурыУправленияПерсоналом.ОткрытьФормуНастройкаПараметровУчета");
			ОткрытьСведенияОСтавках.Добавить("НалоговыйУчет");
			ОткрытьСведенияОСтавках.Добавить(Новый Структура("Организация, Год", ВыборкаПоШапкеДокумента.Организация, ВыборкаПоШапкеДокумента.Год));
			Расшифровки.Добавить(Новый Структура("Представление,Расшифровка", Формат(ВыборкаПоШапкеДокумента.Год, "ЧЦ=4; ЧГ="), ОткрытьСведенияОСтавках));
	
			СтрокаСообщения = ОбщегоНазначенияЗК.ПреобразоватьСтрокуИнтерфейса("организации") + " " + ВыборкаПоШапкеДокумента.ОрганизацияПредставление;
			ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке(
			"Для " + СтрокаСообщения + " на %% год не указан вид тарифа страховых взносов в ПФР, ФОМС и ФСС!",
					Отказ, Заголовок, Перечисления.ВидыСообщений.Ошибка, Расшифровки);
					
		КонецЕсли;
		
	КонецЕсли;	
	
КонецПроцедуры // ПроверитьЗаполнениеШапки()

// Выполняет проверку правильности заполнения строки табличной части налогов
Процедура ПроверитьЗаполнениеСтрокиВзносов(ВыборкаПоВзносам, Отказ, Заголовок = "")
	
	СтрокаНачалаСообщенияОбОшибке = "В строке номер """+ СокрЛП(ВыборкаПоВзносам.НомерСтроки) +
	""" табл. части Исчисленные страховые взносы: ";
	
	Если Не ЗначениеЗаполнено(ВыборкаПоВзносам.ФизЛицо) Тогда
		ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке(СтрокаНачалаСообщенияОбОшибке  + "Не указан сотрудник!", Отказ, Заголовок);
	КонецЕсли;
	
КонецПроцедуры // ПроверитьЗаполнениеСтрокиВзносов()

Процедура ПроверитьЗаполнениеСтрокиСкидок(Выборка, Отказ, Заголовок = "")
	
	СтрокаНачалаСообщенияОбОшибке = "В строке номер """+ СокрЛП(Выборка.НомерСтроки) +
	""" табл. части Необлагаемые суммы: ";
	
	Если Не ЗначениеЗаполнено(Выборка.ФизЛицо) Тогда
		ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке(СтрокаНачалаСообщенияОбОшибке  + "Не указан сотрудник!", Отказ, Заголовок);
	КонецЕсли;
	
КонецПроцедуры // ПроверитьЗаполнениеСтрокиВзносов()

// Добавляет запись в движения по регистру учета налогов
Процедура ДобавитьСтрокуВДвиженияПоВзносам(ВыборкаПоВзносам, ВыборкаПоШапкеДокумента)
	
	СтрокаНабора = Движения.СтраховыеВзносыИсчисленные.Добавить();
	ЗаполнитьЗначенияСвойств(СтрокаНабора,ВыборкаПоШапкеДокумента);
	ЗаполнитьЗначенияСвойств(СтрокаНабора,ВыборкаПоВзносам);
	
КонецПроцедуры // ДобавитьСтрокуВДвиженияПоВзносам()

Процедура СформироватьДвиженияПоСкидкам(ВыборкаПоШапкеДокумента, ДанныеОДоходах = Неопределено) Экспорт
	
	Если ДанныеОДоходах = Неопределено Тогда
		Отказ = Ложь;
		ДанныеОДоходах = РегистрыНакопления.СтраховыеВзносыСведенияОДоходах.СоздатьНаборЗаписей().ВыгрузитьКолонки();
		РасчетСтраховыхВзносовПереопределяемый.СформироватьДвиженияПоДоходам(ЭтотОбъект, ВыборкаПоШапкеДокумента, "", Отказ, ДанныеОДоходах);
		Если Отказ Тогда
			Возврат	
		КонецЕсли;
		НаборЗаписейОДоходах = Движения.СтраховыеВзносыСведенияОДоходах;
	Иначе
		НаборЗаписейОДоходах = ДанныеОДоходах;
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Регистратор",	ВыборкаПоШапкеДокумента.Ссылка);
	Запрос.УстановитьПараметр("ПериодРегистрации",ВыборкаПоШапкеДокумента.ПериодРегистрации);
	Запрос.УстановитьПараметр("НачалоПериода",	НачалоМесяца(ВыборкаПоШапкеДокумента.ПериодРегистрации));
	Запрос.УстановитьПараметр("КонецПериода",	КонецМесяца(ВыборкаПоШапкеДокумента.ПериодРегистрации));
	Запрос.УстановитьПараметр("ОбособленноеПодразделение",	ВыборкаПоШапкеДокумента.ОбособленноеПодразделение);
	Запрос.УстановитьПараметр("ТаблицаДоходов",	ДанныеОДоходах);
	Запрос.УстановитьПараметр("НеоблагаемыеСуммы", НеоблагаемыеСуммыДоходов);
	
	ТаблицаРаспределения = Новый ТаблицаЗначений;
	ТаблицаРаспределения.Колонки.Добавить("ФизЛицо");
	ТаблицаРаспределения.Колонки.Добавить("ВидДохода");
	ТаблицаРаспределения.Колонки.Добавить("ОблагаетсяПоДополнительномуТарифу");
	ТаблицаРаспределения.Колонки.Добавить("ВидРасчета");
	ТаблицаРаспределения.Колонки.Добавить("Результат");
	ТаблицаРаспределения.Колонки.Добавить("ОблагаетсяЕНВД");
	
	Если ПериодРегистрации < ПроведениеРасчетов.ДатаРасширенияПеречняЛьготныхТарифовСтраховыхВзносов() Тогда
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	СтраховыеВзносыСведенияОДоходах.ФизЛицо,
		|	СтраховыеВзносыСведенияОДоходах.ВидДохода,
		|	СтраховыеВзносыСведенияОДоходах.ОблагаетсяЕНВД,
		|	СтраховыеВзносыСведенияОДоходах.ОблагаетсяПоДополнительномуТарифу,
		|	СтраховыеВзносыСведенияОДоходах.Результат,
		|	СтраховыеВзносыСведенияОДоходах.ВидРасчета
		|ПОМЕСТИТЬ ВТДанныеДоходовДокумента
		|ИЗ
		|	&ТаблицаДоходов КАК СтраховыеВзносыСведенияОДоходах
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РасчетИсчисленный.ФизЛицо КАК ФизЛицо,
		|	РасчетИсчисленный.Скидка,
		|	РасчетИсчисленный.ВидДохода КАК ВидДохода
		|ПОМЕСТИТЬ ВТДанныеДокумента
		|ИЗ
		|	&НеоблагаемыеСуммы КАК РасчетИсчисленный
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо,
		|	ВидДохода
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеДокумента.ФизЛицо КАК ФизЛицо,
		|	ДанныеДокумента.ВидДохода КАК ВидДохода,
		|	СведенияОДоходах.Результат КАК Результат,
		|	СведенияОДоходах.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	СведенияОДоходах.ВидРасчета КАК ВидРасчета,
		|	СведенияОДоходах.ОблагаетсяПоДополнительномуТарифу КАК ОблагаетсяПоДополнительномуТарифу
		|ПОМЕСТИТЬ ВТСведенияОДоходах
		|ИЗ
		|	ВТДанныеДокумента КАК ДанныеДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СтраховыеВзносыСведенияОДоходах КАК СведенияОДоходах
		|		ПО ДанныеДокумента.ФизЛицо = СведенияОДоходах.ФизЛицо
		|			И ДанныеДокумента.ВидДохода = СведенияОДоходах.ВидДохода
		|ГДЕ
		|	НАЧАЛОПЕРИОДА(СведенияОДоходах.Период, МЕСЯЦ) = &ПериодРегистрации
		|	И СведенияОДоходах.ОбособленноеПодразделение = &ОбособленноеПодразделение
		|	И СведенияОДоходах.Регистратор <> &Регистратор
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДанныеДокумента.ФизЛицо,
		|	ДанныеДокумента.ВидДохода,
		|	СведенияОДоходах.Результат,
		|	СведенияОДоходах.ОблагаетсяЕНВД,
		|	СведенияОДоходах.ВидРасчета,
		|	СведенияОДоходах.ОблагаетсяПоДополнительномуТарифу
		|ИЗ
		|	ВТДанныеДокумента КАК ДанныеДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДанныеДоходовДокумента КАК СведенияОДоходах
		|		ПО ДанныеДокумента.ФизЛицо = СведенияОДоходах.ФизЛицо
		|			И ДанныеДокумента.ВидДохода = СведенияОДоходах.ВидДохода
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо,
		|	ВидДохода
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(СведенияОДоходах.Результат) КАК Результат,
		|	СведенияОДоходах.ФизЛицо КАК ФизЛицо,
		|	СведенияОДоходах.ВидДохода КАК ВидДохода,
		|	СведенияОДоходах.ОблагаетсяЕНВД,
		|	СведенияОДоходах.ВидРасчета,
		|	СведенияОДоходах.ОблагаетсяПоДополнительномуТарифу
		|ПОМЕСТИТЬ ВТДоходы
		|ИЗ
		|	ВТСведенияОДоходах КАК СведенияОДоходах
		|
		|СГРУППИРОВАТЬ ПО
		|	СведенияОДоходах.ФизЛицо,
		|	СведенияОДоходах.ВидДохода,
		|	СведенияОДоходах.ОблагаетсяЕНВД,
		|	СведенияОДоходах.ВидРасчета,
		|	СведенияОДоходах.ОблагаетсяПоДополнительномуТарифу
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо,
		|	ВидДохода
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеДокумента.ФизЛицо КАК ФизЛицо,
		|	ДанныеДокумента.ВидДохода КАК ВидДохода,
		|	ДанныеДокумента.Скидка,
		|	СведенияОДоходах.ОблагаетсяЕНВД,
		|	ЕСТЬNULL(СведенияОДоходах.Результат, 0) КАК Результат,
		|	СведенияОДоходах.ВидРасчета,
		|	СведенияОДоходах.ОблагаетсяПоДополнительномуТарифу
		|ИЗ
		|	ВТДанныеДокумента КАК ДанныеДокумента
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДоходы КАК СведенияОДоходах
		|		ПО ДанныеДокумента.ФизЛицо = СведенияОДоходах.ФизЛицо
		|			И ДанныеДокумента.ВидДохода = СведенияОДоходах.ВидДохода
		|
		|УПОРЯДОЧИТЬ ПО
		|	ФизЛицо,
		|	ВидДохода";
	ИначеЕсли ПериодРегистрации < ПроведениеРасчетов.ДатаСниженияТарифовСтраховыхВзносов() Тогда
		
		ТаблицаРаспределения.Колонки.Добавить("ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам");
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	СтраховыеВзносыСведенияОДоходах.ФизЛицо,
		|	СтраховыеВзносыСведенияОДоходах.ВидДохода,
		|	СтраховыеВзносыСведенияОДоходах.ОблагаетсяЕНВД,
		|	СтраховыеВзносыСведенияОДоходах.ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам,
		|	СтраховыеВзносыСведенияОДоходах.ОблагаетсяПоДополнительномуТарифу,
		|	СтраховыеВзносыСведенияОДоходах.Результат,
		|	СтраховыеВзносыСведенияОДоходах.ВидРасчета
		|ПОМЕСТИТЬ ВТДанныеДоходовДокумента
		|ИЗ
		|	&ТаблицаДоходов КАК СтраховыеВзносыСведенияОДоходах
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РасчетИсчисленный.ФизЛицо КАК ФизЛицо,
		|	РасчетИсчисленный.Скидка,
		|	РасчетИсчисленный.ВидДохода КАК ВидДохода
		|ПОМЕСТИТЬ ВТДанныеДокумента
		|ИЗ
		|	&НеоблагаемыеСуммы КАК РасчетИсчисленный
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо,
		|	ВидДохода
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеДокумента.ФизЛицо КАК ФизЛицо,
		|	ДанныеДокумента.ВидДохода КАК ВидДохода,
		|	СведенияОДоходах.Результат КАК Результат,
		|	СведенияОДоходах.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	СведенияОДоходах.ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам КАК ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам,
		|	СведенияОДоходах.ВидРасчета КАК ВидРасчета,
		|	СведенияОДоходах.ОблагаетсяПоДополнительномуТарифу КАК ОблагаетсяПоДополнительномуТарифу
		|ПОМЕСТИТЬ ВТСведенияОДоходах
		|ИЗ
		|	ВТДанныеДокумента КАК ДанныеДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СтраховыеВзносыСведенияОДоходах КАК СведенияОДоходах
		|		ПО ДанныеДокумента.ФизЛицо = СведенияОДоходах.ФизЛицо
		|			И ДанныеДокумента.ВидДохода = СведенияОДоходах.ВидДохода
		|ГДЕ
		|	НАЧАЛОПЕРИОДА(СведенияОДоходах.Период, МЕСЯЦ) = &ПериодРегистрации
		|	И СведенияОДоходах.ОбособленноеПодразделение = &ОбособленноеПодразделение
		|	И СведенияОДоходах.Регистратор <> &Регистратор
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДанныеДокумента.ФизЛицо,
		|	ДанныеДокумента.ВидДохода,
		|	СведенияОДоходах.Результат,
		|	СведенияОДоходах.ОблагаетсяЕНВД,
		|	СведенияОДоходах.ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам,
		|	СведенияОДоходах.ВидРасчета,
		|	СведенияОДоходах.ОблагаетсяПоДополнительномуТарифу
		|ИЗ
		|	ВТДанныеДокумента КАК ДанныеДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДанныеДоходовДокумента КАК СведенияОДоходах
		|		ПО ДанныеДокумента.ФизЛицо = СведенияОДоходах.ФизЛицо
		|			И ДанныеДокумента.ВидДохода = СведенияОДоходах.ВидДохода
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо,
		|	ВидДохода
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(СведенияОДоходах.Результат) КАК Результат,
		|	СведенияОДоходах.ФизЛицо КАК ФизЛицо,
		|	СведенияОДоходах.ВидДохода КАК ВидДохода,
		|	СведенияОДоходах.ОблагаетсяЕНВД,
		|	СведенияОДоходах.ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам,
		|	СведенияОДоходах.ВидРасчета,
		|	СведенияОДоходах.ОблагаетсяПоДополнительномуТарифу
		|ПОМЕСТИТЬ ВТДоходы
		|ИЗ
		|	ВТСведенияОДоходах КАК СведенияОДоходах
		|
		|СГРУППИРОВАТЬ ПО
		|	СведенияОДоходах.ФизЛицо,
		|	СведенияОДоходах.ОблагаетсяЕНВД,
		|	СведенияОДоходах.ВидДохода,
		|	СведенияОДоходах.ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам,
		|	СведенияОДоходах.ВидРасчета,
		|	СведенияОДоходах.ОблагаетсяПоДополнительномуТарифу
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо,
		|	ВидДохода
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеДокумента.ФизЛицо КАК ФизЛицо,
		|	ДанныеДокумента.ВидДохода КАК ВидДохода,
		|	ДанныеДокумента.Скидка,
		|	СведенияОДоходах.ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам,
		|	СведенияОДоходах.ОблагаетсяЕНВД,
		|	ЕСТЬNULL(СведенияОДоходах.Результат, 0) КАК Результат,
		|	СведенияОДоходах.ВидРасчета,
		|	СведенияОДоходах.ОблагаетсяПоДополнительномуТарифу
		|ИЗ
		|	ВТДанныеДокумента КАК ДанныеДокумента
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДоходы КАК СведенияОДоходах
		|		ПО ДанныеДокумента.ФизЛицо = СведенияОДоходах.ФизЛицо
		|			И ДанныеДокумента.ВидДохода = СведенияОДоходах.ВидДохода
		|
		|УПОРЯДОЧИТЬ ПО
		|	ФизЛицо,
		|	ВидДохода";
		
	ИначеЕсли ПериодРегистрации < ПроведениеРасчетов.ДатаВводаДополнительногоТарифаЗаРаботыСДосрочнойПенсией() Тогда
		
		ТаблицаРаспределения.Колонки.Добавить("ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам");
		ТаблицаРаспределения.Колонки.Добавить("ЯвляетсяДоходомФармацевта");
		ТаблицаРаспределения.Колонки.Добавить("ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ");
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	СтраховыеВзносыСведенияОДоходах.ФизЛицо,
		|	СтраховыеВзносыСведенияОДоходах.ВидДохода,
		|	СтраховыеВзносыСведенияОДоходах.ОблагаетсяЕНВД,
		|	СтраховыеВзносыСведенияОДоходах.ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам,
		|	СтраховыеВзносыСведенияОДоходах.ОблагаетсяПоДополнительномуТарифу,
		|	СтраховыеВзносыСведенияОДоходах.ЯвляетсяДоходомФармацевта,
		|	СтраховыеВзносыСведенияОДоходах.ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ,
		|	СтраховыеВзносыСведенияОДоходах.Результат,
		|	СтраховыеВзносыСведенияОДоходах.ВидРасчета
		|ПОМЕСТИТЬ ВТДанныеДоходовДокумента
		|ИЗ
		|	&ТаблицаДоходов КАК СтраховыеВзносыСведенияОДоходах
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РасчетИсчисленный.ФизЛицо КАК ФизЛицо,
		|	РасчетИсчисленный.Скидка,
		|	РасчетИсчисленный.ВидДохода КАК ВидДохода
		|ПОМЕСТИТЬ ВТДанныеДокумента
		|ИЗ
		|	&НеоблагаемыеСуммы КАК РасчетИсчисленный
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо,
		|	ВидДохода
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеДокумента.ФизЛицо КАК ФизЛицо,
		|	ДанныеДокумента.ВидДохода КАК ВидДохода,
		|	СведенияОДоходах.Результат КАК Результат,
		|	СведенияОДоходах.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	СведенияОДоходах.ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам КАК ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам,
		|	СведенияОДоходах.ЯвляетсяДоходомФармацевта,
		|	СведенияОДоходах.ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ,
		|	СведенияОДоходах.ВидРасчета КАК ВидРасчета,
		|	СведенияОДоходах.ОблагаетсяПоДополнительномуТарифу КАК ОблагаетсяПоДополнительномуТарифу
		|ПОМЕСТИТЬ ВТСведенияОДоходах
		|ИЗ
		|	ВТДанныеДокумента КАК ДанныеДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СтраховыеВзносыСведенияОДоходах КАК СведенияОДоходах
		|		ПО ДанныеДокумента.ФизЛицо = СведенияОДоходах.ФизЛицо
		|			И ДанныеДокумента.ВидДохода = СведенияОДоходах.ВидДохода
		|ГДЕ
		|	НАЧАЛОПЕРИОДА(СведенияОДоходах.Период, МЕСЯЦ) = &ПериодРегистрации
		|	И СведенияОДоходах.ОбособленноеПодразделение = &ОбособленноеПодразделение
		|	И СведенияОДоходах.Регистратор <> &Регистратор
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДанныеДокумента.ФизЛицо,
		|	ДанныеДокумента.ВидДохода,
		|	СведенияОДоходах.Результат,
		|	СведенияОДоходах.ОблагаетсяЕНВД,
		|	СведенияОДоходах.ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам,
		|	СведенияОДоходах.ЯвляетсяДоходомФармацевта,
		|	СведенияОДоходах.ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ,
		|	СведенияОДоходах.ВидРасчета,
		|	СведенияОДоходах.ОблагаетсяПоДополнительномуТарифу
		|ИЗ
		|	ВТДанныеДокумента КАК ДанныеДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДанныеДоходовДокумента КАК СведенияОДоходах
		|		ПО ДанныеДокумента.ФизЛицо = СведенияОДоходах.ФизЛицо
		|			И ДанныеДокумента.ВидДохода = СведенияОДоходах.ВидДохода
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо,
		|	ВидДохода
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(СведенияОДоходах.Результат) КАК Результат,
		|	СведенияОДоходах.ФизЛицо КАК ФизЛицо,
		|	СведенияОДоходах.ВидДохода КАК ВидДохода,
		|	СведенияОДоходах.ОблагаетсяЕНВД,
		|	СведенияОДоходах.ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам,
		|	СведенияОДоходах.ЯвляетсяДоходомФармацевта,
		|	СведенияОДоходах.ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ,
		|	СведенияОДоходах.ВидРасчета,
		|	СведенияОДоходах.ОблагаетсяПоДополнительномуТарифу
		|ПОМЕСТИТЬ ВТДоходы
		|ИЗ
		|	ВТСведенияОДоходах КАК СведенияОДоходах
		|
		|СГРУППИРОВАТЬ ПО
		|	СведенияОДоходах.ФизЛицо,
		|	СведенияОДоходах.ОблагаетсяЕНВД,
		|	СведенияОДоходах.ВидДохода,
		|	СведенияОДоходах.ЯвляетсяДоходомФармацевта,
		|	СведенияОДоходах.ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ,
		|	СведенияОДоходах.ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам,
		|	СведенияОДоходах.ВидРасчета,
		|	СведенияОДоходах.ОблагаетсяПоДополнительномуТарифу
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо,
		|	ВидДохода
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеДокумента.ФизЛицо КАК ФизЛицо,
		|	ДанныеДокумента.ВидДохода КАК ВидДохода,
		|	ДанныеДокумента.Скидка,
		|	СведенияОДоходах.ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам,
		|	СведенияОДоходах.ОблагаетсяЕНВД,
		|	СведенияОДоходах.ЯвляетсяДоходомФармацевта,
		|	СведенияОДоходах.ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ,
		|	ЕСТЬNULL(СведенияОДоходах.Результат, 0) КАК Результат,
		|	СведенияОДоходах.ВидРасчета,
		|	СведенияОДоходах.ОблагаетсяПоДополнительномуТарифу
		|ИЗ
		|	ВТДанныеДокумента КАК ДанныеДокумента
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДоходы КАК СведенияОДоходах
		|		ПО ДанныеДокумента.ФизЛицо = СведенияОДоходах.ФизЛицо
		|			И ДанныеДокумента.ВидДохода = СведенияОДоходах.ВидДохода
		|
		|УПОРЯДОЧИТЬ ПО
		|	ФизЛицо,
		|	ВидДохода";
	ИначеЕсли ПериодРегистрации < РасчетСтраховыхВзносов.ДатаОбъединенияСтраховойИНакопительнойЧастейВзносовПФР() Тогда
		
		ТаблицаРаспределения.Колонки.Добавить("ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам");
		ТаблицаРаспределения.Колонки.Добавить("ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией");
		ТаблицаРаспределения.Колонки.Добавить("ЯвляетсяДоходомФармацевта");
		ТаблицаРаспределения.Колонки.Добавить("ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ");
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	СтраховыеВзносыСведенияОДоходах.ФизЛицо,
		|	СтраховыеВзносыСведенияОДоходах.ВидДохода,
		|	СтраховыеВзносыСведенияОДоходах.ОблагаетсяЕНВД,
		|	СтраховыеВзносыСведенияОДоходах.ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам,
		|	СтраховыеВзносыСведенияОДоходах.ОблагаетсяПоДополнительномуТарифу,
		|	СтраховыеВзносыСведенияОДоходах.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
		|	СтраховыеВзносыСведенияОДоходах.ЯвляетсяДоходомФармацевта,
		|	СтраховыеВзносыСведенияОДоходах.ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ,
		|	СтраховыеВзносыСведенияОДоходах.Результат,
		|	СтраховыеВзносыСведенияОДоходах.ВидРасчета
		|ПОМЕСТИТЬ ВТДанныеДоходовДокумента
		|ИЗ
		|	&ТаблицаДоходов КАК СтраховыеВзносыСведенияОДоходах
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РасчетИсчисленный.ФизЛицо КАК ФизЛицо,
		|	РасчетИсчисленный.Скидка,
		|	РасчетИсчисленный.ВидДохода КАК ВидДохода
		|ПОМЕСТИТЬ ВТДанныеДокумента
		|ИЗ
		|	&НеоблагаемыеСуммы КАК РасчетИсчисленный
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо,
		|	ВидДохода
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеДокумента.ФизЛицо КАК ФизЛицо,
		|	ДанныеДокумента.ВидДохода КАК ВидДохода,
		|	СведенияОДоходах.Результат КАК Результат,
		|	СведенияОДоходах.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	СведенияОДоходах.ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам КАК ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам,
		|	СведенияОДоходах.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией КАК ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
		|	СведенияОДоходах.ЯвляетсяДоходомФармацевта,
		|	СведенияОДоходах.ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ,
		|	СведенияОДоходах.ВидРасчета КАК ВидРасчета,
		|	СведенияОДоходах.ОблагаетсяПоДополнительномуТарифу КАК ОблагаетсяПоДополнительномуТарифу
		|ПОМЕСТИТЬ ВТСведенияОДоходах
		|ИЗ
		|	ВТДанныеДокумента КАК ДанныеДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СтраховыеВзносыСведенияОДоходах КАК СведенияОДоходах
		|		ПО ДанныеДокумента.ФизЛицо = СведенияОДоходах.ФизЛицо
		|			И ДанныеДокумента.ВидДохода = СведенияОДоходах.ВидДохода
		|ГДЕ
		|	НАЧАЛОПЕРИОДА(СведенияОДоходах.Период, МЕСЯЦ) = &ПериодРегистрации
		|	И СведенияОДоходах.ОбособленноеПодразделение = &ОбособленноеПодразделение
		|	И СведенияОДоходах.Регистратор <> &Регистратор
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДанныеДокумента.ФизЛицо,
		|	ДанныеДокумента.ВидДохода,
		|	СведенияОДоходах.Результат,
		|	СведенияОДоходах.ОблагаетсяЕНВД,
		|	СведенияОДоходах.ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам,
		|	СведенияОДоходах.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
		|	СведенияОДоходах.ЯвляетсяДоходомФармацевта,
		|	СведенияОДоходах.ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ,
		|	СведенияОДоходах.ВидРасчета,
		|	СведенияОДоходах.ОблагаетсяПоДополнительномуТарифу
		|ИЗ
		|	ВТДанныеДокумента КАК ДанныеДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДанныеДоходовДокумента КАК СведенияОДоходах
		|		ПО ДанныеДокумента.ФизЛицо = СведенияОДоходах.ФизЛицо
		|			И ДанныеДокумента.ВидДохода = СведенияОДоходах.ВидДохода
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо,
		|	ВидДохода
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(СведенияОДоходах.Результат) КАК Результат,
		|	СведенияОДоходах.ФизЛицо КАК ФизЛицо,
		|	СведенияОДоходах.ВидДохода КАК ВидДохода,
		|	СведенияОДоходах.ОблагаетсяЕНВД,
		|	СведенияОДоходах.ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам,
		|	СведенияОДоходах.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
		|	СведенияОДоходах.ЯвляетсяДоходомФармацевта,
		|	СведенияОДоходах.ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ,
		|	СведенияОДоходах.ВидРасчета,
		|	СведенияОДоходах.ОблагаетсяПоДополнительномуТарифу
		|ПОМЕСТИТЬ ВТДоходы
		|ИЗ
		|	ВТСведенияОДоходах КАК СведенияОДоходах
		|
		|СГРУППИРОВАТЬ ПО
		|	СведенияОДоходах.ФизЛицо,
		|	СведенияОДоходах.ОблагаетсяЕНВД,
		|	СведенияОДоходах.ВидДохода,
		|	СведенияОДоходах.ЯвляетсяДоходомФармацевта,
		|	СведенияОДоходах.ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ,
		|	СведенияОДоходах.ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам,
		|	СведенияОДоходах.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
		|	СведенияОДоходах.ВидРасчета,
		|	СведенияОДоходах.ОблагаетсяПоДополнительномуТарифу
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо,
		|	ВидДохода
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеДокумента.ФизЛицо КАК ФизЛицо,
		|	ДанныеДокумента.ВидДохода КАК ВидДохода,
		|	ДанныеДокумента.Скидка,
		|	СведенияОДоходах.ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам,
		|	СведенияОДоходах.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
		|	СведенияОДоходах.ОблагаетсяЕНВД,
		|	СведенияОДоходах.ЯвляетсяДоходомФармацевта,
		|	СведенияОДоходах.ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ,
		|	ЕСТЬNULL(СведенияОДоходах.Результат, 0) КАК Результат,
		|	СведенияОДоходах.ВидРасчета,
		|	СведенияОДоходах.ОблагаетсяПоДополнительномуТарифу
		|ИЗ
		|	ВТДанныеДокумента КАК ДанныеДокумента
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДоходы КАК СведенияОДоходах
		|		ПО ДанныеДокумента.ФизЛицо = СведенияОДоходах.ФизЛицо
		|			И ДанныеДокумента.ВидДохода = СведенияОДоходах.ВидДохода
		|
		|УПОРЯДОЧИТЬ ПО
		|	ФизЛицо,
		|	ВидДохода";
	Иначе
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	РасчетИсчисленный.ФизЛицо,
		|	РасчетИсчисленный.ДатаПолученияДохода,
		|	РасчетИсчисленный.ВидДохода
		|ПОМЕСТИТЬ ВТДанныеДокумента
		|ИЗ
		|	&НеоблагаемыеСуммы КАК РасчетИсчисленный
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеДокумента.ФизЛицо КАК ФизЛицо,
		|	ДанныеДокумента.ВидДохода КАК ВидДохода,
		|	ДанныеДокумента.ДатаПолученияДохода,
		|	СведенияОДоходах.Результат КАК Результат,
		|	СведенияОДоходах.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	СведенияОДоходах.ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам КАК ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам,
		|	СведенияОДоходах.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией КАК ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
		|	СведенияОДоходах.КлассУсловийТруда,
		|	СведенияОДоходах.ЯвляетсяДоходомФармацевта,
		|	СведенияОДоходах.ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ,
		|	СведенияОДоходах.ВидРасчета КАК ВидРасчета,
		|	СведенияОДоходах.ОблагаетсяПоДополнительномуТарифу КАК ОблагаетсяПоДополнительномуТарифу
		|ИЗ
		|	ВТДанныеДокумента КАК ДанныеДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СтраховыеВзносыСведенияОДоходах КАК СведенияОДоходах
		|		ПО ДанныеДокумента.ФизЛицо = СведенияОДоходах.ФизЛицо
		|			И ДанныеДокумента.ВидДохода = СведенияОДоходах.ВидДохода
		|			И ДанныеДокумента.ДатаПолученияДохода = СведенияОДоходах.ДатаПолученияДохода
		|ГДЕ
		|	НАЧАЛОПЕРИОДА(СведенияОДоходах.Период, МЕСЯЦ) = &ПериодРегистрации
		|	И СведенияОДоходах.ОбособленноеПодразделение = &ОбособленноеПодразделение
		|	И СведенияОДоходах.Регистратор <> &Регистратор";
		
		ВременнаяТаблица = ДанныеОДоходах.Скопировать();
		ОбщегоНазначенияЗК.ЗагрузитьВТаблицуЗначений(Запрос.Выполнить().Выгрузить(), ВременнаяТаблица);
		РасчетСтраховыхВзносов.СформироватьДвиженияПоСкидкам(НаборЗаписейОДоходах, ВыборкаПоШапкеДокумента, НеоблагаемыеСуммыДоходов, ВременнаяТаблица);
		
		Возврат
		
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.СледующийПоЗначениюПоля("ФизЛицо") Цикл
		
		Пока Выборка.СледующийПоЗначениюПоля("ВидДохода") Цикл
			
			СкидкаКРаспределению = Выборка.Скидка;
			
			ТаблицаРаспределения.Очистить();
			Пока Выборка.Следующий() Цикл
				ЗаполнитьЗначенияСвойств(ТаблицаРаспределения.Добавить(),Выборка);
			КонецЦикла;
			
			МассивКоэффициентов = ТаблицаРаспределения.ВыгрузитьКолонку("Результат");
			МассивСуммКНачислению = ОбщегоНазначенияЗК.РаспределитьПропорционально(СкидкаКРаспределению,МассивКоэффициентов);
			
			Если МассивСуммКНачислению = Неопределено Тогда
				
				СтрокаДвижения = НаборЗаписейОДоходах.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаДвижения,ВыборкаПоШапкеДокумента);  // Период, Организация, ОбособленноеПодразделение
				ЗаполнитьЗначенияСвойств(СтрокаДвижения,Выборка,,"Результат"); // ФизЛицо,ВидДохода,ОблагаетсяЕНВД,ВидРасчета, ОблагаетсяПоДополнительномуТарифу, ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам
				// ресурсы
				СтрокаДвижения.Скидка = СкидкаКРаспределению;
				
			Иначе
				
				Для каждого СтрокаТЗ Из ТаблицаРаспределения Цикл
					
					СтрокаДвижения = НаборЗаписейОДоходах.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаДвижения,ВыборкаПоШапкеДокумента);  // Период, Организация, ОбособленноеПодразделение
					ЗаполнитьЗначенияСвойств(СтрокаДвижения,СтрокаТЗ,,"Результат"); // ФизЛицо,ВидДохода,ОблагаетсяЕНВД,ВидРасчета,ОблагаетсяПоДополнительномуТарифу, ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам
					// ресурсы
					СтрокаДвижения.Скидка = МассивСуммКНачислению[ТаблицаРаспределения.Индекс(СтрокаТЗ)];
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПроведения(Отказ, Режим)
	
	ОбработкаКомментариев = глЗначениеПеременной("глОбработкаСообщений");
	ОбработкаКомментариев.УдалитьСообщения();
	
	// Заголовок для сообщений об ошибках проведения.
	Заголовок = ОбщегоНазначенияЗК.ПредставлениеДокументаПриПроведении(Ссылка);
	
	ВыборкаПоШапкеДокумента = СформироватьЗапросПоШапке().Выбрать();
	ВыборкаПоШапкеДокумента.Следующий();
	ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ, Заголовок);
	
	Движения.СтраховыеВзносыИсчисленные.мВыполнятьДополнительныеДвижения = Истина;
	Движения.ПособияСоциальномуСтрахованию.мВыполнятьДополнительныеДвижения	= Истина;
	
	Для каждого СтрокаТЧ Из ИсчисленныеСтраховыеВзносы Цикл
		ПроверитьЗаполнениеСтрокиВзносов(СтрокаТЧ, Отказ);
		Если Не Отказ Тогда
			ДобавитьСтрокуВДвиженияПоВзносам(СтрокаТЧ, ВыборкаПоШапкеДокумента);
		КонецЕсли;
	КонецЦикла;
	
	Для каждого СтрокаТЧ Из НеоблагаемыеСуммыДоходов Цикл
		ПроверитьЗаполнениеСтрокиСкидок(СтрокаТЧ, Отказ, Заголовок);
	КонецЦикла; 
	
	Если Не Отказ Тогда
		РасчетСтраховыхВзносовПереопределяемый.ДополнительныеДействияОбработкиПроведения(ЭтотОбъект, ВыборкаПоШапкеДокумента, Заголовок, Отказ);
		СформироватьДвиженияПоСкидкам(ВыборкаПоШапкеДокумента);	
		Если ВыборкаПоШапкеДокумента.ПериодРегистрации >= РасчетСтраховыхВзносов.ДатаВозмещенияФССВзносовСОплаты4ДнейУходаЗаДетьмиИнвалидами() Тогда
			Для каждого НаборЗаписей Из Движения Цикл
				ТипНабораЗаписей = ТипЗнч(НаборЗаписей);
				Если ТипНабораЗаписей = Тип("РегистрНакопленияНаборЗаписей.СтраховыеВзносыИсчисленные") 
					Или ТипНабораЗаписей = Тип("РегистрНакопленияНаборЗаписей.СтраховыеВзносыСведенияОДоходах") Тогда
					НаборЗаписей.Записать();
				КонецЕсли;
			КонецЦикла;
			РасчетСтраховыхВзносов.СформироватьДвиженияПоВзносамСОплаты4ДнейУходаЗаДетьмиИнвалидами(Движения, ВыборкаПоШапкеДокумента, ИсчисленныеСтраховыеВзносы.ВыгрузитьКолонку("ФизЛицо"));	
		КонецЕсли;
	КонецЕсли;
	
	Если Отказ Тогда
		ОбработкаКомментариев.ПоказатьСообщения();
	КонецЕсли;
	
КонецПроцедуры // ОбработкаПроведения()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	МассивТЧ = Новый Массив();
	МассивТЧ.Добавить(ИсчисленныеСтраховыеВзносы);
	
	КраткийСоставДокумента = ПроцедурыУправленияПерсоналом.ЗаполнитьКраткийСоставДокумента(МассивТЧ, "Физлицо");
	
КонецПроцедуры // ПередЗаписью()


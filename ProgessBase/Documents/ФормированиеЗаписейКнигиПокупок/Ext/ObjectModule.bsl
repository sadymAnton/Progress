Перем мУдалятьДвижения Экспорт;

Перем мВалютаРегламентированногоУчета Экспорт;

Перем мВестиУчетНДС Экспорт;

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОКУМЕНТА

#Если Клиент Тогда
// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходимое количество копий.
//
//  Название макета печати передается в качестве параметра,
// по переданному названию находим имя макета в соответствии.
//
// Параметры:
//  НазваниеМакета - строка, название макета.
//
Процедура Печать(ИмяМакета, КоличествоЭкземпляров = 1, НаПринтер = Ложь) Экспорт

	Если ЭтоНовый() Тогда
		Предупреждение("Документ можно распечатать только после его записи");
		Возврат;
	ИначеЕсли Не УправлениеДопПравамиПользователей.РазрешитьПечатьНепроведенныхДокументов(Проведен) Тогда
		Предупреждение("Недостаточно полномочий для печати непроведенного документа!");
		Возврат;
	КонецЕсли;

	Если Не РаботаСДиалогами.ПроверитьМодифицированность(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;

	Если ТипЗнч(ИмяМакета) = Тип("ДвоичныеДанные") Тогда

		ТабДокумент = УниверсальныеМеханизмы.НапечататьВнешнююФорму(Ссылка, ИмяМакета);

		Если ТабДокумент = Неопределено Тогда
			Возврат

		КонецЕсли; 
		
	КонецЕсли;

	УниверсальныеМеханизмы.НапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер, ОбщегоНазначения.СформироватьЗаголовокДокумента(ЭтотОбъект), Ссылка);

КонецПроцедуры // Печать()

// Возвращает доступные варианты печати документа
//
// Возвращаемое значение:
//  Структура, каждая строка которой соответствует одному из вариантов печати
//  
Функция ПолучитьСтруктуруПечатныхФорм() Экспорт

	Возврат Новый Структура;

КонецФункции // ПолучитьСтруктуруПечатныхФорм()

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ АВТОЗАПОЛНЕНИЯ СТРОК ДОКУМЕНТА

// Процедура вызывается при нажатии на кнопку "Запонить" в диалоге документа
// Реализует алгоритм автоматического заполнения табличной части.
//
Процедура ЗаполнитьДокумент(ОшибкаЗаполнения = Ложь, Сообщать = Истина, СтрокаСообщения = "", ОтменитьПроведение = Ложь, УдалятьНезаполненные = Ложь) Экспорт
	
	Если Проведен Тогда
		Если ОтменитьПроведение Тогда
			Записать(РежимЗаписиДокумента.ОтменаПроведения);
		Иначе
			ОшибкаЗаполнения = Истина;
			СтрокаСообщения = " перед заполнением требуется отменить проведение документа";
			Если Сообщать Тогда
				ОбщегоНазначения.СообщитьОбОшибке("Документ не заполнен:" + СтрокаСообщения, , Строка(Ссылка));
			КонецЕсли; 
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ЗаполнитьСтроки_ВычетПоПриобретеннымЦенностям(, Ложь, СтрокаСообщения, ОшибкаЗаполнения, УдалятьНезаполненные);
	ЗаполнитьСтроки_НДСсАвансов(, , , , УдалятьНезаполненные);
	ЗаполнитьСтроки_НДСсАвансовВыданных(, УдалятьНезаполненные);
	ЗаполнитьСтроки_ВычетНДСПоНалоговомуАгенту(, , , , УдалятьНезаполненные);
	
	Если НЕ ПредъявленНДСКВычету0 Тогда
		ЗаполнитьСтроки_ВычетПриИзмененииСтоимостиВСторонуУменьшения(Ложь);
	КонецЕсли;	
	
	Если НЕ (ВычетПоПриобретеннымЦенностям.Количество() > 0 
		ИЛИ НДСсАвансов.Количество() > 0
 		ИЛИ НДСсАвансовВыданных.Количество() > 0
 		ИЛИ ВычетНДСПоНалоговомуАгенту.Количество() > 0
		ИЛИ ВычетПриИзмененииСтоимостиВСторонуУменьшения.Количество() > 0) Тогда
		ОшибкаЗаполнения = Истина;
		СтрокаСообщения = СтрокаСообщения+Символы.ПС+" - не обнаружены записи к отражению в книге покупок"
	КонецЕсли;	

   Если ОшибкаЗаполнения Тогда
		Если Сообщать Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Документ не заполнен:"+СтрокаСообщения,,Строка(Ссылка));
		КонецЕсли; 
		Возврат;
	КонецЕсли; 
	
КонецПроцедуры // ЗаполнитьСтрокиДокумента()

////////////////////////////////////////////////////////////////////////////////
// Заполнение табличной части "Вычет НДС по приобретенным ценностям"

// Процедура выполняет автоматическое заполнение табличной части документа
// Вызывается из процедуры КоманднаяПанельВычетПоПриобретеннымЦенностямЗаполнить
//
Процедура ЗаполнитьСтроки_ВычетПоПриобретеннымЦенностям(ОтменитьПроведение = Ложь, Сообщать = Истина, СтрокаСообщения = "", ОшибкаЗаполнения = Ложь, УдалятьНезаполненные = Ложь) Экспорт
	
	Если Проведен Тогда
		Если ОтменитьПроведение Тогда
			Записать(РежимЗаписиДокумента.ОтменаПроведения);
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	УчетнаяПолитикаНУ = ОбщегоНазначения.ПолучитьПараметрыУчетнойПолитикиРегл(Дата, Организация, Ложь);
	Если Не ЗначениеЗаполнено(УчетнаяПолитикаНУ) Тогда
		ОшибкаЗаполнения = Истина;
		СтрокаСообщения = "Не указаны параметры учетной политики ("+СокрЛП(Организация)+") на " + Формат(Дата, "ДЛФ=DD") + Символы.ПС 
						+ "Табличное поле «Вычет НДС по приобретенным ценностям» не может быть заполнено автоматически.";
		Если Сообщать Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Документ не заполнен:"+СтрокаСообщения,,Строка(Ссылка));
		КонецЕсли; 
		Возврат;
	КонецЕсли;
	
	ТаблицаРезультатов = ВычетПоПриобретеннымЦенностям.ВыгрузитьКолонки();
	
	КонтролироватьОплатуДляСФсДатойМенее01012006 = Истина;
	ПоложенияПереходногоПериода2006_ОткорректироватьСуммуДоступнуюКВычету = ложь;
	
	ОшибкаПолученияУчетнойПолитики2005 = Ложь;
	Если Дата>= '20080101' тогда
		КонтролироватьОплатуДляСФсДатойМенее01012006 = Ложь;
	ИначеЕсли Дата >= '20060101' Тогда
		УчетнаяПолитикаНУ_31122005 = ОбщегоНазначения.ПолучитьПараметрыУчетнойПолитикиРегл('20051231', Организация);
		Если НЕ ЗначениеЗаполнено(УчетнаяПолитикаНУ_31122005) Тогда
			ОшибкаПолученияУчетнойПолитики2005 = Истина;
		КонецЕсли; 
		Если ОшибкаПолученияУчетнойПолитики2005 Или УчетнаяПолитикаНУ_31122005.МоментОпределенияНалоговойБазыНДС = Перечисления.МоментыОпределенияНалоговойБазыНДС.ПоОтгрузке тогда
			// Учетная политика на 31.12.2005 велась по отгрузке, требуется отработка положений переходного периода по НДС.
			КонтролироватьОплатуДляСФсДатойМенее01012006 = Ложь;
			Если Дата<'20060601' Тогда
				// Корректировка суммы, которую можно принять к вычету на основании
				// п. 10. статьи 2 ФЗ №119-ФЗ - 1/6 в течении первого полугодия 2006 года
				// Вся сумма должна быть списана "равными долями" в течении первого полугодия,
				// поэтому не требуется контроль 1/6 в 6-м месяце.
				ПоложенияПереходногоПериода2006_ОткорректироватьСуммуДоступнуюКВычету = Истина;
			КонецЕсли; 
		// Отработка положений переходного периода по НДС
		Иначе
			КонтролироватьОплатуДляСФсДатойМенее01012006 = Ложь;
			ПоложенияПереходногоПериода2006_ОткорректироватьСуммуДоступнуюКВычету = Ложь;
		КонецЕсли; 
	КонецЕсли; 
	
	Если ПредъявленНДСКВычету0 Тогда
		Дерево_НДСкВычету = ЗаполнитьНДСКВычетуПоДаннымРегистраНДСПредъявленныйРеализация0(КонтролироватьОплатуДляСФсДатойМенее01012006);
	Иначе
		Дерево_НДСкВычету = ЗаполнитьНДСКВычетуПоДаннымРегистраНДСПредъявленный(КонтролироватьОплатуДляСФсДатойМенее01012006);
	КонецЕсли;
	
	Если Дерево_НДСкВычету.Строки.Количество() = 0 Тогда
		// Дальнейшая обработка не требуется, не обнаружен НДС, который может быть принят к вычету.
		ВычетПоПриобретеннымЦенностям.Очистить();
		Возврат;
	КонецЕсли;
	
	Если ОшибкаПолученияУчетнойПолитики2005 и Дерево_НДСкВычету.Строки[0].СчетФактураДата < '20060101' Тогда
		СтрокаСообщения = "Не указаны параметры учетной политики ("+СокрЛП(Организация)+") на " + Формат('20051231', "ДЛФ=DD")+ Символы.ПС 
						+ "Табличное поле «Вычет НДС по приобретенным ценностям» не может быть заполнено автоматически.";
		ОшибкаЗаполнения = Истина;
		Если Сообщать Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Документ не заполнен:"+СтрокаСообщения,,Строка(Ссылка));
		КонецЕсли; 
		Возврат;
	КонецЕсли; 
	
	// Определение суммы, которую можно принять к вычету на основании
	// п. 10. статьи 2 ФЗ №119-ФЗ - 1/6 в течении первого полугодия 2006 года
	Дерево_НДСкВычету.Колонки.Добавить("ОпределенаДоля_119ФЗ_2_10", Новый описаниеТипов("Булево"));
	Дерево_НДСкВычету.Колонки.Добавить("КВычету_СНДС_Часть", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	Дерево_НДСкВычету.Колонки.Добавить("КВычету_НДС_Часть", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	Если ПоложенияПереходногоПериода2006_ОткорректироватьСуммуДоступнуюКВычету Тогда
		ОтработкаПоложенииПереходногоПериода2006_ОткорректироватьСуммуДоступнуюКВычету(Дерево_НДСкВычету);
	КонецЕсли; 
	
	СписокСчетовФактур = ОбщегоНазначения.УдалитьПовторяющиесяЭлементыМассива(Дерево_НДСкВычету.Строки.ВыгрузитьКолонку("СчетФактура"), Истина);
	
	// Ограничиваем получаемые распределенные оплаты только отфактурованными поступлениями.
	// Отсутствие СФ допускается только для НДС, уплаченного на таможне, а оплаты по нему тоже не регистрируются.
	
	ДатаПоискаСчетовФактур = ПолучитьДатуПоискаСчетовФактурПоПриобретеннымЦенностям(Дата);
	
	ТаблицаСФ = УчетНДС.ОпределитьНаличиеСчетовФактурПолученных(
		Дата(2000, 01, 01),
		Дата,
		Организация,
		СписокСчетовФактур,
		Ложь,
		Истина,
		Истина,
		ДатаПоискаСчетовФактур);
		
	ОтфактурованныеПоступления = ОбщегоНазначения.УдалитьПовторяющиесяЭлементыМассива(ТаблицаСФ.ВыгрузитьКолонку("Документ"), Истина);
	
	ВерсияКодовВидовОпераций = УчетНДС.ВерсияКодовВидовОпераций(Дата);
	
	Для Каждого СчетФактураДокумент Из СписокСчетовФактур Цикл
		
		Если ТипЗнч(СчетФактураДокумент) = Тип("ДокументСсылка.РеализацияТоваровУслуг")
			И ОтфактурованныеПоступления.Найти(СчетФактураДокумент) = Неопределено Тогда
			
			// Возврат по реализации без счета-фактуры
			ОтфактурованныеПоступления.Добавить(СчетФактураДокумент);
		
		КонецЕсли;
		
	КонецЦикла;
	
	РаспределенныеОплаты = ПолучитьДанныеОРаспределенныхОплатахПоСпискуСФ(ОтфактурованныеПоступления);
	
	Если УчетнаяПолитикаНУ.НДСРежимУчетаРаспределенныхОплат <> Перечисления.НДСРежимУчетаРаспределенныхОплат.Приоритет_НДСМожетБытьПринятКВычету Тогда
		// Пессимистическая политика отражения вычетов. Сначала оплаты относятся на НДС включенный в стоимость (заблокированный к вычету),
		// лишь затем на НДС, который может быть принят к вычету.
		ДеревоНДС_ВычетЗаблокирован = ПолучитьСведенияПоЗаблокированномуНДС(ОтфактурованныеПоступления);
		
		РаспределитьОплатыПоДеревуСФ(
			ДеревоНДС_ВычетЗаблокирован,
			ТаблицаРезультатов,
			ОтфактурованныеПоступления,
			РаспределенныеОплаты,
			Ложь);
	КонецЕсли; 
	
	РаспределитьОплатыПоДеревуСФ(
		Дерево_НДСкВычету, 
		ТаблицаРезультатов,
		ОтфактурованныеПоступления,
		РаспределенныеОплаты, ,
		КонтролироватьОплатуДляСФсДатойМенее01012006);
	
	ПолучитьДанныеОДокументахОплатыКомандировочныеРасходы(ТаблицаРезультатов);
	
	Для Каждого СтрокаСчетФактура Из ТаблицаРезультатов Цикл
		
		Если ВерсияКодовВидовОпераций > 1 Тогда
			СчетФактураПоДокументу = ТаблицаСФ.Найти(СтрокаСчетФактура.СчетФактура, "Документ");

			Если СчетФактураПоДокументу <> Неопределено Тогда
				СтрокаСчетФактура.ДатаПолученияСчетаФактуры = СчетФактураПоДокументу.СчетФактураДата; //Blik 290616 54067
				Если НЕ ЗначениеЗаполнено(СтрокаСчетФактура.КодВидаОперации)
					ИЛИ СтрокаСчетФактура.КодВидаОперации = "01" Тогда   //??? смысл менять код 01 если он используется и не заменяется
					// Получим код вида операции из счета-фактуры
					//начало изменений Ожиганов 28.06.2016 б/н исправление ошибок, после обновления блока по НДС 
					Если  ТипЗнч(СчетФактураПоДокументу.СчетФактура) = Тип("ДокументСсылка.СчетФактураВыданный")
					  или ТипЗнч(СчетФактураПоДокументу.СчетФактура) = Тип("ДокументСсылка.СчетФактураПолученный")	
					  или ТипЗнч(СчетФактураПоДокументу.СчетФактура) = Тип("ДокументСсылка.ОтражениеРеализацииТоваровИУслугНДС")	
					  или ТипЗнч(СчетФактураПоДокументу.СчетФактура) = Тип("ДокументСсылка.ОтражениеПоступленияТоваровИУслугНДС")	
					Тогда
					
						СтрокаСчетФактура.КодВидаОперации = УчетНДС.АктуальныйКодВидаОперации(
						ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СчетФактураПоДокументу.СчетФактура, "КодВидаОперации"), ВерсияКодовВидовОпераций);
					
					КонецЕсли;
					//СтрокаСчетФактура.КодВидаОперации = УчетНДС.АктуальныйКодВидаОперации(
					//СчетФактураПоДокументу.КодВидаОперации, ВерсияКодовВидовОпераций);
					//конец изменений 
				КонецЕсли;
				
			КонецЕсли;
		
		КонецЕсли;
		
	КонецЦикла;
	
	// Удаление строк, в которых есть незаполненные обязательные поля
	Если УдалятьНезаполненные Тогда
		ОбработкаТабличныхЧастейСервер.УдалитьНезаполненныеСтроки(ТаблицаРезультатов, "ВидЦенности, СчетФактура, СтавкаНДС");
	КонецЕсли;

	ВычетПоПриобретеннымЦенностям.Загрузить(ТаблицаРезультатов);

КонецПроцедуры // ЗаполнитьСтрокиДокумента()

// Вызывается из процедуры ЗаполнитьСтроки_ВычетПоПриобретеннымЦенностям.
// Заполняет ТЧ ВычетПоПриобретеннымЦенностям по данным регистра НДСПредъявленный
Функция ЗаполнитьНДСКВычетуПоДаннымРегистраНДСПредъявленный(КонтролироватьОплатуДляСФсДатойМенее01012006 = истина)

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НДСПредъявленныйОстатки.Организация,
	|	НДСПредъявленныйОстатки.СчетФактура КАК СчетФактура,
	|	МАКСИМУМ(НДСПредъявленныйОстатки.ИсправленныйСчетФактура) КАК ИсправленныйСчетФактура,
	|	НДСПредъявленныйОстатки.ВидЦенности,
	|	НДСПредъявленныйОстатки.СтавкаНДС,
	|	НДСПредъявленныйОстатки.СчетУчетаНДС,
	|	НДСПредъявленныйОстатки.Поставщик,
	|	СУММА(ЕСТЬNULL(НДСПредъявленныйОстатки.СуммаБезНДСОстаток, 0) - ЕСТЬNULL(НДСпоОСиНМАОстатки.СуммаБезНДСОстаток, 0) - ЕСТЬNULL(НДСПредъявленныйРеализация0Остатки.СуммаБезНДСОстаток, 0)) КАК КВычету_БезНДС,
	|	СУММА(ЕСТЬNULL(НДСПредъявленныйОстатки.НДСОстаток, 0) - ЕСТЬNULL(НДСПредъявленныйРеализация0Остатки.НДСОстаток, 0) - ЕСТЬNULL(НДСпоОСиНМАОстатки.НДСОстаток, 0)) КАК КВычету_НДС,
	|	СУММА(ЕСТЬNULL(НДСПредъявленныйОстатки.СуммаБезНДСОстаток, 0) - ЕСТЬNULL(НДСпоОСиНМАОстатки.СуммаБезНДСОстаток, 0) - ЕСТЬNULL(НДСПредъявленныйРеализация0Остатки.СуммаБезНДСОстаток, 0) + ЕСТЬNULL(НДСПредъявленныйОстатки.НДСОстаток, 0) - ЕСТЬNULL(НДСПредъявленныйРеализация0Остатки.НДСОстаток, 0) - ЕСТЬNULL(НДСпоОСиНМАОстатки.НДСОстаток, 0)) КАК КВычету_СНДС,
	|	ЕСТЬNULL(НДСПредъявленныйОстатки.СчетФактура.Дата, &Дата) КАК СчетФактураДата,
	|	ВЫБОР
	|		КОГДА НДСПредъявленныйОстатки.ВидЦенности В (&ВидыЦенностей_ОплатаПоНДС)
	|			ТОГДА 1
	|		КОГДА НДСПредъявленныйОстатки.ВидЦенности В (&ВидыЦенностей_ОплатаПоНДССМР)
	|			ТОГДА ВЫБОР
	|					КОГДА ЕСТЬNULL(НДСПредъявленныйОстатки.СчетФактура.Дата, &Дата) < ДАТАВРЕМЯ(2009, 1, 1)
	|						ТОГДА 1
	|					ИНАЧЕ 5
	|				КОНЕЦ
	|		КОГДА НДСПредъявленныйОстатки.ВидЦенности В (&ВидыЦенностей_БезОплаты)
	|			ТОГДА 2
	|		КОГДА НДСПредъявленныйОстатки.ВидЦенности В (&ВидыЦенностей_Возврат)
	|			ТОГДА 4
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ЕСТЬNULL(НДСПредъявленныйОстатки.СчетФактура.Дата, &Дата) >= &Начало2006Года
	|					ТОГДА 3
	|				КОГДА НЕ &КонтролироватьОплатуДляСФсДатойМенее01012006
	|					ТОГДА 3
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ КАК ПорядокОплаты,
	|	ВЫБОР
	|		КОГДА &Дата < ДАТАВРЕМЯ(2015, 1, 1)
	|			ТОГДА """"
	|		КОГДА НДСПредъявленныйОстатки.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Возврат)
	|			ТОГДА ВЫБОР
	|					КОГДА НЕ НДСПредъявленныйОстатки.СчетФактура ССЫЛКА Документ.ВозвратТоваровОтПокупателя
	|						ТОГДА ""16""
	|					ИНАЧЕ ВЫБОР
	|							КОГДА ВЫРАЗИТЬ(НДСПредъявленныйОстатки.СчетФактура КАК Документ.ВозвратТоваровОтПокупателя).НомерРасходногоКассовогоОрдера <> """"
	|								ТОГДА ""17""
	|							КОГДА &Дата < ДАТАВРЕМЯ(2016, 7, 1)
	|								ТОГДА ""03""
	|							ИНАЧЕ ""01""
	|						КОНЕЦ
	|				КОНЕЦ
	|		КОГДА НДСПредъявленныйОстатки.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ТаможенныеПлатежи)
	|			ТОГДА ""20""
	|		КОГДА НДСПредъявленныйОстатки.СчетФактура ССЫЛКА Документ.СчетФактураПолученный
	|				И ВЫРАЗИТЬ(НДСПредъявленныйОстатки.СчетФактура КАК Документ.СчетФактураПолученный).БланкСтрогойОтчетности
	|			ТОГДА ""23""
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК КодВидаОперации
	|ИЗ
	|	РегистрНакопления.НДСПредъявленный.Остатки(
	|			&ДатаГраница,
	|			Организация = &Организация
	|				И НЕ СчетФактура ССЫЛКА Документ.КорректировкаРеализации
	|				И НЕ ВидЦенности В (&ИсключаемыеВидыЦенностей)
	|				И (ДоговорКонтрагента = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|					ИЛИ НЕ ВидЦенности В (&ВидыЦенностей_НалоговыйАгент))) КАК НДСПредъявленныйОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДСпоОСиНМА.Остатки(
	|				&ДатаГраница,
	|				Организация = &Организация
	|					И НДСВключенВСтоимость = ЛОЖЬ
	|					И НЕ ВидЦенности В (&ИсключаемыеВидыЦенностей)
	|					И НеВлияетНаВычет = ЛОЖЬ) КАК НДСпоОСиНМАОстатки
	|		ПО НДСПредъявленныйОстатки.СчетФактура = НДСпоОСиНМАОстатки.СчетФактура
	|			И НДСПредъявленныйОстатки.ВидЦенности = НДСпоОСиНМАОстатки.ВидЦенности
	|			И НДСПредъявленныйОстатки.СтавкаНДС = НДСпоОСиНМАОстатки.СтавкаНДС
	|			И НДСПредъявленныйОстатки.СчетУчетаНДС = НДСпоОСиНМАОстатки.СчетУчетаНДС
	|			И (НДСпоОСиНМАОстатки.СуммаБезНДСОстаток + НДСпоОСиНМАОстатки.НДСОстаток > 0)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДСПредъявленныйРеализация0.Остатки(
	|				&ДатаГраница,
	|				Организация = &Организация
	|					И НЕ ВидЦенности В (&ИсключаемыеВидыЦенностей)) КАК НДСПредъявленныйРеализация0Остатки
	|		ПО НДСПредъявленныйОстатки.СчетФактура = НДСПредъявленныйРеализация0Остатки.СчетФактура
	|			И НДСПредъявленныйОстатки.ВидЦенности = НДСПредъявленныйРеализация0Остатки.ВидЦенности
	|			И НДСПредъявленныйОстатки.СтавкаНДС = НДСПредъявленныйРеализация0Остатки.СтавкаНДС
	|			И НДСПредъявленныйОстатки.СчетУчетаНДС = НДСПредъявленныйРеализация0Остатки.СчетУчетаНДС
	|			И (НДСПредъявленныйРеализация0Остатки.СуммаБезНДСОстаток + НДСПредъявленныйРеализация0Остатки.НДСОстаток > 0)
	|ГДЕ
	|	(НДСПредъявленныйОстатки.СуммаБезНДСОстаток > 0
	|			ИЛИ НДСПредъявленныйОстатки.НДСОстаток > 0)
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСПредъявленныйОстатки.Организация,
	|	НДСПредъявленныйОстатки.СчетФактура,
	|	НДСПредъявленныйОстатки.ВидЦенности,
	|	НДСПредъявленныйОстатки.СтавкаНДС,
	|	НДСПредъявленныйОстатки.СчетУчетаНДС,
	|	НДСПредъявленныйОстатки.Поставщик,
	|	ЕСТЬNULL(НДСПредъявленныйОстатки.СчетФактура.Дата, &Дата),
	|	ВЫБОР
	|		КОГДА НДСПредъявленныйОстатки.ВидЦенности В (&ВидыЦенностей_ОплатаПоНДС)
	|			ТОГДА 1
	|		КОГДА НДСПредъявленныйОстатки.ВидЦенности В (&ВидыЦенностей_ОплатаПоНДССМР)
	|			ТОГДА ВЫБОР
	|					КОГДА ЕСТЬNULL(НДСПредъявленныйОстатки.СчетФактура.Дата, &Дата) < ДАТАВРЕМЯ(2009, 1, 1)
	|						ТОГДА 1
	|					ИНАЧЕ 5
	|				КОНЕЦ
	|		КОГДА НДСПредъявленныйОстатки.ВидЦенности В (&ВидыЦенностей_БезОплаты)
	|			ТОГДА 2
	|		КОГДА НДСПредъявленныйОстатки.ВидЦенности В (&ВидыЦенностей_Возврат)
	|			ТОГДА 4
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ЕСТЬNULL(НДСПредъявленныйОстатки.СчетФактура.Дата, &Дата) >= &Начало2006Года
	|					ТОГДА 3
	|				КОГДА НЕ &КонтролироватьОплатуДляСФсДатойМенее01012006
	|					ТОГДА 3
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ
	|
	|ИМЕЮЩИЕ
	|	СУММА(ЕСТЬNULL(НДСПредъявленныйОстатки.СуммаБезНДСОстаток, 0) - ЕСТЬNULL(НДСпоОСиНМАОстатки.СуммаБезНДСОстаток, 0) - ЕСТЬNULL(НДСПредъявленныйРеализация0Остатки.СуммаБезНДСОстаток, 0) + ЕСТЬNULL(НДСПредъявленныйОстатки.НДСОстаток, 0) - ЕСТЬNULL(НДСПредъявленныйРеализация0Остатки.НДСОстаток, 0) - ЕСТЬNULL(НДСпоОСиНМАОстатки.НДСОстаток, 0)) > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	СчетФактураДата
	|ИТОГИ
	|	СУММА(КВычету_БезНДС),
	|	СУММА(КВычету_НДС),
	|	СУММА(КВычету_СНДС)
	|ПО
	|	СчетФактура,
	|	ПорядокОплаты";

	Запрос.УстановитьПараметр("Организация",  Организация);
	Запрос.УстановитьПараметр("Дата", КонецДня(Дата));
	Запрос.УстановитьПараметр("ДатаГраница", новый Граница(КонецДня(Дата),ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Начало2006Года", '20060101');
	Запрос.УстановитьПараметр("КонтролироватьОплатуДляСФсДатойМенее01012006", КонтролироватьОплатуДляСФсДатойМенее01012006);

	// Исключаемые из анализа виды ценностей
	ИсключаемыеВидыЦенностей = Новый СписокЗначений;
	ИсключаемыеВидыЦенностей.Добавить(Перечисления.ВидыЦенностей.АвансыПолученные);
	ИсключаемыеВидыЦенностей.Добавить(Перечисления.ВидыЦенностей.АвансыПолученные0);
	ИсключаемыеВидыЦенностей.Добавить(Перечисления.ВидыЦенностей.ВозвратАвансовПолученных);
	ИсключаемыеВидыЦенностей.Добавить(Перечисления.ВидыЦенностей.АвансыВыданные);

	// Виды ценностей с особым порядком распределения оплат - по НДС выплаченному в бюджет
	ВидыЦенностей_ОплатаПоНДС = Новый СписокЗначений;
	ВидыЦенностей_ОплатаПоНДС.Добавить(Перечисления.ВидыЦенностей.НалоговыйАгентАренда);
	ВидыЦенностей_ОплатаПоНДС.Добавить(Перечисления.ВидыЦенностей.НалоговыйАгентРеализацияИмущества);
	ВидыЦенностей_ОплатаПоНДС.Добавить(Перечисления.ВидыЦенностей.НалоговыйАгентИностранцы);
	ВидыЦенностей_ОплатаПоНДС.Добавить(Перечисления.ВидыЦенностей.ВнутреннееПотребление);
	// Виды ценностей с особым порядком распределения оплат - по НДС выплаченному в бюджет
	ВидыЦенностей_ОплатаПоНДССМР = Новый СписокЗначений;
	ВидыЦенностей_ОплатаПоНДССМР.Добавить(Перечисления.ВидыЦенностей.СМРСобственнымиСилами);
	
	// Виды ценностей с особым порядком распределения оплат - оплата не определяется
	ВидыЦенностей_БезОплаты = Новый СписокЗначений;
	ВидыЦенностей_БезОплаты.Добавить(Перечисления.ВидыЦенностей.ТаможенныеПлатежи);
	ВидыЦенностей_БезОплаты.Добавить(Перечисления.ВидыЦенностей.ТаможенныеПлатежиОС);
	ВидыЦенностей_Возврат = Новый СписокЗначений;
	ВидыЦенностей_Возврат.Добавить(Перечисления.ВидыЦенностей.Возврат);
	
	ВидыЦенностей_НалоговыйАгент = УчетНДС.ВидыЦенностиНалоговыйАгентПоступление();
	
	Запрос.УстановитьПараметр("ИсключаемыеВидыЦенностей", ИсключаемыеВидыЦенностей);
	Запрос.УстановитьПараметр("ВидыЦенностей_ОплатаПоНДС", ВидыЦенностей_ОплатаПоНДС);
	Запрос.УстановитьПараметр("ВидыЦенностей_ОплатаПоНДССМР", ВидыЦенностей_ОплатаПоНДССМР);
	Запрос.УстановитьПараметр("ВидыЦенностей_БезОплаты", ВидыЦенностей_БезОплаты);
	Запрос.УстановитьПараметр("ВидыЦенностей_Возврат", ВидыЦенностей_Возврат);
	Запрос.УстановитьПараметр("ВидыЦенностей_НалоговыйАгент", ВидыЦенностей_НалоговыйАгент);
	
	Возврат Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	
КонецФункции // ЗаполнитьНДСКВычетуПоДаннымРегистраНДСПредъявленный()

// Вызывается из процедуры ЗаполнитьСтроки_ВычетПоПриобретеннымЦенностям.
// Заполняет ТЧ ВычетПоПриобретеннымЦенностям по данным регистра НДСПредъявленныйРеализация0
Функция ЗаполнитьНДСКВычетуПоДаннымРегистраНДСПредъявленныйРеализация0(КонтролироватьОплатуДляСФсДатойМенее01012006 = истина)

	ТекстЗапроса = 

	"ВЫБРАТЬ
	|	НДСПредъявленныйРеализация0Остатки.Организация,
	|	НДСПредъявленныйРеализация0Остатки.СчетФактура КАК СчетФактура,
	|	НДСПредъявленныйРеализация0Остатки.ВидЦенности КАК ВидЦенности,
	|	НДСПредъявленныйРеализация0Остатки.СтавкаНДС КАК СтавкаНДС,
	|	НДСПредъявленныйРеализация0Остатки.СчетУчетаНДС КАК СчетУчетаНДС,
	|	ЕСТЬNULL(НДСПредъявленныйРеализация0Остатки.СуммаБезНДСОстаток, 0) КАК КВычету_БезНДС,
	|	ЕСТЬNULL(НДСПредъявленныйРеализация0Остатки.НДСОстаток, 0) КАК КВычету_НДС,
	|	ЕСТЬNULL(НДСПредъявленныйРеализация0Остатки.СуммаБезНДСОстаток, 0) + ЕСТЬNULL(НДСПредъявленныйРеализация0Остатки.НДСОстаток, 0) КАК КВычету_СНДС,
	|	ЕСТЬNULL(НДСПредъявленныйРеализация0Остатки.СчетФактура.Дата, &Дата) КАК СчетФактураДата,
	|	ВЫБОР
	|		КОГДА НДСПредъявленныйРеализация0Остатки.ВидЦенности В (&ВидыЦенностей_ОплатаПоНДС)
	|			ТОГДА 1
	|		КОГДА НДСПредъявленныйРеализация0Остатки.ВидЦенности В (&ВидыЦенностей_БезОплаты)
	|			ТОГДА 2
	|		КОГДА НДСПредъявленныйРеализация0Остатки.ВидЦенности В (&ВидыЦенностей_Возврат)
	|			ТОГДА 4
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ЕСТЬNULL(НДСПредъявленныйРеализация0Остатки.СчетФактура.Дата, &Дата) >= &Начало2006Года
	|					ТОГДА 3
	|				КОГДА НЕ &КонтролироватьОплатуДляСФсДатойМенее01012006
	|					ТОГДА 3
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ КАК ПорядокОплаты,
	|	НДСПредъявленныйРеализация0Остатки.ДокументОтгрузки,
	|	НДСПредъявленныйОстатки.Поставщик,
	|	НДСПредъявленныйРеализация0Остатки.Состояние
	|ПОМЕСТИТЬ ВТ_НДСПредъявленныйРеализация0Остатки




	|ИЗ
	|	РегистрНакопления.НДСПредъявленныйРеализация0.Остатки(
	|			&ДатаГраница,
	|			Организация = &Организация
	|				И НЕ ВидЦенности В (&ИсключаемыеВидыЦенностей)
	|				И Состояние В (&ОтрабатываемыеСостояния)) КАК НДСПредъявленныйРеализация0Остатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.НДСПредъявленный.Остатки(
	|				&ДатаГраница,
	|				Организация = &Организация
	|					И НЕ ВидЦенности В (&ИсключаемыеВидыЦенностей)) КАК НДСПредъявленныйОстатки
	|		ПО НДСПредъявленныйРеализация0Остатки.СчетФактура = НДСПредъявленныйОстатки.СчетФактура
	|			И НДСПредъявленныйРеализация0Остатки.ВидЦенности = НДСПредъявленныйОстатки.ВидЦенности
	|			И НДСПредъявленныйРеализация0Остатки.СтавкаНДС = НДСПредъявленныйОстатки.СтавкаНДС
	|			И НДСПредъявленныйРеализация0Остатки.СчетУчетаНДС = НДСПредъявленныйОстатки.СчетУчетаНДС
	|			И (НДСПредъявленныйОстатки.СуммаБезНДСОстаток + НДСПредъявленныйОстатки.НДСОстаток > 0)
	|ГДЕ
	|	(НДСПредъявленныйРеализация0Остатки.СуммаБезНДСОстаток > 0
	|			ИЛИ НДСПредъявленныйРеализация0Остатки.НДСОстаток > 0)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура,
	|	ВидЦенности,
	|	СтавкаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_НДСПредъявленныйРеализация0Остатки.СчетФактура КАК СчетФактура
	|ПОМЕСТИТЬ СписокСчетовФактур
	|ИЗ
	|	ВТ_НДСПредъявленныйРеализация0Остатки КАК ВТ_НДСПредъявленныйРеализация0Остатки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура";
	
	ТекстЗапроса = ТекстЗапроса + "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	// Получаем информацию по восстановлению НДС.
	
	Если Дата < '20150101' Тогда
	
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	НЕОПРЕДЕЛЕНО КАК Регистратор,
		|	НЕОПРЕДЕЛЕНО КАК СчетФактура,
		|	0 КАК СуммаБезНДС,
		|	0 КАК НДС,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПустаяСсылка) КАК ВидЦенности,
		|	ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.ПустаяСсылка) КАК СтавкаНДС
		|ПОМЕСТИТЬ ВТ_НДСПродажи";
	
	Иначе
		
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	МАКСИМУМ(НДСЗаписиКнигиПродаж.Регистратор) КАК Регистратор,
		|	НДСЗаписиКнигиПродаж.СчетФактура КАК СчетФактура,
		|	СУММА(НДСЗаписиКнигиПродаж.СуммаБезНДС) КАК СуммаБезНДС,
		|	СУММА(НДСЗаписиКнигиПродаж.НДС) КАК НДС,
		|	НДСЗаписиКнигиПродаж.ВидЦенности КАК ВидЦенности,
		|	НДСЗаписиКнигиПродаж.СтавкаНДС КАК СтавкаНДС
		|ПОМЕСТИТЬ ВТ_НДСПродажи
		|ИЗ
		|	РегистрНакопления.НДСЗаписиКнигиПродаж КАК НДСЗаписиКнигиПродаж
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СписокСчетовФактур КАК СписокСчетовФактур
		|		ПО НДСЗаписиКнигиПродаж.СчетФактура = СписокСчетовФактур.СчетФактура
		|ГДЕ
		|	НДСЗаписиКнигиПродаж.Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.ВосстановлениеНДС)
		|	И НДСЗаписиКнигиПродаж.Период <= &Дата
		|
		|СГРУППИРОВАТЬ ПО
		|	НДСЗаписиКнигиПродаж.СтавкаНДС,
		|	НДСЗаписиКнигиПродаж.ВидЦенности,
		|	НДСЗаписиКнигиПродаж.СчетФактура
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	СчетФактура,
		|	ВидЦенности,
		|	СтавкаНДС";
			
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|" +
	"ВЫБРАТЬ
	|	ВТ_НДСПредъявленныйРеализация0Остатки.СчетФактура КАК СчетФактура,
	|	ВТ_НДСПредъявленныйРеализация0Остатки.СчетФактураДата КАК СчетФактураДата,
	|	ВТ_НДСПредъявленныйРеализация0Остатки.ВидЦенности КАК ВидЦенности,
	|	ВТ_НДСПредъявленныйРеализация0Остатки.СтавкаНДС КАК СтавкаНДС,
	|	ВТ_НДСПредъявленныйРеализация0Остатки.СчетУчетаНДС КАК СчетУчетаНДС,
	|	ВТ_НДСПредъявленныйРеализация0Остатки.КВычету_БезНДС КАК КВычету_БезНДС,
	|	ВТ_НДСПредъявленныйРеализация0Остатки.КВычету_НДС КАК КВычету_НДС,
	|	ВТ_НДСПредъявленныйРеализация0Остатки.КВычету_СНДС КАК КВычету_СНДС,
	|	ВТ_НДСПредъявленныйРеализация0Остатки.ПорядокОплаты КАК ПорядокОплаты,
	|	ВТ_НДСПредъявленныйРеализация0Остатки.ДокументОтгрузки КАК ДокументОтгрузки,
	|	ВТ_НДСПредъявленныйРеализация0Остатки.Поставщик КАК Поставщик,
	|	ВТ_НДСПредъявленныйРеализация0Остатки.Состояние КАК Состояние,
	|	ВЫБОР
	|		КОГДА &Дата < ДАТАВРЕМЯ(2015, 1, 1)
	|			ТОГДА """"
	|		КОГДА ЕСТЬNULL(ВТ_НДСПродажи.НДС, 0) = 0
	|			ТОГДА ""01""
	|		КОГДА ВТ_НДСПредъявленныйРеализация0Остатки.КВычету_НДС = ЕСТЬNULL(ВТ_НДСПродажи.НДС, 0)
	|			ТОГДА ""25""
	|		ИНАЧЕ ""25;01""
	|	КОНЕЦ КАК КодВидаОперации
	|ИЗ
	|	ВТ_НДСПредъявленныйРеализация0Остатки КАК ВТ_НДСПредъявленныйРеализация0Остатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_НДСПродажи КАК ВТ_НДСПродажи
	|		ПО ВТ_НДСПредъявленныйРеализация0Остатки.СчетФактура = ВТ_НДСПродажи.СчетФактура
	|			И ВТ_НДСПредъявленныйРеализация0Остатки.ВидЦенности = ВТ_НДСПродажи.ВидЦенности
	|			И ВТ_НДСПредъявленныйРеализация0Остатки.СтавкаНДС = ВТ_НДСПродажи.СтавкаНДС
	|			И (НЕ ВТ_НДСПредъявленныйРеализация0Остатки.ДокументОтгрузки = ВТ_НДСПродажи.Регистратор)
	|
	|УПОРЯДОЧИТЬ ПО
	|	СчетФактураДата
	|ИТОГИ
	|	СУММА(КВычету_БезНДС),
	|	СУММА(КВычету_НДС),
	|	СУММА(КВычету_СНДС)
	|ПО
	|	СчетФактура,
	|	ПорядокОплаты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_НДСПродажи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ СписокСчетовФактур
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_НДСПредъявленныйРеализация0Остатки";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Организация",  Организация);
	Запрос.УстановитьПараметр("Дата", КонецДня(Дата));
	Запрос.УстановитьПараметр("ДатаГраница", новый Граница(КонецДня(Дата),ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Начало2006Года", '20060101');
	Запрос.УстановитьПараметр("КонтролироватьОплатуДляСФсДатойМенее01012006", КонтролироватьОплатуДляСФсДатойМенее01012006);
	
	// Исключаемые из анализа виды ценностей
	ИсключаемыеВидыЦенностей = Новый СписокЗначений;
	ИсключаемыеВидыЦенностей.Добавить(Перечисления.ВидыЦенностей.АвансыПолученные);
	ИсключаемыеВидыЦенностей.Добавить(Перечисления.ВидыЦенностей.АвансыПолученные0);
	ИсключаемыеВидыЦенностей.Добавить(Перечисления.ВидыЦенностей.ВозвратАвансовПолученных);
	ИсключаемыеВидыЦенностей.Добавить(Перечисления.ВидыЦенностей.АвансыВыданные);

	// Виды ценностей с особым порядком распределения оплат - по НДС выплаченному в бюджет
	ВидыЦенностей_ОплатаПоНДС = Новый СписокЗначений;
	ВидыЦенностей_ОплатаПоНДС.Добавить(Перечисления.ВидыЦенностей.НалоговыйАгентАренда);
	ВидыЦенностей_ОплатаПоНДС.Добавить(Перечисления.ВидыЦенностей.НалоговыйАгентРеализацияИмущества);
	ВидыЦенностей_ОплатаПоНДС.Добавить(Перечисления.ВидыЦенностей.НалоговыйАгентИностранцы);
	ВидыЦенностей_ОплатаПоНДС.Добавить(Перечисления.ВидыЦенностей.ВнутреннееПотребление);
	ВидыЦенностей_ОплатаПоНДС.Добавить(Перечисления.ВидыЦенностей.СМРСобственнымиСилами);
	
	// Виды ценностей с особым порядком распределения оплат - оплата не определяется
	ВидыЦенностей_БезОплаты = Новый СписокЗначений;
	ВидыЦенностей_БезОплаты.Добавить(Перечисления.ВидыЦенностей.ТаможенныеПлатежи);
	ВидыЦенностей_БезОплаты.Добавить(Перечисления.ВидыЦенностей.ТаможенныеПлатежиОС);
	ВидыЦенностей_Возврат = Новый СписокЗначений;
	ВидыЦенностей_Возврат.Добавить(Перечисления.ВидыЦенностей.Возврат);
	
	Запрос.УстановитьПараметр("ИсключаемыеВидыЦенностей", ИсключаемыеВидыЦенностей);
	Запрос.УстановитьПараметр("ВидыЦенностей_ОплатаПоНДС", ВидыЦенностей_ОплатаПоНДС);
	Запрос.УстановитьПараметр("ВидыЦенностей_БезОплаты", ВидыЦенностей_БезОплаты);
	Запрос.УстановитьПараметр("ВидыЦенностей_Возврат", ВидыЦенностей_Возврат);
	
	ОтрабатываемыеСостояния = Новый СписокЗначений;
	ОтрабатываемыеСостояния.Добавить(Перечисления.НДССостоянияРеализация0.ПодтвержденаРеализация0);
	ОтрабатываемыеСостояния.Добавить(Перечисления.НДССостоянияРеализация0.НеПодтвержденаРеализация0);
	
	Запрос.УстановитьПараметр("ОтрабатываемыеСостояния",ОтрабатываемыеСостояния);
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	// Проверяем возможность предъявления к вычету по остаткам регистра "НДС предъявленный)
	// (вычет не может превышать текущий остаток)
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НДСПредъявленныйОстатки.СчетФактура КАК СчетФактура,
		|	НДСПредъявленныйОстатки.ВидЦенности,
		|	НДСПредъявленныйОстатки.СтавкаНДС,
		|	НДСПредъявленныйОстатки.СчетУчетаНДС,
		|	НДСПредъявленныйОстатки.СуммаБезНДСОстаток + НДСПредъявленныйОстатки.НДСОстаток КАК СуммаСНДС
		|ИЗ
		|	РегистрНакопления.НДСПредъявленный.Остатки(
		|		&ДатаГраница,
		|		Организация = &Организация
		|			И (НЕ ВидЦенности В (&ИсключаемыеВидыЦенностей))
		|			И СчетФактура В (&СписокСчетовФактур)) КАК НДСПредъявленныйОстатки
		|ГДЕ
		|	НДСПредъявленныйОстатки.СуммаБезНДСОстаток + НДСПредъявленныйОстатки.НДСОстаток > 0
		|ИТОГИ
		|	СУММА(СуммаСНДС)
		|ПО
		|	СчетФактура";
		
	Запрос.УстановитьПараметр("СписокСчетовФактур", РезультатЗапроса.Строки.ВыгрузитьКолонку("СчетФактура"));
    СтрокиСФкУдалению = новый массив();
	ДоступныйКВычетуНДС = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	СтруктураОтбора = НОвый структура("ВидЦенности, СтавкаНДС, СчетУчетаНДС");
	Для каждого СтрокаСФ Из РезультатЗапроса.Строки Цикл
		СтрокаСФ_НДСПРедъявленный = ДоступныйКВычетуНДС.строки.Найти(СтрокаСФ.СчетФактура,"СчетФактура");
		Если СтрокаСФ_НДСПРедъявленный = Неопределено Тогда
			// Не найдены положитьельные остатки по СФ
			// НДС по СФ не может быть принят к вычету
			СтрокиСФкУдалению.Добавить(СтрокаСФ);
			Продолжить;
		ИначеЕсли СтрокаСФ_НДСПРедъявленный.СуммаСНДС >= СтрокаСФ.КВычету_СНДС тогда
			// Сумма НДС доступного к вычету по СФ не менее, чем сумма списываемая документом.
			// Корректировка не требуется
			Продолжить;
		КонецЕсли;

		Для каждого СтрокаПорядкаОплат Из СтрокаСФ.Строки Цикл
		
			СтрокиКУдалению = Новый Массив();
			Для каждого СтрокаРасшифровки Из СтрокаПорядкаОплат.Строки Цикл
				ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаРасшифровки);
				СтрокиПоОтбору = СтрокаСФ_НДСПРедъявленный.строки.НайтиСтроки(СтруктураОтбора);
				Если СтрокиПоОтбору.Количество() = 0 Тогда
					СтрокиКУдалению.Добавить(СтрокаРасшифровки);
					Продолжить;
				КонецЕсли;
				//может быть выбрана только одна строка
				СтрокаПоОтбору = СтрокиПоОтбору[0];
				Если СтрокаПоОтбору.СуммаСНДС = 0 тогда 
					СтрокиКУдалению.Добавить(СтрокаРасшифровки);
					Продолжить;
				КонецЕсли;
				
				Если СтрокаПоОтбору.СуммаСНДС >= СтрокаРасшифровки.КВычету_СНДС тогда
					СтрокаПоОтбору.СуммаСНДС = СтрокаПоОтбору.СуммаСНДС - СтрокаРасшифровки.КВычету_СНДС;
				Иначе
					СтрокаРасшифровки.КВычету_НДС = (СтрокаРасшифровки.КВычету_НДС*СтрокаПоОтбору.СуммаСНДС/СтрокаРасшифровки.КВычету_СНДС);
					СтрокаРасшифровки.КВычету_СНДС = СтрокаПоОтбору.СуммаСНДС;
					СтрокаРасшифровки.КВычету_БезНДС = СтрокаРасшифровки.КВычету_СНДС - СтрокаРасшифровки.КВычету_НДС;
					СтрокаПоОтбору.СуммаСНДС = 0;
				КонецЕсли;
			КонецЦикла; 
			Для каждого СтрокаКУдалению	из СтрокиКУдалению Цикл
			    СтрокаПорядкаОплат.Строки.Удалить(СтрокаКУдалению);
			КонецЦикла; 
			
			СтрокаПорядкаОплат.КВычету_НДС = СтрокаПорядкаОплат.Строки.Итог("КВычету_НДС");
			СтрокаПорядкаОплат.КВычету_СНДС = СтрокаПорядкаОплат.Строки.Итог("КВычету_СНДС");
			СтрокаПорядкаОплат.КВычету_БезНДС = СтрокаПорядкаОплат.Строки.Итог("КВычету_БезНДС");
		
		КонецЦикла; 
		СтрокаСФ.КВычету_НДС = СтрокаСФ.Строки.Итог("КВычету_НДС");
		СтрокаСФ.КВычету_СНДС = СтрокаСФ.Строки.Итог("КВычету_СНДС");
		СтрокаСФ.КВычету_БезНДС = СтрокаСФ.Строки.Итог("КВычету_БезНДС");
		
		Если СтрокаСФ.КВычету_СНДС = 0 Тогда
			СтрокиСФкУдалению.Добавить(СтрокаСФ);
		КонецЕсли;
	КонецЦикла; 
	
	Для каждого СтрокаКУдалению	из СтрокиСФкУдалению Цикл
		РезультатЗапроса.Строки.Удалить(СтрокаКУдалению);
	КонецЦикла; 
	
	Возврат РезультатЗапроса;

КонецФункции // ЗаполнитьНДСКВычетуПоДаннымРегистраНДСПредъявленныйРеализация0()

// Вызывается из процедуры ЗаполнитьСтроки_ВычетПоПриобретеннымЦенностям.
// Получает информацию о НДС включенном в стоимость и прочим блокирующим событиям 
// для блокировки распределенных оплат
Функция ПолучитьСведенияПоЗаблокированномуНДС(СписокСчетовФактур)

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НДСВключенныйВСтоимостьОбороты.СчетФактура КАК СчетФактура,
	|	ЕСТЬNULL(НДСВключенныйВСтоимостьОбороты.СуммаБезНДСОборот, 0) + ЕСТЬNULL(НДСВключенныйВСтоимостьОбороты.НДСОборот, 0) КАК КВычету_СНДС,
	|	ЕСТЬNULL(НДСВключенныйВСтоимостьОбороты.НДСОборот, 0) КАК КВычету_НДС,
	|	НДСВключенныйВСтоимостьОбороты.СчетФактура.Дата КАК СчетФактураДата,
	|	ВЫБОР
	|		КОГДА НДСВключенныйВСтоимостьОбороты.ВидЦенности В (&ВидыЦенностей_ОплатаПоНДС)
	|			ТОГДА 1
	|		КОГДА НДСВключенныйВСтоимостьОбороты.ВидЦенности В (&ВидыЦенностей_БезОплаты)
	|			ТОГДА 2
	|		КОГДА НДСВключенныйВСтоимостьОбороты.ВидЦенности В (&ВидыЦенностей_Возврат)
	|			ТОГДА 4
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПорядокОплаты
	|ИЗ
	|	РегистрНакопления.НДСВключенныйВСтоимость.Обороты(
	|		,
	|		&ДатаГраница,
	|		Период,
	|		Организация = &Организация
	|		    И СчетФактура В (&СписокСчетовФактур)
	|		    И (НЕ ВидЦенности В (&ИсключаемыеВидыЦенностей))) КАК НДСВключенныйВСтоимостьОбороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НДСпоОСиНМАОстатки.СчетФактура,
	|	ЕСТЬNULL(НДСпоОСиНМАОстатки.СуммаБезНДСОстаток, 0) + ЕСТЬNULL(НДСпоОСиНМАОстатки.НДСОстаток, 0),
	|	ЕСТЬNULL(НДСпоОСиНМАОстатки.НДСОстаток, 0),
	|	НДСпоОСиНМАОстатки.СчетФактура.Дата,
	|	ВЫБОР
	|		КОГДА НДСпоОСиНМАОстатки.ВидЦенности В (&ВидыЦенностей_ОплатаПоНДС)
	|			ТОГДА 1
	|		КОГДА НДСпоОСиНМАОстатки.ВидЦенности В (&ВидыЦенностей_БезОплаты)
	|			ТОГДА 2
	|		КОГДА НДСпоОСиНМАОстатки.ВидЦенности В (&ВидыЦенностей_Возврат)
	|			ТОГДА 4
	|		ИНАЧЕ 0
	|	КОНЕЦ
	|ИЗ
	|	РегистрНакопления.НДСпоОСиНМА.Остатки(
	|		&ДатаГраница,
	|		Организация = &Организация
	|			И СчетФактура В (&СписокСчетовФактур)
	|			И НДСВключенВСтоимость = ЛОЖЬ
	|			И (НЕ ВидЦенности В (&ИсключаемыеВидыЦенностей))
	|			И НеВлияетНаВычет = ЛОЖЬ) КАК НДСпоОСиНМАОстатки
	|
	|УПОРЯДОЧИТЬ ПО
	|	СчетФактураДата
	|ИТОГИ
	|	СУММА(КВычету_СНДС),
	|	СУММА(КВычету_НДС)
	|ПО
	|	СчетФактура,
	|	ПорядокОплаты";
	
	Если не ПредъявленНДСКВычету0 Тогда
		ДополнениеПоРеализации0 = "
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	НДСПредъявленныйРеализация0Остатки.СчетФактура,
		|	ЕСТЬNULL(НДСПредъявленныйРеализация0Остатки.СуммаБезНДСОстаток, 0) + ЕСТЬNULL(НДСПредъявленныйРеализация0Остатки.НДСОстаток, 0),
		|	ЕСТЬNULL(НДСПредъявленныйРеализация0Остатки.НДСОстаток, 0),
		|	НДСПредъявленныйРеализация0Остатки.СчетФактура.Дата,
		|	ВЫБОР
		|		КОГДА НДСПредъявленныйРеализация0Остатки.ВидЦенности В (&ВидыЦенностей_ОплатаПоНДС)
		|			ТОГДА 1
		|		КОГДА НДСПредъявленныйРеализация0Остатки.ВидЦенности В (&ВидыЦенностей_БезОплаты)
		|			ТОГДА 2
		|		КОГДА НДСПредъявленныйРеализация0Остатки.ВидЦенности В (&ВидыЦенностей_Возврат)
		|			ТОГДА 4
		|		ИНАЧЕ 0
		|	КОНЕЦ
		|ИЗ
		|	РегистрНакопления.НДСПредъявленныйРеализация0.Остатки(
		|		&ДатаГраница,
		|		Организация = &Организация
		|		    И СчетФактура В (&СписокСчетовФактур)
		|		    И (НЕ ВидЦенности В (&ИсключаемыеВидыЦенностей))) КАК НДСПредъявленныйРеализация0Остатки
		| УПОРЯДОЧИТЬ ПО
		| ";
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "УПОРЯДОЧИТЬ ПО",ДополнениеПоРеализации0);
	КонецЕсли; 

	Запрос.УстановитьПараметр("Организация",  Организация);
	Запрос.УстановитьПараметр("ДатаГраница", новый Граница(КонецДня(Дата),ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Дата", КонецДня(Дата));

	// Исключаемые из анализа виды ценностей
	ИсключаемыеВидыЦенностей = Новый СписокЗначений;
	ИсключаемыеВидыЦенностей.Добавить(Перечисления.ВидыЦенностей.АвансыПолученные);
	ИсключаемыеВидыЦенностей.Добавить(Перечисления.ВидыЦенностей.АвансыПолученные0);
	ИсключаемыеВидыЦенностей.Добавить(Перечисления.ВидыЦенностей.ВозвратАвансовПолученных);

	// Виды ценностей с особым порядком распределения оплат - по НДС выплаченному в бюджет
	ВидыЦенностей_ОплатаПоНДС = Новый СписокЗначений;
	ВидыЦенностей_ОплатаПоНДС.Добавить(Перечисления.ВидыЦенностей.НалоговыйАгентАренда);
	ВидыЦенностей_ОплатаПоНДС.Добавить(Перечисления.ВидыЦенностей.НалоговыйАгентРеализацияИмущества);
	ВидыЦенностей_ОплатаПоНДС.Добавить(Перечисления.ВидыЦенностей.НалоговыйАгентИностранцы);
	ВидыЦенностей_ОплатаПоНДС.Добавить(Перечисления.ВидыЦенностей.ВнутреннееПотребление);
	ВидыЦенностей_ОплатаПоНДС.Добавить(Перечисления.ВидыЦенностей.СМРСобственнымиСилами);
	
	// Виды ценностей с особым порядком распределения оплат - оплата не определяется
	ВидыЦенностей_БезОплаты = Новый СписокЗначений;
	ВидыЦенностей_БезОплаты.Добавить(Перечисления.ВидыЦенностей.ТаможенныеПлатежи);
	ВидыЦенностей_БезОплаты.Добавить(Перечисления.ВидыЦенностей.ТаможенныеПлатежиОС);
	ВидыЦенностей_Возврат = Новый СписокЗначений;
	ВидыЦенностей_Возврат.Добавить(Перечисления.ВидыЦенностей.Возврат);
	
	Запрос.УстановитьПараметр("ИсключаемыеВидыЦенностей", ИсключаемыеВидыЦенностей);
	Запрос.УстановитьПараметр("ВидыЦенностей_ОплатаПоНДС", ВидыЦенностей_ОплатаПоНДС);
	Запрос.УстановитьПараметр("ВидыЦенностей_БезОплаты", ВидыЦенностей_БезОплаты);
	Запрос.УстановитьПараметр("ВидыЦенностей_Возврат", ВидыЦенностей_Возврат);
	Запрос.УстановитьПараметр("СписокСчетовФактур", СписокСчетовФактур);
	
	Возврат Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	
КонецФункции // ПолучитьСведенияПоЗаблокированномуНДС()

// Процедура вызывается из ЗаполнитьСтроки_ВычетПоПриобретеннымЦенностям.
// По списку счетов-фактур определяет суммы не использованных ранее распределенных оплат.
Функция ПолучитьДанныеОРаспределенныхОплатахПоСпискуСФ(СписокСчетовФактур)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РасчетыПоПриобретениюВУсловныхЕдиницахОрганизацииОбороты.Организация,
	|	РасчетыПоПриобретениюВУсловныхЕдиницахОрганизацииОбороты.Период,
	|	РасчетыПоПриобретениюВУсловныхЕдиницахОрганизацииОбороты.Регистратор,
	|	РасчетыПоПриобретениюВУсловныхЕдиницахОрганизацииОбороты.Документ,
	|	РасчетыПоПриобретениюВУсловныхЕдиницахОрганизацииОбороты.ДатаОплаты,
	|	РасчетыПоПриобретениюВУсловныхЕдиницахОрганизацииОбороты.СуммаРегРасход,
	|	РасчетыПоПриобретениюВУсловныхЕдиницахОрганизацииОбороты.СуммаРегПриход,
	|	ВЫБОР
	|		КОГДА РасчетыПоПриобретениюВУсловныхЕдиницахОрганизацииОбороты.Регистратор В (&СписокСчетовФактур)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоДокументПриобретения
	|ПОМЕСТИТЬ РасчетыПоПриобретению
	|ИЗ
	|	РегистрНакопления.РасчетыПоПриобретениюВУсловныхЕдиницахОрганизации.Обороты(, &ДатаГраница, Регистратор, Организация = &Организация) КАК РасчетыПоПриобретениюВУсловныхЕдиницахОрганизацииОбороты
	|ГДЕ
	|	(РасчетыПоПриобретениюВУсловныхЕдиницахОрганизацииОбороты.Регистратор В (&СписокСчетовФактур)
	|			ИЛИ РасчетыПоПриобретениюВУсловныхЕдиницахОрганизацииОбороты.Документ В (&СписокСчетовФактур))
	|	И (НЕ РасчетыПоПриобретениюВУсловныхЕдиницахОрганизацииОбороты.Регистратор = РасчетыПоПриобретениюВУсловныхЕдиницахОрганизацииОбороты.Документ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетыПоПриобретению.Организация,
	|	ВЫБОР
	|		КОГДА РасчетыПоПриобретению.ЭтоДокументПриобретения
	|			ТОГДА РасчетыПоПриобретению.Регистратор
	|		ИНАЧЕ РасчетыПоПриобретению.Документ
	|	КОНЕЦ КАК СчетФактура,
	|	ВЫБОР
	|		КОГДА РасчетыПоПриобретению.ЭтоДокументПриобретения
	|			ТОГДА РасчетыПоПриобретению.Документ
	|		ИНАЧЕ РасчетыПоПриобретению.Регистратор
	|	КОНЕЦ КАК ДокументОплаты,
	|	НАЧАЛОПЕРИОДА(ВЫБОР
	|		КОГДА РасчетыПоПриобретению.ЭтоДокументПриобретения
	|			ТОГДА РасчетыПоПриобретению.ДатаОплаты
	|		ИНАЧЕ РасчетыПоПриобретению.Период
	|	КОНЕЦ, День) КАК ДатаОплаты,
	|	ВЫБОР
	|		КОГДА РасчетыПоПриобретению.ЭтоДокументПриобретения
	|			ТОГДА РасчетыПоПриобретению.СуммаРегРасход
	|		ИНАЧЕ РасчетыПоПриобретению.СуммаРегПриход
	|	КОНЕЦ КАК РаспределеннаяОплата,
	|	ЛОЖЬ КАК РасчетыСБюджетом
	|ИЗ
	|	РасчетыПоПриобретению КАК РасчетыПоПриобретению
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НДСУчетРаспределенныхОплатПоставщикамОстатки.Организация,
	|	НДСУчетРаспределенныхОплатПоставщикамОстатки.СчетФактура,
	|	НДСУчетРаспределенныхОплатПоставщикамОстатки.ДокументОплаты,
	|	НАЧАЛОПЕРИОДА(ЕСТЬNULL(НДСУчетРаспределенныхОплатПоставщикамОстатки.ДокументОплаты.ДатаОплаты, НДСУчетРаспределенныхОплатПоставщикамОстатки.ДокументОплаты.Дата), День),
	|	НДСУчетРаспределенныхОплатПоставщикамОстатки.РаспределеннаяСуммаОстаток,
	|	ИСТИНА
	|ИЗ
	|	РегистрНакопления.НДСУчетРаспределенныхОплатПоставщикам.Остатки(
	|			&ДатаГраница,
	|			Организация = &Организация
	|				И СчетФактура В (&СписокСчетовФактур)
	|				И (НЕ ДокументОплаты = НЕОПРЕДЕЛЕНО)
	|				И РасчетыСБюджетом = ИСТИНА) КАК НДСУчетРаспределенныхОплатПоставщикамОстатки
	|ГДЕ
	|	НДСУчетРаспределенныхОплатПоставщикамОстатки.СчетФактура В(&СписокСчетовФактур)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НДСКорректировкиПриВалютныхРасчетах.Организация,
	|	НДСКорректировкиПриВалютныхРасчетах.СчетФактура,
	|	НДСКорректировкиПриВалютныхРасчетах.Регистратор,
	|	НАЧАЛОПЕРИОДА(НДСКорректировкиПриВалютныхРасчетах.Период, День),
	|	НДСКорректировкиПриВалютныхРасчетах.СуммаБезНДС,
	|	Ложь
	|ИЗ
	|	РегистрНакопления.НДСПредъявленный КАК НДСКорректировкиПриВалютныхРасчетах
	|
	|ГДЕ
	|   НДСКорректировкиПриВалютныхРасчетах.Период <= &Дата
	|	И НДСКорректировкиПриВалютныхРасчетах.Организация = &Организация
	|	И НДСКорректировкиПриВалютныхРасчетах.СчетФактура В (&СписокСчетовФактур)
	|	И НДСКорректировкиПриВалютныхРасчетах.ВидРегламентнойОперации = Значение(Перечисление.ВидыРегламентныхОпераций.КорректировкиПриВалютныхРасчетах)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаОплаты";
	
	Запрос.УстановитьПараметр("Дата", 		КонецДня(Дата));
	Запрос.УстановитьПараметр("Организация", 		Организация);
	Запрос.УстановитьПараметр("СписокСчетовФактур", СписокСчетовФактур);
	Запрос.УстановитьПараметр("ДатаГраница", новый Граница(КонецДня(Дата),ВидГраницы.Включая));
	
	РаспределенныеОплаты = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.Прямой);
	РаспределенныеОплаты.Свернуть("Организация, СчетФактура, ДокументОплаты, ДатаОплаты, РасчетыСБюджетом","РаспределеннаяОплата");
	
	РаспределенныеОплаты.Колонки.Добавить("QueryId", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(6)));

	Если РаспределенныеОплаты.Количество()>0 Тогда
		QueryId = Новый Массив(РаспределенныеОплаты.Количество());
		Для Счетчик=0 По РаспределенныеОплаты.Количество()-1 Цикл
			QueryId[Счетчик] = Счетчик;
		КонецЦикла; 
		РаспределенныеОплаты.ЗагрузитьКолонку(QueryId,"QueryId");
	КонецЕсли; 
	
	Возврат РаспределенныеОплаты;	

КонецФункции // ПолучитьДанныеОРаспределенныхОплатахПоСпискуСФ()
 
// Процедура вызывается из ЗаполнитьСтроки_ВычетПоПриобретеннымЦенностям.
// По списку счетов-фактур распределяет по видам ценностей суммы не использованных ранее распределенных оплат.
Процедура РаспределитьОплатыПоДеревуСФ(Дерево_НДСкВычету, ТаблицаРезультатов, СписокСчетовФактур, РаспределенныеОплаты, ОтражатьВРеестре = Истина,КонтролироватьОплатуДляСФсДатойМенее01012006 = Истина)

	НДСНалоговыйПериод = Неопределено;
	
	ОтфактурованныеПоступления = Новый СписокЗначений();
	ОтфактурованныеПоступления.ЗагрузитьЗначения(СписокСчетовФактур);
	
	Построитель_РаспределенныеОплаты = Новый построительЗапроса();
	Построитель_РаспределенныеОплаты.ИсточникДанных = Новый ОписаниеИсточникаДанных(РаспределенныеОплаты);
	
	// Подготовка структуры отбора 
	Отбор = Построитель_РаспределенныеОплаты.Отбор;
	Отбор.Добавить("СчетФактура");
	Отбор.СчетФактура.Использование = Истина;
	Отбор.Добавить("РасчетыСБюджетом");
	Отбор.РасчетыСБюджетом.Использование = Истина;
	Отбор.Добавить("РаспределеннаяОплата");
	Отбор.РаспределеннаяОплата.ВидСравнения = ВидСравнения.Больше;
	Отбор.РаспределеннаяОплата.Значение = 0;
	Отбор.РаспределеннаяОплата.Использование = Истина;
	
	Построитель_РаспределенныеОплаты.Порядок.Добавить("ДатаОплаты");
	
	
	ТаблицаОплат = Новый ТаблицаЗначений();
	ТаблицаОплат.Колонки.Добавить("ДокументОплаты");
	ТаблицаОплат.Колонки.Добавить("ДатаОплаты",Новый ОписаниеТипов("Дата", , , Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)));
	ТаблицаОплат.Колонки.Добавить("СуммаОплаты",Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	
	ТаблицаИсточникаПостроителя = Построитель_РаспределенныеОплаты.ИсточникДанных.ИсточникДанных;
	Для каждого СтрокаСФ Из Дерево_НДСкВычету.Строки Цикл
		Для каждого СтрокаПорядокОплаты  Из СтрокаСФ.Строки Цикл
			Если СтрокаСФ.КВычету_СНДС = 0 Тогда
				Продолжить;
			КонецЕсли; 
			
			ТаблицаОплат.Очистить();
			Если СтрокаПорядокОплаты.ПорядокОплаты = 2 Тогда
				// Наличие оплаты не определяется
				СтрокаОплаты = ТаблицаОплат.Добавить();
				СтрокаОплаты.ДокументОплаты = СтрокаСФ.СчетФактура;
				СтрокаОплаты.ДатаОплаты = СтрокаСФ.СчетФактураДата;
				СтрокаОплаты.СуммаОплаты = СтрокаСФ.КВычету_СНДС;
			ИначеЕсли СтрокаПорядокОплаты.ПорядокОплаты = 4 
				И Не ОтфактурованныеПоступления.НайтиПоЗначению(СтрокаСФ.СчетФактура) = Неопределено Тогда
				// Наличие оплаты не определяется
				СтрокаОплаты = ТаблицаОплат.Добавить();
				СтрокаОплаты.ДокументОплаты = Неопределено;
				СтрокаОплаты.ДатаОплаты = Неопределено;
				СтрокаОплаты.СуммаОплаты = СтрокаСФ.КВычету_СНДС;
			Иначе
				Отбор = Построитель_РаспределенныеОплаты.Отбор;
				Отбор.СчетФактура.Значение = СтрокаПорядокОплаты.СчетФактура;
				Отбор.РасчетыСБюджетом.Значение = (СтрокаПорядокОплаты.ПорядокОплаты = 1 Или СтрокаПорядокОплаты.ПорядокОплаты = 5);
				Построитель_РаспределенныеОплаты.Выполнить();
				Если Построитель_РаспределенныеОплаты.Результат.Пустой() Тогда
					// Оплата не обнаружена
					Если СтрокаПорядокОплаты.ПорядокОплаты = 3 
						и не ОтфактурованныеПоступления.НайтиПоЗначению(СтрокаСФ.СчетФактура) = неопределено 
						Тогда
						Если СтрокаПорядокОплаты.ОпределенаДоля_119ФЗ_2_10 Тогда
						    СуммаОплаты = Мин(СтрокаПорядокОплаты.КВычету_СНДС_Часть,СтрокаПорядокОплаты.КВычету_СНДС);
						Иначе
							СуммаОплаты = СтрокаПорядокОплаты.КВычету_СНДС;
						КонецЕсли; 
						
						Если СуммаОплаты = 0 тогда
							Продолжить;
						КонецЕсли;
						
						// Наличие оплаты желательно, но не обязательно для принятия к вычету
						СтрокаОплаты = ТаблицаОплат.Добавить();
						СтрокаОплаты.ДокументОплаты = Неопределено;
						СтрокаОплаты.ДатаОплаты = Неопределено;
						СтрокаОплаты.СуммаОплаты = СуммаОплаты;
					ИначеЕсли СтрокаПорядокОплаты.ПорядокОплаты = 5 Тогда
						СуммаОплаты = СтрокаПорядокОплаты.КВычету_НДС;
						
						Если СуммаОплаты = 0 Тогда
							Продолжить;
						КонецЕсли;
						
						// Наличие оплаты желательно, но не обязательно для принятия к вычету
						СтрокаОплаты = ТаблицаОплат.Добавить();
						СтрокаОплаты.ДокументОплаты = Неопределено;
						СтрокаОплаты.ДатаОплаты = Неопределено;
						СтрокаОплаты.СуммаОплаты = СуммаОплаты;
					Иначе	
						Продолжить;
					КонецЕсли;
				Иначе
					ВыборкаОплат = Построитель_РаспределенныеОплаты.Результат.Выгрузить(ОбходРезультатаЗапроса.Прямой);
					
					Если СтрокаПорядокОплаты.ПорядокОплаты = 1 
						Или СтрокаПорядокОплаты.ПорядокОплаты = 5 Тогда
						// Оплата определяется по сумме НДС
						СуммаКПогашению = СтрокаПорядокОплаты.КВычету_НДС;
					ИначеЕсли СтрокаПорядокОплаты.ПорядокОплаты = 6 Тогда
						// Оплата определяется в общем порядке (по сумме без НДС)
						СуммаКПогашению = СтрокаПорядокОплаты.КВычету_БезНДС;
					Иначе
						// Оплата определяется в общем порядке (по сумме с НДС)
						СуммаКПогашению = СтрокаПорядокОплаты.КВычету_СНДС;
					КонецЕсли; 
					
					Для каждого СтрокаРаспределеннойОплаты Из ВыборкаОплат Цикл
						СтрокаОплаты = ТаблицаОплат.Добавить();
						СтрокаОплаты.ДокументОплаты = СтрокаРаспределеннойОплаты.ДокументОплаты;
						СтрокаОплаты.ДатаОплаты = СтрокаРаспределеннойОплаты.ДатаОплаты;
						СтрокаОплаты.СуммаОплаты = Мин(СуммаКПогашению,СтрокаРаспределеннойОплаты.РаспределеннаяОплата);
						
						СуммаКПогашению = СуммаКПогашению - СтрокаОплаты.СуммаОплаты;
						ТаблицаИсточникаПостроителя[СтрокаРаспределеннойОплаты.QueryId].РаспределеннаяОплата = ТаблицаИсточникаПостроителя[СтрокаРаспределеннойОплаты.QueryId].РаспределеннаяОплата - СтрокаОплаты.СуммаОплаты;
						
						Если СуммаКПогашению = 0 Тогда
						    Прервать;
						КонецЕсли;
					КонецЦикла;
					
					Если СуммаКПогашению > 0 
						И (СтрокаПорядокОплаты.ПорядокОплаты = 3
						Или СтрокаПорядокОплаты.ПорядокОплаты = 5) Тогда
						// Наличие оплаты желательно, но не обязательно для принятия к вычету
						Если СтрокаПорядокОплаты.ОпределенаДоля_119ФЗ_2_10 Тогда
							ПредложеноКВычетуПоОплатам = СтрокаПорядокОплаты.КВычету_СНДС - СуммаКПогашению;
							СуммаКПогашению = Макс(0, СтрокаПорядокОплаты.КВычету_СНДС_Часть - ПредложеноКВычетуПоОплатам);
						КонецЕсли;
						
						Если не СуммаКПогашению = 0 Тогда
							СтрокаОплаты = ТаблицаОплат.Добавить();
							СтрокаОплаты.ДокументОплаты = Неопределено;
							СтрокаОплаты.ДатаОплаты = Неопределено;
							СтрокаОплаты.СуммаОплаты = СуммаКПогашению;
						КонецЕсли; 
					КонецЕсли;
				КонецЕсли; 
			КонецЕсли;
			Если ТаблицаОплат.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли; 
			
			Если не ОтражатьВРеестре тогда
				// Оплаты заблокированы, дальнейшая обработка не требуется
				Продолжить;
			КонецЕсли;
			
			ТаблицаОплат.Свернуть("ДокументОплаты, ДатаОплаты","СуммаОплаты");
			
			Для Каждого СтрокаОплаты Из ТаблицаОплат Цикл
				Если СтрокаПорядокОплаты.ПорядокОплаты = 1 
					Или СтрокаПорядокОплаты.ПорядокОплаты = 5 Тогда
					МассивБазиса = СтрокаПорядокОплаты.Строки.ВыгрузитьКолонку("КВычету_НДС");
					КРаспределениюНДС = СтрокаОплаты.СуммаОплаты;
					Массив_НДС = СтрокаПорядокОплаты.Строки.ВыгрузитьКолонку("КВычету_НДС");
					Если СтрокаПорядокОплаты.Строки.Итог("КВычету_НДС") <> 0 Тогда
						КРаспределениюСНДС = Окр(СтрокаОплаты.СуммаОплаты * СтрокаПорядокОплаты.Строки.Итог("КВычету_СНДС")/СтрокаПорядокОплаты.Строки.Итог("КВычету_НДС"),2);
						Если КРаспределениюСНДС > 0 Тогда
							МассивРезультата_СНДС = ОбщегоНазначения.РаспределитьПропорционально(КРаспределениюСНДС ,СтрокаПорядокОплаты.Строки.ВыгрузитьКолонку("КВычету_СНДС"));
						КонецЕсли;
					КонецЕсли;
				ИначеЕсли СтрокаПорядокОплаты.ПорядокОплаты = 6 Тогда
					МассивБазиса = СтрокаПорядокОплаты.Строки.ВыгрузитьКолонку("КВычету_БезНДС");
					Массив_НДС = СтрокаПорядокОплаты.Строки.ВыгрузитьКолонку("КВычету_НДС");
					КРаспределениюНДС = СтрокаПорядокОплаты.Строки.Итог("КВычету_НДС");
					КРаспределениюНДС = Окр(КРаспределениюНДС * СтрокаОплаты.СуммаОплаты /СтрокаПорядокОплаты.Строки.Итог("КВычету_БезНДС"),2);
					КРаспределениюСНДС = Окр(СтрокаОплаты.СуммаОплаты * СтрокаПорядокОплаты.Строки.Итог("КВычету_СНДС")/СтрокаПорядокОплаты.Строки.Итог("КВычету_БезНДС"),2);
					Если КРаспределениюСНДС > 0 Тогда
						МассивРезультата_СНДС = ОбщегоНазначения.РаспределитьПропорционально(КРаспределениюСНДС ,СтрокаПорядокОплаты.Строки.ВыгрузитьКолонку("КВычету_СНДС"));
					КонецЕсли;
				ИначеЕсли СтрокаПорядокОплаты.ПорядокОплаты = 3 и СтрокаПорядокОплаты.ОпределенаДоля_119ФЗ_2_10 и НЕ ЗначениеЗаполнено(СтрокаОплаты.ДокументОплаты)Тогда
					МассивБазиса =  СтрокаПорядокОплаты.Строки.ВыгрузитьКолонку("КВычету_СНДС_Часть");
					КРаспределениюНДС = 0;
					Массив_НДС =  СтрокаПорядокОплаты.Строки.ВыгрузитьКолонку("КВычету_НДС_Часть");
					Для НомерЭлемента = 0 по Массив_НДС.Количество()-1 Цикл
						КРаспределениюНДС = КРаспределениюНДС + Массив_НДС[НомерЭлемента];
					КонецЦикла; 
					Если СтрокаПорядокОплаты.Строки.Итог("КВычету_СНДС_Часть") = 0 Тогда
						КРаспределениюНДС = 0;
						Продолжить;
					иначе
						КРаспределениюНДС = Окр(КРаспределениюНДС * СтрокаОплаты.СуммаОплаты /СтрокаПорядокОплаты.Строки.Итог("КВычету_СНДС_Часть"),2);
					КонецЕсли; 
					
				Иначе	
					МассивБазиса =  СтрокаПорядокОплаты.Строки.ВыгрузитьКолонку("КВычету_СНДС");
					КРаспределениюНДС = Окр(СтрокаОплаты.СуммаОплаты * СтрокаПорядокОплаты.Строки.Итог("КВычету_НДС")/СтрокаПорядокОплаты.Строки.Итог("КВычету_СНДС"),2);
					Массив_НДС = СтрокаПорядокОплаты.Строки.ВыгрузитьКолонку("КВычету_НДС");
				КонецЕсли; 
				
				МассивРезультата = ОбщегоНазначения.РаспределитьПропорционально(СтрокаОплаты.СуммаОплаты,МассивБазиса);
				
				Если (СтрокаПорядокОплаты.ПорядокОплаты = 1 
					Или СтрокаПорядокОплаты.ПорядокОплаты = 5
					Или СтрокаПорядокОплаты.ПорядокОплаты = 6)
					И КРаспределениюСНДС > 0 Тогда
					МассивРезультата = МассивРезультата_СНДС;
				КонецЕсли; 
				
				Если КРаспределениюНДС <> 0 Тогда
					МассивРезультата_НДС = ОбщегоНазначения.РаспределитьПропорционально(КРаспределениюНДС, Массив_НДС);
				Иначе
					МассивРезультата_НДС = Новый Массив(Массив_НДС.Количество());
				КонецЕсли; 
				
				НомерСтроки = 0;
				Для Каждого СтрокаЗадолжености Из СтрокаПорядокОплаты.Строки Цикл
					Если МассивРезультата[НомерСтроки] = 0 И ?(КРаспределениюНДС = 0,Истина, МассивРезультата_НДС[НомерСтроки] = 0) Тогда
						НомерСтроки = НомерСтроки+1;
						Продолжить;
					КонецЕсли; 
					СтрокаРезультата = ТаблицаРезультатов.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаРезультата, СтрокаЗадолжености);

					СтрокаРезультата.ДокументОплаты	= СтрокаОплаты.ДокументОплаты;
					Если ПредъявленНДСКВычету0 Тогда
						СтрокаРезультата.ДокументОтгрузки	= СтрокаЗадолжености.ДокументОтгрузки;
						СтрокаРезультата.Состояние	= СтрокаЗадолжености.Состояние;
					КонецЕсли; 
					
					СтрокаРезультата.ДатаОплаты		= СтрокаОплаты.ДатаОплаты;
					
					СтрокаРезультата.СуммаБезНДС	= МассивРезультата[НомерСтроки]- ?(КРаспределениюНДС = 0, 0, МассивРезультата_НДС[НомерСтроки]);
					СтрокаРезультата.НДС			= ?(СтрокаПорядокОплаты.ПорядокОплаты = 1 Или СтрокаПорядокОплаты.ПорядокОплаты = 5 Или СтрокаПорядокОплаты.ПорядокОплаты = 6,МассивРезультата_НДС[НомерСтроки], МассивРезультата[НомерСтроки]- СтрокаРезультата.СуммаБезНДС);
					
					Если (ПредъявленНДСКВычету0 И СтрокаРезультата.Состояние = Перечисления.НДССостоянияРеализация0.НеПодтвержденаРеализация0) 
						Или ((СтрокаРезультата.СуммаБезНДС < 0 Или СтрокаРезультата.НДС < 0) И СтрокаЗадолжености.СчетФактураДата >= '20060101') Тогда
						Если Дата >= '20060530' Тогда
							Если НДСНалоговыйПериод = Неопределено Тогда
								НДСНалоговыйПериод = УчетНДС.ПолучитьУПНДСНалоговыйПериод(Организация, Дата);
							КонецЕсли;
							СтрокаРезультата.ЗаписьДополнительногоЛиста = Истина;
							Если ПредъявленНДСКВычету0 Тогда
								СтрокаРезультата.КорректируемыйПериод = СтрокаЗадолжености.ДокументОтгрузки.Дата;
							Иначе 
								СтрокаРезультата.КорректируемыйПериод = СтрокаЗадолжености.СчетФактураДата;
							КонецЕсли;
							Если ?(НДСНалоговыйПериод = Перечисления.Периодичность.Месяц, 
									КонецМесяца(СтрокаРезультата.КорректируемыйПериод) = КонецМесяца(Дата),
									КонецКвартала(СтрокаРезультата.КорректируемыйПериод) = КонецКвартала(Дата)) Тогда
								СтрокаРезультата.ЗаписьДополнительногоЛиста = Ложь;
								СтрокаРезультата.КорректируемыйПериод = '00010101';
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
						
					СтрокаЗадолжености.КВычету_БезНДС = СтрокаЗадолжености.КВычету_БезНДС - СтрокаРезультата.СуммаБезНДС;
					СтрокаЗадолжености.КВычету_НДС = СтрокаЗадолжености.КВычету_НДС - СтрокаРезультата.НДС;
					СтрокаЗадолжености.КВычету_СНДС = СтрокаЗадолжености.КВычету_СНДС - (СтрокаРезультата.СуммаБезНДС + СтрокаРезультата.НДС);
					Если СтрокаПорядокОплаты.ПорядокОплаты = 3 и СтрокаПорядокОплаты.ОпределенаДоля_119ФЗ_2_10 Тогда
						СтрокаЗадолжености.КВычету_НДС_Часть = СтрокаЗадолжености.КВычету_НДС_Часть - СтрокаРезультата.НДС;
						СтрокаЗадолжености.КВычету_СНДС_Часть = СтрокаЗадолжености.КВычету_СНДС_Часть - (СтрокаРезультата.СуммаБезНДС + СтрокаРезультата.НДС);
					КонецЕсли; 
					
					НомерСтроки = НомерСтроки+1;
				КонецЦикла; 
				
			КонецЦикла; 
			
			
			
		КонецЦикла; 
		
	
	КонецЦикла; 
	
	РаспределенныеОплаты = ТаблицаИсточникаПостроителя.Скопировать();

КонецПроцедуры // РаспределитьОплатыПоДеревуСФ()

// Процедура из процедуры ЗаполнитьСтроки_ВычетПоПриобретеннымЦенностям.
// Корректирует сумму, доступную к вычету исходя из положений преходного периода,
// установленных в п.10 статьи 2 федерального закона №119-ФЗ
Процедура ОтработкаПоложенииПереходногоПериода2006_ОткорректироватьСуммуДоступнуюКВычету(Дерево_НДСкВычету)
	
	Если Месяц(дата) > 6 Тогда
		Возврат;
	КонецЕсли;
	
	// Определение списка СФ (с аналитикой) с датой ранее 2006 года, по которым НДС был предъявлен к вычету в текущем месяце
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОстаткиНаНачалоГода.СчетФактура КАК СчетФактура,
	|	ОстаткиНаНачалоГода.ВидЦенности,
	|	ОстаткиНаНачалоГода.СтавкаНДС,
	|	ОстаткиНаНачалоГода.СчетУчетаНДС,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(НДСЗаписиКнигиПокупокОбороты.СуммаБезНДСОборот, 0) > 0
	|			ТОГДА НДСЗаписиКнигиПокупокОбороты.СуммаБезНДСОборот
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаБезНДС_ПринятоКВычетуВТекущемМесяце,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(НДСЗаписиКнигиПокупокОбороты.НДСОборот, 0) > 0
	|			ТОГДА НДСЗаписиКнигиПокупокОбороты.НДСОборот
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК НДС_ПринятоКВычетуВТекущемМесяце,
	|	ВЫБОР
	|		КОГДА ОстаткиНаНачалоГода.СуммаБезНДСНаНачалоГода_ЕжемесячнаяДоля > 0
	|			ТОГДА ОстаткиНаНачалоГода.СуммаБезНДСНаНачалоГода_ЕжемесячнаяДоля
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаБезНДСНаНачалоГода_ЕжемесячнаяДоля,
	|	ВЫБОР
	|		КОГДА ОстаткиНаНачалоГода.НДСНаНачалоГода_ЕжемесячнаяДоля > 0
	|			ТОГДА ОстаткиНаНачалоГода.НДСНаНачалоГода_ЕжемесячнаяДоля
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК НДСНаНачалоГода_ЕжемесячнаяДоля
	|ИЗ
	|	(ВЫБРАТЬ
	|		НДСПредъявленныйОстатки.Организация КАК Организация,
	|		НДСПредъявленныйОстатки.СчетФактура КАК СчетФактура,
	|		НДСПредъявленныйОстатки.ВидЦенности КАК ВидЦенности,
	|		НДСПредъявленныйОстатки.СтавкаНДС КАК СтавкаНДС,
	|		НДСПредъявленныйОстатки.СчетУчетаНДС КАК СчетУчетаНДС,
	|		НДСПредъявленныйОстатки.Поставщик КАК Поставщик,
	|		НДСПредъявленныйОстатки.СуммаБезНДСОстаток / 6 КАК СуммаБезНДСНаНачалоГода_ЕжемесячнаяДоля,
	|		НДСПредъявленныйОстатки.НДСОстаток / 6 КАК НДСНаНачалоГода_ЕжемесячнаяДоля
	|	ИЗ
	|		РегистрНакопления.НДСПредъявленный.Остатки(
	|			&НачалоГода2006,
	|			Организация = &Организация
	|			    И СчетФактура.Дата < &НачалоГода2006
	|			    И (НЕ ВидЦенности В (&ИсключаемыеВидыЦенностей))) КАК НДСПредъявленныйОстатки) КАК ОстаткиНаНачалоГода
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДСЗаписиКнигиПокупок.Обороты(
	|		&НачалоМесяца,
	|		&КонецМесяца,
	|		Период,
	|		Организация = &Организация
	|		    И СчетФактура.Дата < &НачалоГода2006
	|		    И (НЕ ВидЦенности В (&ИсключаемыеВидыЦенностей))
	|		    И Событие = &ОтбираемоеСобытие) КАК НДСЗаписиКнигиПокупокОбороты
	|		ПО НДСЗаписиКнигиПокупокОбороты.Организация = ОстаткиНаНачалоГода.Организация
	|			И НДСЗаписиКнигиПокупокОбороты.Поставщик = ОстаткиНаНачалоГода.Поставщик
	|			И НДСЗаписиКнигиПокупокОбороты.СчетФактура = ОстаткиНаНачалоГода.СчетФактура
	|			И НДСЗаписиКнигиПокупокОбороты.ВидЦенности = ОстаткиНаНачалоГода.ВидЦенности
	|			И НДСЗаписиКнигиПокупокОбороты.СтавкаНДС = ОстаткиНаНачалоГода.СтавкаНДС
	|			И НДСЗаписиКнигиПокупокОбороты.СчетУчетаНДС = ОстаткиНаНачалоГода.СчетУчетаНДС
	|ГДЕ
	|	(ОстаткиНаНачалоГода.СуммаБезНДСНаНачалоГода_ЕжемесячнаяДоля > 0
	|			ИЛИ ОстаткиНаНачалоГода.НДСНаНачалоГода_ЕжемесячнаяДоля > 0)
	|ИТОГИ
	|	СУММА(СуммаБезНДС_ПринятоКВычетуВТекущемМесяце),
	|	СУММА(НДС_ПринятоКВычетуВТекущемМесяце),
	|	СУММА(СуммаБезНДСНаНачалоГода_ЕжемесячнаяДоля),
	|	СУММА(НДСНаНачалоГода_ЕжемесячнаяДоля)
	|ПО
	|	СчетФактура";
	
	Запрос.УстановитьПараметр("Организация",  Организация);
	Запрос.УстановитьПараметр("НачалоМесяца", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("КонецМесяца",  КонецМесяца(Дата));
	Запрос.УстановитьПараметр("НачалоГода2006", '20060101');
	Запрос.УстановитьПараметр("ОтбираемоеСобытие", Перечисления.СобытияПоНДСПокупки[?(ПредъявленНДСКВычету0,"ПредъявленНДСКВычету0","ПредъявленНДСКВычету")]);
	
	// Исключаемые из анализа виды ценностей
	ИсключаемыеВидыЦенностей = Новый СписокЗначений;
	ИсключаемыеВидыЦенностей.Добавить(Перечисления.ВидыЦенностей.АвансыПолученные);
	ИсключаемыеВидыЦенностей.Добавить(Перечисления.ВидыЦенностей.АвансыПолученные0);
	ИсключаемыеВидыЦенностей.Добавить(Перечисления.ВидыЦенностей.НалоговыйАгентАренда);
	ИсключаемыеВидыЦенностей.Добавить(Перечисления.ВидыЦенностей.НалоговыйАгентРеализацияИмущества);
	ИсключаемыеВидыЦенностей.Добавить(Перечисления.ВидыЦенностей.НалоговыйАгентИностранцы);
	ИсключаемыеВидыЦенностей.Добавить(Перечисления.ВидыЦенностей.ВнутреннееПотребление);
	ИсключаемыеВидыЦенностей.Добавить(Перечисления.ВидыЦенностей.СМРСобственнымиСилами);
	ИсключаемыеВидыЦенностей.Добавить(Перечисления.ВидыЦенностей.СМРПодрядные);
	ИсключаемыеВидыЦенностей.Добавить(Перечисления.ВидыЦенностей.ТаможенныеПлатежи);
	ИсключаемыеВидыЦенностей.Добавить(Перечисления.ВидыЦенностей.ТаможенныеПлатежиОС);
	ИсключаемыеВидыЦенностей.Добавить(Перечисления.ВидыЦенностей.Возврат);
	
	Запрос.УстановитьПараметр("ИсключаемыеВидыЦенностей", ИсключаемыеВидыЦенностей);
	
	НаличиеОстатковНаНачалоГода = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Для каждого СтрокаСФ Из Дерево_НДСкВычету.Строки Цикл
		Если СтрокаСФ.СчетФактураДата<'20060101' тогда
			СтрокаОстатковНаНачалоГода_СчетФактура = НаличиеОстатковНаНачалоГода.Строки.НайтиСтроки(Новый Структура("СчетФактура",СтрокаСФ.СчетФактура));
			Если СтрокаОстатковНаНачалоГода_СчетФактура.Количество() = 0 Тогда
				// Остатки по указанному счету-фактуре на начло года не обнаружены.
				// Расчет доли к вычету (ограничение) не производится.
				Продолжить;
			КонецЕсли; 
			СтрокаОстатковНаНачалоГода_СчетФактура = СтрокаОстатковНаНачалоГода_СчетФактура[0];
			
			
			КВычетуПоСФ_СНДС = Макс(0,СтрокаОстатковНаНачалоГода_СчетФактура.СуммаБезНДСНаНачалоГода_ЕжемесячнаяДоля + СтрокаОстатковНаНачалоГода_СчетФактура.НДСНаНачалоГода_ЕжемесячнаяДоля);
			Если (СтрокаОстатковНаНачалоГода_СчетФактура.СуммаБезНДС_ПринятоКВычетуВТекущемМесяце+ СтрокаОстатковНаНачалоГода_СчетФактура.НДС_ПринятоКВычетуВТекущемМесяце) > 0 Тогда
				КВычетуПоСФ_СНДС = макс(0,КВычетуПоСФ_СНДС - (СтрокаОстатковНаНачалоГода_СчетФактура.СуммаБезНДС_ПринятоКВычетуВТекущемМесяце+ СтрокаОстатковНаНачалоГода_СчетФактура.НДС_ПринятоКВычетуВТекущемМесяце));
			КонецЕсли; 
			
			Если КВычетуПоСФ_СНДС = 0 Тогда
				// Доля определялась, но вся сумма, доступная к вычету в текущем месяце, уже принята.
				Продолжить;
			КонецЕсли; 
			
			Для каждого СтрокаПорядокОплаты  Из СтрокаСФ.Строки Цикл
				Если СтрокаПорядокОплаты.ПорядокОплаты = 3 Тогда
					СтрокаПорядокОплаты.ОпределенаДоля_119ФЗ_2_10 = истина;
					Для каждого СтрокаТаблицы из СтрокаПорядокОплаты.Строки Цикл
						СтрокаОстатковНаНачалоГода_Расшифровка = СтрокаОстатковНаНачалоГода_СчетФактура.Строки.НайтиСтроки(Новый структура("ВидЦенности, СтавкаНДС, СчетУчетаНДС",СтрокаТаблицы.ВидЦенности,СтрокаТаблицы.СтавкаНДС,СтрокаТаблицы.СчетУчетаНДС));
						
						Если СтрокаОстатковНаНачалоГода_Расшифровка.Количество() =0 Тогда
							// Остатки в указанном разрезе аналитики не обнаружены.
							// Расчет доли к вычету (ограничение) не производится.
							СтрокаТаблицы.КВычету_СНДС_Часть = СтрокаТаблицы.КВычету_СНДС;
							СтрокаТаблицы.КВычету_НДС_Часть = СтрокаТаблицы.КВычету_НДС;
							Продолжить;
						Иначе
							СтрокаОстатковНаНачалоГода_Расшифровка = СтрокаОстатковНаНачалоГода_Расшифровка[0];
						КонецЕсли;
						
						СтрокаТаблицы.ОпределенаДоля_119ФЗ_2_10 = истина;
						
						КВычету_СНДС = Макс(0,СтрокаОстатковНаНачалоГода_Расшифровка.СуммаБезНДСНаНачалоГода_ЕжемесячнаяДоля + СтрокаОстатковНаНачалоГода_Расшифровка.НДСНаНачалоГода_ЕжемесячнаяДоля);
						Если (СтрокаОстатковНаНачалоГода_Расшифровка.СуммаБезНДС_ПринятоКВычетуВТекущемМесяце+ СтрокаОстатковНаНачалоГода_Расшифровка.НДС_ПринятоКВычетуВТекущемМесяце) > 0 Тогда
							КВычету_СНДС = макс(0,КВычету_СНДС - (СтрокаОстатковНаНачалоГода_Расшифровка.СуммаБезНДС_ПринятоКВычетуВТекущемМесяце+ СтрокаОстатковНаНачалоГода_Расшифровка.НДС_ПринятоКВычетуВТекущемМесяце));
						КонецЕсли; 
						
						Если КВычету_СНДС = 0 Тогда
							// Доля определялась, но вся сумма, доступная к вычету в текущем месяце, уже принята.
							Продолжить;
						КонецЕсли; 
						
						КВычету_НДС = СтрокаОстатковНаНачалоГода_Расшифровка.НДСНаНачалоГода_ЕжемесячнаяДоля;
						Если (СтрокаОстатковНаНачалоГода_Расшифровка.НДС_ПринятоКВычетуВТекущемМесяце) > 0 Тогда
							КВычету_НДС = макс(0,КВычету_НДС - (СтрокаОстатковНаНачалоГода_Расшифровка.НДС_ПринятоКВычетуВТекущемМесяце));
						КонецЕсли; 
						
						//Расчет суммы без НДС
						Если СтрокаТаблицы.ВидЦенности = Перечисления.ВидыЦенностей.СМРПодрядные Тогда
							// СМР подрядные отрабатываются в соответствии со ст.3 п.2 №119-ФЗ, а не с положениями ст.2 п.10 №119-ФЗ.
							// Суммы к вычету разблокируется поэтапно (1/12) с использование специального документа.
							// Пересчет сумм при формировании записей книги покупок не требуется.
							СтрокаТаблицы.КВычету_СНДС_Часть = СтрокаТаблицы.КВычету_СНДС;
							СтрокаТаблицы.КВычету_НДС_Часть = СтрокаТаблицы.КВычету_НДС;
							
						Иначе
							СтрокаТаблицы.КВычету_СНДС_Часть = Макс(Мин(КВычету_СНДС,СтрокаТаблицы.КВычету_СНДС),0);
							СтрокаТаблицы.КВычету_НДС_Часть = Макс(Мин(КВычету_НДС,СтрокаТаблицы.КВычету_НДС),0);
						КонецЕсли; 
						
					КонецЦикла;
					
					СтрокаПорядокОплаты.КВычету_НДС_Часть		= СтрокаПорядокОплаты.Строки.Итог("КВычету_НДС_Часть");
					СтрокаПорядокОплаты.КВычету_СНДС_Часть		= СтрокаПорядокОплаты.Строки.Итог("КВычету_СНДС_Часть");
					
				КонецЕсли; 
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры
 
// Заполнение табличной части "Вычет НДС по приобретенным ценностям"
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// Заполнение таблиячной части "Вычет НДС с полученных авансов"

// Процедура выполняет автоматическое заполнение табличной части документа
// Вызывается из процедуры КоманднаяПанельНДСсАвансовЗаполнить
//
Процедура ЗаполнитьСтроки_НДСсАвансов(ОтменитьПроведение = Ложь, Сообщать = Истина, СтрокаСообщения = "", ОшибкаЗаполнения = Ложь, УдалятьНезаполненные = Ложь) Экспорт
	
	Если Проведен Тогда
		Если ОтменитьПроведение Тогда
			Записать(РежимЗаписиДокумента.ОтменаПроведения);
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	УчетнаяПолитикаНУ = ОбщегоНазначения.ПолучитьПараметрыУчетнойПолитикиРегл(Дата, Организация);
	Если НЕ ЗначениеЗаполнено(УчетнаяПолитикаНУ) Тогда
		ОшибкаЗаполнения = Истина;
	КонецЕсли; 
	Если ОшибкаЗаполнения Тогда
		СтрокаСообщения = "Не указаны параметры учетной политики ("+СокрЛП(Организация)+") на " + Формат(Дата, "ДЛФ=DD") + Символы.ПС 
						+ "Табличное поле «Вычет НДС по приобретенным ценностям» не может быть заполнено автоматически.";
		Если Сообщать Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Документ не заполнен:"+СтрокаСообщения,,Строка(Ссылка));
		КонецЕсли; 
		Возврат;
	КонецЕсли;

	ТаблицаРезультатов = НДСсАвансов.ВыгрузитьКолонки();
	
	Если ПредъявленНДСКВычету0 Тогда
		ЗаполнитьНДСсАвансовПоДаннымРегистраНДСсАвансовИНДСПредъявленныйРеализация0(ТаблицаРезультатов);

	Иначе	
		Дерево_НДСсАвансов = ЗаполнитьНДСсАвансовПоДаннымРегистраНДСсАвансов();
		Если Дерево_НДСсАвансов.Строки.Количество() = 0 Тогда
			// Дальнейшая обработка не требуется, не обнаружен НДС, который может быть принят к вычету.
			НДСсАвансов.Очистить();
			ЗаполнитьЗачетАвансовПокупателейПоКомиссии();
            Возврат;
		КонецЕсли;
		
		СписокСчетовФактур = ОбщегоНазначения.УдалитьПовторяющиесяЭлементыМассива(Дерево_НДСсАвансов.Строки.ВыгрузитьКолонку("СчетФактура"),Истина);
		
		ТаблицаЗачетовАвансов = ПолучитьДанныеОЗачтенныхАвансахПоСпискуСФ(СписокСчетовФактур);
		
		РаспределитьЗачетыАвансаПоСФ(Дерево_НДСсАвансов, ТаблицаРезультатов,СписокСчетовФактур,ТаблицаЗачетовАвансов);
	КонецЕсли; 
	
	СтрокиКУдалению = ТаблицаРезультатов.НайтиСтроки(Новый Структура("СуммаБезНДС, НДС, ВалютнаяСумма", 0, 0, 0));
	
	Для каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
		ТаблицаРезультатов.Удалить(СтрокаКУдалению);
	КонецЦикла;

	// Удаление строк, в которых есть незаполненные обязательные поля
	Если УдалятьНезаполненные Тогда
		
		ОбязательныеПоля = "Покупатель, ДоговорКонтрагента, СчетФактура, СтавкаНДС";
		Если ПредъявленНДСКВычету0 Тогда
			ОбязательныеПоля = ОбязательныеПоля + ", Состояние";
		КонецЕсли;
		ОбработкаТабличныхЧастейСервер.УдалитьНезаполненныеСтроки(ТаблицаРезультатов, ОбязательныеПоля);
		
	КонецЕсли;
	
	ПолучитьДанныеОДокументахОплатыВозвратАванса(ТаблицаРезультатов);
	
	НДСсАвансов.Загрузить(ТаблицаРезультатов);
	
	Если НЕ ПредъявленНДСКВычету0 Тогда
		ЗаполнитьЗачетАвансовПокупателейПоКомиссии();
	КонецЕсли;	
	
КонецПроцедуры // ЗаполнитьСтрокиДокумента()

// Вызывается из процедуры ЗаполнитьСтроки_НДСсАвансов.
// Заполняет ТЧ ВычетПоПриобретеннымЦенностям по данным регистра НДСПредъявленный
Функция ЗаполнитьНДСсАвансовПоДаннымРегистраНДСсАвансов()

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НДСсАвансовОстатки.СчетФактура КАК СчетФактура,
	|	НДСсАвансовОстатки.СтавкаНДС,
	|	НДСсАвансовОстатки.СчетФактура.Дата КАК СчетФактураДата,
	|	НДСсАвансовОстатки.Покупатель,
	|	НДСсАвансовОстатки.ДоговорКонтрагента,
	|	НДСсАвансовОстатки.ИсправленныйСчетФактура КАК ИсправленныйСчетФактура,
	|	ВЫБОР
	|		КОГДА НДСсАвансовОстатки.ДоговорКонтрагента.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.СКомиссионером)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ДоговорСКомиссионером,
	|	НДСсАвансовОстатки.ВалютаАванса,
	|	СУММА(ВЫБОР
	|			КОГДА ЕСТЬNULL(НДСсАвансовПоРеализации0.СуммаБезНДСОстаток, 0) > НДСсАвансовОстатки.СуммаБезНДСОстаток
	|				ТОГДА 0
	|			ИНАЧЕ НДСсАвансовОстатки.СуммаБезНДСОстаток - ЕСТЬNULL(НДСсАвансовПоРеализации0.СуммаБезНДСОстаток, 0)
	|		КОНЕЦ) КАК СуммаБезНДС,
	|	СУММА(ВЫБОР
	|			КОГДА ЕСТЬNULL(НДСсАвансовПоРеализации0.НДСОстаток, 0) > НДСсАвансовОстатки.НДСОстаток
	|				ТОГДА 0
	|			ИНАЧЕ НДСсАвансовОстатки.НДСОстаток - ЕСТЬNULL(НДСсАвансовПоРеализации0.НДСОстаток, 0)
	|		КОНЕЦ) КАК НДС,
	|	СУММА(ВЫРАЗИТЬ((ВЫБОР
	|				КОГДА ЕСТЬNULL(НДСсАвансовПоРеализации0.СуммаБезНДСОстаток, 0) > НДСсАвансовОстатки.СуммаБезНДСОстаток
	|					ТОГДА 0
	|				ИНАЧЕ НДСсАвансовОстатки.СуммаБезНДСОстаток - ЕСТЬNULL(НДСсАвансовПоРеализации0.СуммаБезНДСОстаток, 0)
	|			КОНЕЦ + ВЫБОР
	|				КОГДА ЕСТЬNULL(НДСсАвансовПоРеализации0.НДСОстаток, 0) > НДСсАвансовОстатки.НДСОстаток
	|					ТОГДА 0
	|				ИНАЧЕ НДСсАвансовОстатки.НДСОстаток - ЕСТЬNULL(НДСсАвансовПоРеализации0.НДСОстаток, 0)
	|			КОНЕЦ) * НДСсАвансовОстатки.ВалютнаяСуммаСНДСОстаток / ВЫБОР
	|				КОГДА НДСсАвансовОстатки.СуммаБезНДСОстаток + НДСсАвансовОстатки.НДСОстаток > 0
	|					ТОГДА НДСсАвансовОстатки.СуммаБезНДСОстаток + НДСсАвансовОстатки.НДСОстаток
	|				ИНАЧЕ 1
	|			КОНЕЦ КАК ЧИСЛО(15, 2))) КАК ВалютнаяСуммаСНДС
	|ИЗ
	|	РегистрНакопления.НДСсАвансов.Остатки(
	|			&ДатаГраница,
	|			Организация = &Организация
	|				И ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.АвансыПолученные)) КАК НДСсАвансовОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			НДСПредъявленныйРеализация0Остатки.СчетФактура КАК СчетФактура,
	|			НДСПредъявленныйРеализация0Остатки.СтавкаНДС КАК СтавкаНДС,
	|			НДСПредъявленныйРеализация0Остатки.СуммаБезНДСОстаток КАК СуммаБезНДСОстаток,
	|			НДСПредъявленныйРеализация0Остатки.НДСОстаток КАК НДСОстаток,
	|			НДСПредъявленныйРеализация0Остатки.ДокументОтгрузки.Контрагент КАК Покупатель,
	|			НДСПредъявленныйРеализация0Остатки.ДокументОтгрузки.ДоговорКонтрагента КАК ДоговорКонтрагента
	|		ИЗ
	|			РегистрНакопления.НДСПредъявленныйРеализация0.Остатки(
	|					&ДатаГраница,
	|					Организация = &Организация
	|						И ВидЦенности В (&ВидыЦенностейАванс)) КАК НДСПредъявленныйРеализация0Остатки) КАК НДСсАвансовПоРеализации0
	|		ПО НДСсАвансовОстатки.СчетФактура = НДСсАвансовПоРеализации0.СчетФактура
	|			И НДСсАвансовОстатки.СтавкаНДС = НДСсАвансовПоРеализации0.СтавкаНДС
	|			И НДСсАвансовОстатки.Покупатель = НДСсАвансовПоРеализации0.Покупатель
	|			И НДСсАвансовОстатки.ДоговорКонтрагента = НДСсАвансовПоРеализации0.ДоговорКонтрагента
	|ГДЕ
	|	(НДСсАвансовОстатки.СуммаБезНДСОстаток > 0
	|			ИЛИ НДСсАвансовОстатки.НДСОстаток > 0)
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСсАвансовОстатки.СчетФактура,
	|	НДСсАвансовОстатки.СтавкаНДС,
	|	НДСсАвансовОстатки.СчетФактура.Дата,
	|	НДСсАвансовОстатки.ВалютаАванса,
	|	НДСсАвансовОстатки.ДоговорКонтрагента,
	|	ВЫБОР
	|		КОГДА НДСсАвансовОстатки.ДоговорКонтрагента.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.СКомиссионером)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	НДСсАвансовОстатки.Покупатель,
	|	НДСсАвансовПоРеализации0.СуммаБезНДСОстаток,
	|	НДСсАвансовПоРеализации0.НДСОстаток,
	|	НДСсАвансовОстатки.ИсправленныйСчетФактура
	|
	|УПОРЯДОЧИТЬ ПО
	|	СчетФактураДата
	|ИТОГИ
	|	МАКСИМУМ(ДоговорСКомиссионером)
	|ПО
	|	СчетФактура";

	Запрос.УстановитьПараметр("Организация",  Организация);
	Запрос.УстановитьПараметр("Дата", КонецДня(Дата));
	Запрос.УстановитьПараметр("ДатаГраница", новый Граница(КонецДня(Дата),ВидГраницы.Включая));
	
	// Анализируемые виды ценностей
	ВидыЦенностейАванс = Новый СписокЗначений;
	ВидыЦенностейАванс.Добавить(Перечисления.ВидыЦенностей.АвансыПолученные);
	ВидыЦенностейАванс.Добавить(Перечисления.ВидыЦенностей.АвансыПолученные0);
	ВидыЦенностейАванс.Добавить(Перечисления.ВидыЦенностей.ВозвратАвансовПолученных);
	
	Запрос.УстановитьПараметр("ВидыЦенностейАванс", ВидыЦенностейАванс);
    	
	Дерево_НДСсАвансов = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Если Дерево_НДСсАвансов.Строки.Количество() = 0 тогда 
		Возврат Дерево_НДСсАвансов;
	КонецЕсли;
	
	СписокСчетовФактур = ОбщегоНазначения.УдалитьПовторяющиесяЭлементыМассива(Дерево_НДСсАвансов.Строки.ВыгрузитьКолонку("СчетФактура"),Истина);

	// Получим остатки по авансам для определения суммы незачтенного аванса.
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОстатки.Документ КАК ДокументОплаты,
	|	РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОстатки.ДоговорКонтрагента.ВалютаВзаиморасчетов КАК ВалютаРасчетов,
	|	СУММА(РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОстатки.СуммаРегОстаток) КАК Сумма,
	|	СУММА(ВЫБОР
	|			КОГДА ЕСТЬNULL(РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОстатки.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах, ЛОЖЬ)
	|				ТОГДА РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОстатки.СуммаРегОстаток
	|			ИНАЧЕ РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОстатки.СуммаВзаиморасчетовОстаток
	|		КОНЕЦ) КАК ВалютнаяСумма,
	|	ЕСТЬNULL(РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОстатки.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах, ЛОЖЬ) КАК РасчетыВУсловныхЕдиницах,
	|	РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОстатки.Контрагент КАК Покупатель,
	|	РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОстатки.ДоговорКонтрагента,
	|	ЛОЖЬ Как ДоговорСКомиссионером
	|ИЗ
	|	РегистрНакопления.РасчетыПоРеализацииВУсловныхЕдиницахОрганизации.Остатки(
	|			&ДатаГраница,
	|			Организация = &Организация
	|				И Документ В (&СписокСчетовФактур)) КАК РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОстатки
	|ГДЕ
	|	ВЫБОР
	|			КОГДА ЕСТЬNULL(РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОстатки.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах, ЛОЖЬ)
	|				ТОГДА РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОстатки.СуммаРегОстаток
	|			ИНАЧЕ РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОстатки.СуммаВзаиморасчетовОстаток
	|		КОНЕЦ > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОстатки.Документ,
	|	РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОстатки.ДоговорКонтрагента.ВалютаВзаиморасчетов,
	|	РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОстатки.Контрагент,
	|	РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОстатки.ДоговорКонтрагента,
	|	ЕСТЬNULL(РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОстатки.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах, ЛОЖЬ)
	|ИТОГИ ПО
	|	ДокументОплаты";
	
	Запрос.УстановитьПараметр("Дата", 				КонецДня(Дата));
	Запрос.УстановитьПараметр("Организация", 		Организация);
	Запрос.УстановитьПараметр("СписокСчетовФактур", СписокСчетовФактур);
	Запрос.УстановитьПараметр("ДатаГраница", новый Граница(КонецДня(Дата),ВидГраницы.Включая));
	
	НепогашенныеАвансы = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Для каждого НепогашенныйАвансПоСФ Из НепогашенныеАвансы.Строки Цикл
		ЗафиксированныйАвансПоСФ = Дерево_НДСсАвансов.Строки.Найти(НепогашенныйАвансПоСФ.ДокументОплаты,"СчетФактура");
		Если ЗафиксированныйАвансПоСФ = Неопределено Тогда
		    Продолжить;
		КонецЕсли; 
		
		Для каждого СтрокаЗафиксированногоАванса Из НепогашенныйАвансПоСФ.Строки Цикл
			
			ВалютаАванса = ?(СтрокаЗафиксированногоАванса.РасчетыВУсловныхЕдиницах или НЕ ЗначениеЗаполнено(СтрокаЗафиксированногоАванса.ВалютаРасчетов),мВалютаРегламентированногоУчета,СтрокаЗафиксированногоАванса.ВалютаРасчетов);
			
			Отбор = Новый Структура("Покупатель,ДоговорКонтрагента, ВалютаАванса, ДоговорСКомиссионером", СтрокаЗафиксированногоАванса.Покупатель, СтрокаЗафиксированногоАванса.ДоговорКонтрагента, ВалютаАванса, Ложь);
			
			СтрокиАвансаПоОтбору = ЗафиксированныйАвансПоСФ.Строки.НайтиСтроки(Отбор);
			Если СтрокиАвансаПоОтбору.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли; 
			
			МассивСуммаБезНДС = новый Массив();
			МассивСуммаНДС = новый Массив();
			МассивВалютнаяСуммаСНДС = новый Массив();
			
			СуммаБезНДС = 0;
			СуммаНДС = 0;
			ВалютнаяСуммаСНДС = 0;
			Для каждого СтрокаАвансаПоОтбору Из СтрокиАвансаПоОтбору Цикл
				МассивСуммаБезНДС.Добавить(СтрокаАвансаПоОтбору.СуммаБезНДС);
				МассивСуммаНДС.Добавить(СтрокаАвансаПоОтбору.НДС);
				МассивВалютнаяСуммаСНДС.Добавить(СтрокаАвансаПоОтбору.ВалютнаяСуммаСНДС);
				
				СуммаБезНДС = СуммаБезНДС + СтрокаАвансаПоОтбору.СуммаБезНДС;
				СуммаНДС = СуммаНДС + СтрокаАвансаПоОтбору.НДС;
				ВалютнаяСуммаСНДС = ВалютнаяСуммаСНДС + СтрокаАвансаПоОтбору.ВалютнаяСуммаСНДС
			КонецЦикла; 
			
			Если ВалютнаяСуммаСНДС = 0 Тогда
				Продолжить;
			КонецЕсли; 
			
			Если СтрокаЗафиксированногоАванса.ВалютаРасчетов = мВалютаРегламентированногоУчета или 
				НЕ ЗначениеЗаполнено(СтрокаЗафиксированногоАванса.ВалютаРасчетов) Тогда
				// Сопоставление по рублевой сумме
				ВалютнаяСуммаСНДС = Окр(ВалютнаяСуммаСНДС*Мин(СтрокаЗафиксированногоАванса.Сумма, (СуммаБезНДС+СуммаНДС))/(СуммаБезНДС+СуммаНДС), 2);
				СуммаСНДС = Мин(СтрокаЗафиксированногоАванса.Сумма, (СуммаБезНДС+СуммаНДС));
				Если СуммаСНДС = 0 Тогда
					Продолжить;
				КонецЕсли;
				
			Иначе
				// Сопоставление по валютной сумме
				СуммаСНДС = Окр((СуммаБезНДС+СуммаНДС)*Мин(СтрокаЗафиксированногоАванса.ВалютнаяСумма, ВалютнаяСуммаСНДС)/ВалютнаяСуммаСНДС,2);
				ВалютнаяСуммаСНДС = Мин(СтрокаЗафиксированногоАванса.ВалютнаяСумма, ВалютнаяСуммаСНДС);
				
				Если ВалютнаяСуммаСНДС < 0 Тогда
					ШаблонСообщения = НСтр("ru = 'Обнаружен отрицательный валютный остаток по авансам, в размере %1 %2
						| по контрагенту %3 (договор <%4>)
						| по документу %5'");
					
					ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						ШаблонСообщения, ВалютнаяСуммаСНДС, ВалютаАванса,
						СтрокаЗафиксированногоАванса.Покупатель, СтрокаЗафиксированногоАванса.ДоговорКонтрагента,
						СтрокаЗафиксированногоАванса.ДокументОплаты);
					ОбщегоНазначения.СообщитьОбОшибке(ТекстСообщения,, Строка(Ссылка));
					
					Продолжить;
				ИначеЕсли ВалютнаяСуммаСНДС = 0 Тогда
					Продолжить;
				КонецЕсли; 
				
			КонецЕсли; 
			
			СуммаБезНДС = Окр(СуммаБезНДС * СуммаСНДС/(СуммаБезНДС+СуммаНДС),2);
			СуммаНДС = СуммаСНДС - СуммаБезНДС;
			
			Если СуммаБезНДС>0 Тогда
				МассивСуммаБезНДС = ОбщегоНазначения.РаспределитьПропорционально(СуммаБезНДС,МассивСуммаБезНДС);
			КонецЕсли;
			
			Если СуммаНДС>0 Тогда
				МассивСуммаНДС = ОбщегоНазначения.РаспределитьПропорционально(СуммаНДС,МассивСуммаНДС);
			КонецЕсли; 
			
			Если ВалютнаяСуммаСНДС >0 Тогда
				МассивВалютнаяСуммаСНДС = ОбщегоНазначения.РаспределитьПропорционально(ВалютнаяСуммаСНДС,МассивВалютнаяСуммаСНДС);
			КонецЕсли; 
			
			Счетчик =0;
			Для каждого СтрокаАвансаПоОтбору Из СтрокиАвансаПоОтбору Цикл
				СтрокаАвансаПоОтбору.СуммаБезНДС = СтрокаАвансаПоОтбору.СуммаБезНДС - ?(СуммаБезНДС>0,МассивСуммаБезНДС[Счетчик],0);
				СтрокаАвансаПоОтбору.НДС = СтрокаАвансаПоОтбору.НДС - ?(СуммаНДС>0,МассивСуммаНДС[Счетчик],0);
				СтрокаАвансаПоОтбору.ВалютнаяСуммаСНДС = СтрокаАвансаПоОтбору.ВалютнаяСуммаСНДС - ?(ВалютнаяСуммаСНДС>0,МассивВалютнаяСуммаСНДС[Счетчик],0);
				
				
				Счетчик = Счетчик +1;
			КонецЦикла; 
		КонецЦикла; 
	КонецЦикла;
	
 	КУдалениюСФ = новый массив();
	Для каждого СтрокаПоСФ Из Дерево_НДСсАвансов.Строки Цикл
		СтрокиКУдалению = Новый Массив();
		Для каждого СтрокаАванса Из СтрокаПоСФ.Строки Цикл
			Если СтрокаАванса.СуммаБезНДС = 0 и СтрокаАванса.НДС = 0  и СтрокаАванса.ВалютнаяСуммаСНДС = 0 Тогда
				СтрокиКУдалению.Добавить(СтрокаАванса);
			КонецЕсли; 
		КонецЦикла; 
		
		Для каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
			СтрокаПоСФ.Строки.Удалить(СтрокаКУдалению);
		КонецЦикла; 
		Если СтрокаПоСФ.Строки.Количество() = 0 тогда
			КУдалениюСФ.Добавить(СтрокаПоСФ);
		Иначе
			СтрокаПоСФ.СуммаБезНДС = СтрокаПоСФ.Строки.Итог("СуммаБезНДС");
			СтрокаПоСФ.НДС = СтрокаПоСФ.Строки.Итог("НДС");
			СтрокаПоСФ.ВалютнаяСуммаСНДС = СтрокаПоСФ.Строки.Итог("ВалютнаяСуммаСНДС");
		КонецЕсли;
	КонецЦикла; 
	
	Для каждого СтрокаКУдалению Из КУдалениюСФ Цикл
		Дерево_НДСсАвансов.Строки.Удалить(СтрокаКУдалению);
	КонецЦикла; 
	
	Возврат Дерево_НДСсАвансов;
	
КонецФункции // ЗаполнитьНДСКВычетуПоДаннымРегистраНДСПредъявленный()

// Вызывается из процедуры ЗаполнитьСтроки_НДСсАвансов.
// Заполняет ТЧ ВычетПоПриобретеннымЦенностям по данным регистра НДСПредъявленный
Функция  ЗаполнитьНДСсАвансовПоДаннымРегистраНДСсАвансовИНДСПредъявленныйРеализация0(ТаблицаРезультатов)

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НДСПредъявленныйРеализация0Остатки.СчетФактура КАК СчетФактура,
	|	НДСПредъявленныйРеализация0Остатки.Состояние,
	|	НДСПредъявленныйРеализация0Остатки.ДокументОтгрузки,
	|	НДСПредъявленныйРеализация0Остатки.СтавкаНДС,
	|	СУММА(НДСПредъявленныйРеализация0Остатки.СуммаБезНДСОстаток) КАК СуммаБезНДС,
	|	СУММА(НДСПредъявленныйРеализация0Остатки.НДСОстаток) КАК НДС,
	|	НДСПредъявленныйРеализация0Остатки.ДокументОтгрузки.Контрагент КАК Покупатель,
	|	НДСПредъявленныйРеализация0Остатки.ДокументОтгрузки.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	НДСПредъявленныйРеализация0Остатки.ДокументОтгрузки.Дата
	|ИЗ
	|	РегистрНакопления.НДСПредъявленныйРеализация0.Остатки(
	|		&ДатаГраница,
	|		Организация = &Организация
	|		    И ВидЦенности В (&ВидыЦенностейАванс)
	|		    И состояние В (&ОтрабатываемыеСостояния)) КАК НДСПредъявленныйРеализация0Остатки
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСПредъявленныйРеализация0Остатки.Состояние,
	|	НДСПредъявленныйРеализация0Остатки.СчетФактура,
	|	НДСПредъявленныйРеализация0Остатки.СтавкаНДС,
	|	НДСПредъявленныйРеализация0Остатки.ДокументОтгрузки,
	|	НДСПредъявленныйРеализация0Остатки.ДокументОтгрузки.Контрагент,
	|	НДСПредъявленныйРеализация0Остатки.ДокументОтгрузки.ДоговорКонтрагента,
	|	НДСПредъявленныйРеализация0Остатки.ДокументОтгрузки.Дата
	|
	|УПОРЯДОЧИТЬ ПО
	|	НДСПредъявленныйРеализация0Остатки.СчетФактура.Дата
	|ИТОГИ ПО
	|	СчетФактура";

	Запрос.УстановитьПараметр("Организация",  Организация);
	Запрос.УстановитьПараметр("Дата", КонецДня(Дата));
	Запрос.УстановитьПараметр("ДатаГраница", новый Граница(КонецДня(Дата),ВидГраницы.Включая));
	
	// Анализируемые виды ценностей
	ВидыЦенностейАванс = Новый СписокЗначений;
	ВидыЦенностейАванс.Добавить(Перечисления.ВидыЦенностей.АвансыПолученные);
	ВидыЦенностейАванс.Добавить(Перечисления.ВидыЦенностей.АвансыПолученные0);
	ВидыЦенностейАванс.Добавить(Перечисления.ВидыЦенностей.ВозвратАвансовПолученных);
	
	Запрос.УстановитьПараметр("ВидыЦенностейАванс", ВидыЦенностейАванс);
	
	// Отрабатываемые состояния (ожидание 0% не отрабатываем)
	ОтрабатываемыеСостояния = Новый СписокЗначений;
	ОтрабатываемыеСостояния.Добавить(Перечисления.НДССостоянияРеализация0.ПодтвержденаРеализация0);
	ОтрабатываемыеСостояния.Добавить(Перечисления.НДССостоянияРеализация0.НеПодтвержденаРеализация0);
	
	Запрос.УстановитьПараметр("ОтрабатываемыеСостояния", ОтрабатываемыеСостояния);

	АвансыНДСПредъявленныйРеализация0 = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам); 
	
	Если АвансыНДСПредъявленныйРеализация0.Строки.Количество() = 0 Тогда
	    Возврат ТаблицаРезультатов;
	КонецЕсли; 
	
	СписокСчетовФактур = ОбщегоНазначения.УдалитьПовторяющиесяЭлементыМассива( АвансыНДСПредъявленныйРеализация0.Строки.ВыгрузитьКолонку("СчетФактура"),Истина);
	
	Запрос.УстановитьПараметр("СписокСчетовФактур", СписокСчетовФактур);
	
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НДСсАвансовОстатки.СчетФактура КАК СчетФактура,
	|	НДСсАвансовОстатки.Покупатель,
	|	НДСсАвансовОстатки.ДоговорКонтрагента,
	|	НДСсАвансовОстатки.СтавкаНДС,
	|	НДСсАвансовОстатки.ВалютаАванса,
	|	СУММА(НДСсАвансовОстатки.СуммаБезНДСОстаток) КАК СуммаБезНДС,
	|	СУММА(НДСсАвансовОстатки.НДСОстаток) КАК НДС,
	|	СУММА(НДСсАвансовОстатки.ВалютнаяСуммаСНДСОстаток) КАК ВалютнаяСуммаСНДС
	|ИЗ
	|	РегистрНакопления.НДСсАвансов.Остатки(
	|			&ДатаГраница,
	|			Организация = &Организация
	|				И СчетФактура В (&СписокСчетовФактур)
	|				И ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.АвансыПолученные)) КАК НДСсАвансовОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСсАвансовОстатки.СчетФактура,
	|	НДСсАвансовОстатки.СтавкаНДС,
	|	НДСсАвансовОстатки.Покупатель,
	|	НДСсАвансовОстатки.ДоговорКонтрагента,
	|	НДСсАвансовОстатки.ВалютаАванса
	|
	|УПОРЯДОЧИТЬ ПО
	|	НДСсАвансовОстатки.СчетФактура.Дата
	|ИТОГИ ПО
	|	СчетФактура";
	
	ЗафиксированныеАвансы = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Если ЗафиксированныеАвансы.Строки.Количество() = 0 тогда
		Возврат ТаблицаРезультатов;
	КонецЕсли;
	
	Для каждого СтрокаСФ Из АвансыНДСПредъявленныйРеализация0.Строки Цикл
		
		ЗафиксированныйАвансПоСФ = ЗафиксированныеАвансы.Строки.Найти(СтрокаСФ.СчетФактура,"СчетФактура");
		
		Если ЗафиксированныйАвансПоСФ = Неопределено Тогда
		    Продолжить;
		КонецЕсли; 
		
		Для каждого СтрокаЗафиксированногоАванса Из ЗафиксированныйАвансПоСФ.Строки Цикл
			
			Отбор = Новый Структура("Покупатель,ДоговорКонтрагента,СтавкаНДС",СтрокаЗафиксированногоАванса.Покупатель, СтрокаЗафиксированногоАванса.ДоговорКонтрагента, СтрокаЗафиксированногоАванса.СтавкаНДС);
			
			СтрокиАванса0 = СтрокаСФ.Строки.НайтиСтроки(Отбор);
			Если СтрокиАванса0.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли; 
			
			МассивСуммаБезНДС = новый Массив();
			МассивСуммаНДС = новый Массив();
			МассивСуммаСНДС = новый Массив();
			СуммаБезНДС = 0;
			СуммаНДС = 0;
			Для каждого СтрокаАванса0 Из СтрокиАванса0 Цикл
				МассивСуммаБезНДС.Добавить(СтрокаАванса0.СуммаБезНДС);
				МассивСуммаНДС.Добавить(СтрокаАванса0.НДС);
				МассивСуммаСНДС.Добавить(СтрокаАванса0.СуммаБезНДС+СтрокаАванса0.НДС);
				
				СуммаБезНДС = СуммаБезНДС + СтрокаАванса0.СуммаБезНДС;
				СуммаНДС = СуммаНДС + СтрокаАванса0.НДС;
			КонецЦикла; 
			
			СуммаБезНДС = Мин(СтрокаЗафиксированногоАванса.СуммаБезНДС, СуммаБезНДС);
			СуммаНДС = Мин(СтрокаЗафиксированногоАванса.НДС, СуммаНДС);
			
			Если СуммаБезНДС>0 Тогда
				МассивСуммаБезНДС = ОбщегоНазначения.РаспределитьПропорционально(СуммаБезНДС,МассивСуммаБезНДС);
			КонецЕсли; 
			Если СуммаНДС>0 Тогда
				МассивСуммаНДС = ОбщегоНазначения.РаспределитьПропорционально(СуммаНДС,МассивСуммаНДС);
			КонецЕсли; 
			
			Если СуммаБезНДС + СуммаНДС >0 Тогда
				ВалютнаяСуммаСНДС = Окр(СтрокаЗафиксированногоАванса.ВалютнаяСуммаСНДС * (СуммаБезНДС+СуммаНДС)/(СтрокаЗафиксированногоАванса.СуммаБезНДС+СтрокаЗафиксированногоАванса.НДС),2);
			Иначе 
				ВалютнаяСуммаСНДС = 0;
			КонецЕсли; 
			
			
			Если ВалютнаяСуммаСНДС>0 Тогда
				МассивСуммаСНДС = ОбщегоНазначения.РаспределитьПропорционально(ВалютнаяСуммаСНДС,МассивСуммаСНДС);
			КонецЕсли;
			
			Счетчик =0;
			Для каждого СтрокаАванса0 Из СтрокиАванса0 Цикл
			    СтрокаРезультата = ТаблицаРезультатов.Добавить();
				СтрокаРезультата.СчетФактура		= СтрокаАванса0.СчетФактура;
				СтрокаРезультата.Покупатель			= СтрокаАванса0.Покупатель;
				СтрокаРезультата.ДоговорКонтрагента	= СтрокаАванса0.ДоговорКонтрагента;
				СтрокаРезультата.СтавкаНДС			= СтрокаАванса0.СтавкаНДС;
				СтрокаРезультата.ДокументОтгрузки	= СтрокаАванса0.ДокументОтгрузки;
				СтрокаРезультата.Состояние			= СтрокаАванса0.Состояние;
				СтрокаРезультата.ВалютаДокумента	= СтрокаЗафиксированногоАванса.ВалютаАванса;
				
				//Если СтрокаРезультата.Состояние = Перечисления.НДССостоянияРеализация0.ПодтвержденаРеализация0 Тогда
				СтрокаРезультата.ДатаСобытия	= СтрокаАванса0.ДокументОтгрузкиДата;
				//Иначе
				//	СтрокаРезультата.ДатаСобытия	= СтрокаАванса0.ДокументОтгрузкиДата;
				//КонецЕсли; 
				
				СтрокаРезультата.СуммаБезНДС		= ?(СуммаБезНДС>0,МассивСуммаБезНДС[Счетчик],0);
				СтрокаРезультата.НДС				= ?(СуммаНДС>0,МассивСуммаНДС[Счетчик],0);
				СтрокаРезультата.ВалютнаяСумма		= ?(ВалютнаяСуммаСНДС>0,МассивСуммаСНДС[Счетчик],0);
				
				СтрокаАванса0.СуммаБезНДС = СтрокаАванса0.СуммаБезНДС - СтрокаРезультата.СуммаБезНДС;
				СтрокаАванса0.НДС = СтрокаАванса0.НДС - СтрокаРезультата.НДС;
				
			    Счетчик = Счетчик +1;
			КонецЦикла; 
		КонецЦикла; 
	КонецЦикла; 
	
	Возврат ТаблицаРезультатов;
	
КонецФункции // ЗаполнитьНДСКВычетуПоДаннымРегистраНДСПредъявленный()

// Процедура вызывается из ЗаполнитьСтроки_НДСсАвансов.
// По списку счетов-фактур определяет суммы не использованных ранее распределенных оплат.
Функция ПолучитьДанныеОЗачтенныхАвансахПоСпискуСФ(СписокСчетовФактур)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОбороты.Регистратор КАК Регистратор,
	|	НАЧАЛОПЕРИОДА(РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОбороты.Период, ДЕНЬ) КАК ДатаСобытия,
	|	РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОбороты.Документ КАК ДокументОплаты,
	|	РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОбороты.Контрагент КАК Покупатель,
	|	РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОбороты.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОбороты.СуммаРегРасход КАК Сумма,
	|	РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОбороты.СуммаВзаиморасчетовРасход КАК ВалютнаяСумма
	|ПОМЕСТИТЬ ВТРасчетыПоРеализации
	|ИЗ
	|	РегистрНакопления.РасчетыПоРеализацииВУсловныхЕдиницахОрганизации.Обороты(
	|			,
	|			&ДатаГраница,
	|			Регистратор,
	|			Организация = &Организация
	|				И Документ В (&СписокСчетовФактур)) КАК РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОбороты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор,
	|	Покупатель,
	|	ДоговорКонтрагента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетыПоРеализацииОрганизации.Регистратор КАК Регистратор,
	|	РасчетыПоРеализацииОрганизации.Контрагент КАК Контрагент,
	|	РасчетыПоРеализацииОрганизации.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	МИНИМУМ(РасчетыПоРеализацииОрганизации.КоррСчет) КАК КоррСчет
	|ПОМЕСТИТЬ ВТТаблицаКорСчетов
	|ИЗ
	|	РегистрСведений.РасчетыПоРеализацииОрганизации КАК РасчетыПоРеализацииОрганизации
	|ГДЕ
	|	РасчетыПоРеализацииОрганизации.Организация = &Организация
	|	И (РасчетыПоРеализацииОрганизации.Регистратор, РасчетыПоРеализацииОрганизации.Контрагент, РасчетыПоРеализацииОрганизации.ДоговорКонтрагента) В
	|			(ВЫБРАТЬ
	|				ВТРасчетыПоРеализации.Регистратор,
	|				ВТРасчетыПоРеализации.Покупатель,
	|				ВТРасчетыПоРеализации.ДоговорКонтрагента
	|			ИЗ
	|				ВТРасчетыПоРеализации КАК ВТРасчетыПоРеализации)
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетыПоРеализацииОрганизации.Регистратор,
	|	РасчетыПоРеализацииОрганизации.Контрагент,
	|	РасчетыПоРеализацииОрганизации.ДоговорКонтрагента
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор,
	|	Контрагент,
	|	ДоговорКонтрагента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТРасчетыПоРеализации.ДокументОплаты КАК ДокументОплаты,
	|	ВТРасчетыПоРеализации.ДоговорКонтрагента.ВалютаВзаиморасчетов КАК ВалютаРасчетов,
	|	ВТРасчетыПоРеализации.Покупатель,
	|	ВТРасчетыПоРеализации.ДоговорКонтрагента,
	|	СУММА(ВТРасчетыПоРеализации.Сумма) КАК Сумма,
	|	СУММА(ВТРасчетыПоРеализации.ВалютнаяСумма) КАК ВалютнаяСумма,
	|	ВТРасчетыПоРеализации.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах КАК РасчетыВУсловныхЕдиницах,
	|	ВТРасчетыПоРеализации.ДатаСобытия КАК Дата,
	|	ЛОЖЬ КАК ДоговорСКомиссионером,
	|	ВЫБОР
	|		КОГДА ВТТаблицаКорСчетов.КоррСчет В (&СчетаУчетаДенежныхСредствОрганизации)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ВозвратАвансовПолученных
	|ИЗ
	|	ВТРасчетыПоРеализации КАК ВТРасчетыПоРеализации
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТТаблицаКорСчетов КАК ВТТаблицаКорСчетов
	|		ПО ВТРасчетыПоРеализации.Регистратор = ВТТаблицаКорСчетов.Регистратор
	|			И ВТРасчетыПоРеализации.Покупатель = ВТТаблицаКорСчетов.Контрагент
	|			И ВТРасчетыПоРеализации.ДоговорКонтрагента = ВТТаблицаКорСчетов.ДоговорКонтрагента
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА ВТТаблицаКорСчетов.КоррСчет В (&СчетаУчетаДенежныхСредствОрганизации)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	ВТРасчетыПоРеализации.ДоговорКонтрагента.ВалютаВзаиморасчетов,
	|	ВТРасчетыПоРеализации.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах,
	|	ВТРасчетыПоРеализации.ДоговорКонтрагента,
	|	ВТРасчетыПоРеализации.Покупатель,
	|	ВТРасчетыПоРеализации.ДокументОплаты,
	|	ВТРасчетыПоРеализации.ДатаСобытия
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОтчетКомиссионераОПродажахДенежныеСредства.Ссылка,
	|	&ВалютаРегламентированногоУчета,
	|	ОтчетКомиссионераОПродажахДенежныеСредства.Ссылка.Контрагент,
	|	ОтчетКомиссионераОПродажахДенежныеСредства.Ссылка.ДоговорКонтрагента,
	|	ОтчетКомиссионераОПродажахДенежныеСредства.Сумма,
	|	ОтчетКомиссионераОПродажахДенежныеСредства.Сумма,
	|	ЛОЖЬ,
	|	ОтчетКомиссионераОПродажахДенежныеСредства.Ссылка.Дата,
	|	ИСТИНА,
	|	ЛОЖЬ
	|ИЗ
	|	Документ.ОтчетКомиссионераОПродажах.ДенежныеСредства КАК ОтчетКомиссионераОПродажахДенежныеСредства
	|ГДЕ
	|	ОтчетКомиссионераОПродажахДенежныеСредства.Ссылка.Дата >= &НачалоКвартала
	|	И ОтчетКомиссионераОПродажахДенежныеСредства.Ссылка.Дата <= &Дата
	|	И ОтчетКомиссионераОПродажахДенежныеСредства.Ссылка.Организация = &Организация
	|	И ОтчетКомиссионераОПродажахДенежныеСредства.ВидОтчетаПоПлатежам = ЗНАЧЕНИЕ(Перечисление.ВидыОтчетовПоПлатежамКомиссия.ЗачетАванса)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТРасчетыПоРеализации.ДатаСобытия УБЫВ
	|ИТОГИ ПО
	|	ДокументОплаты";
	 	 
	Запрос.УстановитьПараметр("Дата", 				КонецДня(Дата));
	Запрос.УстановитьПараметр("НачалоКвартала", 	НачалоКвартала(Дата));
	Запрос.УстановитьПараметр("Организация", 		Организация);
	Запрос.УстановитьПараметр("СписокСчетовФактур", СписокСчетовФактур);
	Запрос.УстановитьПараметр("ДатаГраница", новый Граница(КонецДня(Дата),ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", мВалютаРегламентированногоУчета);
	
	СчетаУчетаДенежныхСредствОрганизации = Новый Массив();
	СчетаУчетаДенежныхСредствОрганизации.Добавить(ПланыСчетов.Хозрасчетный.РасчетныеСчета);
	СчетаУчетаДенежныхСредствОрганизации.Добавить(ПланыСчетов.Хозрасчетный.ВалютныеСчета);
	СчетаУчетаДенежныхСредствОрганизации.Добавить(ПланыСчетов.Хозрасчетный.КассаОрганизации);
	СчетаУчетаДенежныхСредствОрганизации.Добавить(ПланыСчетов.Хозрасчетный.ОперационнаяКасса);
	СчетаУчетаДенежныхСредствОрганизации.Добавить(ПланыСчетов.Хозрасчетный.КассаОрганизацииВал);
	
	Запрос.УстановитьПараметр("СчетаУчетаДенежныхСредствОрганизации", СчетаУчетаДенежныхСредствОрганизации);
	
	Возврат Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);	

КонецФункции // ПолучитьДанныеОРаспределенныхОплатахПоСпискуСФ()

// Процедура осуществляет автоматическое распределение фактов зачета авансов
// на документы отгрузки, оформленные в рамках соответствующего договора
//
Процедура РаспределитьЗачетыАвансаПоСФ(Дерево_НДСсАвансов, ТаблицаРезультатов,СписокСчетовФактур,ТаблицаЗачетовАвансов)
	
	Для каждого ПогашенныйАвансПоСФ Из ТаблицаЗачетовАвансов.Строки Цикл
		
		ЗафиксированныйАвансПоСФ = Дерево_НДСсАвансов.Строки.Найти(ПогашенныйАвансПоСФ.ДокументОплаты,"СчетФактура");

		Для Каждого СтрокаПогашенияАванса Из ПогашенныйАвансПоСФ.Строки Цикл
			
			Если СтрокаПогашенияАванса.ДоговорСКомиссионером Тогда
				
				ЗафиксированныйАвансПоСФ = Дерево_НДСсАвансов.Строки.Найти(Истина, "ДоговорСКомиссионером");
				
			КонецЕсли;
				
			Если ЗафиксированныйАвансПоСФ = Неопределено Тогда				
				Продолжить;                                				
			КонецЕсли;                                     				
			
			ВалютаАванса = ?(СтрокаПогашенияАванса.РасчетыВУсловныхЕдиницах или НЕ ЗначениеЗаполнено(СтрокаПогашенияАванса.ВалютаРасчетов),мВалютаРегламентированногоУчета,СтрокаПогашенияАванса.ВалютаРасчетов);
			
			Отбор = Новый Структура("Покупатель,ДоговорКонтрагента, ВалютаАванса, ДоговорСКомиссионером",СтрокаПогашенияАванса.Покупатель, СтрокаПогашенияАванса.ДоговорКонтрагента, ВалютаАванса, СтрокаПогашенияАванса.ДоговорСКомиссионером);
			
			СтрокиАвансаПоОтбору = ЗафиксированныйАвансПоСФ.Строки.НайтиСтроки(Отбор);
			Если СтрокиАвансаПоОтбору.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли; 
			
			МассивСуммаБезНДС = новый Массив();
			МассивСуммаНДС = новый Массив();
			МассивВалютнаяСуммаСНДС = новый Массив();
			
			СуммаБезНДС = 0;
			СуммаНДС = 0;
			ВалютнаяСуммаСНДС = 0;
			Для каждого СтрокаАвансаПоОтбору Из СтрокиАвансаПоОтбору Цикл
				МассивСуммаБезНДС.Добавить(СтрокаАвансаПоОтбору.СуммаБезНДС);
				МассивСуммаНДС.Добавить(СтрокаАвансаПоОтбору.НДС);
				МассивВалютнаяСуммаСНДС.Добавить(СтрокаАвансаПоОтбору.ВалютнаяСуммаСНДС);
				
				СуммаБезНДС = СуммаБезНДС + СтрокаАвансаПоОтбору.СуммаБезНДС;
				СуммаНДС = СуммаНДС + СтрокаАвансаПоОтбору.НДС;
				ВалютнаяСуммаСНДС = ВалютнаяСуммаСНДС + СтрокаАвансаПоОтбору.ВалютнаяСуммаСНДС
			КонецЦикла; 
			
			Если ВалютаАванса = мВалютаРегламентированногоУчета Тогда
				Если СуммаБезНДС+СуммаНДС = 0 тогда
					Продолжить;
				КонецЕсли; 
				// Сопоставление по рублевой сумме
				ВалютнаяСуммаСНДС = Окр(ВалютнаяСуммаСНДС*Мин(СтрокаПогашенияАванса.Сумма, (СуммаБезНДС+СуммаНДС))/(СуммаБезНДС+СуммаНДС), 2);
				СуммаСНДС = Мин(СтрокаПогашенияАванса.Сумма, (СуммаБезНДС+СуммаНДС));
				Если СуммаСНДС = 0 Тогда
					Продолжить;
				КонецЕсли;
			Иначе
				Если ВалютнаяСуммаСНДС = 0 Тогда
					Продолжить;
				КонецЕсли; 
				// Сопоставление по валютной сумме
				СуммаСНДС = Окр((СуммаБезНДС+СуммаНДС)*Мин(СтрокаПогашенияАванса.ВалютнаяСумма, ВалютнаяСуммаСНДС)/ВалютнаяСуммаСНДС,2);
				ВалютнаяСуммаСНДС = Мин(СтрокаПогашенияАванса.ВалютнаяСумма, ВалютнаяСуммаСНДС);
				
				Если ВалютнаяСуммаСНДС = 0 ИЛИ СуммаСНДС = 0 Тогда
					Продолжить;
				КонецЕсли; 
				
			КонецЕсли; 
			
			СуммаБезНДС = Окр(СуммаБезНДС * СуммаСНДС/(СуммаБезНДС+СуммаНДС),2);
			СуммаНДС = СуммаСНДС - СуммаБезНДС;
			
			Если СуммаБезНДС>0 Тогда
				МассивСуммаБезНДС = ОбщегоНазначения.РаспределитьПропорционально(СуммаБезНДС,МассивСуммаБезНДС);
			КонецЕсли;
			
			Если СуммаНДС>0 Тогда
				МассивСуммаНДС = ОбщегоНазначения.РаспределитьПропорционально(СуммаНДС,МассивСуммаНДС);
			КонецЕсли; 
			
			Если ВалютнаяСуммаСНДС >0 Тогда
				МассивВалютнаяСуммаСНДС = ОбщегоНазначения.РаспределитьПропорционально(ВалютнаяСуммаСНДС,МассивВалютнаяСуммаСНДС);
			КонецЕсли; 
			
			Счетчик =0;
			Для каждого СтрокаАвансаПоОтбору Из СтрокиАвансаПоОтбору Цикл
				Если ?(СуммаБезНДС>0,МассивСуммаБезНДС[Счетчик],0) = 0 и ?(СуммаНДС>0,МассивСуммаНДС[Счетчик],0) = 0 и ?(ВалютнаяСуммаСНДС>0,МассивВалютнаяСуммаСНДС[Счетчик],0)= 0  Тогда
					Продолжить;
				КонецЕсли; 
				
				СтрокаРезультата = ТаблицаРезультатов.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаРезультата, СтрокаАвансаПоОтбору);
				СтрокаРезультата.ВалютаДокумента = СтрокаАвансаПоОтбору.ВалютаАванса;
				СтрокаРезультата.ДатаСобытия	 = СтрокаПогашенияАванса.Дата;
				СтрокаРезультата.ВозвратАвансовПолученных = СтрокаПогашенияАванса.ВозвратАвансовПолученных;
				
				СтрокаРезультата.СуммаБезНДС	 = ?(СуммаБезНДС>0,МассивСуммаБезНДС[Счетчик],0);
				СтрокаРезультата.НДС			 = ?(СуммаНДС>0,МассивСуммаНДС[Счетчик],0);
				СтрокаРезультата.ВалютнаяСумма	 = ?(ВалютнаяСуммаСНДС>0,МассивВалютнаяСуммаСНДС[Счетчик],0);
				
				СтрокаАвансаПоОтбору.СуммаБезНДС = СтрокаАвансаПоОтбору.СуммаБезНДС - ?(СуммаБезНДС>0,МассивСуммаБезНДС[Счетчик],0);
				СтрокаАвансаПоОтбору.НДС = СтрокаАвансаПоОтбору.НДС - ?(СуммаНДС>0,МассивСуммаНДС[Счетчик],0);
				СтрокаАвансаПоОтбору.ВалютнаяСуммаСНДС = СтрокаАвансаПоОтбору.ВалютнаяСуммаСНДС - ?(ВалютнаяСуммаСНДС>0,МассивВалютнаяСуммаСНДС[Счетчик],0);
				
				Счетчик = Счетчик +1;
			КонецЦикла; 
		КонецЦикла; 
	КонецЦикла;
	
 	КУдалениюСФ = Новый массив();
	Для Каждого СтрокаПоСФ Из Дерево_НДСсАвансов.Строки Цикл
		СтрокиКУдалению = Новый Массив();
		Для Каждого СтрокаАванса Из СтрокаПоСФ.Строки Цикл
			Если (СтрокаАванса.СуммаБезНДС = 0 И СтрокаАванса.НДС = 0 И СтрокаАванса.ВалютнаяСуммаСНДС = 0) 
				Или СтрокаАванса.ДоговорСКомиссионером Тогда				
				Продолжить;
			КонецЕсли; 
			
			СтрокаРезультата = ТаблицаРезультатов.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаРезультата, СтрокаАванса);
			СтрокаРезультата.ВалютаДокумента = СтрокаАванса.ВалютаАванса;
			СтрокаРезультата.ВалютнаяСумма   = СтрокаАванса.ВалютнаяСуммаСНДС;
			
		КонецЦикла; 
		
		Для каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
			СтрокаПоСФ.Строки.Удалить(СтрокаКУдалению);
		КонецЦикла; 
		Если СтрокаПоСФ.Строки.Количество() = 0 тогда
			КУдалениюСФ.Добавить(СтрокаПоСФ);
		Иначе
			СтрокаПоСФ.СуммаБезНДС = СтрокаПоСФ.Строки.Итог("СуммаБезНДС");
			СтрокаПоСФ.НДС = СтрокаПоСФ.Строки.Итог("НДС");
			СтрокаПоСФ.ВалютнаяСуммаСНДС = СтрокаПоСФ.Строки.Итог("ВалютнаяСуммаСНДС");
		КонецЕсли;
	КонецЦикла; 
	
КонецПроцедуры

Процедура ЗаполнитьЗачетАвансовПокупателейПоКомиссии()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Дата", Новый Граница(КонецДня(Дата), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НДСЗаписиКнигиПродажОбороты.Покупатель КАК Покупатель,
	|	НДСЗаписиКнигиПродажОбороты.СчетФактура,
	|	НДСЗаписиКнигиПродажОбороты.СтавкаНДС КАК СтавкаНДС,
	|	НДСЗаписиКнигиПродажОбороты.ДокументОплаты КАК ДокументОплаты,
	|	НАЧАЛОПЕРИОДА(НДСЗаписиКнигиПродажОбороты.ДатаСобытия, ДЕНЬ) КАК ДатаСобытия,
	|	НДСЗаписиКнигиПродажОбороты.ИсправленныйСчетФактура,
	|	НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПродажОбороты.НДСОборот
	|ПОМЕСТИТЬ ВТКнигаПродаж
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|			,
	|			&Дата,
	|			,
	|			ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.АвансыПолученные)
	|				И Организация = &Организация
	|				И СчетФактура ССЫЛКА Документ.СчетФактураВыданный) КАК НДСЗаписиКнигиПродажОбороты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НДСЗаписиКнигиПродажОбороты.СчетФактура,
	|	НДСЗаписиКнигиПродажОбороты.ИсправленныйСчетФактура,
	|	СтавкаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПокупокОбороты.СчетФактура,
	|	НДСЗаписиКнигиПокупокОбороты.ИсправленныйСчетФактура,
	|	НДСЗаписиКнигиПокупокОбороты.СтавкаНДС КАК СтавкаНДС,
	|	НДСЗаписиКнигиПокупокОбороты.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПокупокОбороты.НДСОборот
	|ПОМЕСТИТЬ ВТКнигаПокупок
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок.Обороты(
	|			,
	|			&Дата,
	|			,
	|			ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.АвансыПолученные)
	|				И Организация = &Организация
	|				И СчетФактура ССЫЛКА Документ.СчетФактураВыданный) КАК НДСЗаписиКнигиПокупокОбороты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НДСЗаписиКнигиПокупокОбороты.СчетФактура,
	|	НДСЗаписиКнигиПокупокОбороты.ИсправленныйСчетФактура,
	|	СтавкаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КнигаПродаж.Покупатель КАК Покупатель,
	|	КнигаПродаж.СчетФактура,
	|	КнигаПродаж.ДокументОплаты,
	|	КнигаПродаж.СтавкаНДС КАК СтавкаНДС,
	|	КнигаПродаж.ДатаСобытия КАК ДатаСобытия,
	|	КнигаПродаж.ИсправленныйСчетФактура,
	|	СУММА(ЕСТЬNULL(КнигаПродаж.СуммаБезНДСОборот, 0) - ЕСТЬNULL(КнигаПокупок.СуммаБезНДСОборот, 0)) КАК СуммаБезНДСОборот,
	|	СУММА(ЕСТЬNULL(КнигаПродаж.НДСОборот, 0) - ЕСТЬNULL(КнигаПокупок.НДСОборот, 0)) КАК НДСОборот
	|ПОМЕСТИТЬ ВТНДСКВычету
	|ИЗ
	|	ВТКнигаПродаж КАК КнигаПродаж
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКнигаПокупок КАК КнигаПокупок
	|		ПО КнигаПродаж.СтавкаНДС = КнигаПокупок.СтавкаНДС
	|			И КнигаПродаж.СчетФактура = КнигаПокупок.СчетФактура
	|			И КнигаПродаж.ИсправленныйСчетФактура = КнигаПокупок.ИсправленныйСчетФактура
	|ГДЕ
	|	ЕСТЬNULL(КнигаПродаж.СуммаБезНДСОборот, 0) - ЕСТЬNULL(КнигаПокупок.СуммаБезНДСОборот, 0) > 0
	|	И ЕСТЬNULL(КнигаПродаж.НДСОборот, 0) - ЕСТЬNULL(КнигаПокупок.НДСОборот, 0) > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	КнигаПродаж.Покупатель,
	|	КнигаПродаж.СчетФактура,
	|	КнигаПродаж.ДокументОплаты,
	|	КнигаПродаж.СтавкаНДС,
	|	КнигаПродаж.ДатаСобытия,
	|	КнигаПродаж.ИсправленныйСчетФактура
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Покупатель,
	|	СтавкаНДС,
	|	КнигаПродаж.ДокументОплаты,
	|	ДатаСобытия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСАвансыПоДоговорамКомиссииОстатки.СчетФактура КАК СчетФактура,
	|	НДСАвансыПоДоговорамКомиссииОстатки.СтавкаНДС КАК СтавкаНДС,
	|	НДСАвансыПоДоговорамКомиссииОстатки.Покупатель КАК Покупатель,
	|	НДСАвансыПоДоговорамКомиссииОстатки.ДатаСобытия КАК ДатаСобытия,
	|	НДСАвансыПоДоговорамКомиссииОстатки.СуммаБезНДСОстаток,
	|	НДСАвансыПоДоговорамКомиссииОстатки.НДСОстаток
	|ПОМЕСТИТЬ ВТАвансыПоДоговорамКомиссии
	|ИЗ
	|	РегистрНакопления.НДСАвансыПоДоговорамКомиссии.Остатки(&Дата, Организация = &Организация) КАК НДСАвансыПоДоговорамКомиссииОстатки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Покупатель,
	|	ДатаСобытия,
	|	СтавкаНДС,
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТНДСКВычету.СчетФактура,
	|	ВТНДСКВычету.СтавкаНДС,
	|	ВТНДСКВычету.Покупатель,
	|	ВТНДСКВычету.ДатаСобытия,
	|	ВТНДСКВычету.ИсправленныйСчетФактура КАК ИсправленныйСчетФактура,
	|	ЕСТЬNULL(ВТНДСКВычету.СуммаБезНДСОборот, 0) - ЕСТЬNULL(ВТАвансыПоДоговорамКомиссии.СуммаБезНДСОстаток, 0) КАК СуммаБезНДС,
	|	ЕСТЬNULL(ВТНДСКВычету.НДСОборот, 0) - ЕСТЬNULL(ВТАвансыПоДоговорамКомиссии.НДСОстаток, 0) КАК НДС,
	|	ВТНДСКВычету.ДокументОплаты
	|ИЗ
	|	ВТНДСКВычету КАК ВТНДСКВычету
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТАвансыПоДоговорамКомиссии КАК ВТАвансыПоДоговорамКомиссии
	|		ПО (ВТАвансыПоДоговорамКомиссии.СчетФактура = ВТНДСКВычету.ДокументОплаты)
	|			И (ВТАвансыПоДоговорамКомиссии.СтавкаНДС = ВТНДСКВычету.СтавкаНДС)
	|			И (ВТАвансыПоДоговорамКомиссии.Покупатель = ВТНДСКВычету.Покупатель)
	|			И (ВТАвансыПоДоговорамКомиссии.ДатаСобытия = ВТНДСКВычету.ДатаСобытия)
	|ГДЕ
	|	ЕСТЬNULL(ВТНДСКВычету.СуммаБезНДСОборот, 0) - ЕСТЬNULL(ВТАвансыПоДоговорамКомиссии.СуммаБезНДСОстаток, 0) > 0
	|	И ЕСТЬNULL(ВТНДСКВычету.НДСОборот, 0) - ЕСТЬNULL(ВТАвансыПоДоговорамКомиссии.НДСОстаток, 0) > 0";
	
	ТаблицаОстатков = Запрос.Выполнить().Выгрузить();
	
	// Зачет аванса
	Для Каждого СтрокаОстатка Из ТаблицаОстатков Цикл
		
		Если СтрокаОстатка.СуммаБезНДС > 0 ИЛИ СтрокаОстатка.НДС > 0 Тогда
			
			СтрокаДокумента = НДСсАвансов.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаДокумента, СтрокаОстатка); 			
			СтрокаДокумента.ДатаСобытия = Дата;
			СтрокаДокумента.ВалютаДокумента = Константы.ВалютаРегламентированногоУчета.Получить();
			СтрокаДокумента.ВалютнаяСумма = СтрокаОстатка.СуммаБезНДС + СтрокаОстатка.НДС;
			
		КонецЕсли;	
		
	КонецЦикла;	
	      	
КонецПроцедуры	

// Заполнение табличной части "Вычет НДС с полученных авансов"
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// Заполнение табличной части "Вычет НДС с выданных авансов"

// Процедура выполняет автоматическое заполнение табличной части документа
// Вызывается из процедуры КоманднаяПанельНДСсАвансовЗаполнить
//
Процедура ЗаполнитьСтроки_НДСсАвансовВыданных(ОтменитьПроведение = Ложь, УдалятьНезаполненные = Ложь) Экспорт
	
	Если ПредъявленНДСКВычету0 Тогда
		Возврат;
	КонецЕсли;	
		
	Если Проведен Тогда
		Если ОтменитьПроведение Тогда
			Записать(РежимЗаписиДокумента.ОтменаПроведения);
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ТаблицаРезультатов = НДСсАвансовВыданных.ВыгрузитьКолонки();
	
	ЗаполнитьНДССАвансовВыданныхПоДаннымРегистраНДСПредъявленный(ТаблицаРезультатов);
	
	// Удаление строк, в которых есть незаполненные обязательные поля
	Если УдалятьНезаполненные Тогда
		ОбработкаТабличныхЧастейСервер.УдалитьНезаполненныеСтроки(ТаблицаРезультатов, "Поставщик, ДоговорКонтрагента, СчетФактура, СтавкаНДС");
	КонецЕсли;
	
	НДСсАвансовВыданных.Загрузить(ТаблицаРезультатов);
	
КонецПроцедуры // ЗаполнитьСтрокиДокумента()

Процедура ЗаполнитьНДССАвансовВыданныхПоДаннымРегистраНДСПредъявленный(ТаблицаРезультатов)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ДатаГраница", новый Граница(КонецДня(Дата),ВидГраницы.Включая));
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НДСПредъявленныйОстатки.СчетФактура,
	|	НДСПредъявленныйОстатки.СтавкаНДС,
	|	НДСПредъявленныйОстатки.Поставщик,
	|	НДСПредъявленныйОстатки.ДоговорКонтрагента,
	|	НДСПредъявленныйОстатки.ИсправленныйСчетФактура,
	|	НДСПредъявленныйОстатки.СуммаБезНДСОстаток КАК СуммаБезНДС,
	|	НДСПредъявленныйОстатки.НДСОстаток КАК НДС
	|ИЗ
	|	РегистрНакопления.НДСПредъявленный.Остатки(
	|			&ДатаГраница,
	|			Организация = &Организация
	|				И ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.АвансыВыданные)) КАК НДСПредъявленныйОстатки";
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(Результат.Выгрузить(), ТаблицаРезультатов);
	КонецЕсли;
	
КонецПроцедуры

// Заполнение табличной части "Вычет НДС с авансов выданных"
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// Заполнение табличной части "Вычет НДС по налоговому агенту"

// Процедура выполняет автоматическое заполнение табличной части документа
// Вызывается из процедуры КоманднаяПанельВычетПоПриобретеннымЦенностямЗаполнить
//
Процедура ЗаполнитьСтроки_ВычетНДСПоНалоговомуАгенту(ОтменитьПроведение = Ложь, Сообщать = Истина, СтрокаСообщения = "", ОшибкаЗаполнения = Ложь, УдалятьНезаполненные = Ложь) Экспорт
	
	Если Проведен Тогда
		Если ОтменитьПроведение Тогда
			Записать(РежимЗаписиДокумента.ОтменаПроведения);
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ПредъявленНДСКВычету0 Тогда
		Дерево_НДСкВычету = ЗаполнитьНДСПоНалоговомуАгентуКВычетуПоДаннымРегистраНДСПредъявленный();
	Иначе
		Дерево_НДСкВычету = ЗаполнитьНДСПоНалоговомуАгентуКВычетуПоДаннымРегистраНДСПредъявленныйРеализация0();
	КонецЕсли;
	
	Если Дерево_НДСкВычету.Строки.Количество()=0 Тогда
		// Дальнейшая обработка не требуется, не обнаружен НДС, который может быть принят к вычету.
		ВычетНДСПоНалоговомуАгенту.Очистить();
		Возврат;
	КонецЕсли;
	
	СписокСчетовФактур = ОбщегоНазначения.УдалитьПовторяющиесяЭлементыМассива(Дерево_НДСкВычету.Строки.ВыгрузитьКолонку("СчетФактура"),Истина);
	
	// Определить оплаты поступления
	РаспределенныеОплаты = ПолучитьДанныеОРаспределенныхОплатахПоСпискуСФ(СписокСчетовФактур);
	
	ИсключитьУчтенныеОплатыНалоговогоАгента(РаспределенныеОплаты);
	
	ТаблицаРезультатов = ВычетНДСПоНалоговомуАгенту.ВыгрузитьКолонки();
	
	РаспределитьОплатыПоДеревуСФ(Дерево_НДСкВычету, ТаблицаРезультатов, СписокСчетовФактур, РаспределенныеОплаты,);
		
	ИсключитьНДСНалоговогоАгентаНеоплаченныйВБюджет(ТаблицаРезультатов);
	
	ПолучитьДанныеОДокументахОплатыАгентскогоНДСВБюджет(ТаблицаРезультатов);	
	
	ОтфактурованныеВычеты = ОпределитьНаличиеСчетовФактурНалоговогоАгента(ТаблицаРезультатов);
	
	СтруктураОтбора = Новый Структура("ДокументОплаты, ДоговорКонтрагента");
	
	ВычетНДСПоНалоговомуАгенту.Очистить();
	Для Каждого СтрокаТаблицы Из ТаблицаРезультатов Цикл
		
		ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаТаблицы);
		МассивСчетовФактур = ОтфактурованныеВычеты.НайтиСтроки(СтруктураОтбора);
		
		Если МассивСчетовФактур.Количество() <> 0 Тогда
			
			НоваяСтрока = ВычетНДСПоНалоговомуАгенту.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			Если МассивСчетовФактур[0].Исправление Тогда
				НоваяСтрока.ИсправленныйСчетФактура = МассивСчетовФактур[0].Ссылка;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
		
	// Удаление строк, в которых есть незаполненные обязательные поля
	Если УдалятьНезаполненные Тогда
		ОбработкаТабличныхЧастейСервер.УдалитьНезаполненныеСтроки(ТаблицаРезультатов, "ВидЦенности, СчетФактура, СтавкаНДС, Поставщик, ДоговорКонтрагента, ДокументОплаты, ДатаОплаты");
	КонецЕсли;
	
КонецПроцедуры

Функция ОпределитьНаличиеСчетовФактурНалоговогоАгента(ТаблицаДокументов)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументОплаты", ТаблицаДокументов.ВыгрузитьКолонку("ДокументОплаты"));
	Запрос.УстановитьПараметр("ДоговорКонтрагента", ТаблицаДокументов.ВыгрузитьКолонку("ДоговорКонтрагента"));
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СчетФактураВыданный.ДокументОснование КАК ДокументОплаты,
	|	МАКСИМУМ(СчетФактураВыданный.Ссылка) КАК Ссылка,
	|	МАКСИМУМ(СчетФактураВыданный.НомерИсправления) КАК НомерИсправления,
	|	МАКСИМУМ(СчетФактураВыданный.Исправление) КАК Исправление,
	|	МАКСИМУМ(СчетФактураВыданный.ДоговорКонтрагента) КАК ДоговорКонтрагента
	|ИЗ
	|	Документ.СчетФактураВыданный КАК СчетФактураВыданный
	|ГДЕ
	|	СчетФактураВыданный.ДокументОснование В(&ДокументОплаты)
	|	И СчетФактураВыданный.ДоговорКонтрагента В(&ДоговорКонтрагента)
	|	И СчетФактураВыданный.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НалоговыйАгент)
	|	И СчетФактураВыданный.Проведен = ИСТИНА
	|
	|СГРУППИРОВАТЬ ПО
	|	СчетФактураВыданный.ДокументОснование";
				 
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Вызывается из процедуры ЗаполнитьСтроки_ВычетНДСПоНалоговомуАгенту.
// Заполняет ТЧ ВычетНДСПоНалоговомуАгенту по данным регистра НДСПредъявленный
Функция ЗаполнитьНДСПоНалоговомуАгентуКВычетуПоДаннымРегистраНДСПредъявленный()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НДСПредъявленныйОстатки.Организация,
		|	НДСПредъявленныйОстатки.СчетФактура КАК СчетФактура,
		|	НДСПредъявленныйОстатки.ВидЦенности,
		|	НДСПредъявленныйОстатки.СтавкаНДС,
		|	НДСПредъявленныйОстатки.СчетУчетаНДС,
		|	НДСПредъявленныйОстатки.Поставщик,
		|	НДСПредъявленныйОстатки.ДоговорКонтрагента,
		|	СУММА(ЕСТЬNULL(НДСПредъявленныйОстатки.СуммаБезНДСОстаток, 0) - ЕСТЬNULL(НДСпоОСиНМАОстатки.СуммаБезНДСОстаток, 0) - ЕСТЬNULL(НДСПредъявленныйРеализация0Остатки.СуммаБезНДСОстаток, 0)) КАК КВычету_БезНДС,
		|	СУММА(ЕСТЬNULL(НДСПредъявленныйОстатки.НДСОстаток, 0) - ЕСТЬNULL(НДСПредъявленныйРеализация0Остатки.НДСОстаток, 0) - ЕСТЬNULL(НДСпоОСиНМАОстатки.НДСОстаток, 0)) КАК КВычету_НДС,
		|	СУММА(ЕСТЬNULL(НДСПредъявленныйОстатки.СуммаБезНДСОстаток, 0) - ЕСТЬNULL(НДСпоОСиНМАОстатки.СуммаБезНДСОстаток, 0) - ЕСТЬNULL(НДСПредъявленныйРеализация0Остатки.СуммаБезНДСОстаток, 0) + ЕСТЬNULL(НДСПредъявленныйОстатки.НДСОстаток, 0) - ЕСТЬNULL(НДСПредъявленныйРеализация0Остатки.НДСОстаток, 0) - ЕСТЬNULL(НДСпоОСиНМАОстатки.НДСОстаток, 0)) КАК КВычету_СНДС,
		|	ЕСТЬNULL(НДСПредъявленныйОстатки.СчетФактура.Дата, &Дата) КАК СчетФактураДата,
		|	6 КАК ПорядокОплаты
		|ИЗ
		|	РегистрНакопления.НДСПредъявленный.Остатки(
		|			&ДатаГраница,
		|			Организация = &Организация
		|				И ВидЦенности В (&ВидыЦенностей_НалоговыйАгент)
		|				И ДоговорКонтрагента <> ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)) КАК НДСПредъявленныйОстатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДСпоОСиНМА.Остатки(
		|				&ДатаГраница,
		|				Организация = &Организация
		|					И НДСВключенВСтоимость = ЛОЖЬ
		|					И ВидЦенности В (&ВидыЦенностей_НалоговыйАгент)
		|					И НеВлияетНаВычет = ЛОЖЬ) КАК НДСпоОСиНМАОстатки
		|		ПО НДСПредъявленныйОстатки.СчетФактура = НДСпоОСиНМАОстатки.СчетФактура
		|			И НДСПредъявленныйОстатки.ВидЦенности = НДСпоОСиНМАОстатки.ВидЦенности
		|			И НДСПредъявленныйОстатки.СтавкаНДС = НДСпоОСиНМАОстатки.СтавкаНДС
		|			И НДСПредъявленныйОстатки.СчетУчетаНДС = НДСпоОСиНМАОстатки.СчетУчетаНДС
		|			И (НДСпоОСиНМАОстатки.СуммаБезНДСОстаток + НДСпоОСиНМАОстатки.НДСОстаток > 0)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДСПредъявленныйРеализация0.Остатки(
		|				&ДатаГраница,
		|				Организация = &Организация
		|					И ВидЦенности В (&ВидыЦенностей_НалоговыйАгент)) КАК НДСПредъявленныйРеализация0Остатки
		|		ПО НДСПредъявленныйОстатки.СчетФактура = НДСПредъявленныйРеализация0Остатки.СчетФактура
		|			И НДСПредъявленныйОстатки.ВидЦенности = НДСПредъявленныйРеализация0Остатки.ВидЦенности
		|			И НДСПредъявленныйОстатки.СтавкаНДС = НДСПредъявленныйРеализация0Остатки.СтавкаНДС
		|			И НДСПредъявленныйОстатки.СчетУчетаНДС = НДСПредъявленныйРеализация0Остатки.СчетУчетаНДС
		|			И (НДСПредъявленныйРеализация0Остатки.СуммаБезНДСОстаток + НДСПредъявленныйРеализация0Остатки.НДСОстаток > 0)
		|ГДЕ
		|	(НДСПредъявленныйОстатки.СуммаБезНДСОстаток > 0
		|			ИЛИ НДСПредъявленныйОстатки.НДСОстаток > 0)
		|
		|СГРУППИРОВАТЬ ПО
		|	НДСПредъявленныйОстатки.Организация,
		|	НДСПредъявленныйОстатки.СчетФактура,
		|	НДСПредъявленныйОстатки.ВидЦенности,
		|	НДСПредъявленныйОстатки.СтавкаНДС,
		|	НДСПредъявленныйОстатки.СчетУчетаНДС,
		|	НДСПредъявленныйОстатки.Поставщик,
		|	НДСПредъявленныйОстатки.ДоговорКонтрагента,
		|	ЕСТЬNULL(НДСПредъявленныйОстатки.СчетФактура.Дата, &Дата)
		|
		|ИМЕЮЩИЕ
		|	СУММА(ЕСТЬNULL(НДСПредъявленныйОстатки.СуммаБезНДСОстаток, 0) - ЕСТЬNULL(НДСпоОСиНМАОстатки.СуммаБезНДСОстаток, 0) - ЕСТЬNULL(НДСПредъявленныйРеализация0Остатки.СуммаБезНДСОстаток, 0) + ЕСТЬNULL(НДСПредъявленныйОстатки.НДСОстаток, 0) - ЕСТЬNULL(НДСПредъявленныйРеализация0Остатки.НДСОстаток, 0) - ЕСТЬNULL(НДСпоОСиНМАОстатки.НДСОстаток, 0)) > 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	СчетФактураДата
		|ИТОГИ
		|	СУММА(КВычету_БезНДС),
		|	СУММА(КВычету_НДС),
		|	СУММА(КВычету_СНДС)
		|ПО
		|	СчетФактура,
		|	ПорядокОплаты";

	Запрос.УстановитьПараметр("Организация",  Организация);
	Запрос.УстановитьПараметр("Дата", КонецДня(Дата));
	Запрос.УстановитьПараметр("ДатаГраница", Новый Граница(КонецДня(Дата),ВидГраницы.Включая));

	ВидыЦенностей_НалоговыйАгент = УчетНДС.ВидыЦенностиНалоговыйАгентПоступление();
	
	Запрос.УстановитьПараметр("ВидыЦенностей_НалоговыйАгент", ВидыЦенностей_НалоговыйАгент);
	
	Возврат Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	
КонецФункции // ЗаполнитьНДСКВычетуПоДаннымРегистраНДСПредъявленный()

// Вызывается из процедуры ЗаполнитьСтроки_ВычетНДСПоНалоговомуАгенту.
// Заполняет ТЧ ВычетНДСПоНалоговомуАгенту по данным регистра НДСПредъявленный
Функция ЗаполнитьНДСПоНалоговомуАгентуКВычетуПоДаннымРегистраНДСПредъявленныйРеализация0()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НДСПредъявленныйРеализация0Остатки.Организация,
		|	НДСПредъявленныйРеализация0Остатки.СчетФактура КАК СчетФактура,
		|	НДСПредъявленныйРеализация0Остатки.ВидЦенности,
		|	НДСПредъявленныйРеализация0Остатки.СтавкаНДС,
		|	НДСПредъявленныйРеализация0Остатки.СчетУчетаНДС,
		|	НДСПредъявленныйОстатки.Поставщик,
		|	НДСПредъявленныйОстатки.ДоговорКонтрагента,
		|	НДСПредъявленныйРеализация0Остатки.ДокументОтгрузки,
		|	НДСПредъявленныйРеализация0Остатки.Состояние,
		|	СУММА(ЕСТЬNULL(НДСПредъявленныйРеализация0Остатки.СуммаБезНДСОстаток, 0)) КАК КВычету_БезНДС,
		|	СУММА(ЕСТЬNULL(НДСПредъявленныйРеализация0Остатки.НДСОстаток, 0)) КАК КВычету_НДС,
		|	СУММА(ЕСТЬNULL(НДСПредъявленныйРеализация0Остатки.СуммаБезНДСОстаток, 0) + ЕСТЬNULL(НДСПредъявленныйРеализация0Остатки.НДСОстаток, 0)) КАК КВычету_СНДС,
		|	ЕСТЬNULL(НДСПредъявленныйРеализация0Остатки.СчетФактура.Дата, &Дата) КАК СчетФактураДата,
		|	6 КАК ПорядокОплаты
		|ИЗ
		|	РегистрНакопления.НДСПредъявленныйРеализация0.Остатки(
		|			&ДатаГраница,
		|			Организация = &Организация
		|				И ВидЦенности В (&ВидыЦенностей_НалоговыйАгент)
		|				И Состояние В (&ОтрабатываемыеСостояния)) КАК НДСПредъявленныйРеализация0Остатки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.НДСПредъявленный.Остатки(
		|				&ДатаГраница,
		|				Организация = &Организация
		|					И ВидЦенности В (&ВидыЦенностей_НалоговыйАгент)
		|					И ДоговорКонтрагента <> ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)) КАК НДСПредъявленныйОстатки
		|		ПО НДСПредъявленныйРеализация0Остатки.СчетФактура = НДСПредъявленныйОстатки.СчетФактура
		|			И НДСПредъявленныйРеализация0Остатки.ВидЦенности = НДСПредъявленныйОстатки.ВидЦенности
		|			И НДСПредъявленныйРеализация0Остатки.СтавкаНДС = НДСПредъявленныйОстатки.СтавкаНДС
		|			И НДСПредъявленныйРеализация0Остатки.СчетУчетаНДС = НДСПредъявленныйОстатки.СчетУчетаНДС
		|			И (НДСПредъявленныйОстатки.СуммаБезНДСОстаток + НДСПредъявленныйОстатки.НДСОстаток > 0)
		|ГДЕ
		|	(НДСПредъявленныйОстатки.СуммаБезНДСОстаток > 0
		|			ИЛИ НДСПредъявленныйОстатки.НДСОстаток > 0)
		|
		|СГРУППИРОВАТЬ ПО
		|	НДСПредъявленныйРеализация0Остатки.Организация,
		|	НДСПредъявленныйРеализация0Остатки.СчетФактура,
		|	НДСПредъявленныйРеализация0Остатки.ВидЦенности,
		|	НДСПредъявленныйРеализация0Остатки.СтавкаНДС,
		|	НДСПредъявленныйРеализация0Остатки.СчетУчетаНДС,
		|	НДСПредъявленныйОстатки.Поставщик,
		|	НДСПредъявленныйОстатки.ДоговорКонтрагента,
		|	НДСПредъявленныйРеализация0Остатки.ДокументОтгрузки,
		|	НДСПредъявленныйРеализация0Остатки.Состояние,
		|	ЕСТЬNULL(НДСПредъявленныйРеализация0Остатки.СчетФактура.Дата, &Дата)
		|
		|УПОРЯДОЧИТЬ ПО
		|	СчетФактураДата
		|ИТОГИ
		|	СУММА(КВычету_БезНДС),
		|	СУММА(КВычету_НДС),
		|	СУММА(КВычету_СНДС)
		|ПО
		|	СчетФактура,
		|	ПорядокОплаты";

	Запрос.УстановитьПараметр("Организация",  Организация);
	Запрос.УстановитьПараметр("Дата", КонецДня(Дата));
	Запрос.УстановитьПараметр("ДатаГраница", Новый Граница(КонецДня(Дата),ВидГраницы.Включая));

	ВидыЦенностей_НалоговыйАгент = УчетНДС.ВидыЦенностиНалоговыйАгентПоступление();
	
	Запрос.УстановитьПараметр("ВидыЦенностей_НалоговыйАгент", ВидыЦенностей_НалоговыйАгент);
	
	ОтрабатываемыеСостояния = Новый СписокЗначений;
	ОтрабатываемыеСостояния.Добавить(Перечисления.НДССостоянияРеализация0.ПодтвержденаРеализация0);
	ОтрабатываемыеСостояния.Добавить(Перечисления.НДССостоянияРеализация0.НеПодтвержденаРеализация0);
	
	Запрос.УстановитьПараметр("ОтрабатываемыеСостояния",ОтрабатываемыеСостояния);
	
	Возврат Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	
КонецФункции // ЗаполнитьНДСКВычетуПоДаннымРегистраНДСПредъявленный()

Процедура ИсключитьНДСНалоговогоАгентаНеоплаченныйВБюджет(ТаблицаРезультатов)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТаблицаРезультатов", ТаблицаРезультатов);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ДатаГраница", Новый Граница(КонецДня(Дата), ВидГраницы.Включая));
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаРезультатов.Поставщик КАК Поставщик,
	|	ТаблицаРезультатов.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ТаблицаРезультатов.ДокументОплаты КАК ДокументОплаты
	|ПОМЕСТИТЬ ТаблицаРезультатов
	|ИЗ
	|	&ТаблицаРезультатов КАК ТаблицаРезультатов
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Поставщик,
	|	ДоговорКонтрагента,
	|	ДокументОплаты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Субконто1 КАК Поставщик,
	|	ХозрасчетныйОстатки.Субконто2 КАК ДоговорКонтрагента,
	|	ХозрасчетныйОстатки.Субконто3 КАК ДокументОплаты,
	|	ХозрасчетныйОстатки.СуммаОстатокКт КАК НДС
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&ДатаГраница,
	|			Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДСНалоговогоАгента),
	|			,
	|			Организация = &Организация
	|				И (Субконто1, Субконто2, Субконто3) В
	|					(ВЫБРАТЬ
	|						ТаблицаРезультатов.Поставщик,
	|						ТаблицаРезультатов.ДоговорКонтрагента,
	|						ТаблицаРезультатов.ДокументОплаты
	|					ИЗ
	|						ТаблицаРезультатов КАК ТаблицаРезультатов)) КАК ХозрасчетныйОстатки
	|ГДЕ
	|	ХозрасчетныйОстатки.СуммаОстатокКт > 0";
	
	НеоплаченныйНДС = Запрос.Выполнить().Выгрузить();
	
	Отбор = Новый Структура("ДокументОплаты, Поставщик, ДоговорКонтрагента");

	СтрокиКУдалению = Новый Массив;
	
	Для Каждого СтрокаТаблицы Из НеоплаченныйНДС Цикл
		
		ЗаполнитьЗначенияСвойств(Отбор, СтрокаТаблицы);
		СтрокиОтбора = ТаблицаРезультатов.НайтиСтроки(Отбор);
		
		Для Каждого СтрокаОтбора Из СтрокиОтбора Цикл
			
			Если СтрокаОтбора.НДС <> 0 Тогда
				НДС = Мин(СтрокаТаблицы.НДС, СтрокаОтбора.НДС);
				СтрокаОтбора.СуммаБезНДС = СтрокаОтбора.СуммаБезНДС - Окр(СтрокаОтбора.СуммаБезНДС*НДС/СтрокаОтбора.НДС, 2);
				СтрокаОтбора.НДС = СтрокаОтбора.НДС - НДС;
				
				СтрокаТаблицы.НДС = СтрокаТаблицы.НДС - НДС;
			КонецЕсли;
			
			Если СтрокаОтбора.СуммаБезНДС = 0 Или СтрокаОтбора.НДС = 0 Тогда
				СтрокиКУдалению.Добавить(СтрокаОтбора);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;	
	
	Для Каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
		ТаблицаРезультатов.Удалить(СтрокаКУдалению);
	КонецЦикла;
	
КонецПроцедуры

Процедура ИсключитьУчтенныеОплатыНалоговогоАгента(РаспределенныеОплаты)
	
	РаспределенныеОплаты.Колонки.Добавить("ДатаОплатыДень",Новый ОписаниеТипов("Дата", , , Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)));
	Для Каждого Оплата Из РаспределенныеОплаты Цикл
		Оплата.ДатаОплатыДень = НачалоДня(Оплата.ДатаОплаты);
	КонецЦикла;	
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НДСЗаписиКнигиПокупокОбороты.СчетФактура,
	|	НДСЗаписиКнигиПокупокОбороты.ДокументОплаты,
	|	НДСЗаписиКнигиПокупокОбороты.ДатаОплаты КАК ДатаОплатыДень,
	|	СУММА(НДСЗаписиКнигиПокупокОбороты.СуммаБезНДСОборот) КАК СуммаБезНДСОборот,
	|	СУММА(НДСЗаписиКнигиПокупокОбороты.НДСОборот) КАК НДСОборот
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок.Обороты(
	|			,
	|			&ДатаГраница,
	|			,
	|			СчетФактура В (&СписокСчетовФактур)
	|				И Организация = &Организация
	|				И ВидЦенности В (&ВидыЦенностей_НалоговыйАгент)) КАК НДСЗаписиКнигиПокупокОбороты
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСЗаписиКнигиПокупокОбороты.ДокументОплаты,
	|	НДСЗаписиКнигиПокупокОбороты.СчетФактура,
	|	НДСЗаписиКнигиПокупокОбороты.ДатаОплаты";
	
	ВидыЦенностей_НалоговыйАгент = УчетНДС.ВидыЦенностиНалоговыйАгентПоступление();
	
	Запрос.УстановитьПараметр("Организация", 					Организация);
	Запрос.УстановитьПараметр("ВидыЦенностей_НалоговыйАгент", 	ВидыЦенностей_НалоговыйАгент);
	Запрос.УстановитьПараметр("СписокСчетовФактур", 			РаспределенныеОплаты.ВыгрузитьКолонку("СчетФактура"));
	Запрос.УстановитьПараметр("ДатаГраница", 					Новый Граница(КонецДня(Дата),ВидГраницы.Включая));
	
	СтруктураОтбора = Новый Структура("ДокументОплаты, СчетФактура, ДатаОплатыДень");
	
	Переиндексировать = Ложь;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ЗаполнитьЗначенияСвойств(СтруктураОтбора, Выборка);
		СтрокиОплат = РаспределенныеОплаты.НайтиСтроки(СтруктураОтбора);
		Если СтрокиОплат.Количество() > 0 Тогда
			СтрокиОплат[0].РаспределеннаяОплата = СтрокиОплат[0].РаспределеннаяОплата - Выборка.СуммаБезНДСОборот;
			Если СтрокиОплат[0].РаспределеннаяОплата <= 0 Тогда
				РаспределенныеОплаты.Удалить(СтрокиОплат[0]);
				Переиндексировать = Истина;
			КонецЕсли;			
		КонецЕсли;
	КонецЦикла;
	
	Если Переиндексировать Тогда
		Если РаспределенныеОплаты.Количество()>0 Тогда
			QueryId = Новый Массив(РаспределенныеОплаты.Количество());
			Для Счетчик=0 По РаспределенныеОплаты.Количество()-1 Цикл
				QueryId[Счетчик] = Счетчик;
			КонецЦикла; 
			РаспределенныеОплаты.ЗагрузитьКолонку(QueryId,"QueryId");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Заполнение табличной части "Вычет НДС по налоговому агенту"
////////////////////////////////////////////////////////////////////////////////


// Заполнение табличной части "Вычет НДС при изменении стоимости в сторону уменьшения"

Процедура ЗаполнитьСтроки_ВычетПриИзмененииСтоимостиВСторонуУменьшения(Сообщать) Экспорт
		
	Если Проведен Тогда
		Записать(РежимЗаписиДокумента.ОтменаПроведения);
	КонецЕсли;
	
	ОшибкаЗаполнения = Ложь;
	
	УчетнаяПолитикаНУ = ОбщегоНазначения.ПолучитьПараметрыУчетнойПолитики(Дата, ОшибкаЗаполнения, Организация);
	
	Если ОшибкаЗаполнения Тогда
		
		Если Сообщать Тогда
			СтрокаСообщения = "Не указаны параметры учетной политики ("+СокрЛП(Организация)+") на " + Формат(Дата, "ДЛФ=DD") + Символы.ПС 
				+ "Табличное поле «Вычет при изменении стоимости в сторону уменьшения» не может быть заполнено автоматически.";
			ОбщегоНазначения.СообщитьОбОшибке("Документ не заполнен:" + СтрокаСообщения, , Строка(Ссылка));
		КонецЕсли;
		
		Возврат;
		
	КонецЕсли;
    		
	ТаблицаРезультатов = ЗаполнитьВычетПриИзмененииСтоимостиПоДаннымРегистраНДСПредъявленный();
		
	Если ТаблицаРезультатов.Количество() = 0 Тогда
		// Дальнейшая обработка не требуется, не обнаружен НДС, который может быть принят к вычету.
		ВычетПриИзмененииСтоимостиВСторонуУменьшения.Очистить();
		Возврат;
	КонецЕсли;
		
	ВычетПриИзмененииСтоимостиВСторонуУменьшения.Загрузить(ТаблицаРезультатов);
	ВычетПриИзмененииСтоимостиВСторонуУменьшения.Сортировать("СчетФактура, Поставщик, ВидЦенности, СтавкаНДС, СчетУчетаНДС");
		
КонецПроцедуры	

Функция ЗаполнитьВычетПриИзмененииСтоимостиПоДаннымРегистраНДСПредъявленный()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ДатаГраница", Новый Граница(КонецДня(Дата), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ДатаСФНеБолее", КонецДня(Дата));

	Если УчетНДС.ПолучитьВерсиюПостановления(Дата) = 1 Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	НДСПредъявленныйОстатки.Организация,
		|	НДСПредъявленныйОстатки.СчетФактура КАК СчетФактура,
		|	НДСПредъявленныйОстатки.ВидЦенности,
		|	НДСПредъявленныйОстатки.СтавкаНДС,
		|	НДСПредъявленныйОстатки.СчетУчетаНДС,
		|	НДСПредъявленныйОстатки.Поставщик,
		|	НДСПредъявленныйОстатки.ИсправленныйСчетФактура,
		|	НДСПредъявленныйОстатки.СуммаБезНДСОстаток КАК СуммаБезНДС,
		|	НДСПредъявленныйОстатки.НДСОстаток КАК НДС
		|ИЗ
		|	РегистрНакопления.НДСПредъявленный.Остатки(
		|			&ДатаГраница,
		|			Организация = &Организация
		|				И СчетФактура ССЫЛКА Документ.КорректировкаРеализации) КАК НДСПредъявленныйОстатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданный
		|		ПО НДСПредъявленныйОстатки.СчетФактура = СчетФактураВыданный.ДокументОснование
		|ГДЕ
		|	(НЕ СчетФактураВыданный.Ссылка ЕСТЬ NULL )
		|	И СчетФактураВыданный.Ссылка.Дата <= &ДатаСФНеБолее"; 
		
	ИначеЕсли Дата < '20150101' Тогда
			
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	НДСПредъявленныйОстатки.Организация,
		|	НДСПредъявленныйОстатки.СчетФактура КАК СчетФактура,
		|	НДСПредъявленныйОстатки.ВидЦенности,
		|	НДСПредъявленныйОстатки.СтавкаНДС,
		|	НДСПредъявленныйОстатки.СчетУчетаНДС,
		|	НДСПредъявленныйОстатки.Поставщик,
		|	НДСПредъявленныйОстатки.ИсправленныйСчетФактура,
		|	НДСПредъявленныйОстатки.СуммаБезНДСОстаток КАК СуммаБезНДС,
		|	ВЫБОР
		|		КОГДА НДСПредъявленныйОстатки.ИсправленныйСчетФактура ССЫЛКА Документ.КорректировкаРеализации
		|			ТОГДА НДСПредъявленныйОстатки.ИсправленныйСчетФактура
		|		ИНАЧЕ НДСПредъявленныйОстатки.СчетФактура
		|	КОНЕЦ КАК ДокументОснование,
		|	НДСПредъявленныйОстатки.СуммаБезНДСОстаток,
		|	НДСПредъявленныйОстатки.НДСОстаток
		|ПОМЕСТИТЬ НДСПредъявленныйОстатки
		|ИЗ
		|	РегистрНакопления.НДСПредъявленный.Остатки(
		|			&ДатаГраница,
		|			Организация = &Организация
		|				И СчетФактура ССЫЛКА Документ.КорректировкаРеализации) КАК НДСПредъявленныйОстатки
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ДокументОснование
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НДСПредъявленныйОстатки.Организация КАК Организация,
		|	НДСПредъявленныйОстатки.СчетФактура КАК СчетФактура,
		|	НДСПредъявленныйОстатки.ВидЦенности КАК ВидЦенности,
		|	НДСПредъявленныйОстатки.СтавкаНДС КАК СтавкаНДС,
		|	НДСПредъявленныйОстатки.СчетУчетаНДС КАК СчетУчетаНДС,
		|	НДСПредъявленныйОстатки.Поставщик КАК Поставщик,
		|	НДСПредъявленныйОстатки.ИсправленныйСчетФактура КАК ИсправленныйСчетФактура,
		|	НДСПредъявленныйОстатки.СуммаБезНДСОстаток КАК СуммаБезНДС,
		|	НДСПредъявленныйОстатки.НДСОстаток КАК НДС
		|ИЗ
		|	НДСПредъявленныйОстатки КАК НДСПредъявленныйОстатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданный
		|		ПО НДСПредъявленныйОстатки.ДокументОснование = СчетФактураВыданный.ДокументОснование
		|ГДЕ
		|	НЕ СчетФактураВыданный.Ссылка ЕСТЬ NULL 
		|	И НЕ СчетФактураВыданный.Ссылка.ПометкаУдаления
		|	И СчетФактураВыданный.Ссылка.ВидСчетаФактуры <> ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаАванс)
		|	И СчетФактураВыданный.Ссылка.Выставлен
		|	И СчетФактураВыданный.Ссылка.ДатаВыставления <= &ДатаСФНеБолее";
		
	Иначе
		
		// В соответствии с письмом Минфина от 20 января 2015 г. N 03-07-05/1271
		// при уменьшении стоимости отгруженных товаров (выполненных работ, оказанных услуг) продавец, 
		// имеет право принять к вычету налог на добавленную стоимость в сумме разницы между суммами налога, 
		// на основании первичных учетных документов, подтверждающих согласие (факт уведомления) покупателя на уменьшение стоимости 
		// товаров (работ, услуг). При этом указанные первичные документы подлежат регистрации продавцом в книге покупок
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	НДСПредъявленныйОстатки.Организация,
		|	НДСПредъявленныйОстатки.СчетФактура,
		|	НДСПредъявленныйОстатки.ВидЦенности,
		|	НДСПредъявленныйОстатки.СтавкаНДС,
		|	НДСПредъявленныйОстатки.СчетУчетаНДС,
		|	НДСПредъявленныйОстатки.Поставщик,
		|	НДСПредъявленныйОстатки.ИсправленныйСчетФактура,
		|	ВЫБОР
		|		КОГДА НДСПредъявленныйОстатки.ИсправленныйСчетФактура ССЫЛКА Документ.КорректировкаРеализации
		|			ТОГДА НДСПредъявленныйОстатки.ИсправленныйСчетФактура
		|		ИНАЧЕ НДСПредъявленныйОстатки.СчетФактура
		|	КОНЕЦ КАК ДокументОснование,
		|	НДСПредъявленныйОстатки.СуммаБезНДСОстаток,
		|	НДСПредъявленныйОстатки.НДСОстаток
		|ПОМЕСТИТЬ НДСПредъявленныйОстатки
		|ИЗ
		|	РегистрНакопления.НДСПредъявленный.Остатки(
		|			&ДатаГраница,
		|			Организация = &Организация
		|				И СчетФактура ССЫЛКА Документ.КорректировкаРеализации) КАК НДСПредъявленныйОстатки
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ДокументОснование
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НДСПредъявленныйОстатки.Организация КАК Организация,
		|	НДСПредъявленныйОстатки.СчетФактура КАК СчетФактура,
		|	НДСПредъявленныйОстатки.ВидЦенности КАК ВидЦенности,
		|	НДСПредъявленныйОстатки.СтавкаНДС КАК СтавкаНДС,
		|	НДСПредъявленныйОстатки.СчетУчетаНДС КАК СчетУчетаНДС,
		|	НДСПредъявленныйОстатки.Поставщик КАК Поставщик,
		|	НДСПредъявленныйОстатки.ИсправленныйСчетФактура КАК ИсправленныйСчетФактура,
		|	НДСПредъявленныйОстатки.СуммаБезНДСОстаток КАК СуммаБезНДС,
		|	НДСПредъявленныйОстатки.НДСОстаток КАК НДС
		|ИЗ
		|	НДСПредъявленныйОстатки КАК НДСПредъявленныйОстатки";
		
	КонецЕсли;
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

// Проверяет правильность заполнения шапки документа.
// Если какой-то из реквизитов шапки, влияющий на проведение, не заполнен или
// заполнен не корректно, то выставляется флаг отказа в проведении.
// Проверяется также правильность заполнения реквизитов ссылочных полей документа.
// Проверка выполняется по объекту и по выборке из результата запроса по шапке.
//
// Параметры: 
//  СтруктураШапкиДокумента - структура, содержащая рексвизиты шапки документа и результаты запроса по шапке,
//  Отказ                   - флаг отказа в проведении,
//  Заголовок               - строка, заголовок сообщения об ошибке проведения.
//
Процедура ПроверитьЗаполнениеШапки(Отказ, Заголовок)

	// Укажем, что надо проверить:
	СтруктураОбязательныхПолей = Новый Структура("Организация");
	
	// Теперь вызовем общую процедуру проверки.
	ЗаполнениеДокументов.ПроверитьЗаполнениеШапкиДокумента(ЭтотОбъект, СтруктураОбязательныхПолей, Отказ, Заголовок);
	
КонецПроцедуры // ПроверитьЗаполнениеШапки()

// Выгружает результат запроса в табличную часть, добавляет ей необходимые колонки для проведения.
//
//
Функция ПодготовитьТаблицуПоВычетам(РезультатЗапросаПоВычетам, СтруктураШапкиДокумента, Отказ, Заголовок)

	ТаблицаВычетов = РезультатЗапросаПоВычетам.Выгрузить();
	
	МоментОпределенияНалоговойБазыНДСОплата = Неопределено;
	НачалоНалоговогоПериода = ?(СтруктураШапкиДокумента.НДСНалоговыйПериод = Перечисления.Периодичность.Квартал, НачалоКвартала(СтруктураШапкиДокумента.Дата), НачалоМесяца(СтруктураШапкиДокумента.Дата));
	КонецНалоговогоПериода = ?(СтруктураШапкиДокумента.НДСНалоговыйПериод = Перечисления.Периодичность.Квартал, КонецКвартала(СтруктураШапкиДокумента.Дата), КонецМесяца(СтруктураШапкиДокумента.Дата));
	
	Для Каждого СтрокаТаблицы Из ТаблицаВычетов Цикл
		Если Не ЗначениеЗаполнено(СтрокаТаблицы.ДатаСчетаФактуры) Тогда
			СтрокаТаблицы.ДатаСчетаФактуры = СтруктураШапкиДокумента.Дата;
		КонецЕсли;
							
		Если СтрокаТаблицы.ДатаОплаты = '00010101' Тогда
			Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.ДатаДокументаОплаты) Тогда
				Если (СтруктураШапкиДокумента.Дата < '20060101') Тогда
					СтрокаТаблицы.ДатаОплаты = СтруктураШапкиДокумента.Дата;
				ИначеЕсли СтрокаТаблицы.ДатаСчетаФактуры < '20060101' Тогда
					Если МоментОпределенияНалоговойБазыНДСОплата = Неопределено Тогда
						УчетнаяПолитикаНУ = ОбщегоНазначения.ПолучитьПараметрыУчетнойПолитикиРегл('20051231', СтруктураШапкиДокумента.Организация);
                        Если НЕ ЗначениеЗаполнено(УчетнаяПолитикаНУ) Тогда
							Отказ = Истина;
						КонецЕсли; 
						Если Не Отказ Тогда
							МоментОпределенияНалоговойБазыНДСОплата = (УчетнаяПолитикаНУ.МоментОпределенияНалоговойБазыНДС = Перечисления.МоментыОпределенияНалоговойБазыНДС.ПоОплате);
						Иначе
							МоментОпределенияНалоговойБазыНДСОплата = Ложь;
						КонецЕсли;
					КонецЕсли;
					Если МоментОпределенияНалоговойБазыНДСОплата Тогда
						СтрокаТаблицы.ДатаОплаты = СтруктураШапкиДокумента.Дата;
					КонецЕсли;
				КонецЕсли;
			Иначе
				СтрокаТаблицы.ДатаОплаты = СтрокаТаблицы.ДатаДокументаОплаты;
			КонецЕсли;
		КонецЕсли; 
		
		Если СтруктураШапкиДокумента.ПредъявленНДСКВычету0 Тогда
			Если СтруктураШапкиДокумента.Дата < '20060101' Тогда
				СтрокаТаблицы.ДатаСобытия = Макс(?(НЕ ЗначениеЗаполнено(СтрокаТаблицы.ДатаОплаты), СтруктураШапкиДокумента.Дата, СтрокаТаблицы.ДатаОплаты), СтрокаТаблицы.ДатаСчетаФактуры, ?(НЕ ЗначениеЗаполнено(СтрокаТаблицы.ДокументОтгрузкиДата), СтруктураШапкиДокумента.Дата, СтрокаТаблицы.ДокументОтгрузкиДата));
			Иначе
				СтрокаТаблицы.ДатаСобытия = ?(НЕ ЗначениеЗаполнено(СтрокаТаблицы.ДокументОтгрузкиДата), СтруктураШапкиДокумента.Дата, СтрокаТаблицы.ДокументОтгрузкиДата);
				Если Не (СтрокаТаблицы.ДатаСобытия >= НачалоНалоговогоПериода И СтрокаТаблицы.ДатаСобытия <= КонецНалоговогоПериода) Тогда
					СтрокаТаблицы.ДатаСобытия = СтруктураШапкиДокумента.Дата;
				КонецЕсли;
			КонецЕсли;
		Иначе
			Если СтруктураШапкиДокумента.Дата < '20060101' Тогда
				СтрокаТаблицы.ДатаСобытия = Макс(?(СтрокаТаблицы.ДатаОплаты = '00010101', СтруктураШапкиДокумента.Дата, СтрокаТаблицы.ДатаОплаты), СтрокаТаблицы.ДатаСчетаФактуры);
			Иначе
				Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.ИсправленныйСчетФактура) Тогда
					Если СтрокаТаблицы.ДатаСчетаФактуры >= НачалоНалоговогоПериода И СтрокаТаблицы.ДатаСчетаФактуры <= КонецНалоговогоПериода Тогда
						СтрокаТаблицы.ДатаСобытия = СтрокаТаблицы.ДатаСчетаФактуры;
					Иначе
						СтрокаТаблицы.ДатаСобытия = СтруктураШапкиДокумента.Дата;
					КонецЕсли;
				Иначе
					Если СтрокаТаблицы.ИсправленныйСчетФактураДата >= НачалоНалоговогоПериода 
						И СтрокаТаблицы.ИсправленныйСчетФактураДата <= КонецНалоговогоПериода Тогда
						СтрокаТаблицы.ДатаСобытия = СтрокаТаблицы.ИсправленныйСчетФактураДата;
					Иначе
						СтрокаТаблицы.ДатаСобытия = СтруктураШапкиДокумента.Дата;
					КонецЕсли;
				КонецЕсли;	
			КонецЕсли;
		КонецЕсли; 
		
		Если СтрокаТаблицы.ВидЦенности = Перечисления.ВидыЦенностей.СМРСобственнымиСилами Тогда
			// Контрагент по данным виждам ценностей не указывается
			СтрокаТаблицы.Поставщик = Неопределено;
		КонецЕсли; 
		
		Если Не СтрокаТаблицы.ЗаписьДополнительногоЛиста Тогда
			СтрокаТаблицы.КорректируемыйПериод = '00010101000000';
		КонецЕсли;
		
	КонецЦикла; 
	
	ТаблицаВычетов.Колонки.Добавить("Событие");
	ТаблицаВычетов.ЗаполнитьЗначения( ?(СтруктураШапкиДокумента.ПредъявленНДСКВычету0, Перечисления.СобытияПоНДСПокупки.ПредъявленНДСКВычету0, Перечисления.СобытияПоНДСПокупки.ПредъявленНДСКВычету),"Событие");
	
	Возврат ТаблицаВычетов;

КонецФункции // ПодготовитьТаблицуПоОплатам()

// Выгружает результат запроса в табличную часть, добавляет ей необходимые колонки для проведения.
//
//
Функция ПодготовитьТаблицуПоАвансам(РезультатЗапросаПоАвансам, СтруктураШапкиДокумента)
	
	ТаблицаПоАвансам = РезультатЗапросаПоАвансам.Выгрузить();
	
	ТаблицаПоАвансам.Колонки.Добавить("Событие");
	ТаблицаПоАвансам.ЗаполнитьЗначения( ?(СтруктураШапкиДокумента.ПредъявленНДСКВычету0, 
		Перечисления.СобытияПоНДСПокупки.ПредъявленНДСКВычету0, Перечисления.СобытияПоНДСПокупки.ПредъявленНДСКВычету), "Событие");
	
	ТаблицаПоАвансам.Колонки.Добавить("ВидЦенности", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыЦенностей"));
	ТаблицаПоАвансам.ЗаполнитьЗначения(Перечисления.ВидыЦенностей.АвансыПолученные, "ВидЦенности");
	
	ТаблицаПоАвансам.Колонки.Добавить("КодВидаОперации", ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(10));
	Если СтруктураШапкиДокумента.Дата >= '20150101' Тогда
		ТаблицаПоАвансам.ЗаполнитьЗначения("22", "КодВидаОперации");
	КонецЕсли;
	
	Для каждого СтрокаТаблицы из ТаблицаПоАвансам Цикл
		
		Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.ДатаОплаты) Тогда
			СтрокаТаблицы.ДатаОплаты = СтруктураШапкиДокумента.Дата;
		КонецЕсли; 
		
		Если НЕ СтрокаТаблицы.ЗаписьДополнительногоЛиста Тогда
			СтрокаТаблицы.КорректируемыйПериод = '00010101000000';
		КонецЕсли;
		
		Если СтрокаТаблицы.ВозвратАвансовПолученных Тогда
			СтрокаТаблицы.ВидЦенности = Перечисления.ВидыЦенностей.ВозвратАвансовПолученных;		                   		
		КонецЕсли; 
		
	КонецЦикла; 
	
	Возврат ТаблицаПоАвансам;
	
КонецФункции // ПодготовитьТаблицуПоОплатам()

// Выгружает результат запроса в табличную часть, добавляет ей необходимые колонки для проведения.
//
Функция ПодготовитьТаблицуПоАвансамВыданным(РезультатЗапросаПоАвансамВыданным, СтруктураШапкиДокумента)
	
	ТаблицаПоАвансамВыданным = РезультатЗапросаПоАвансамВыданным.Выгрузить();
	
	ТаблицаПоАвансамВыданным.Колонки.Добавить("Событие", Новый ОписаниеТипов("ПеречислениеСсылка.СобытияПоНДСПокупки"));
	ТаблицаПоАвансамВыданным.ЗаполнитьЗначения(Перечисления.СобытияПоНДСПокупки.ПредъявленНДСКВычету, "Событие");
	
	ТаблицаПоАвансамВыданным.Колонки.Добавить("ВидЦенности", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыЦенностей"));
	ТаблицаПоАвансамВыданным.ЗаполнитьЗначения(Перечисления.ВидыЦенностей.АвансыВыданные,"ВидЦенности");
	
	ТаблицаПоАвансамВыданным.Колонки.Добавить("ДатаСобытия", Новый ОписаниеТипов("Дата", , , Новый КвалификаторыДаты(ЧастиДаты.Дата)));
	
	ТаблицаПоАвансамВыданным.Колонки.Добавить("ДатаДокументаОплаты", Новый ОписаниеТипов("Дата", , , Новый КвалификаторыДаты(ЧастиДаты.Дата)));
	ТаблицаПоАвансамВыданным.Колонки.Добавить("НомерДокументаОплаты");
	
	НачалоНалоговогоПериода = ?(СтруктураШапкиДокумента.НДСНалоговыйПериод = Перечисления.Периодичность.Месяц, НачалоМесяца(СтруктураШапкиДокумента.Дата), НачалоКвартала(СтруктураШапкиДокумента.Дата));
	КонецНалоговогоПериода = ?(СтруктураШапкиДокумента.НДСНалоговыйПериод = Перечисления.Периодичность.Месяц, КонецМесяца(СтруктураШапкиДокумента.Дата), КонецКвартала(СтруктураШапкиДокумента.Дата));
	
	Для Каждого СтрокаТаблицы Из ТаблицаПоАвансамВыданным Цикл
		
		Если НЕ СтрокаТаблицы.ЗаписьДополнительногоЛиста Тогда
			СтрокаТаблицы.КорректируемыйПериод = '00010101000000';
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаТаблицы.ДатаСчетаФактуры) 
			И СтрокаТаблицы.ДатаСчетаФактуры >= НачалоНалоговогоПериода
			И СтрокаТаблицы.ДатаСчетаФактуры <= КонецНалоговогоПериода Тогда
			СтрокаТаблицы.ДатаСобытия = СтрокаТаблицы.ДатаСчетаФактуры;
		Иначе
			СтрокаТаблицы.ДатаСобытия = СтруктураШапкиДокумента.Дата;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаТаблицы.СчетФактура) Тогда
			СтрокаТаблицы.НомерДокументаОплаты = ОбщегоНазначения.ПолучитьНомерНаПечать(СтрокаТаблицы.СчетФактура);
			СтрокаТаблицы.ДатаДокументаОплаты = СтрокаТаблицы.ДатаСчетаФактуры;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ТаблицаПоАвансамВыданным;
	
КонецФункции // ПодготовитьТаблицуПоОплатам()

// Выгружает результат запроса в табличную часть, добавляет ей необходимые колонки для проведения.
//
//
Функция ПодготовитьТаблицуПоВычетамНалоговыйАгент(РезультатЗапросаПоВычетамНалоговыйАгент, СтруктураШапкиДокумента, Отказ, Заголовок)

	ТаблицаВычетовНалоговыйАгент = РезультатЗапросаПоВычетамНалоговыйАгент.Выгрузить();
	
	ВерсияПостановления1137 = УчетНДС.ВерсияПостановленияНДС1137(СтруктураШапкиДокумента.Дата);
	
	ТаблицаВычетовНалоговыйАгент.Колонки.Добавить("НомерДокументаОплаты");	
	
	Для Каждого СтрокаТаблицы из ТаблицаВычетовНалоговыйАгент Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.ДатаСчетаФактуры) Тогда
			СтрокаТаблицы.ДатаСчетаФактуры = СтруктураШапкиДокумента.Дата;
		КонецЕсли;
							
		Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.ДатаОплаты) Тогда
			СтрокаТаблицы.ДатаОплаты = СтрокаТаблицы.ДатаДокументаОплаты;
		КонецЕсли; 
		
		Если Не СтрокаТаблицы.ЗаписьДополнительногоЛиста Тогда
			СтрокаТаблицы.КорректируемыйПериод = '00010101000000';
		КонецЕсли;
		
		Если ВерсияПостановления1137 = 3 
			И ЗначениеЗаполнено(СтрокаТаблицы.ДокументОплатыНДС) Тогда
			СтрокаТаблицы.НомерДокументаОплаты = ОбщегоНазначения.ПолучитьНомерНаПечать(СтрокаТаблицы.ДокументОплатыНДС);
		КонецЕсли;			
		
	КонецЦикла; 
	
	ТаблицаВычетовНалоговыйАгент.Колонки.Добавить("Событие");
	ТаблицаВычетовНалоговыйАгент.ЗаполнитьЗначения( ?(СтруктураШапкиДокумента.ПредъявленНДСКВычету0, Перечисления.СобытияПоНДСПокупки.ПредъявленНДСКВычету0, Перечисления.СобытияПоНДСПокупки.ПредъявленНДСКВычету),"Событие");
	
	Возврат ТаблицаВычетовНалоговыйАгент;

КонецФункции // ПодготовитьТаблицуПоОплатам()

// Проверяет правильность заполнения строк табличной части.
//
//
Процедура ПроверитьЗаполнениеТабличнойЧастиПоВычетам(СтруктураШапкиДокумента,ТаблицаПоВычетам, Отказ, Заголовок)

	// Укажем, что надо проверить:
	СтруктураОбязательныхПолей = Новый Структура("ВидЦенности, СчетФактура, СтавкаНДС"); 
	
	Если СтруктураШапкиДокумента.ПредъявленНДСКВычету0 Тогда
		СтруктураОбязательныхПолей.Вставить("Состояние"); 
	КонецЕсли; 
	// Теперь вызовем общую процедуру проверки.
	ЗаполнениеДокументов.ПроверитьЗаполнениеТабличнойЧасти(ЭтотОбъект, "ВычетПоПриобретеннымЦенностям", СтруктураОбязательныхПолей, Отказ, Заголовок);
	
	//Проверка полей без прекращения проведения или по условию
	СтрокаСообщения = "Не заполнены документ оплаты и дата оплаты. В книге покупок по данной записи в качестве даты оплаты будет установлена дата текущего документа!";
	СтрокаСообщенияДокОтгрузки = "Не заполнен документ отгрузки при реализации товара по ставке НДС 0%!";
	СтрокаСообщенияПоставщик = "Не заполнено значение реквизита ""Поставщик""!";
	СтрокаСообщенияКорПериод = "Не заполнено значение реквизита ""Корректируемый период""!";
	СтрокаСообщенияСчетУчетаНДС = "Не заполнено значение реквизита ""Счет учета НДС""!";

	Для каждого СтрокаТаблицы из ТаблицаПоВычетам Цикл
		
		// Проверка на заполнение контрагента
		Если не СтрокаТаблицы.ВидЦенности = Перечисления.ВидыЦенностей.СМРСобственнымиСилами и НЕ ЗначениеЗаполнено(СтрокаТаблицы.Поставщик) Тогда
			СтрокаНачалаСообщенияОбОшибке = "В строке номер """+ СокрЛП(СтрокаТаблицы.НомерСтроки) + """ табличной части ""Вычет по приобретенным ценностям"" : ";
			ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке+СтрокаСообщенияПоставщик,Отказ,Заголовок);
		КонецЕсли; 
		
		// Проверка на заполнение счета учета НДС
		Если не СтрокаТаблицы.НДС = 0 и не ЗначениеЗаполнено(СтрокаТаблицы.СчетУчетаНДС) Тогда
			СтрокаНачалаСообщенияОбОшибке = "В строке номер """+ СокрЛП(СтрокаТаблицы.НомерСтроки) + """ табличной части ""Вычет по приобретенным ценностям"" : ";
			ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке+СтрокаСообщенияСчетУчетаНДС,Отказ,Заголовок);
		КонецЕсли; 
		
		Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.ДокументОплаты) и НЕ ЗначениеЗаполнено(СтрокаТаблицы.ДатаСобытия) Тогда
			СтрокаНачалаСообщенияОбОшибке = "В строке номер """+ СокрЛП(СтрокаТаблицы.НомерСтроки) + """ табличной части ""Вычет по приобретенным ценностям"" : ";
			ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке+СтрокаСообщения,,Заголовок,СтатусСообщения.Внимание);
		КонецЕсли;
		
		Если СтруктураШапкиДокумента.ПредъявленНДСКВычету0 и НЕ ЗначениеЗаполнено(СтрокаТаблицы.ДокументОтгрузки) Тогда
			СтрокаНачалаСообщенияОбОшибке = "В строке номер """+ СокрЛП(СтрокаТаблицы.НомерСтроки) + """ табличной части ""Вычет по приобретенным ценностям"" : ";
			ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке+СтрокаСообщенияДокОтгрузки,,Заголовок,СтатусСообщения.Внимание);
		КонецЕсли;
		
		Если СтрокаТаблицы.ЗаписьДополнительногоЛиста и НЕ ЗначениеЗаполнено(СтрокаТаблицы.КорректируемыйПериод) Тогда
			СтрокаНачалаСообщенияОбОшибке = "В строке номер """+ СокрЛП(СтрокаТаблицы.НомерСтроки) + """ табличной части ""Вычет по приобретенным ценностям"" : ";
			ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке+СтрокаСообщенияКорПериод,,Заголовок,СтатусСообщения.Внимание);
		КонецЕсли;
		
	КонецЦикла; 

КонецПроцедуры // ПроверитьЗаполнениеТабличнойЧастиТовары()

// Проверяет правильность заполнения строк табличной части.
//
//
Процедура ПроверитьЗаполнениеТабличнойЧастиПоАвансам(СтруктураШапкиДокумента, ТаблицаПоАвансам, Отказ, Заголовок)
	
	// Укажем, что надо проверить:
	СтруктураОбязательныхПолей = Новый Структура("Покупатель, СчетФактура, СтавкаНДС"); 
	Если СтруктураШапкиДокумента.ПредъявленНДСКВычету0 Тогда
		СтруктураОбязательныхПолей.Вставить("Состояние");
	КонецЕсли; 
	
	// Теперь вызовем общую процедуру проверки.
	ЗаполнениеДокументов.ПроверитьЗаполнениеТабличнойЧасти(ЭтотОбъект, "НДСсАвансов", СтруктураОбязательныхПолей, Отказ, Заголовок);
	
	//Проверка полей без прекращения проведения
	СтрокаСообщенияДокОтгрузки = "Не заполнен документ отгрузки при реализации товара по ставке НДС 0%!";
 	СтрокаСообщения = "Не заполнена дата зачета аванса. В качестве даты зачета аванса будет использоваться дата текущего документа!";
	СтрокаСообщенияКорПериод = "Не заполнено значение реквизита ""Корректируемый период""!";
	СтрокаСообщенияДоговор = "Не заполнено значение реквизита ""Договор контрагента""!";

	Для каждого СтрокаТаблицы из ТаблицаПоАвансам Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.ДатаСобытия) Тогда
			СтрокаНачалаСообщенияОбОшибке = "В строке номер """+ СокрЛП(СтрокаТаблицы.НомерСтроки) + """ табличной части ""Вычет НДС с полученных авансов"": ";
			ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке+СтрокаСообщения,,Заголовок,СтатусСообщения.Внимание);
		КонецЕсли;
		Если СтруктураШапкиДокумента.ПредъявленНДСКВычету0 и НЕ ЗначениеЗаполнено(СтрокаТаблицы.ДокументОтгрузки) Тогда
			СтрокаНачалаСообщенияОбОшибке = "В строке номер """+ СокрЛП(СтрокаТаблицы.НомерСтроки) + """ табличной части ""Вычет НДС с полученных авансов"": ";
			ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке+СтрокаСообщенияДокОтгрузки,,Заголовок,СтатусСообщения.Внимание);
		КонецЕсли;
		Если СтрокаТаблицы.ЗаписьДополнительногоЛиста и НЕ ЗначениеЗаполнено(СтрокаТаблицы.КорректируемыйПериод) Тогда
			СтрокаНачалаСообщенияОбОшибке = "В строке номер """+ СокрЛП(СтрокаТаблицы.НомерСтроки) + """ табличной части ""Вычет НДС с полученных авансов"": ";
			ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке+СтрокаСообщенияКорПериод,,Заголовок,СтатусСообщения.Внимание);
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.ДоговорКонтрагента) 
			И ТипЗнч(СтрокаТаблицы.СчетФактура) <> Тип("ДокументСсылка.СчетФактураВыданный") Тогда
			СтрокаНачалаСообщенияОбОшибке = "В строке номер """+ СокрЛП(СтрокаТаблицы.НомерСтроки) + """ табличной части ""Вычет НДС с полученных авансов"": ";
			ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + СтрокаСообщенияДоговор, ,Заголовок, СтатусСообщения.Внимание);	
		КонецЕсли;	
	КонецЦикла; 
	
КонецПроцедуры // ПроверитьЗаполнениеТабличнойЧастиТовары()

// Проверяет правильность заполнения строк табличной части.
//
Процедура ПроверитьЗаполнениеТабличнойЧастиПоАвансамВыданным(СтруктураШапкиДокумента, ТаблицаПоАвансамВыданным, Отказ, Заголовок)
	
	// Укажем, что надо проверить:
	СтруктураОбязательныхПолей = Новый Структура("Поставщик, ДоговорКонтрагента, СчетФактура, СтавкаНДС"); 
	
	// Теперь вызовем общую процедуру проверки.
	ЗаполнениеДокументов.ПроверитьЗаполнениеТабличнойЧасти(ЭтотОбъект, "НДСсАвансовВыданных", СтруктураОбязательныхПолей, Отказ, Заголовок);
	
	//Проверка полей без прекращения проведения
	СтрокаСообщенияКорПериод = "Не заполнено значение реквизита ""Корректируемый период""!";

	Для Каждого СтрокаТаблицы Из ТаблицаПоАвансамВыданным Цикл
		Если СтрокаТаблицы.ЗаписьДополнительногоЛиста И Не ЗначениеЗаполнено(СтрокаТаблицы.КорректируемыйПериод) Тогда
			СтрокаНачалаСообщенияОбОшибке = "В строке номер """+ СокрЛП(СтрокаТаблицы.НомерСтроки) + """ табличной части ""Вычет НДС с выданных авансов"": ";
			ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + СтрокаСообщенияКорПериод, , Заголовок, СтатусСообщения.Внимание);
		КонецЕсли;
	КонецЦикла; 
	
КонецПроцедуры // ПроверитьЗаполнениеТабличнойЧастиТовары()

Процедура ПроверитьЗаполнениеТабличнойЧастиПоВычетамНалоговыйАгент(СтруктураШапкиДокумента, ТаблицаПоВычетамНалоговыйАгент, Отказ, Заголовок)

	// Укажем, что надо проверить:
	СтруктураОбязательныхПолей = Новый Структура("ВидЦенности, СчетФактура, СтавкаНДС, Поставщик, ДоговорКонтрагента, ДокументОплаты, ДатаОплаты"); 
	
	Если СтруктураШапкиДокумента.ПредъявленНДСКВычету0 Тогда
		СтруктураОбязательныхПолей.Вставить("Состояние"); 
	КонецЕсли; 
	// Теперь вызовем общую процедуру проверки.
	ЗаполнениеДокументов.ПроверитьЗаполнениеТабличнойЧасти(ЭтотОбъект, "ВычетНДСПоНалоговомуАгенту", СтруктураОбязательныхПолей, Отказ, Заголовок);
	
	//Проверка полей без прекращения проведения
	СтрокаСообщенияСчетУчетаНДС = "Не заполнено значение реквизита ""Счет учета НДС""!";
	СтрокаСообщенияКорПериод = "Не заполнено значение реквизита ""Корректируемый период""!";

	Для Каждого СтрокаТаблицы Из ТаблицаПоВычетамНалоговыйАгент Цикл
		СтрокаНачалаСообщенияОбОшибке = "В строке номер """+ СокрЛП(СтрокаТаблицы.НомерСтроки) + """ табличной части ""Вычет по приобретенным ценностям"": ";
		Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.СчетУчетаНДС) и СтрокаТаблицы.НДС <> 0 Тогда
			ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + СтрокаСообщенияСчетУчетаНДС, Отказ, Заголовок, СтатусСообщения.Внимание);
		КонецЕсли;
		Если СтрокаТаблицы.ЗаписьДополнительногоЛиста и НЕ ЗначениеЗаполнено(СтрокаТаблицы.КорректируемыйПериод) Тогда
			ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + СтрокаСообщенияКорПериод, , Заголовок, СтатусСообщения.Внимание);
		КонецЕсли;

	КонецЦикла; 

КонецПроцедуры // ПроверитьЗаполнениеТабличнойЧастиТовары()

// По результату запроса по шапке документа формируем движения по регистрам.
//
//
Процедура ДвиженияПоРегистрам(СтруктураШапкиДокумента, ТаблицаПоВычетам, ТаблицапоАвансам, ТаблицаПоАвансамВыданным, ТаблицаПоВычетамНалоговыйАгент, ТаблицаПоВычетамПриИзмененииСтоимости, Отказ, Заголовок)
	
	ДвиженияПоВычетамНДСпоПриобретеннымЦенностям(СтруктураШапкиДокумента, ТаблицаПоВычетам, Отказ, Заголовок);
	ДвиженияПоВычетамНДСсАвансов(СтруктураШапкиДокумента, ТаблицаПоАвансам, Отказ, Заголовок);
	ДвиженияПоВычетамНДСсАвансовВыданных(СтруктураШапкиДокумента, ТаблицаПоАвансамВыданным, Отказ, Заголовок);
	ДвиженияПоВычетамНДСпоНалоговомуАгенту(СтруктураШапкиДокумента, ТаблицаПоВычетамНалоговыйАгент, Отказ, Заголовок);
	
	ДвиженияПоВычетамПриИзмененииСтоимости(СтруктураШапкиДокумента, ТаблицаПоВычетамПриИзмененииСтоимости, Отказ, Заголовок);
	      	
КонецПроцедуры // ДвиженияПоРегистрам()

// По результату запроса по шапке документа формируем движения по регистрам.
// Отрабатывает по табличной части "Вычет НДС по приобретенным ценностям"
//
Процедура ДвиженияПоВычетамНДСпоПриобретеннымЦенностям(СтруктураШапкиДокумента, ТаблицаПоВычетам, Отказ, Заголовок)

	Если ТаблицаПоВычетам.КОличество()=0 Тогда
		Возврат;
	КонецЕсли; 
	
	Если мВестиУчетНДС Тогда
	// Отражение по регистру "НДС предъявленный"
		ТаблицаДвижений_НДСПредъявленный = Движения.НДСПредъявленный.ВыгрузитьКолонки();
		ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(ТаблицаПоВычетам,ТаблицаДвижений_НДСПредъявленный);
		ТаблицаДвижений_НДСПредъявленный.Свернуть(
			"Период,Активность,Организация,СчетФактура,ВидЦенности,СтавкаНДС,СчетУчетаНДС,Поставщик,ДатаСобытия,Событие,ИсправленныйСчетФактура",
			"СуммаБезНДС,НДС");
		
		Движения.НДСПредъявленный.мПериод = СтруктураШапкиДокумента.Дата;
		Движения.НДСПредъявленный.мТаблицаДвижений = ТаблицаДвижений_НДСПредъявленный;
		Движения.НДСПредъявленный.ВыполнитьРасход();
		
		// Виды ценностей с особой обработкой расчетов
		ВидыЦенностейОтражаемыеВРасчетах = Новый Массив();
		ВидыЦенностейОтражаемыеВРасчетах.Добавить(Перечисления.ВидыЦенностей.НалоговыйАгентАренда);
		ВидыЦенностейОтражаемыеВРасчетах.Добавить(Перечисления.ВидыЦенностей.НалоговыйАгентРеализацияИмущества);
		ВидыЦенностейОтражаемыеВРасчетах.Добавить(Перечисления.ВидыЦенностей.НалоговыйАгентИностранцы);
		ВидыЦенностейОтражаемыеВРасчетах.Добавить(Перечисления.ВидыЦенностей.СМРСобственнымиСилами);
		
		// Отражение по регистру взаиморасчетов
		ТаблицаДвижений_НДСУчетРаспределенныхОплатПоставщикам = Движения.НДСУчетРаспределенныхОплатПоставщикам.ВыгрузитьКолонки();
		
		Для Каждого СтрокаВычета Из ТаблицаПоВычетам Цикл
			Если ВидыЦенностейОтражаемыеВРасчетах.Найти(СтрокаВычета.ВидЦенности) <> Неопределено Тогда
				Если СтрокаВычета.ВидЦенности = Перечисления.ВидыЦенностей.СМРСобственнымиСилами
					И СтрокаВычета.ДатаСчетаФактуры >= '20090101' Тогда
					Продолжить;
				КонецЕсли;
				СтрокаРасчетов = ТаблицаДвижений_НДСУчетРаспределенныхОплатПоставщикам.Добавить();
				СтрокаРасчетов.Организация = СтрокаВычета.Организация;
				СтрокаРасчетов.СчетФактура = СтрокаВычета.СчетФактура;
				СтрокаРасчетов.ДокументОплаты = СтрокаВычета.ДокументОплаты;
				СтрокаРасчетов.РасчетыСБюджетом = Истина;
				СтрокаРасчетов.РаспределеннаяСумма = СтрокаВычета.НДС;
			КонецЕсли; 
		КонецЦикла;

		ТаблицаДвижений_НДСУчетРаспределенныхОплатПоставщикам.Свернуть("Период,Активность,Организация,СчетФактура,ДокументОплаты,РасчетыСБюджетом,ДатаСобытия","РаспределеннаяСумма");
		
		Движения.НДСУчетРаспределенныхОплатПоставщикам.мПериод = СтруктураШапкиДокумента.Дата;
		Движения.НДСУчетРаспределенныхОплатПоставщикам.мТаблицаДвижений = ТаблицаДвижений_НДСУчетРаспределенныхОплатПоставщикам;
		Движения.НДСУчетРаспределенныхОплатПоставщикам.ВыполнитьРасход();
	КонецЕсли;
	
	// Отражение по регистру НДСЗаписиКнигиПокупок
	ТаблицаДвижений_НДСЗаписиКнигиПокупок = Движения.НДСЗаписиКнигиПокупок.ВыгрузитьКолонки();
	
	Если СтруктураШапкиДокумента.ПредъявленНДСКВычету0 Тогда
		ТаблицаДвижений_НДСЗаписиКнигиПокупок.Колонки.Добавить("ДокументОтгрузкиДата",Новый ОписаниеТипов("Дата", , , Новый КвалификаторыДаты(ЧастиДаты.Дата)));
		ТаблицаДвижений_НДСЗаписиКнигиПокупок.Колонки.Добавить("Состояние",Новый ОписаниеТипов("ПеречислениеСсылка.НДССостоянияРеализация0"));
		ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(ТаблицаПоВычетам,ТаблицаДвижений_НДСЗаписиКнигиПокупок);
		
		
		Для каждого СтрокаЗаписи Из ТаблицаДвижений_НДСЗаписиКнигиПокупок Цикл
			Если СтрокаЗаписи.Состояние = Перечисления.НДССостоянияРеализация0.НеПодтвержденаРеализация0 тогда
				Если СтрокаЗаписи.ЗаписьДополнительногоЛиста = Ложь Тогда
					СтрокаЗаписи.Период = Макс(СтрокаЗаписи.ДатаОплаты, СтрокаЗаписи.ДокументОтгрузкиДата);
				Иначе
					СтрокаЗаписи.Период = СтруктураШапкиДокумента.Дата;
				КонецЕсли; 
				СтрокаЗаписи.Событие = Перечисления.СобытияПоНДСПокупки.НеПодтвержденаСтавка0;
				
			КонецЕсли;
		КонецЦикла; 
		ТаблицаДвижений_НДСЗаписиКнигиПокупок.Колонки.Удалить(ТаблицаДвижений_НДСЗаписиКнигиПокупок.Колонки.ДокументОтгрузкиДата);
		ТаблицаДвижений_НДСЗаписиКнигиПокупок.Колонки.Удалить(ТаблицаДвижений_НДСЗаписиКнигиПокупок.Колонки.Состояние);
		
		Если мВестиУчетНДС Тогда
			// Отражение по регистру НДСПредъявленныйРеализация0
			ТаблицаДвижений_НДСПредъявленныйРеализация0 = Движения.НДСПредъявленныйРеализация0.ВыгрузитьКолонки();
			ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(ТаблицаПоВычетам,ТаблицаДвижений_НДСПредъявленныйРеализация0);
			
			Движения.НДСПредъявленныйРеализация0.мПериод 		 = СтруктураШапкиДокумента.Дата;
			Движения.НДСПредъявленныйРеализация0.мТаблицаДвижений = ТаблицаДвижений_НДСПредъявленныйРеализация0;
			Движения.НДСПредъявленныйРеализация0.ВыполнитьРасход(Ложь);
		КонецЕсли;
		
	Иначе
		ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(ТаблицаПоВычетам, ТаблицаДвижений_НДСЗаписиКнигиПокупок);
	КонецЕсли; 
	
	ТаблицаДвижений_НДСЗаписиКнигиПокупок.Свернуть(
		"Период,Активность,Организация,СчетФактура,ДокументОплаты,ДатаОплаты,СтавкаНДС,СчетУчетаНДС,Поставщик,ЗаписьДополнительногоЛиста,
		|КорректируемыйПериод,ДатаСобытия,Событие,ВидЦенности,ИсправленныйСчетФактура,НомерДокументаОплаты,ДатаДокументаОплаты,КодВидаОперации",
		"СуммаБезНДС,НДС");
		
	Движения.НДСЗаписиКнигиПокупок.мПериод 			= СтруктураШапкиДокумента.Дата;
	Движения.НДСЗаписиКнигиПокупок.мТаблицаДвижений = ТаблицаДвижений_НДСЗаписиКнигиПокупок;
	Движения.НДСЗаписиКнигиПокупок.ДобавитьДвижение(Ложь);
	
	// Сформировать проводи по записям книги покупок
	Для каждого ТекСтрокаВычета Из ТаблицаПоВычетам Цикл
		Если ТекСтрокаВычета.НДС = 0 Тогда
			ПродолжитЬ;
		КонецЕсли; 
		
		Движение = Движения.Хозрасчетный.Добавить();
		Движение.Период = СтруктураШапкиДокумента.Дата;
		Движение.Организация =  СтруктураШапкиДокумента.Организация;
		Движение.Сумма = ТекСтрокаВычета.НДС;
		Движение.НомерЖурнала = "НДС";
		
		Если не ПредъявленНДСКВычету0 тогда
			Движение.Содержание = "НДС к вычету";
		ИначеЕсли ТекСтрокаВычета.Состояние = Перечисления.НДССостоянияРеализация0.ПодтвержденаРеализация0 тогда 
			Движение.Содержание = "НДС к вычету по реализации 0%";
		Иначе
			Движение.Содержание = "НДС к вычету по реализации 0% (не подтверждена)";
		КонецЕсли;
		
		Если ПредъявленНДСКВычету0 и ТекСтрокаВычета.Состояние = Перечисления.НДССостоянияРеализация0.ПодтвержденаРеализация0 Тогда
			Движение.СчетДт = ПланыСчетов.Хозрасчетный.НДСпоЭкспортуКВозмещению; //68.22.2
			БухгалтерскийУчет.УстановитьСубконто(Движение.СчетДт, Движение.СубконтоДт, "Контрагенты", ТекСтрокаВычета.ДокументОтгрузкиКонтрагент);
			БухгалтерскийУчет.УстановитьСубконто(Движение.СчетДт, Движение.СубконтоДт, "СФВыданные",  ТекСтрокаВычета.ДокументОтгрузки);
		Иначе
			Движение.СчетДт = ПланыСчетов.Хозрасчетный.НДС; //68.02
			БухгалтерскийУчет.УстановитьСубконто(Движение.СчетДт, Движение.СубконтоДт, "ВидыПлатежейВГосБюджет", Перечисления.ВидыПлатежейВГосБюджет.Налог);
		КонецЕсли;
		
		
		Если ПредъявленНДСКВычету0 Тогда // подстраховка для ручного ввода
			Движение.СчетКт = ПланыСчетов.Хозрасчетный.НДСПоТоварамРеализованнымПоСтавке0; //19.07
		Иначе
			Движение.СчетКт = ТекСтрокаВычета.СчетУчетаНДС; //19.чч
		КонецЕсли;

		БухгалтерскийУчет.УстановитьСубконто(Движение.СчетКт, Движение.СубконтоКт, "Контрагенты", ТекСтрокаВычета.Поставщик);
		БухгалтерскийУчет.УстановитьСубконто(Движение.СчетКт, Движение.СубконтоКт, "СФПолученные", ТекСтрокаВычета.СчетФактура);
		
		Если ПредъявленНДСКВычету0 Тогда // Дополнительное субконто по 19.07
			БухгалтерскийУчет.УстановитьСубконто(Движение.СчетКт, Движение.СубконтоКт, "ДокументыРеализации", ТекСтрокаВычета.ДокументОтгрузки);
		КонецЕсли;
		
		Если ПредъявленНДСКВычету0 
			и ТекСтрокаВычета.Состояние = Перечисления.НДССостоянияРеализация0.ПодтвержденаРеализация0 
			и СтруктураШапкиДокумента.ИмеетсяРешениеОВозмещенииНДС 
			Тогда
			
			Движение = Движения.Хозрасчетный.Добавить();
			Движение.Период = СтруктураШапкиДокумента.Дата;
			Движение.Организация =  СтруктураШапкиДокумента.Организация;
			Движение.Сумма = ТекСтрокаВычета.НДС;
			Движение.НомерЖурнала = "НДС";
			Движение.Содержание = "Возмещение НДС при реализации 0%";
			Движение.СчетДт = ПланыСчетов.Хозрасчетный.НДС; //68.02
			БухгалтерскийУчет.УстановитьСубконто(Движение.СчетДт, Движение.СубконтоДт, "ВидыПлатежейВГосБюджет", Перечисления.ВидыПлатежейВГосБюджет.Налог);
			
			Движение.СчетКт = ПланыСчетов.Хозрасчетный.НДСпоЭкспортуКВозмещению; //68.22.2
			БухгалтерскийУчет.УстановитьСубконто(Движение.СчетКт, Движение.СубконтоКт, "Контрагенты", ТекСтрокаВычета.ДокументОтгрузкиКонтрагент);
			БухгалтерскийУчет.УстановитьСубконто(Движение.СчетКт, Движение.СубконтоКт, "СФВыданные",  ТекСтрокаВычета.ДокументОтгрузки);
        КонецЕсли;
	КонецЦикла; 

КонецПроцедуры // ДвиженияПоВычетамНДСпоПриобретеннымЦенностям()
 
// По результату запроса по шапке документа формируем движения по регистрам.
// Отрабатывает по табличной части "Вычет НДС по приобретенным ценностям"
//
Процедура ДвиженияПоВычетамНДСсАвансов(СтруктураШапкиДокумента, ТаблицаПоАвансам, Отказ, Заголовок)

	Если ТаблицаПоАвансам.Количество() = 0 Тогда
		Возврат;
	КонецЕсли; 
	
	Если мВестиУчетНДС Тогда
		// Отражение по регистру "НДС предъявленный"
		ТаблицаДвижений_НДСсАвансов = Движения.НДСсАвансов.ВыгрузитьКолонки();
		
		Для каждого СтрокаТаблицыИсточника Из ТаблицаПоАвансам Цикл
			
			Если ТипЗнч(СтрокаТаблицыИсточника.СчетФактура) = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
				Продолжить;
			КонецЕсли;
			
			СтрокаТаблицыПриемника = ТаблицаДвижений_НДСсАвансов.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТаблицыПриемника, СтрокаТаблицыИсточника);
			
			Если СтрокаТаблицыПриемника.ВидЦенности = Перечисления.ВидыЦенностей.ВозвратАвансовПолученных Тогда
				СтрокаТаблицыПриемника.ВидЦенности = Перечисления.ВидыЦенностей.АвансыПолученные;
			КонецЕсли; 
			
		КонецЦикла;
		
		ТаблицаДвижений_НДСсАвансов.Свернуть(
			"Период,Активность,Организация,ВидЦенности,СчетФактура,Покупатель,ДоговорКонтрагента,СтавкаНДС,ВалютаАванса,ДатаСобытия,ИсправленныйСчетФактура",
			"СуммаБезНДС,НДС,ВалютнаяСуммаСНДС");
		
		Движения.НДСсАвансов.мПериод = СтруктураШапкиДокумента.Дата;
		Движения.НДСсАвансов.мТаблицаДвижений = ТаблицаДвижений_НДСсАвансов;
		Движения.НДСсАвансов.ВыполнитьРасход();
	КонецЕсли;
	
	
	// Отражение по регистру НДСЗаписиКнигиПокупок
	ТаблицаДвижений_НДСЗаписиКнигиПокупок = Движения.НДСЗаписиКнигиПокупок.ВыгрузитьКолонки();
	
	Если СтруктураШапкиДокумента.ПредъявленНДСКВычету0 Тогда
		ТаблицаДвижений_НДСЗаписиКнигиПокупок.Колонки.Добавить("ДокументОтгрузкиДата",Новый ОписаниеТипов("Дата", , , Новый КвалификаторыДаты(ЧастиДаты.Дата)));
		ТаблицаДвижений_НДСЗаписиКнигиПокупок.Колонки.Добавить("Состояние",Новый ОписаниеТипов("ПеречислениеСсылка.НДССостоянияРеализация0"));
		
		ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(ТаблицаПоАвансам,ТаблицаДвижений_НДСЗаписиКнигиПокупок);
		
		Для каждого СтрокаЗаписи Из ТаблицаДвижений_НДСЗаписиКнигиПокупок Цикл
			Если СтрокаЗаписи.Состояние = Перечисления.НДССостоянияРеализация0.НеПодтвержденаРеализация0 тогда
				Если Не СтрокаЗаписи.ЗаписьДополнительногоЛиста Тогда
					СтрокаЗаписи.Период = Макс(СтрокаЗаписи.ДатаОплаты, СтрокаЗаписи.ДокументОтгрузкиДата);
				Иначе
					СтрокаЗаписи.Период = СтруктураШапкиДокумента.Дата;
				КонецЕсли;
				СтрокаЗаписи.Событие = Перечисления.СобытияПоНДСПокупки.НеПодтвержденаСтавка0;
			КонецЕсли;
		КонецЦикла; 
		ТаблицаДвижений_НДСЗаписиКнигиПокупок.Колонки.Удалить(ТаблицаДвижений_НДСЗаписиКнигиПокупок.Колонки.ДокументОтгрузкиДата);
		ТаблицаДвижений_НДСЗаписиКнигиПокупок.Колонки.Удалить(ТаблицаДвижений_НДСЗаписиКнигиПокупок.Колонки.Состояние);
		
		Если мВестиУчетНДС Тогда
			// Отражение по регистру НДСсАвансовРеализация0
			ТаблицаДвижений_НДСПредъявленныйРеализация0 = Движения.НДСПредъявленныйРеализация0.ВыгрузитьКолонки();
			ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(ТаблицаПоАвансам,ТаблицаДвижений_НДСПредъявленныйРеализация0);
			
			Движения.НДСПредъявленныйРеализация0.мПериод 		 = СтруктураШапкиДокумента.Дата;
			Движения.НДСПредъявленныйРеализация0.мТаблицаДвижений = ТаблицаДвижений_НДСПредъявленныйРеализация0;
			Движения.НДСПредъявленныйРеализация0.ВыполнитьРасход(Ложь);
		КонецЕсли;
		
	Иначе
		ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(ТаблицаПоАвансам, ТаблицаДвижений_НДСЗаписиКнигиПокупок);
	КонецЕсли; 
	
	ТаблицаДвижений_НДСЗаписиКнигиПокупок.Свернуть(
		"Период,Активность,Организация,СчетФактура,ДокументОплаты,ДатаОплаты,СтавкаНДС,СчетУчетаНДС,Поставщик,ДоговорКонтрагента,
		|ДатаСобытия,Событие,ВидЦенности,ЗаписьДополнительногоЛиста,КорректируемыйПериод,ИсправленныйСчетФактура,КодВидаОперации",
		"СуммаБезНДС,НДС");
		
	Движения.НДСЗаписиКнигиПокупок.мПериод 			= СтруктураШапкиДокумента.Дата;
	Движения.НДСЗаписиКнигиПокупок.мТаблицаДвижений = ТаблицаДвижений_НДСЗаписиКнигиПокупок;
	Движения.НДСЗаписиКнигиПокупок.ДобавитьДвижение(Ложь);
	
	// Сформировать проводки по записям книги покупок
	Для каждого ТекСтрокаВычета Из ТаблицаПоАвансам Цикл
		Если ТекСтрокаВычета.НДС = 0 Тогда
			ПродолжитЬ;
		КонецЕсли; 
		
		Движение = Движения.Хозрасчетный.Добавить();
		Движение.Период = СтруктураШапкиДокумента.Дата;
		Движение.Организация =  СтруктураШапкиДокумента.Организация;
		Движение.Сумма = ТекСтрокаВычета.НДС;
		Движение.НомерЖурнала = "НДС";
		
		СодержаниеПроводки = ?(Дата >= '20060101', "Вычет НДС по предоплате", "Вычет НДС с аванса");
		Если не СтруктураШапкиДокумента.ПредъявленНДСКВычету0 тогда
			Движение.Содержание = СодержаниеПроводки;
		ИначеЕсли ТекСтрокаВычета.Состояние = Перечисления.НДССостоянияРеализация0.ПодтвержденаРеализация0 тогда 
			Движение.Содержание = СодержаниеПроводки + " по реализации 0%";
		Иначе
			Движение.Содержание = СодержаниеПроводки + " по реализации 0% (не подтверждена)";
		КонецЕсли;
		
		Если ПредъявленНДСКВычету0 и ТекСтрокаВычета.Состояние = Перечисления.НДССостоянияРеализация0.ПодтвержденаРеализация0 Тогда
			Движение.СчетДт = ПланыСчетов.Хозрасчетный.НДСпоЭкспортуКВозмещению; //68.22.2
			БухгалтерскийУчет.УстановитьСубконто(Движение.СчетДт, Движение.СубконтоДт, "Контрагенты", ТекСтрокаВычета.Покупатель);
			БухгалтерскийУчет.УстановитьСубконто(Движение.СчетДт, Движение.СубконтоДт, "СФВыданные",  ТекСтрокаВычета.ДокументОтгрузки);
		Иначе
			Движение.СчетДт = ПланыСчетов.Хозрасчетный.НДС; //68.02
			БухгалтерскийУчет.УстановитьСубконто(Движение.СчетДт, Движение.СубконтоДт, "ВидыПлатежейВГосБюджет", Перечисления.ВидыПлатежейВГосБюджет.Налог);
		КонецЕсли;
		
		Движение.СчетКт = ПланыСчетов.Хозрасчетный.НДСпоАвансамИПредоплатам; //76.АВ

		БухгалтерскийУчет.УстановитьСубконто(Движение.СчетКт, Движение.СубконтоКт, "Контрагенты", ТекСтрокаВычета.Покупатель);
		БухгалтерскийУчет.УстановитьСубконто(Движение.СчетКт, Движение.СубконтоКт, "СФВыданные", ТекСтрокаВычета.СчетФактура);

		Если СтруктураШапкиДокумента.ПредъявленНДСКВычету0 
			и ТекСтрокаВычета.Состояние = Перечисления.НДССостоянияРеализация0.ПодтвержденаРеализация0 
			и СтруктураШапкиДокумента.ИмеетсяРешениеОВозмещенииНДС 
			Тогда
			
			Движение = Движения.Хозрасчетный.Добавить();
			Движение.Период			= СтруктураШапкиДокумента.Дата;
			Движение.Организация	=  СтруктураШапкиДокумента.Организация;
			Движение.Сумма			= ТекСтрокаВычета.НДС;
			Движение.НомерЖурнала	= "НДС";
			Движение.Содержание		= "Возмещение НДС при реализации 0%";
			
			Движение.СчетДт			= ПланыСчетов.Хозрасчетный.НДС; //68.02
			БухгалтерскийУчет.УстановитьСубконто(Движение.СчетДт, Движение.СубконтоДт, "ВидыПлатежейВГосБюджет", Перечисления.ВидыПлатежейВГосБюджет.Налог);
			
			Движение.СчетКт			= ПланыСчетов.Хозрасчетный.НДСпоЭкспортуКВозмещению; //68.22.2
			БухгалтерскийУчет.УстановитьСубконто(Движение.СчетКт, Движение.СубконтоКт, "Контрагенты", ТекСтрокаВычета.Покупатель);
			БухгалтерскийУчет.УстановитьСубконто(Движение.СчетКт, Движение.СубконтоКт, "СФВыданные",  ТекСтрокаВычета.ДокументОтгрузки);
			
        КонецЕсли;
		
	КонецЦикла; 

КонецПроцедуры // ДвиженияПоВычетамНДСсАвансов()

// По результату запроса по шапке документа формируем движения по регистрам.
// Отрабатывает по табличной части "Вычет НДС по приобретенным ценностям"
//
Процедура ДвиженияПоВычетамНДСсАвансовВыданных(СтруктураШапкиДокумента, ТаблицаПоАвансамВыданным, Отказ, Заголовок)

	Если ТаблицаПоАвансамВыданным.Количество() = 0 Тогда
		Возврат;
	КонецЕсли; 
	
	// Отражение по регистру НДСПредъявленный
	ТаблицаДвижений_НДСПредъявленный = Движения.НДСПредъявленный.ВыгрузитьКолонки();
	ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(ТаблицаПоАвансамВыданным, ТаблицаДвижений_НДСПредъявленный);
	ТаблицаДвижений_НДСПредъявленный.Свернуть(
		"Период, Активность, Организация, СчетФактура, ВидЦенности, СтавкаНДС, Поставщик, ДоговорКонтрагента, ДатаСобытия, Событие, ИсправленныйСчетФактура", 
		"СуммаБезНДС, НДС");
		
	Движения.НДСПредъявленный.мПериод = СтруктураШапкиДокумента.Дата;
	Движения.НДСПредъявленный.мТаблицаДвижений = ТаблицаДвижений_НДСПредъявленный;
	Движения.НДСПредъявленный.ВыполнитьРасход();
		
	// Отражение по регистру НДСЗаписиКнигиПокупок
	ТаблицаДвижений_НДСЗаписиКнигиПокупок = Движения.НДСЗаписиКнигиПокупок.ВыгрузитьКолонки();
	
	ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(ТаблицаПоАвансамВыданным, ТаблицаДвижений_НДСЗаписиКнигиПокупок);

	ТаблицаДвижений_НДСЗаписиКнигиПокупок.Свернуть(
		"Период,Активность,Организация,СчетФактура,СтавкаНДС,Поставщик,ДоговорКонтрагента,ДатаСобытия,Событие,
		|ВидЦенности,ЗаписьДополнительногоЛиста,КорректируемыйПериод,ИсправленныйСчетФактура,НомерДокументаОплаты,ДатаДокументаОплаты", 
		"СуммаБезНДС, НДС");
		
	Движения.НДСЗаписиКнигиПокупок.мПериод 			= СтруктураШапкиДокумента.Дата;
	Движения.НДСЗаписиКнигиПокупок.мТаблицаДвижений = ТаблицаДвижений_НДСЗаписиКнигиПокупок;
	Движения.НДСЗаписиКнигиПокупок.ДобавитьДвижение(Ложь);
	
	// Сформировать проводки по записям книги покупок
	Для Каждого ТекСтрокаВычета Из ТаблицаПоАвансамВыданным Цикл
		
		Если ТекСтрокаВычета.НДС = 0 Тогда
			Продолжить;
		КонецЕсли; 
		
		Движение 		      = Движения.Хозрасчетный.Добавить();
		Движение.Период 	  = СтруктураШапкиДокумента.Дата;
		Движение.Организация  = СтруктураШапкиДокумента.Организация;
		Движение.Сумма 		  = ТекСтрокаВычета.НДС;
		Движение.Содержание   = "Вычет НДС с выданного аванса";
		Движение.НомерЖурнала = "НДС";
                         		
		Движение.СчетДт = ПланыСчетов.Хозрасчетный.НДС; // 68.02
		БухгалтерскийУчет.УстановитьСубконто(Движение.СчетДт, Движение.СубконтоДт, "ВидыПлатежейВГосБюджет", Перечисления.ВидыПлатежейВГосБюджет.Налог);
		
		Движение.СчетКт = ПланыСчетов.Хозрасчетный.НДСпоАвансамИПредоплатамВыданным; // 76.ВА
		БухгалтерскийУчет.УстановитьСубконто(Движение.СчетКт, Движение.СубконтоКт, "Контрагенты", ТекСтрокаВычета.Поставщик);
		БухгалтерскийУчет.УстановитьСубконто(Движение.СчетКт, Движение.СубконтоКт, "СФПолученные", ТекСтрокаВычета.СчетФактура);

	КонецЦикла; 

КонецПроцедуры // ДвиженияПоВычетамНДСпоПриобретеннымЦенностям()

// По результату запроса по шапке документа формируем движения по регистрам.
// Отрабатывает по табличной части "Вычет НДС по приобретенным ценностям"
//
Процедура ДвиженияПоВычетамНДСпоНалоговомуАгенту(СтруктураШапкиДокумента, ТаблицаПоВычетамНалоговыйАгент, Отказ, Заголовок)

	Если ТаблицаПоВычетамНалоговыйАгент.Количество()=0 Тогда
		Возврат;
	КонецЕсли; 
	
	// Отражение по регистру "НДС предъявленный"
	ТаблицаДвижений_НДСПредъявленный = Движения.НДСПредъявленный.ВыгрузитьКолонки();
	ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(ТаблицаПоВычетамНалоговыйАгент, ТаблицаДвижений_НДСПредъявленный);
	ТаблицаДвижений_НДСПредъявленный.Свернуть(
		"Период,Активность,Организация,СчетФактура,ВидЦенности,СтавкаНДС,СчетУчетаНДС,Поставщик,ДоговорКонтрагента,ДатаСобытия,Событие,ИсправленныйСчетФактура",
		"СуммаБезНДС,НДС");
	
	Движения.НДСПредъявленный.мПериод = СтруктураШапкиДокумента.Дата;
	Движения.НДСПредъявленный.мТаблицаДвижений = ТаблицаДвижений_НДСПредъявленный;
	Движения.НДСПредъявленный.ВыполнитьРасход();
																																  	
	// Отражение по регистру НДСЗаписиКнигиПокупок
	ТаблицаДвижений_НДСЗаписиКнигиПокупок = Движения.НДСЗаписиКнигиПокупок.ВыгрузитьКолонки();
	
	Если СтруктураШапкиДокумента.ПредъявленНДСКВычету0 Тогда
		ТаблицаДвижений_НДСЗаписиКнигиПокупок.Колонки.Добавить("ДокументОтгрузкиДата",ОбщегоНазначения.ПолучитьОписаниеТиповДаты(ЧастиДаты.Дата));
		ТаблицаДвижений_НДСЗаписиКнигиПокупок.Колонки.Добавить("Состояние",Новый ОписаниеТипов("ПеречислениеСсылка.НДССостоянияРеализация0"));
		ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(ТаблицаПоВычетамНалоговыйАгент,ТаблицаДвижений_НДСЗаписиКнигиПокупок);
		
		
		Для каждого СтрокаЗаписи Из ТаблицаДвижений_НДСЗаписиКнигиПокупок Цикл
			Если СтрокаЗаписи.Состояние = Перечисления.НДССостоянияРеализация0.НеПодтвержденаРеализация0 тогда
				Если СтрокаЗаписи.ЗаписьДополнительногоЛиста = Ложь Тогда
					СтрокаЗаписи.Период = Макс(СтрокаЗаписи.ДатаОплаты, СтрокаЗаписи.ДокументОтгрузкиДата);
				Иначе
					СтрокаЗаписи.Период = СтруктураШапкиДокумента.Дата;
				КонецЕсли; 
				СтрокаЗаписи.Событие = Перечисления.СобытияПоНДСПокупки.НеПодтвержденаСтавка0;
				
			КонецЕсли;
		КонецЦикла; 
		ТаблицаДвижений_НДСЗаписиКнигиПокупок.Колонки.Удалить(ТаблицаДвижений_НДСЗаписиКнигиПокупок.Колонки.ДокументОтгрузкиДата);
		ТаблицаДвижений_НДСЗаписиКнигиПокупок.Колонки.Удалить(ТаблицаДвижений_НДСЗаписиКнигиПокупок.Колонки.Состояние);
		
		// Отражение по регистру НДСПредъявленныйРеализация0
		ТаблицаДвижений_НДСПредъявленныйРеализация0 = Движения.НДСПредъявленныйРеализация0.ВыгрузитьКолонки();
		ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(ТаблицаПоВычетамНалоговыйАгент,ТаблицаДвижений_НДСПредъявленныйРеализация0);
		
		Движения.НДСПредъявленныйРеализация0.мПериод 		 = СтруктураШапкиДокумента.Дата;
		Движения.НДСПредъявленныйРеализация0.мТаблицаДвижений = ТаблицаДвижений_НДСПредъявленныйРеализация0;
		Движения.НДСПредъявленныйРеализация0.ВыполнитьРасход(Ложь);
		
	Иначе
		ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(ТаблицаПоВычетамНалоговыйАгент,ТаблицаДвижений_НДСЗаписиКнигиПокупок);
	КонецЕсли; 
	
	ТаблицаДвижений_НДСЗаписиКнигиПокупок.Свернуть(
		"Период,Активность,Организация,СчетФактура,ДокументОплаты,ДатаОплаты,НомерДокументаОплаты,ДатаДокументаОплаты,СтавкаНДС,СчетУчетаНДС,Поставщик,ДоговорКонтрагента,ЗаписьДополнительногоЛиста,
		|КорректируемыйПериод,ДатаСобытия,Событие,ВидЦенности,ИсправленныйСчетФактура",
		"СуммаБезНДС,НДС");
		
	Движения.НДСЗаписиКнигиПокупок.мПериод 			= СтруктураШапкиДокумента.Дата;
	Движения.НДСЗаписиКнигиПокупок.мТаблицаДвижений = ТаблицаДвижений_НДСЗаписиКнигиПокупок;
	Движения.НДСЗаписиКнигиПокупок.ДобавитьДвижение(Ложь);
	                                                                                                                                                                                        
	// Сформировать проводи по записям книги покупок
	Для каждого ТекСтрокаВычета Из ТаблицаПоВычетамНалоговыйАгент Цикл
		Если ТекСтрокаВычета.НДС = 0 Тогда
			ПродолжитЬ;
		КонецЕсли; 
		                                                                                                                                                                                               
		Движение = Движения.Хозрасчетный.Добавить();
		Движение.Период = СтруктураШапкиДокумента.Дата;
		Движение.Организация =  СтруктураШапкиДокумента.Организация;
		Движение.Сумма = ТекСтрокаВычета.НДС;
		Движение.Содержание = "НДС";
		Движение.НомерЖурнала = "НДС";
		
		Движение.СчетДт = ПланыСчетов.Хозрасчетный.НДС; //68.02
		БухгалтерскийУчет.УстановитьСубконто(Движение.СчетДт, Движение.СубконтоДт, "ВидыПлатежейВГосБюджет", Перечисления.ВидыПлатежейВГосБюджет.Налог);
		
		Если ПредъявленНДСКВычету0 Тогда // подстраховка для ручного ввода
			Движение.СчетКт = ПланыСчетов.Хозрасчетный.НДСПоТоварамРеализованнымПоСтавке0; //19.07
		Иначе
			Движение.СчетКт = ТекСтрокаВычета.СчетУчетаНДС; //19.чч
		КонецЕсли;

		БухгалтерскийУчет.УстановитьСубконто(Движение.СчетКт, Движение.СубконтоКт, "Контрагенты", ТекСтрокаВычета.Поставщик);
		БухгалтерскийУчет.УстановитьСубконто(Движение.СчетКт, Движение.СубконтоКт, "СФПолученные", ТекСтрокаВычета.СчетФактура);
		
		Если ПредъявленНДСКВычету0 Тогда // Дополнительное субконто по 19.07
			БухгалтерскийУчет.УстановитьСубконто(Движение.СчетКт, Движение.СубконтоКт, "ДокументыРеализации", ТекСтрокаВычета.ДокументОтгрузки);
		КонецЕсли;

	КонецЦикла; 

КонецПроцедуры // ДвиженияПоВычетамНДСпоПриобретеннымЦенностям()

Процедура ДвиженияПоВычетамПриИзмененииСтоимости(СтруктураШапкиДокумента, ТаблицаПоВычетамПриИзмененииСтоимости, Отказ, Заголовок)
	
	ТаблицаПоВычетамПриИзмененииСтоимости.Свернуть(
		"Организация, ДатаСобытия, Поставщик, СчетФактура, ДатаСчетаФактуры, СтавкаНДС, ВидЦенности, СчетУчетаНДС, ИсправленныйСчетФактура, ИсправленныйСчетФактураДата", 
		"СуммаБезНДС, НДС");
		
	Если ТаблицаПоВычетамПриИзмененииСтоимости.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	НачалоНалоговогоПериода = НачалоКвартала(СтруктураШапкиДокумента.Дата);
	КонецНалоговогоПериода  = КонецДня(КонецКвартала(СтруктураШапкиДокумента.Дата));
	
	Для Каждого СтрокаДвижения Из ТаблицаПоВычетамПриИзмененииСтоимости Цикл
		
		Если НЕ ЗначениеЗаполнено(СтрокаДвижения.ИсправленныйСчетФактура) Тогда
			СтрокаДвижения.ИсправленныйСчетФактура = Неопределено;
			Если СтрокаДвижения.ДатаСчетаФактуры >= НачалоНалоговогоПериода 
				И СтрокаДвижения.ДатаСчетаФактуры <= КонецНалоговогоПериода Тогда
				СтрокаДвижения.ДатаСобытия = СтрокаДвижения.ДатаСчетаФактуры;
			Иначе
				СтрокаДвижения.ДатаСобытия = СтруктураШапкиДокумента.Дата;
			КонецЕсли;
		Иначе
			Если СтрокаДвижения.ИсправленныйСчетФактураДата >= НачалоНалоговогоПериода 
				И СтрокаДвижения.ИсправленныйСчетФактураДата <= КонецНалоговогоПериода Тогда
				СтрокаДвижения.ДатаСобытия = СтрокаДвижения.ИсправленныйСчетФактураДата;
			Иначе
				СтрокаДвижения.ДатаСобытия = СтруктураШапкиДокумента.Дата;
			КонецЕсли;
        КонецЕсли;	
		
		Движение = Движения.НДСПредъявленный.ДобавитьРасход();
		
		ЗаполнитьЗначенияСвойств(Движение, СтрокаДвижения);
		Движение.Регистратор = СтруктураШапкиДокумента.Ссылка;
		Движение.ДатаСобытия = СтруктураШапкиДокумента.Дата;
		Движение.Период 	 = СтруктураШапкиДокумента.Дата;
		Движение.Событие 	 = Перечисления.СобытияПоНДСПокупки.ПредъявленНДСКВычету;
		Если СтруктураШапкиДокумента.Дата >= '20150101' Тогда
			//Движение.КодВидаОперации = "18"; // Шевченков, закомментировал 20150317, нет такого реквивизта/измерения
		КонецЕсли;
		
		Движение = Движения.НДСЗаписиКнигиПокупок.Добавить();	
			
		ЗаполнитьЗначенияСвойств(Движение, СтрокаДвижения);
		Движение.Регистратор = СтруктураШапкиДокумента.Ссылка;
		Движение.ДатаСобытия = СтруктураШапкиДокумента.Дата;
		Движение.Период 	 = СтруктураШапкиДокумента.Дата;
		Движение.Событие 	 = Перечисления.СобытияПоНДСПокупки.ПредъявленНДСКВычету;
		Если СтруктураШапкиДокумента.Дата >= '20150101' Тогда
			Движение.КодВидаОперации = "18";
		КонецЕсли;
		
		Если СтрокаДвижения.НДС = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Движение = Движения.Хозрасчетный.Добавить();
				                                                                                                                                                                                               
		Движение.Период = СтруктураШапкиДокумента.Дата;
		Движение.Организация =  СтруктураШапкиДокумента.Организация;
		Движение.Сумма = СтрокаДвижения.НДС;
		Движение.Содержание = "НДС";
		
		Движение.СчетДт = ПланыСчетов.Хозрасчетный.НДС;
		БухгалтерскийУчет.УстановитьСубконто(Движение.СчетДт, Движение.СубконтоДт, "ВидыПлатежейВГосБюджет", Перечисления.ВидыПлатежейВГосБюджет.Налог);
		
		Движение.СчетКт = СтрокаДвижения.СчетУчетаНДС; 
		БухгалтерскийУчет.УстановитьСубконто(Движение.СчетКт, Движение.СубконтоКт, "Контрагенты", СтрокаДвижения.Поставщик);
		БухгалтерскийУчет.УстановитьСубконто(Движение.СчетКт, Движение.СубконтоКт, "СФПолученные", СтрокаДвижения.СчетФактура);
						
	КонецЦикла;	
	
	Движения.НДСПредъявленный.Записывать = Истина;
	Движения.НДСЗаписиКнигиПокупок.Записывать = Истина;
	Движения.Хозрасчетный.Записывать = Истина;
	
КонецПроцедуры	

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Процедура формирует структуру шапки документа и дополнительных полей.
//
Процедура ПодготовитьСтруктуруШапкиДокумента(Заголовок, СтруктураШапкиДокумента) Экспорт
	
	// Заголовок для сообщений об ошибках проведения.
	Заголовок = ОбщегоНазначения.ПредставлениеДокументаПриПроведении(Ссылка);

	// Сформируем запрос на дополнительные параметры, нужные при проведении, по данным шапки документа
	СтруктураШапкиДокумента = ОбщегоНазначения.СформироватьСтруктуруШапкиДокумента(ЭтотОбъект);
	
	СтруктураПолейУчетнойПолитикиНУ = Новый Структура("НДСНалоговыйПериод");
	Если Не СтруктураШапкиДокумента.Свойство("НДСНалоговыйПериод") Тогда
		СтруктураШапкиДокумента.Вставить("НДСНалоговыйПериод", ?(Дата < '20080101', Перечисления.Периодичность.Месяц, Перечисления.Периодичность.Квартал));
	КонецЕсли; 
	ОбщегоНазначения.ДополнитьПоложениямиУчетнойПолитики(СтруктураШапкиДокумента, СтруктураШапкиДокумента.Дата, Ложь, СтруктураШапкиДокумента.Организация, "Нал", СтруктураПолейУчетнойПолитикиНУ);
	
	мВестиУчетНДС = УчетНДС.ПроводитьПоРазделуУчетаНДС(Дата);

КонецПроцедуры // ПодготовитьСтруктуруШапкиДокумента()

// Процедура формирует таблицы документа.
//
Процедура ПодготовитьТаблицыДокумента(СтруктураШапкиДокумента, ТаблицаПоВычетам, ТаблицаПоАвансам, ТаблицаПоАвансамВыданным, ТаблицаПоВычетамНалоговыйАгент, ТаблицаПоВычетамПриИзмененииСтоимости, Отказ, Заголовок) Экспорт
	
	ВерсияПостановления1137 = УчетНДС.ВерсияПостановленияНДС1137(СтруктураШапкиДокумента.Дата);
	
	СтруктураПолей = Новый Структура;
	СтруктураПолей.Вставить("Организация",                 "Ссылка.Организация");
	СтруктураПолей.Вставить("ВидЦенности",                 "ВидЦенности");
	СтруктураПолей.Вставить("Поставщик",                   "Поставщик");
	СтруктураПолей.Вставить("СчетФактура",                 "СчетФактура");
	СтруктураПолей.Вставить("ДатаСчетаФактуры",            "СчетФактура.Дата");
	СтруктураПолей.Вставить("ДокументОплаты",              "ДокументОплаты");
	СтруктураПолей.Вставить("ДатаОплаты",                  "ДатаОплаты");
	СтруктураПолей.Вставить("ДатаСобытия",                 "ДатаОплаты");
	СтруктураПолей.Вставить("СтавкаНДС",                   "СтавкаНДС");
	СтруктураПолей.Вставить("СуммаБезНДС",                 "СуммаБезНДС");
	СтруктураПолей.Вставить("НДС",                         "НДС");
	СтруктураПолей.Вставить("СчетУчетаНДС",                "СчетУчетаНДС");
	СтруктураПолей.Вставить("ЗаписьДополнительногоЛиста",  "ЗаписьДополнительногоЛиста");
	СтруктураПолей.Вставить("КорректируемыйПериод",        "КорректируемыйПериод");
	СтруктураПолей.Вставить("ИсправленныйСчетФактура",     "ИсправленныйСчетФактура");
	СтруктураПолей.Вставить("ИсправленныйСчетФактураДата", "ИсправленныйСчетФактура.Дата");
	СтруктураПолей.Вставить("НомерДокументаОплаты",        "НомерДокументаОплаты");
	СтруктураПолей.Вставить("КодВидаОперации",             "КодВидаОперации");
	
	Если ВерсияПостановления1137 = 3 Тогда
		СтруктураПолей.Вставить("ДатаДокументаОплаты", "ДатаДокументаОплаты");
	Иначе	
		СтруктураПолей.Вставить("ДатаДокументаОплаты", "ДокументОплаты.Дата");
	КонецЕсли;
	
	Если СтруктураШапкиДокумента.ПредъявленНДСКВычету0 Тогда
		СтруктураПолей.Вставить("ДокументОтгрузки",	"ДокументОтгрузки");
		СтруктураПолей.Вставить("ДокументОтгрузкиДата",	"ДокументОтгрузки.Дата");
		СтруктураПолей.Вставить("ДокументОтгрузкиКонтрагент",	"ДокументОтгрузки.Контрагент");
		СтруктураПолей.Вставить("Состояние",	"Состояние");
	КонецЕсли; 

	РезультатЗапросаПоВычетам = УправлениеЗапасами.СформироватьЗапросПоТабличнойЧасти(ЭтотОбъект, "ВычетПоПриобретеннымЦенностям", СтруктураПолей);
	ТаблицаПоВычетам =          ПодготовитьТаблицуПоВычетам(РезультатЗапросаПоВычетам,СтруктураШапкиДокумента, Отказ, Заголовок);
	
	// Авансы полученные
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка",                     Ссылка);
	Запрос.УстановитьПараметр("Организация",                Организация);
	Запрос.УстановитьПараметр("ДатаДокумента",              Дата);
	Запрос.УстановитьПараметр("ПустыеДокументыИсправления", УчетНДС.МассивПустыхИсправленныхСчетовФактур());
	Запрос.УстановитьПараметр("ПредъявленНДСКВычету0",      ПредъявленНДСКВычету0);
	
	Запрос.Текст = ТекстЗапросаАвансыПолученные();
	ТаблицаПоАвансам = Запрос.Выполнить().Выгрузить();
	
	// Изменение стоимости в сторону уменьшения
	
	Запрос.Текст = ТекстЗапросаИзменениеСтоимостиВСторонуУменьшения();
	ТаблицаПоВычетамПриИзмененииСтоимости = Запрос.Выполнить().Выгрузить();
	
	// Авансы выданные
	
	СтруктураПолей = Новый Структура;
	СтруктураПолей.Вставить("Организация",                "Ссылка.Организация");
	СтруктураПолей.Вставить("Поставщик",                  "Поставщик");
	СтруктураПолей.Вставить("ДоговорКонтрагента",         "ДоговорКонтрагента");
	СтруктураПолей.Вставить("СчетФактура",                "СчетФактура");
	СтруктураПолей.Вставить("ДатаСчетаФактуры",           "СчетФактура.Дата");
	СтруктураПолей.Вставить("СтавкаНДС",                  "СтавкаНДС");
	СтруктураПолей.Вставить("СуммаБезНДС",                "СуммаБезНДС");
	СтруктураПолей.Вставить("НДС",                        "НДС");
	СтруктураПолей.Вставить("ЗаписьДополнительногоЛиста", "ЗаписьДополнительногоЛиста");
	СтруктураПолей.Вставить("КорректируемыйПериод",       "КорректируемыйПериод");
	
	РезультатЗапросаПоАвансамВыданным = УправлениеЗапасами.СформироватьЗапросПоТабличнойЧасти(ЭтотОбъект, "НДСсАвансовВыданных", СтруктураПолей);
	ТаблицаПоАвансамВыданным = ПодготовитьТаблицуПоАвансамВыданным(РезультатЗапросаПоАвансамВыданным, СтруктураШапкиДокумента);
	
	// Налоговый агент
	
	СтруктураПолей = Новый Структура;
	СтруктураПолей.Вставить("Организация",                "Ссылка.Организация");
	СтруктураПолей.Вставить("ВидЦенности",                "ВидЦенности");
	СтруктураПолей.Вставить("Поставщик",                  "Поставщик");
	СтруктураПолей.Вставить("ДоговорКонтрагента",         "ДоговорКонтрагента");
	СтруктураПолей.Вставить("СчетФактура",                "СчетФактура");
	СтруктураПолей.Вставить("ДатаСчетаФактуры",           "СчетФактура.Дата");
	СтруктураПолей.Вставить("ДокументОплаты",             "ДокументОплаты");
	СтруктураПолей.Вставить("ДатаОплаты",                 "ДатаОплаты");
	СтруктураПолей.Вставить("ДатаСобытия",                "Ссылка.Дата");
	СтруктураПолей.Вставить("СтавкаНДС",                  "СтавкаНДС");
	СтруктураПолей.Вставить("СуммаБезНДС",                "СуммаБезНДС");
	СтруктураПолей.Вставить("НДС",                        "НДС");
	СтруктураПолей.Вставить("СчетУчетаНДС",               "СчетУчетаНДС");
	СтруктураПолей.Вставить("ЗаписьДополнительногоЛиста", "ЗаписьДополнительногоЛиста");
	СтруктураПолей.Вставить("КорректируемыйПериод",       "КорректируемыйПериод");
	СтруктураПолей.Вставить("ИсправленныйСчетФактура",    "ИсправленныйСчетФактура");
	
	Если СтруктураШапкиДокумента.ПредъявленНДСКВычету0 Тогда
		СтруктураПолей.Вставить("ДокументОтгрузки",       "ДокументОтгрузки");
		СтруктураПолей.Вставить("ДокументОтгрузкиДата",   "ДокументОтгрузки.Дата");
		СтруктураПолей.Вставить("Состояние",              "Состояние");
	КонецЕсли; 
	
	СтруктураПолей.Вставить("ДокументОплатыНДС",          "ДокументОплатыНДС");
	
	Если ВерсияПостановления1137 = 3 Тогда 
		СтруктураПолей.Вставить("ДокументОплатыНДС",      "ДокументОплатыНДС");
		СтруктураПолей.Вставить("ДатаДокументаОплаты",    "ДокументОплатыНДС.Дата");
	Иначе
		СтруктураПолей.Вставить("ДатаДокументаОплаты",    "ДокументОплаты.Дата");
	КонецЕсли;

	РезультатЗапросаПоНалоговомуАгенту = УправлениеЗапасами.СформироватьЗапросПоТабличнойЧасти(ЭтотОбъект, "ВычетНДСПоНалоговомуАгенту", СтруктураПолей);
	ТаблицаПоВычетамНалоговыйАгент = ПодготовитьТаблицуПоВычетамНалоговыйАгент(РезультатЗапросаПоНалоговомуАгенту, СтруктураШапкиДокумента, Отказ, Заголовок);
                   	
КонецПроцедуры // СформироватьТаблицыДокумента()

Процедура ОбработкаПроведения(Отказ, Режим) Экспорт
	
	Перем Заголовок, СтруктураШапкиДокумента, ТаблицаПоВычетам, ТаблицаПоАвансам, ТаблицаПоАвансамВыданным, ТаблицаПоВычетамНалоговыйАгент, ТаблицаПоВычетамПриИзмененииСтоимости;
	
	Если мУдалятьДвижения Тогда
		ОбщегоНазначения.УдалитьДвиженияРегистратора(ЭтотОбъект, Отказ);
	КонецЕсли;

	ПодготовитьСтруктуруШапкиДокумента(Заголовок, СтруктураШапкиДокумента);
	
	// Проверим правильность заполнения шапки документа
	ПроверитьЗаполнениеШапки(Отказ, Заголовок);
	
	ПодготовитьТаблицыДокумента(СтруктураШапкиДокумента, ТаблицаПоВычетам, ТаблицаПоАвансам, ТаблицаПоАвансамВыданным, ТаблицаПоВычетамНалоговыйАгент, ТаблицаПоВычетамПриИзмененииСтоимости, Отказ, Заголовок);
	
	ПроверитьЗаполнениеТабличнойЧастиПоВычетам(СтруктураШапкиДокумента,ТаблицаПоВычетам, Отказ, Заголовок);
	ПроверитьЗаполнениеТабличнойЧастиПоАвансам(СтруктураШапкиДокумента, ТаблицаПоАвансам, Отказ, Заголовок);
	ПроверитьЗаполнениеТабличнойЧастиПоАвансамВыданным(СтруктураШапкиДокумента, ТаблицаПоАвансамВыданным, Отказ, Заголовок);
	ПроверитьЗаполнениеТабличнойЧастиПоВычетамНалоговыйАгент(СтруктураШапкиДокумента, ТаблицаПоВычетамНалоговыйАгент, Отказ, Заголовок);
	
	Если Не Отказ Тогда
		ДвиженияПоРегистрам(СтруктураШапкиДокумента, ТаблицаПоВычетам, ТаблицаПоАвансам, ТаблицаПоАвансамВыданным, ТаблицаПоВычетамНалоговыйАгент, ТаблицаПоВычетамПриИзмененииСтоимости, Отказ, Заголовок);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка  Тогда
		Возврат;
	КонецЕсли;


	 
	мУдалятьДвижения = НЕ ЭтоНовый();
	
КонецПроцедуры // ПередЗаписью

// <- Шевченков, обновление книги покупок/продаж (№31059)  20141022

Процедура ПолучитьДанныеОДокументахОплатыАгентскогоНДСВБюджет(ТаблицаРезультатов)
	
	Если УчетНДС.ВерсияПостановленияНДС1137(Дата) < 3 Тогда
		Возврат;
	КонецЕсли;
	
	МетаданныеДокумента = Метаданные.Документы.ФормированиеЗаписейКнигиПокупок;
	ТипыДокументовОплатыНДС = МетаданныеДокумента.ТабличныеЧасти.ВычетНДСПоНалоговомуАгенту.Реквизиты.ДокументОплатыНДС.Тип.Типы();

	УстановитьПривилегированныйРежим(Истина);
	
	ВидыСубконто = Новый Массив;
	ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
	ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры);
	ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СФВыданные);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("ТаблицаРезультатов", 	ТаблицаРезультатов);
	Запрос.УстановитьПараметр("Организация", 			Организация);
	Запрос.УстановитьПараметр("Дата", 					Новый Граница(КонецДня(Дата), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("КонецПериода", 			КонецДня(Дата));
	Запрос.УстановитьПараметр("ВидыСубконто", 			ВидыСубконто);
	Запрос.УстановитьПараметр("НДСНалоговогоАгента",	ПланыСчетов.Хозрасчетный.НДСНалоговогоАгента);
	Запрос.УстановитьПараметр("СчетПереоценки", 		ПланыСчетов.Хозрасчетный.ПрочиеДоходы);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаРезультатов.ДокументОплаты КАК ДокументОплаты,
	|	ТаблицаРезультатов.Поставщик КАК Поставщик,
	|	ТаблицаРезультатов.ДоговорКонтрагента КАК ДоговорКонтрагента
	|ПОМЕСТИТЬ ТаблицаРезультатов
	|ИЗ
	|	&ТаблицаРезультатов КАК ТаблицаРезультатов
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОплаты,
	|	Поставщик,
	|	ДоговорКонтрагента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОбороты.Субконто1 КАК Поставщик,
	|	ХозрасчетныйОбороты.Субконто2 КАК ДоговорКонтрагента,
	|	ХозрасчетныйОбороты.Субконто3 КАК ДокументОплаты,
	|	ХозрасчетныйОбороты.Регистратор КАК ДокументОплатыНДС
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(
	|			,
	|			&Дата,
	|			Регистратор,
	|			Счет = &НДСНалоговогоАгента,
	|			&ВидыСубконто,
	|			Организация = &Организация
	|				И Субконто1 В
	|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|						ТаблицаРезультатов.Поставщик
	|					ИЗ
	|						ТаблицаРезультатов)
	|				И Субконто2 В
	|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|						ТаблицаРезультатов.ДоговорКонтрагента
	|					ИЗ
	|						ТаблицаРезультатов)
	|				И Субконто3 В
	|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|						ТаблицаРезультатов.ДокументОплаты
	|					ИЗ
	|						ТаблицаРезультатов),
	|			КорСчет <> &СчетПереоценки,
	|			) КАК ХозрасчетныйОбороты
	|ГДЕ
	|	ЕСТЬNULL(ХозрасчетныйОбороты.КорСубконто3, ХозрасчетныйОбороты.Регистратор) <> ХозрасчетныйОбороты.Субконто3
	|	И ХозрасчетныйОбороты.СуммаОборотДт > 0";
	
	ОплаченныйНДС = Запрос.Выполнить().Выгрузить();
	
	Отбор = Новый Структура("ДокументОплаты, Поставщик, ДоговорКонтрагента");

	Для каждого СтрокаТаблицы Из ТаблицаРезультатов Цикл
		
		ЗаполнитьЗначенияСвойств(Отбор, СтрокаТаблицы);
		СтрокиОтбора = ОплаченныйНДС.НайтиСтроки(Отбор);
		
		Для каждого СтрокаОтбора Из СтрокиОтбора Цикл
			СтрокаТаблицы.ДокументОплатыНДС = СтрокаОтбора.ДокументОплатыНДС;
			Прервать;
		КонецЦикла;
			
	КонецЦикла;

КонецПроцедуры

Функция ПолучитьДатуПоискаСчетовФактурПоПриобретеннымЦенностям(ДатаДокумента)
	
	Если ДатаДокумента < '20150101'
		ИЛИ КонецДня(ДатаДокумента) <> КонецКвартала(ДатаДокумента) Тогда 
		Возврат КонецДня(ДатаДокумента);
	Иначе
		
		День = 24 * 60 * 60;
		ДатаПодачиДекларации = ДатаДокумента + 25 * День;
		
		// Перенесем на рабочие дни
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("ДатаКалендаря", ДатаПодачиДекларации);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	РегламентированныйПроизводственныйКалендарь.ДатаКалендаря КАК ДатаКалендаря
		|ПОМЕСТИТЬ РабочиеДни
		|ИЗ
		|	РегистрСведений.РегламентированныйПроизводственныйКалендарь КАК РегламентированныйПроизводственныйКалендарь
		|ГДЕ
		|	РегламентированныйПроизводственныйКалендарь.ВидДня = ЗНАЧЕНИЕ(Перечисление.ВидыДнейПроизводственногоКалендаря.Рабочий)
		|	И РегламентированныйПроизводственныйКалендарь.ДатаКалендаря >= &ДатаКалендаря
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РегламентированныйПроизводственныйКалендарь.ДатаКалендаря
		|ИЗ
		|	РегистрСведений.РегламентированныйПроизводственныйКалендарь КАК РегламентированныйПроизводственныйКалендарь
		|ГДЕ
		|	РегламентированныйПроизводственныйКалендарь.ВидДня = ЗНАЧЕНИЕ(Перечисление.ВидыДнейПроизводственногоКалендаря.Предпраздничный)
		|	И РегламентированныйПроизводственныйКалендарь.ДатаКалендаря >= &ДатаКалендаря
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МИНИМУМ(РабочиеДни.ДатаКалендаря) КАК ДатаКалендаря
		|ИЗ
		|	РабочиеДни КАК РабочиеДни
		|
		|ИМЕЮЩИЕ
		|	НЕ МИНИМУМ(РабочиеДни.ДатаКалендаря) ЕСТЬ NULL ";
		
		Результат = Запрос.Выполнить();
		
		Если НЕ Результат.Пустой() Тогда
			
			Выборка = Результат.Выбрать();
			Если Выборка.Следующий() Тогда
				ДатаПодачиДекларации = Выборка.ДатаКалендаря;
			КонецЕсли;
			
		КонецЕсли;
		
		Возврат КонецДня(ДатаПодачиДекларации);
		
	КонецЕсли;
	
КонецФункции
// ->

Процедура ПолучитьДанныеОДокументахОплатыВозвратАванса(ТаблицаРезультатов)

	Если УчетНДС.ВерсияПостановленияНДС1137(Дата) < 3 Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос();
	
	МассивСчетовАвансов = Новый Массив();
	МассивСчетовАвансов.Добавить(ПланыСчетов.Хозрасчетный.РасчетыПоАвансамПолученным);    // 62.02
	МассивСчетовАвансов.Добавить(ПланыСчетов.Хозрасчетный.РасчетыПоАвансамПолученнымВал); // 62.22
	МассивСчетовАвансов.Добавить(ПланыСчетов.Хозрасчетный.РасчетыПоАвансамПолученнымУЕ);  // 62.32
	
	СубконтоСчетаРасчетов = Новый Массив();
	СубконтоСчетаРасчетов.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
	СубконтоСчетаРасчетов.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры);
	
	СчетаУчетаДенежныхСредствОрганизации = Новый Массив();
	СчетаУчетаДенежныхСредствОрганизации.Добавить(ПланыСчетов.Хозрасчетный.РасчетныеСчета);
	СчетаУчетаДенежныхСредствОрганизации.Добавить(ПланыСчетов.Хозрасчетный.ВалютныеСчета);
	СчетаУчетаДенежныхСредствОрганизации.Добавить(ПланыСчетов.Хозрасчетный.КассаОрганизации);
	СчетаУчетаДенежныхСредствОрганизации.Добавить(ПланыСчетов.Хозрасчетный.ОперационнаяКасса);
	СчетаУчетаДенежныхСредствОрганизации.Добавить(ПланыСчетов.Хозрасчетный.КассаОрганизацииВал);
	
	Запрос.УстановитьПараметр("МассивСчетовАвансов", МассивСчетовАвансов);
	Запрос.УстановитьПараметр("СубконтоСчетаРасчетов",  СубконтоСчетаРасчетов);
	Запрос.УстановитьПараметр("СчетаУчетаДенежныхСредствОрганизации", СчетаУчетаДенежныхСредствОрганизации);
	Запрос.УстановитьПараметр("СписокАвансов", ТаблицаРезультатов);
	Запрос.УстановитьПараметр("Организация", 		Организация);
	Запрос.УстановитьПараметр("ДатаГраница", новый Граница(КонецДня(Дата),ВидГраницы.Включая));
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СписокАвансов.СчетФактура КАК ДокументАванса,
	|	СписокАвансов.ВозвратАвансовПолученных КАК ВозвратАвансовПолученных
	|ПОМЕСТИТЬ СписокСчетовФактур
	|ИЗ
	|	&СписокАвансов КАК СписокАвансов
	|ГДЕ
	|	СписокАвансов.ВозвратАвансовПолученных
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументАванса
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Хозрасчетный.Ссылка КАК Счет
	|ПОМЕСТИТЬ МассивСчетовАвансов
	|ИЗ
	|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|ГДЕ
	|	Хозрасчетный.Ссылка В ИЕРАРХИИ(&МассивСчетовАвансов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Счет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(РасчетыПоРеализации.СуммаРегОборот) КАК СуммаВозврата,
	|	РасчетыПоРеализации.Регистратор КАК ДокументВозвратаАванса,
	|	РасчетыПоРеализации.Период КАК ДатаСобытия,
	|	РасчетыПоРеализации.Документ КАК СчетФактура,
	|	РасчетыПоРеализации.Организация КАК Организация
	|ПОМЕСТИТЬ ДокументыВозвратаАванса
	|ИЗ
	|	РегистрНакопления.РасчетыПоРеализацииВУсловныхЕдиницахОрганизации.Обороты(
	|			,
	|			&ДатаГраница,
	|			Регистратор,
	|			СчетОплаты В
	|					(ВЫБРАТЬ
	|						МассивСчетовАвансов.Счет
	|					ИЗ
	|						МассивСчетовАвансов)
	|				И Организация = &Организация
	|				И Документ В
	|					(ВЫБРАТЬ
	|						СписокСчетовФактур.ДокументАванса
	|					ИЗ
	|						СписокСчетовФактур)) КАК РасчетыПоРеализации
	|ГДЕ
	|	РасчетыПоРеализации.СуммаРегРасход > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетыПоРеализации.Регистратор,
	|	РасчетыПоРеализации.Организация,
	|	РасчетыПоРеализации.Документ,
	|	РасчетыПоРеализации.Период
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	РасчетыПоРеализации.Организация,
	|	РасчетыПоРеализации.Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументыВозвратаАванса.СчетФактура КАК СчетФактура,
	|	НАЧАЛОПЕРИОДА(ДокументыВозвратаАванса.ДатаСобытия, ДЕНЬ) КАК ДатаСобытия,
	|	ДокументыВозвратаАванса.СуммаВозврата КАК СуммаБазис,
	|	ДокументыВозвратаАванса.ДокументВозвратаАванса КАК ДокументОплаты
	|ИЗ
	|	ДокументыВозвратаАванса КАК ДокументыВозвратаАванса
	|
	|УПОРЯДОЧИТЬ ПО
	|	СчетФактура";
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли; 
	
	ОплаченныйНДС = Результат.Выгрузить();
	
	Отбор = Новый Структура("СчетФактура,ДатаСобытия");

	ДополнительныеДокументыОплаты = ТаблицаРезультатов.СкопироватьКолонки();
	
	Для каждого СтрокаТаблицы Из ТаблицаРезультатов Цикл
		
		Если НЕ СтрокаТаблицы.ВозвратАвансовПолученных Тогда
			Продолжить;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(Отбор, СтрокаТаблицы);
		СтрокиОтбора = ОплаченныйНДС.НайтиСтроки(Отбор);
		
		Если СтрокиОтбора.Количество() = 0 Тогда
			Продолжить;
		ИначеЕсли СтрокиОтбора.Количество() = 1 Тогда
			// Документ оплаты один, распределение оплат не требуется
			ЗаполнитьЗначенияСвойств(СтрокаТаблицы, СтрокиОтбора[0]);
			ЗаполнитьДатуНомерДокументаОплатыВСтрокеТаблицы(СтрокаТаблицы, СтрокиОтбора[0]);
			Продолжить;
		КонецЕсли;
		
		БазисРаспределения = Новый Массив();
		ПустойМассив = Новый Массив();
		
		Для каждого СтрокаОтбора Из СтрокиОтбора Цикл
			БазисРаспределения.Добавить(СтрокаОтбора.СуммаБазис);
			ПустойМассив.Добавить(0);
		КонецЦикла;
		
		Если СтрокаТаблицы.СуммаБезНДС > 0 Тогда
			МассивСуммаБезНДС = ОбщегоНазначения.РаспределитьПропорционально(СтрокаТаблицы.СуммаБезНДС, БазисРаспределения);
		Иначе
			МассивСуммаБезНДС = ПустойМассив;
		КонецЕсли;
		
		Если СтрокаТаблицы.НДС > 0 Тогда
			МассивНДС = ОбщегоНазначения.РаспределитьПропорционально(СтрокаТаблицы.НДС, БазисРаспределения);
		Иначе
			МассивНДС = ПустойМассив;
		КонецЕсли; 

		Счетчик = 0;
		ПерваяСтрока = Истина;
		
		Для каждого СтрокаОтбора Из СтрокиОтбора Цикл
			Если ПерваяСтрока Тогда
				ЗаполнитьЗначенияСвойств(СтрокаТаблицы, СтрокаОтбора);
				СтрокаТаблицы.СуммаБезНДС = МассивСуммаБезНДС[Счетчик];
				СтрокаТаблицы.НДС = МассивНДС[Счетчик];
				ПерваяСтрока = Ложь;
				ЗаполнитьДатуНомерДокументаОплатыВСтрокеТаблицы(СтрокаТаблицы, СтрокаОтбора);
			Иначе
				ДополнительнаяСтрока = ДополнительныеДокументыОплаты.Добавить();
				ЗаполнитьЗначенияСвойств(ДополнительнаяСтрока, СтрокаТаблицы, ,"СуммаБезНДС, НДС");
				ЗаполнитьЗначенияСвойств(ДополнительнаяСтрока, СтрокаОтбора);
				ДополнительнаяСтрока.СуммаБезНДС = МассивСуммаБезНДС[Счетчик];
				ДополнительнаяСтрока.НДС = МассивНДС[Счетчик];
				ЗаполнитьДатуНомерДокументаОплатыВСтрокеТаблицы(ДополнительнаяСтрока, СтрокаОтбора);
			КонецЕсли;
			Счетчик = Счетчик + 1;
		КонецЦикла;
		
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ДополнительныеДокументыОплаты, ТаблицаРезультатов);

КонецПроцедуры

Процедура ПолучитьДанныеОДокументахОплатыКомандировочныеРасходы(ТаблицаРезультатов)

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаРезультатов", ТаблицаРезультатов);
	Запрос.УстановитьПараметр("Организация",        Организация);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаРезультатов.СчетФактура КАК СчетФактура
	|ПОМЕСТИТЬ ВТКомандировочныеРасходы
	|ИЗ
	|	&ТаблицаРезультатов КАК ТаблицаРезультатов
	|ГДЕ
	|	ТаблицаРезультатов.СчетФактура ССЫЛКА Документ.СчетФактураПолученный
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТКомандировочныеРасходы.СчетФактура,
	|	ВЫРАЗИТЬ(ВТКомандировочныеРасходы.СчетФактура КАК Документ.СчетФактураПолученный).ДокументОснование КАК ДокументОснование
	|ПОМЕСТИТЬ КомандировочныеРасходы
	|ИЗ
	|	ВТКомандировочныеРасходы КАК ВТКомандировочныеРасходы
	|ГДЕ
	|	ВТКомандировочныеРасходы.СчетФактура ССЫЛКА Документ.СчетФактураПолученный
	|	И ВЫРАЗИТЬ(ВТКомандировочныеРасходы.СчетФактура КАК Документ.СчетФактураПолученный).ДокументОснование ССЫЛКА Документ.АвансовыйОтчет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КомандировочныеРасходы.СчетФактура,
	|	КомандировочныеРасходы.ДокументОснование КАК ДокументОплаты
	|ИЗ
	|	КомандировочныеРасходы КАК КомандировочныеРасходы";

	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	КомандировочныеРасходы = Результат.Выгрузить();
	
	Отбор = Новый Структура("СчетФактура");

	Для каждого СтрокаТаблицы Из ТаблицаРезультатов Цикл
		
		ЗаполнитьЗначенияСвойств(Отбор, СтрокаТаблицы);
		СтрокиОтбора = КомандировочныеРасходы.НайтиСтроки(Отбор);
		
		Для каждого СтрокаОтбора Из СтрокиОтбора Цикл
			ЗаполнитьЗначенияСвойств(СтрокаТаблицы, СтрокаОтбора);
			ЗаполнитьДатуНомерДокументаОплатыВСтрокеТаблицы(СтрокаТаблицы, СтрокаОтбора);
			Прервать;
		КонецЦикла;
			
	КонецЦикла;

КонецПроцедуры

Функция ТекстЗапросаАвансыПолученные()
	
	Возврат
	"ВЫБРАТЬ
	|	ФормированиеЗаписейКнигиПокупокНДСсАвансов.СчетФактура КАК ДокументОснование
	|ПОМЕСТИТЬ ВТ_ДокументыОснования
	|ИЗ
	|	Документ.ФормированиеЗаписейКнигиПокупок.НДСсАвансов КАК ФормированиеЗаписейКнигиПокупокНДСсАвансов
	|ГДЕ
	|	ФормированиеЗаписейКнигиПокупокНДСсАвансов.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СчетФактураВыданный.Ссылка,
	|	СчетФактураВыданный.КодВидаОперации,
	|	СчетФактураВыданный.ДокументОснование КАК ДокументОснование,
	|	СчетФактураВыданный.ДоговорКонтрагента
	|ПОМЕСТИТЬ СчетаФактурыДокументы
	|ИЗ
	|	Документ.СчетФактураВыданный КАК СчетФактураВыданный
	|ГДЕ
	|	СчетФактураВыданный.ДокументОснование В
	|			(ВЫБРАТЬ
	|				ВТ_ДокументыОснования.ДокументОснование
	|			ИЗ
	|				ВТ_ДокументыОснования)
	|	И СчетФактураВыданный.Проведен
	|	И СчетФактураВыданный.Дата >= ДАТАВРЕМЯ(2015, 10, 1)
	|	И СчетФактураВыданный.Организация = &Организация
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗачетАвансовПолученных.Покупатель КАК Покупатель,
	|	ЗачетАвансовПолученных.Покупатель КАК Поставщик,
	|	ВЫБОР
	|		КОГДА ЗачетАвансовПолученных.ВозвратАвансовПолученных
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ВозвратАвансовПолученных)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.АвансыПолученные)
	|	КОНЕЦ КАК ВидЦенности,
	|	ВЫБОР
	|		КОГДА &ПредъявленНДСКВычету0
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПокупки.ПредъявленНДСКВычету0)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПокупки.ПредъявленНДСКВычету)
	|	КОНЕЦ КАК Событие,
	|	ЗачетАвансовПолученных.ДоговорКонтрагента,
	|	ЗачетАвансовПолученных.СчетФактура,
	|	ЗачетАвансовПолученных.СтавкаНДС,
	|	ЗачетАвансовПолученных.СуммаБезНДС,
	|	ЗачетАвансовПолученных.НДС КАК НДС,
	|	ЗачетАвансовПолученных.ДатаСобытия,
	|	ВЫБОР
	|		КОГДА ЗачетАвансовПолученных.ДатаСобытия = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА &ДатаДокумента
	|		ИНАЧЕ ЗачетАвансовПолученных.ДатаСобытия
	|	КОНЕЦ КАК ДатаОплаты,
	|	ЗачетАвансовПолученных.ДокументОтгрузки КАК ДокументОплаты,
	|	ЗачетАвансовПолученных.ДокументОтгрузки КАК ДокументОтгрузки,
	|	ЗачетАвансовПолученных.ДокументОтгрузки.Дата КАК ДокументОтгрузкиДата,
	|	ЗачетАвансовПолученных.ЗаписьДополнительногоЛиста,
	|	ВЫБОР
	|		КОГДА ЗачетАвансовПолученных.ЗаписьДополнительногоЛиста
	|			ТОГДА ЗачетАвансовПолученных.КорректируемыйПериод
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|	КОНЕЦ КАК КорректируемыйПериод,
	|	ЗачетАвансовПолученных.ВалютаДокумента,
	|	ЗачетАвансовПолученных.ВалютнаяСумма,
	|	ВЫБОР
	|		КОГДА ЗачетАвансовПолученных.ИсправленныйСчетФактура В (&ПустыеДокументыИсправления)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ЗачетАвансовПолученных.ИсправленныйСчетФактура
	|	КОНЕЦ КАК ИсправленныйСчетФактура,
	|	ВЫБОР
	|		КОГДА &ДатаДокумента < ДАТАВРЕМЯ(2015, 1, 1)
	|			ТОГДА """"
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ЕСТЬNULL(СчетаФактурыДокументы.КодВидаОперации, ""22"") = ""26""
	|					ТОГДА ""26""
	|				ИНАЧЕ ""22""
	|			КОНЕЦ
	|	КОНЕЦ КАК КодВидаОперации,
	|	ВЫБОР
	|		КОГДА &ДатаДокумента < ДАТАВРЕМЯ(2014, 10, 1)
	|			ТОГДА """"
	|		ИНАЧЕ ЗачетАвансовПолученных.НомерДокументаОплаты
	|	КОНЕЦ КАК НомерДокументаОплаты,
	|	ВЫБОР
	|		КОГДА &ДатаДокумента < ДАТАВРЕМЯ(2014, 10, 1)
	|			ТОГДА ДАТАВРЕМЯ(1, 1, 1)
	|		ИНАЧЕ ЗачетАвансовПолученных.ДатаДокументаОплаты
	|	КОНЕЦ КАК ДатаДокументаОплаты,
	|	ЗачетАвансовПолученных.Ссылка.Организация КАК Организация,
	|	ЗачетАвансовПолученных.Состояние,
	|	ЗачетАвансовПолученных.НомерСтроки
	|ИЗ
	|	Документ.ФормированиеЗаписейКнигиПокупок.НДСсАвансов КАК ЗачетАвансовПолученных
	|		ЛЕВОЕ СОЕДИНЕНИЕ СчетаФактурыДокументы КАК СчетаФактурыДокументы
	|		ПО ЗачетАвансовПолученных.СчетФактура = СчетаФактурыДокументы.ДокументОснование
	|			И ЗачетАвансовПолученных.ДоговорКонтрагента = СчетаФактурыДокументы.ДоговорКонтрагента
	|ГДЕ
	|	ЗачетАвансовПолученных.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЗачетАвансовПолученных.НомерСтроки";
	
КонецФункции

Функция ТекстЗапросаИзменениеСтоимостиВСторонуУменьшения()

	Возврат
	"ВЫБРАТЬ
	|	ВычетПриИзмененииСтоимостиВСторонуУменьшения.СчетФактура КАК ДокументОснование
	|ПОМЕСТИТЬ ВТ_ДокументыОснованияУменьшениеСтоимости
	|ИЗ
	|	Документ.ФормированиеЗаписейКнигиПокупок.ВычетПриИзмененииСтоимостиВСторонуУменьшения КАК ВычетПриИзмененииСтоимостиВСторонуУменьшения
	|ГДЕ
	|	ВычетПриИзмененииСтоимостиВСторонуУменьшения.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СчетФактураВыданный.Ссылка,
	|	СчетФактураВыданный.КодВидаОперацииНаУменьшение КАК КодВидаОперации,
	|	СчетФактураВыданныйДокументыОснования.ДокументОснование КАК ДокументОснование
	|ПОМЕСТИТЬ СчетаФактурыДокументыУменьшениеСтоимости
	|ИЗ
	|	Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный КАК СчетФактураВыданный
	|		ПО СчетФактураВыданныйДокументыОснования.Ссылка = СчетФактураВыданный.Ссылка
	|ГДЕ
	|	СчетФактураВыданныйДокументыОснования.ДокументОснование В
	|			(ВЫБРАТЬ
	|				ВТ_ДокументыОснованияУменьшениеСтоимости.ДокументОснование
	|			ИЗ
	|				ВТ_ДокументыОснованияУменьшениеСтоимости)
	|	И СчетФактураВыданный.Проведен
	|	И СчетФактураВыданный.Дата >= ДАТАВРЕМЯ(2015, 1, 1)
	|	И СчетФактураВыданный.Организация = &Организация
	|	И СчетФактураВыданный.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.Корректировочный)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВычетПриИзмененииСтоимостиВСторонуУменьшения.СчетФактура,
	|	ВычетПриИзмененииСтоимостиВСторонуУменьшения.Поставщик,
	|	НЕОПРЕДЕЛЕНО КАК ДоговорКонтрагента,
	|	ВычетПриИзмененииСтоимостиВСторонуУменьшения.НДС,
	|	ВычетПриИзмененииСтоимостиВСторонуУменьшения.СуммаБезНДС,
	|	ВычетПриИзмененииСтоимостиВСторонуУменьшения.СтавкаНДС,
	|	ВычетПриИзмененииСтоимостиВСторонуУменьшения.СчетУчетаНДС,
	|	НЕОПРЕДЕЛЕНО КАК ДокументОтгрузки,
	|	НЕОПРЕДЕЛЕНО КАК ДокументОплаты,
	|	НЕОПРЕДЕЛЕНО КАК Состояние,
	|	ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПокупки.ПредъявленНДСКВычету) КАК Событие,
	|	ВычетПриИзмененииСтоимостиВСторонуУменьшения.СчетФактура.Дата КАК ДатаСчетаФактуры,
	|	ВычетПриИзмененииСтоимостиВСторонуУменьшения.ИсправленныйСчетФактура.Дата КАК ДатаИсправленногоСчетаФактуры,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаСобытия,
	|	НЕОПРЕДЕЛЕНО КАК ДатаОплаты,
	|	ВычетПриИзмененииСтоимостиВСторонуУменьшения.ВидЦенности,
	|	НЕОПРЕДЕЛЕНО КАК ЗаписьДополнительногоЛиста,
	|	НЕОПРЕДЕЛЕНО КАК КорректируемыйПериод,
	|	ВЫБОР
	|		КОГДА ВычетПриИзмененииСтоимостиВСторонуУменьшения.ИсправленныйСчетФактура В (&ПустыеДокументыИсправления)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ВычетПриИзмененииСтоимостиВСторонуУменьшения.ИсправленныйСчетФактура
	|	КОНЕЦ КАК ИсправленныйСчетФактура,
//начало изменений Ожиганов 28.06.2016 б/н исправление ошибок, после обновления блока по НДС 
	|	ВЫБОР
	|		КОГДА ВычетПриИзмененииСтоимостиВСторонуУменьшения.ИсправленныйСчетФактура В (&ПустыеДокументыИсправления)
	|			ТОГДА ДатаВремя(1,1,1)
	|		ИНАЧЕ ВычетПриИзмененииСтоимостиВСторонуУменьшения.ИсправленныйСчетФактура.Дата
	|	КОНЕЦ КАК ИсправленныйСчетФактураДата,
//конец изменений 
	|	""НДС"" КАК Содержание,
	|	ВычетПриИзмененииСтоимостиВСторонуУменьшения.Ссылка.Дата КАК Период,
	|	ВычетПриИзмененииСтоимостиВСторонуУменьшения.Ссылка КАК Регистратор,
	|	ВычетПриИзмененииСтоимостиВСторонуУменьшения.Ссылка.Организация,
	|	"""" КАК НомерДокументаОплаты,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаДокументаОплаты,
	|	ВЫБОР
	|		КОГДА &ДатаДокумента < ДАТАВРЕМЯ(2015, 1, 1)
	|			ТОГДА """"
	|		ИНАЧЕ ЕСТЬNULL(СчетаФактурыДокументы.КодВидаОперации, ""18"")
	|	КОНЕЦ КАК КодВидаОперации
	|ИЗ
	|	Документ.ФормированиеЗаписейКнигиПокупок.ВычетПриИзмененииСтоимостиВСторонуУменьшения КАК ВычетПриИзмененииСтоимостиВСторонуУменьшения
	|		ЛЕВОЕ СОЕДИНЕНИЕ СчетаФактурыДокументыУменьшениеСтоимости КАК СчетаФактурыДокументы
	|		ПО ВычетПриИзмененииСтоимостиВСторонуУменьшения.СчетФактура = СчетаФактурыДокументы.ДокументОснование
	|ГДЕ
	|	ВычетПриИзмененииСтоимостиВСторонуУменьшения.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВычетПриИзмененииСтоимостиВСторонуУменьшения.НомерСтроки";
	
КонецФункции

Процедура ЗаполнитьДатуНомерДокументаОплатыВСтрокеТаблицы(СтрокаТаблицы, СтрокаОтбора)

	Если ЗначениеЗаполнено(СтрокаОтбора.ДокументОплаты) Тогда
		СтрокаТаблицы.ДатаДокументаОплаты = СтрокаОтбора.ДокументОплаты.Дата;
		СтрокаТаблицы.НомерДокументаОплаты =
			ОбщегоНазначения.ПолучитьНомерНаПечать(СтрокаОтбора.ДокументОплаты);
	КонецЕсли;

КонецПроцедуры

мВалютаРегламентированногоУчета = глЗначениеПеременной("ВалютаРегламентированногоУчета");

Перем мОрганизация, мБанк;

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Проведен Тогда
		ОбъектВБазе = Ссылка.ПолучитьОбъект();
		мОрганизация = ОбъектВБазе.Организация;
		мБанк = ОбъектВБазе.Банк;
	КонецЕсли;
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		Запрос = Новый Запрос;
		
		МассивФизическихЛиц = РаботникиОрганизации.ВыгрузитьКолонку("ФизЛицо");
		Запрос.УстановитьПараметр("ФизическиеЛица", МассивФизическихЛиц);
		Запрос.УстановитьПараметр("Организация", Организация);
		Запрос.УстановитьПараметр("ДокументСсылка", Ссылка);
		
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЗаявкаНаЗакрытиеСчетов.Ссылка КАК Документ,
		|	ПРЕДСТАВЛЕНИЕ(ЗаявкаНаЗакрытиеСчетовРаботникиОрганизации.ФизЛицо) КАК ФизЛицоНаименование
		|ИЗ
		|	Документ.ЗаявкаНаЗакрытиеСчетов.РаботникиОрганизации КАК ЗаявкаНаЗакрытиеСчетовРаботникиОрганизации
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаЗакрытиеСчетов КАК ЗаявкаНаЗакрытиеСчетов
		|		ПО ЗаявкаНаЗакрытиеСчетовРаботникиОрганизации.Ссылка = ЗаявкаНаЗакрытиеСчетов.Ссылка
		|ГДЕ
		|	ЗаявкаНаЗакрытиеСчетов.Организация = &Организация
		|	И ЗаявкаНаЗакрытиеСчетов.Ссылка <> &ДокументСсылка
		|	И ЗаявкаНаЗакрытиеСчетов.Проведен
		|	И ЗаявкаНаЗакрытиеСчетовРаботникиОрганизации.ФизЛицо В(&ФизическиеЛица)";
		
		УстановитьПривилегированныйРежим(Истина);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Количество() > 0 Тогда
			СтрокаСообщения = "На указанных сотрудников уже введены данные о закрытии лицевого счета:";
			Пока Выборка.Следующий() Цикл
				СтрокаСообщения = СтрокаСообщения + Символы.ПС + "    " + Выборка.ФизЛицоНаименование;
			КонецЦикла;
			Сообщить(СтрокаСообщения);
			Отказ = Истина;
		КонецЕсли;
		УстановитьПривилегированныйРежим(Ложь);
		
	КонецЕсли;
	
	МассивТЧ = Новый Массив();
	МассивТЧ.Добавить(РаботникиОрганизации);
	
	КраткийСоставДокумента = ПроцедурыУправленияПерсоналом.ЗаполнитьКраткийСоставДокумента(МассивТЧ, "Физлицо");
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Процедура ЗаполнитьЗакрытиеСчетов() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ФизическиеЛица.Ссылка КАК ФизЛицо,
	|	ДОБАВИТЬКДАТЕ(РаботникиОрганизацийСрезПоследних.Период, ДЕНЬ, -1) КАК ДатаЗакрытия,
	|	ЛицевыеСчетаРаботниковОрганизации.НомерЛицевогоСчета КАК НомерЛицевогоСчета
	|ИЗ
	|	Справочник.ФизическиеЛица КАК ФизическиеЛица
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций.СрезПоследних(
	|				&ДатаСреза,
	|				Организация = &ГоловнаяОрганизация
	|					И Сотрудник.ВидЗанятости <> ЗНАЧЕНИЕ(Перечисление.ВидыЗанятостиВОрганизации.ВнутреннееСовместительство)) КАК РаботникиОрганизацийСрезПоследних
	|		ПО ФизическиеЛица.Ссылка = РаботникиОрганизацийСрезПоследних.Сотрудник.Физлицо
	|			И (РаботникиОрганизацийСрезПоследних.ПричинаИзмененияСостояния = ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.Увольнение))
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЛицевыеСчетаРаботниковОрганизации КАК ЛицевыеСчетаРаботниковОрганизации
	|		ПО ФизическиеЛица.Ссылка = ЛицевыеСчетаРаботниковОрганизации.ФизЛицо
	|			И (ЛицевыеСчетаРаботниковОрганизации.Организация = &Организация)
	|			И (ЛицевыеСчетаРаботниковОрганизации.Банк = &Банк
	|				ИЛИ &Банк = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка))
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаЗакрытиеСчетов.РаботникиОрганизации КАК ЗаявкаНаЗакрытиеСчетовРаботникиОрганизации
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаЗакрытиеСчетов КАК ЗаявкаНаЗакрытиеСчетов
	|			ПО ЗаявкаНаЗакрытиеСчетовРаботникиОрганизации.Ссылка = ЗаявкаНаЗакрытиеСчетов.Ссылка
	|				И (ЗаявкаНаЗакрытиеСчетов.Организация = &Организация)
	|				И (ЗаявкаНаЗакрытиеСчетов.Банк = &Банк
	|					ИЛИ &Банк = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка))
	|				И (ЗаявкаНаЗакрытиеСчетов.Проведен)
	|				И (ЗаявкаНаЗакрытиеСчетов.Ссылка <> &Документ)
	|		ПО ФизическиеЛица.Ссылка = ЗаявкаНаЗакрытиеСчетовРаботникиОрганизации.ФизЛицо
	|ГДЕ
	|	ЗаявкаНаЗакрытиеСчетовРаботникиОрганизации.ФизЛицо ЕСТЬ NULL ";
	
	Запрос.УстановитьПараметр("ГоловнаяОрганизация", ОбщегоНазначенияЗК.ГоловнаяОрганизация(Организация));
	Запрос.УстановитьПараметр("Документ", Ссылка);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Банк", Банк);
	Запрос.УстановитьПараметр("ДатаСреза", КонецДня(Дата) + 1);
	
	РаботникиОрганизации.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры
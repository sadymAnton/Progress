//++ Spl_Апроф 05.09.2014 (k.russkih@a-prof.ru) Добавил кнопку "Файлы" на форму элемента
//++ Spl_Апроф 05.09.2014 (k.russkih@a-prof.ru) Добавил реквизит ДокументОснования "ДокументСсылка.ПР_Претензия" и ввод на основании. Вывел на форму элемента вкладка "дополнительно"
//++ Spl_Апроф 14.01.2015 (k.russkih@a-prof.ru) Добавил реквизит "КП_Отделение" вынес на форму элемента, списка, выбора

Перем мУдалятьДвижения;

Перем мВалютаРегламентированногоУчета Экспорт;

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОКУМЕНТА

#Если Клиент Тогда

// Функция рассчитывает сумму списанную по текущей строке документа
//
Функция РассчитатьСуммуСписания( ТекСтрока, ТабДвижений)
	
	СтруктПоиска = Новый Структура;
	СтруктПоиска.Вставить( "Номенклатура",               ТекСтрока.Номенклатура);
	СтруктПоиска.Вставить( "ХарактеристикаНоменклатуры", ТекСтрока.Характеристика);
	Если (НЕ ТекСтрока.Номенклатура.ВестиУчетПоСериям) ИЛИ (ТекСтрока.Номенклатура.ВестиПартионныйУчетПоСериям) Тогда
		СтруктПоиска.Вставить( "СерияНоменклатуры",          ТекСтрока.Серия);
	КонецЕсли;	
	СтруктПоиска.Вставить( "СчетУчета",                  ТекСтрока.СчетУчетаБУ);
	Если ВидОперации = Перечисления.ВидыОперацийСписаниеТоваров.ТоварыПродукция Тогда
		//для оборудования качество не заполняется
		СтруктПоиска.Вставить( "Качество",                   ТекСтрока.Качество);
	КонецЕсли;
	
	НаборСтрок = ТабДвижений.НайтиСтроки(СтруктПоиска);
	КолСписать = ТекСтрока.Количество;
	Сумма      = 0;
	
	Для Каждого СтрокаДвиж Из НаборСтрок Цикл
		Если КолСписать >= ТекСтрока.Количество Тогда
			Сумма      = Сумма      + СтрокаДвиж.Стоимость;
			КолСписать = КолСписать - СтрокаДвиж.Количество;
			СтрокаДвиж.Стоимость  = 0;
			СтрокаДвиж.Количество = 0;
		Иначе
			Если Не СтрокаДвиж.Количество = 0 Тогда
				СумСписать = Окр( СтрокаДвиж.Стоимость * КолСписать / СтрокаДвиж.Количество, 2, 1);
				Сумма      = Сумма + СумСписать;
				СтрокаДвиж.Стоимость  = СтрокаДвиж.Стоимость  - СумСписать;
				СтрокаДвиж.Количество = СтрокаДвиж.Количество - КолСписать;
			Иначе
				Сумма = Сумма + СтрокаДвиж.Стоимость;
				СтрокаДвиж.Стоимость = 0;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Сумма;
	
КонецФункции // РассчитатьСуммуСписания()

// Функция формирует табличный документ с печатной формой накладной,
// разработанной методистами
//
// Возвращаемое значение:
//  Табличный документ - печатная форма накладной
//
Функция ПечатьСписаниеТоваров()

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТекущийДокумент", ЭтотОбъект.Ссылка);
	Запрос.УстановитьПараметр("ВидСкладаНТТ",    Перечисления.ВидыСкладов.НТТ);
	Запрос.УстановитьПараметр("Организация",     Организация);
	Запрос.УстановитьПараметр("Подразделение",   Подразделение);

	Запрос.Текст =
	"ВЫБРАТЬ
	|	Номер,
	|	Дата,
	|	СписаниеТоваров.Организация,
	|	Склад,
	|	Склад.Представление         КАК ПредставлениеСклада,
	|	ПРЕДСТАВЛЕНИЕ(
	|	ВЫБОР	КОГДА СписаниеТоваров.ОтражатьВБухгалтерскомУчете
	|			ТОГДА ЕСТЬNULL(ТаблицаСоответствиеОрганизации.ПодразделениеОрганизации, """")
	|			ИНАЧЕ СписаниеТоваров.Подразделение
	|	КОНЕЦ) КАК ПредставлениеПодразделения,
	|	ВЫБОР	КОГДА СписаниеТоваров.ОтражатьВБухгалтерскомУчете
	|			ТОГДА ЕСТЬNULL(ТаблицаСоответствиеОрганизации.ПодразделениеОрганизации, """")
	|			ИНАЧЕ СписаниеТоваров.Подразделение
	|	КОНЕЦ КАК Подразделение,
	|	СуммаДокумента,
	|	Константы.ВалютаРегламентированногоУчета ВалютаДокумента,
	|	Товары.(
	|		НомерСтроки,
	|		Номенклатура,
	|		Номенклатура.НаименованиеПолное КАК Товар,
	|		Номенклатура.Код                КАК Код,
	|		Номенклатура.Артикул            КАК Артикул,
	|		КоличествоМест,
	|		Количество,
	|		Качество,
	|		СчетУчетаБУ,
	|		ЕдиницаИзмерения.Представление     КАК ЕдиницаИзмерения,
	|		ЕдиницаИзмеренияМест.Представление КАК ЕдиницаИзмеренияМест,
	|		ВЫБОР
	|			КОГДА Склад.ВидСклада = &ВидСкладаНТТ ТОГДА Цена
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК Цена,
	|		ВЫБОР
	|			КОГДА Склад.ВидСклада = &ВидСкладаНТТ ТОГДА Сумма
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК Сумма,
	|		ХарактеристикаНоменклатуры КАК Характеристика,
	|		СерияНоменклатуры КАК Серия
	|	),
	|	ВозвратнаяТара.(
	|		НомерСтроки,
	|		Номенклатура,
	|		Номенклатура.НаименованиеПолное КАК Товар,
	|		Номенклатура.Код                КАК Код,
	|		Номенклатура.Артикул            КАК Артикул,
	|		Количество,
	|		Номенклатура.ЕдиницаХраненияОстатков.Представление КАК ЕдиницаИзмерения,
	|		0 КАК Цена,
	|		0 КАК Сумма
	|	)
	|ИЗ
	|	Документ.СписаниеТоваров КАК СписаниеТоваров
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|(
	|	ВЫБРАТЬ
	|		СоответсвиеПодразделений.Подразделение,
	|		СоответсвиеПодразделений.Организация,
	|		МИНИМУМ(СоответсвиеПодразделений.ПодразделениеОрганизации) КАК ПодразделениеОрганизации,
	|		МАКСИМУМ(СоответсвиеПодразделений.ПодразделениеОрганизации) КАК ПодразделениеОрг
	|	ИЗ
	|		РегистрСведений.СоответствиеПодразделенийИПодразделенийОрганизаций КАК СоответсвиеПодразделений
	|	ГДЕ
	|		Подразделение = &Подразделение
	|		И Организация = &Организация
	|		И Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|		И Подразделение <> ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|	СГРУППИРОВАТЬ ПО
	|		Подразделение,
	|		Организация
	|	ИМЕЮЩИЕ
	|		КОЛИЧЕСТВО(*) = 1
	|) ТаблицаСоответствиеОрганизации
	|ПО
	|	ТаблицаСоответствиеОрганизации.Подразделение = СписаниеТоваров.Подразделение
	|	И ТаблицаСоответствиеОрганизации.Организация = СписаниеТоваров.Организация
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	Константы КАК Константы
	|ПО
	|	ИСТИНА
	|ГДЕ
	|	СписаниеТоваров.Ссылка = &ТекущийДокумент
	|УПОРЯДОЧИТЬ ПО
	|	Товары.НомерСтроки,
	|	ВозвратнаяТара.НомерСтроки
	|";

	Шапка = Запрос.Выполнить().Выбрать();
	Шапка.Следующий();
	ВыборкаСтрокТовары = Шапка.Товары.Выбрать();
	ВыборкаСтрокТара   = Шапка.ВозвратнаяТара.Выбрать();

	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СписаниеТоваров_Накладная";

	Макет = ПолучитьМакет("Накладная");

	// Выводим шапку накладной
	ОбластьМакета                          = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.ТекстЗаголовка = ОбщегоНазначения.СформироватьЗаголовокДокумента(Шапка, "Списание товаров");
	ТабДокумент.Вывести(ОбластьМакета);

	ОбластьМакета = Макет.ПолучитьОбласть("РевизитыОрганизации");
	ОбластьМакета.Параметры.Заполнить(Шапка);
	ПредставлениеОрганизации = ФормированиеПечатныхФорм.ОписаниеОрганизации(УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(Шапка.Организация, Шапка.Дата), "ПолноеНаименование,");
	ОбластьМакета.Параметры.ПредставлениеОрганизации = ПредставлениеОрганизации;
	ТабДокумент.Вывести(ОбластьМакета);

	ДопКолонка = Константы.ДополнительнаяКолонкаПечатныхФормДокументов.Получить();
	Если ДопКолонка = Перечисления.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул Тогда
		ВыводитьКоды = Истина;
		Колонка = "Артикул";
	ИначеЕсли ДопКолонка = Перечисления.ДополнительнаяКолонкаПечатныхФормДокументов.Код Тогда
		ВыводитьКоды = Истина;
		Колонка = "Код";
	Иначе
		ВыводитьКоды = Ложь;
	КонецЕсли;
	
	//Движения.ПартииТоваровНаСкладахБухгалтерскийУчет.Прочитать();
	
	СтруктраПараметров = Новый Структура();
	СтруктраПараметров.Вставить("Регистратор", ЭтотОбъект.Ссылка);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СписаниеТоваров.Номенклатура,
	|	СписаниеТоваров.ХарактеристикаНоменклатуры,
	|	СписаниеТоваров.СерияНоменклатуры,
	|	СписаниеТоваров.СчетУчетаБУ КАК СчетУчета,
	|	СписаниеТоваров.Качество,
	|	СписаниеТоваров.Количество,
	|	ВЫБОР
	|		КОГДА СписаниеТоваров.Ссылка.Склад.ВидСклада <> ЗНАЧЕНИЕ(Перечисление.ВидыСкладов.НТТ)
	|			ТОГДА ВЫРАЗИТЬ(
	|				ЕСТЬNULL(ТаблицаСебестоимостиОбороты.Стоимость, 0)
	|				* СписаниеТоваров.Количество 
	|				* СписаниеТоваров.Коэффициент
	|				/ ЕСТЬNULL(СписаниеТоваров.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент, 1)
	|				/ ЕСТЬNULL(ТаблицаСебестоимостиОбороты.Количество, 1)
	|	 		КАК ЧИСЛО(15,2))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Стоимость,
	|	СписаниеТоваров.ХарактеристикаНоменклатуры КАК Характеристика,
	|	СписаниеТоваров.СерияНоменклатуры КАК Серия,
	|	СписаниеТоваров.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	Документ.СписаниеТоваров.Товары КАК СписаниеТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСебестоимостиОбороты КАК ТаблицаСебестоимостиОбороты
	|		ПО СписаниеТоваров.Номенклатура = ТаблицаСебестоимостиОбороты.Номенклатура
	|			И СписаниеТоваров.Ссылка = ТаблицаСебестоимостиОбороты.Регистратор
	|			И (СписаниеТоваров.ХарактеристикаНоменклатуры = ТаблицаСебестоимостиОбороты.ХарактеристикаНоменклатуры
	|				ИЛИ ТаблицаСебестоимостиОбороты.ХарактеристикаНоменклатуры = ""Не используется"")
	|			И (СписаниеТоваров.СерияНоменклатуры = ТаблицаСебестоимостиОбороты.СерияНоменклатуры
	|				ИЛИ ТаблицаСебестоимостиОбороты.СерияНоменклатуры = ""Не используется"")
	|			И ((СписаниеТоваров.СчетУчетаБУ = ТаблицаСебестоимостиОбороты.СчетУчета) 
	|				ИЛИ (ТаблицаСебестоимостиОбороты.СчетУчета = ""Не используется""))"+
	?(ВидОперации=Перечисления.ВидыОперацийСписаниеТоваров.ТоварыПродукция,"
	|			И ((СписаниеТоваров.Качество = ТаблицаСебестоимостиОбороты.Качество)
	|				ИЛИ (ТаблицаСебестоимостиОбороты.Качество = ""Не используется""))","")+"
	|ГДЕ
	|	СписаниеТоваров.Ссылка = &Регистратор";
	
	ТабДвижений = УправлениеЗапасами.ПолучитьТаблицуЗатратДляПечатиИЗаполненияДокументов(Ссылка, ТекстЗапроса, СтруктраПараметров);
	
	Если ТабДвижений = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;	
	

	ОбластьНомера = Макет.ПолучитьОбласть("ШапкаТаблицы|НомерСтроки");
	ОбластьКодов  = Макет.ПолучитьОбласть("ШапкаТаблицы|КолонкаКодов");
	ОбластьДанных = Макет.ПолучитьОбласть("ШапкаТаблицы|Данные");

	ТабДокумент.Вывести(ОбластьНомера);
	Если ВыводитьКоды Тогда
		ОбластьКодов.Параметры.ИмяКолонкиКодов = Колонка;
		ТабДокумент.Присоединить(ОбластьКодов);
	КонецЕсли;
	ТабДокумент.Присоединить(ОбластьДанных);
	ОбластьКолонкаТовар = Макет.Область("Товар");
	Если Не ВыводитьКоды Тогда
		ОбластьКолонкаТовар.ШиринаКолонки = ОбластьКолонкаТовар.ШиринаКолонки
		                                  + Макет.Область("КолонкаКодов").ШиринаКолонки;
	КонецЕсли;

	ОбластьНомера = Макет.ПолучитьОбласть("Строка|НомерСтроки");
	ОбластьКодов  = Макет.ПолучитьОбласть("Строка|КолонкаКодов");
	ОбластьДанных = Макет.ПолучитьОбласть("Строка|Данные");

	СуммаИтого = 0;
	Ном        = 0;

	// Товары
	Пока ВыборкаСтрокТовары.Следующий() Цикл

		Если НЕ ЗначениеЗаполнено(ВыборкаСтрокТовары.Номенклатура) Тогда
			Сообщить("В одной из строк не заполнено значение номенклатуры - строка при печати пропущена.", СтатусСообщения.Важное);
			Продолжить;
		КонецЕсли;

		Ном = Ном + 1;

		ОбластьНомера.Параметры.Заполнить(ВыборкаСтрокТовары);
		ОбластьНомера.Параметры.НомерСтроки = Ном;
		ТабДокумент.Вывести(ОбластьНомера);

		Если ВыводитьКоды Тогда
			Если Колонка = "Артикул" Тогда
				ОбластьКодов.Параметры.Артикул = ВыборкаСтрокТовары.Артикул;
			Иначе
				ОбластьКодов.Параметры.Артикул = ВыборкаСтрокТовары.Код;
			КонецЕсли;
			ТабДокумент.Присоединить(ОбластьКодов);
		КонецЕсли;

		ОбластьДанных.Параметры.Заполнить(ВыборкаСтрокТовары);
		ОбластьДанных.Параметры.Товар = ВыборкаСтрокТовары.Товар + ФормированиеПечатныхФорм.ПредставлениеСерий(ВыборкаСтрокТовары);

		Если Склад.ВидСклада <> Перечисления.ВидыСкладов.НТТ Тогда
			ОбластьДанных.Параметры.Сумма = РассчитатьСуммуСписания(ВыборкаСтрокТовары, ТабДвижений);
			ОбластьДанных.Параметры.Цена  = Окр(ОбластьДанных.Параметры.Сумма / ОбластьДанных.Параметры.Количество, 2, 1);
		КонецЕсли;
		
		ТабДокумент.Присоединить(ОбластьДанных);

		СуммаИтого = СуммаИтого + ОбластьДанных.Параметры.Сумма;

	КонецЦикла;

	// Тара
	Пока ВыборкаСтрокТара.Следующий() Цикл

		Если НЕ ЗначениеЗаполнено(ВыборкаСтрокТара.Номенклатура) Тогда
			Сообщить("В одной из строк не заполнено значение номенклатуры - строка при печати пропущена.", СтатусСообщения.Важное);
			Продолжить;
		КонецЕсли;

		Ном = Ном + 1;

		ОбластьНомера.Параметры.Заполнить(ВыборкаСтрокТара);
		ОбластьНомера.Параметры.НомерСтроки = Ном;
		ТабДокумент.Вывести(ОбластьНомера);

		Если ВыводитьКоды Тогда
			Если Колонка = "Артикул" Тогда
				ОбластьКодов.Параметры.Артикул = ВыборкаСтрокТара.Артикул;
			Иначе
				ОбластьКодов.Параметры.Артикул = ВыборкаСтрокТара.Код;
			КонецЕсли;
			ТабДокумент.Присоединить(ОбластьКодов);
		КонецЕсли;

		ОбластьДанных.Параметры.Заполнить(ВыборкаСтрокТара);
		ОбластьДанных.Параметры.Товар = ВыборкаСтрокТара.Товар + " (возвратная тара)";

		ТабДокумент.Присоединить(ОбластьДанных);

		СуммаИтого = СуммаИтого + ВыборкаСтрокТара.Сумма;

	КонецЦикла;

	// Вывести Итого
	ОбластьНомера = Макет.ПолучитьОбласть("Итого|НомерСтроки");
	ОбластьКодов  = Макет.ПолучитьОбласть("Итого|КолонкаКодов");
	ОбластьДанных = Макет.ПолучитьОбласть("Итого|Данные");

	ТабДокумент.Вывести(ОбластьНомера);
	Если ВыводитьКоды Тогда
		ТабДокумент.Присоединить(ОбластьКодов);
	КонецЕсли;
	ОбластьДанных.Параметры.Всего = ОбщегоНазначения.ФорматСумм(СуммаИтого);
	ТабДокумент.Присоединить(ОбластьДанных);

	// Вывести Сумму прописью
	ОбластьМакета                          = Макет.ПолучитьОбласть("СуммаПрописью");
	ОбластьМакета.Параметры.ИтоговаяСтрока = "Всего наименований " + ВыборкаСтрокТовары.Количество()
	                                       + ", на сумму " + ОбщегоНазначения.ФорматСумм(СуммаИтого, мВалютаРегламентированногоУчета);
	ОбластьМакета.Параметры.СуммаПрописью  = ОбщегоНазначения.СформироватьСуммуПрописью(СуммаИтого, мВалютаРегламентированногоУчета);
	ТабДокумент.Вывести(ОбластьМакета);

	// Вывести подписи
	ОбластьМакета = Макет.ПолучитьОбласть("Подписи");
	ТабДокумент.Вывести(ОбластьМакета);

	Возврат ТабДокумент;

КонецФункции // ПечатьСписаниеТоваров()

// Формирует унифицированную форму ТОРГ-16
//
// Возвращаемое значение:
//  Табличный документ - унифицировання форма ТОРГ-16
//
Функция ПечатьТОРГ16()

	ДопКолонка = Константы.ДополнительнаяКолонкаПечатныхФормДокументов.Получить();
	Если ДопКолонка = Перечисления.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул Тогда
		ТоварКод = "Артикул";
	Иначе
		ТоварКод = "Код";
	КонецЕсли;

	СтруктраПараметров = Новый Структура();
	СтруктраПараметров.Вставить("ТекущийДокумент", ЭтотОбъект.Ссылка);
	СтруктраПараметров.Вставить("Регистратор",     ЭтотОбъект.Ссылка);
	СтруктраПараметров.Вставить("ВидСкладаНТТ",    Перечисления.ВидыСкладов.НТТ);
	СтруктраПараметров.Вставить("ТекГраница",      Новый Граница(Дата, ВидГраницы.Включая));
	СтруктраПараметров.Вставить("Организация",     Организация);
	СтруктраПараметров.Вставить("Подразделение",   Подразделение);


	//////|		Номенклатура."+ ТоварКод + "    КАК ТоварКод,
	//////|		Номенклатура."+ ТоварКод + "    КАК ТоварКод,
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СписаниеТоваров.Ссылка.Номер КАК НомерДокумента,
	|	СписаниеТоваров.Ссылка.Дата КАК ДатаДокумента,
	|	СписаниеТоваров.Ссылка.Организация,
	|	СписаниеТоваров.Ссылка.Организация КАК Руководители,
	|	СписаниеТоваров.Ссылка.Склад,
	|	СписаниеТоваров.Ссылка.Склад.Представление КАК ПредставлениеСклада,
	|	ПРЕДСТАВЛЕНИЕ(
	|	ВЫБОР	КОГДА СписаниеТоваров.Ссылка.ОтражатьВБухгалтерскомУчете
	|			ТОГДА ЕСТЬNULL(ТаблицаСоответствиеОрганизации.ПодразделениеОрганизации, """")
	|			ИНАЧЕ СписаниеТоваров.Ссылка.Подразделение
	|	КОНЕЦ) КАК ПредставлениеПодразделения,
	|	ВЫБОР	КОГДА СписаниеТоваров.Ссылка.ОтражатьВБухгалтерскомУчете
	|			ТОГДА ЕСТЬNULL(ТаблицаСоответствиеОрганизации.ПодразделениеОрганизации, """")
	|			ИНАЧЕ СписаниеТоваров.Ссылка.Подразделение
	|	КОНЕЦ КАК Подразделение,
	|	СписаниеТоваров.Ссылка.Основание КАК Основание,
	|	СписаниеТоваров.Ссылка.СуммаДокумента,
	|	Константы.ВалютаРегламентированногоУчета КАК ВалютаДокумента,
	|	СписаниеТоваров.Номенклатура,
	|	СписаниеТоваров.Номенклатура.НаименованиеПолное КАК ТоварНаименование,
	|	СписаниеТоваров.Номенклатура.Код КАК ТоварКод,
	|	СписаниеТоваров.Количество,
	|	СписаниеТоваров.КоличествоМест,
	|	СписаниеТоваров.Качество,
	|	СписаниеТоваров.ЕдиницаИзмерения.Представление КАК ЕдиницаИзмеренияНаименование,
	|	СписаниеТоваров.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код КАК ЕдиницаИзмеренияКодПоОКЕИ,
	|	СписаниеТоваров.ЕдиницаИзмеренияМест.Представление КАК ЕдиницаИзмеренияМестНаименование,
	|	СписаниеТоваров.ЕдиницаИзмерения.Вес КАК МассаОдногоМеста,
	|	ВЫБОР
	|		КОГДА СписаниеТоваров.Ссылка.Склад.ВидСклада <> &ВидСкладаНТТ
	|			ТОГДА ВЫРАЗИТЬ(
	|				ЕСТЬNULL(ТаблицаСебестоимостиОбороты.Стоимость, 0)
	|				* СписаниеТоваров.Количество 
	|				* СписаниеТоваров.Коэффициент
	|				/ ЕСТЬNULL(СписаниеТоваров.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент, 1)
	|				/ ЕСТЬNULL(ТаблицаСебестоимостиОбороты.Количество, 1)
	|	 		КАК ЧИСЛО(15,2))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Стоимость,
	|	СписаниеТоваров.ХарактеристикаНоменклатуры КАК Характеристика,
	|	СписаниеТоваров.СерияНоменклатуры КАК Серия,
	|	1 КАК ТоварТара,
	|	СписаниеТоваров.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	Документ.СписаниеТоваров.Товары КАК СписаниеТоваров
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|(
	|	ВЫБРАТЬ
	|		СоответсвиеПодразделений.Подразделение,
	|		СоответсвиеПодразделений.Организация,
	|		МИНИМУМ(СоответсвиеПодразделений.ПодразделениеОрганизации) КАК ПодразделениеОрганизации,
	|		МАКСИМУМ(СоответсвиеПодразделений.ПодразделениеОрганизации) КАК ПодразделениеОрг
	|	ИЗ
	|		РегистрСведений.СоответствиеПодразделенийИПодразделенийОрганизаций КАК СоответсвиеПодразделений
	|	ГДЕ
	|		Подразделение = &Подразделение
	|		И Организация = &Организация
	|		И Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|		И Подразделение <> ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|	СГРУППИРОВАТЬ ПО
	|		Подразделение,
	|		Организация
	|	ИМЕЮЩИЕ
	|		КОЛИЧЕСТВО(*) = 1
	|) ТаблицаСоответствиеОрганизации
	|ПО
	|	ТаблицаСоответствиеОрганизации.Подразделение = СписаниеТоваров.Ссылка.Подразделение
	|	И ТаблицаСоответствиеОрганизации.Организация = СписаниеТоваров.Ссылка.Организация
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСебестоимостиОбороты КАК ТаблицаСебестоимостиОбороты
	|		ПО СписаниеТоваров.Номенклатура = ТаблицаСебестоимостиОбороты.Номенклатура
	|			И СписаниеТоваров.Ссылка = ТаблицаСебестоимостиОбороты.Регистратор
	|			И ((СписаниеТоваров.ХарактеристикаНоменклатуры = ТаблицаСебестоимостиОбороты.ХарактеристикаНоменклатуры) 
	|				ИЛИ (ТаблицаСебестоимостиОбороты.ХарактеристикаНоменклатуры = ""Не используется""))
	|			И ((СписаниеТоваров.СерияНоменклатуры = ТаблицаСебестоимостиОбороты.СерияНоменклатуры)
	|				ИЛИ (ТаблицаСебестоимостиОбороты.СерияНоменклатуры = ""Не используется""))
	|			И ((СписаниеТоваров.СчетУчетаБУ = ТаблицаСебестоимостиОбороты.СчетУчета) 
	|				ИЛИ (ТаблицаСебестоимостиОбороты.СчетУчета = ""Не используется""))"+
	?(ВидОперации=Перечисления.ВидыОперацийСписаниеТоваров.ТоварыПродукция,"
	|			И ((СписаниеТоваров.Качество = ТаблицаСебестоимостиОбороты.Качество)
	|				ИЛИ (ТаблицаСебестоимостиОбороты.Качество = ""Не используется""))	,",",")+"
	|	Константы КАК Константы
	|ГДЕ
	|	СписаниеТоваров.Ссылка = &ТекущийДокумент
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СписаниеТоваров.Ссылка.Номер,
	|	СписаниеТоваров.Ссылка.Дата,
	|	СписаниеТоваров.Ссылка.Организация,
	|	СписаниеТоваров.Ссылка.Организация,
	|	СписаниеТоваров.Ссылка.Склад,
	|	СписаниеТоваров.Ссылка.Склад.Представление,
	|	ВЫБОР	КОГДА СписаниеТоваров.Ссылка.ОтражатьВБухгалтерскомУчете
	|			ТОГДА ЕСТЬNULL(ТаблицаСоответствиеОрганизации.ПодразделениеОрганизации, """")
	|			ИНАЧЕ СписаниеТоваров.Ссылка.Подразделение
	|	КОНЕЦ КАК ПредставлениеПодразделения,
	|	ВЫБОР	КОГДА СписаниеТоваров.Ссылка.ОтражатьВБухгалтерскомУчете
	|			ТОГДА ЕСТЬNULL(ТаблицаСоответствиеОрганизации.ПодразделениеОрганизации, """")
	|			ИНАЧЕ СписаниеТоваров.Ссылка.Подразделение
	|	КОНЕЦ КАК Подразделение,
	|	СписаниеТоваров.Ссылка.Основание,
	|	СписаниеТоваров.Ссылка.СуммаДокумента,
	|	Константы.ВалютаРегламентированногоУчета,
	|	СписаниеТоваров.Номенклатура,
	|	СписаниеТоваров.Номенклатура.НаименованиеПолное,
	|	СписаниеТоваров.Номенклатура.Код,
	|	СписаниеТоваров.Количество,
	|	NULL,
	|	NULL,
	|	СписаниеТоваров.Номенклатура.БазоваяЕдиницаИзмерения.Представление,
	|	СписаниеТоваров.Номенклатура.БазоваяЕдиницаИзмерения.Код,
	|	"""",
	|	0,
	|	0,
	|	NULL,
	|	NULL,
	|	2,
	|	СписаниеТоваров.НомерСтроки
	|ИЗ
	|	Документ.СписаниеТоваров.ВозвратнаяТара КАК СписаниеТоваров
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|(
	|	ВЫБРАТЬ
	|		СоответсвиеПодразделений.Подразделение,
	|		СоответсвиеПодразделений.Организация,
	|		МИНИМУМ(СоответсвиеПодразделений.ПодразделениеОрганизации) КАК ПодразделениеОрганизации,
	|		МАКСИМУМ(СоответсвиеПодразделений.ПодразделениеОрганизации) КАК ПодразделениеОрг
	|	ИЗ
	|		РегистрСведений.СоответствиеПодразделенийИПодразделенийОрганизаций КАК СоответсвиеПодразделений
	|	ГДЕ
	|		Подразделение = &Подразделение
	|		И Организация = &Организация
	|		И Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|		И Подразделение <> ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|	СГРУППИРОВАТЬ ПО
	|		Подразделение,
	|		Организация
	|	ИМЕЮЩИЕ
	|		КОЛИЧЕСТВО(*) = 1
	|) ТаблицаСоответствиеОрганизации
	|ПО
	|	ТаблицаСоответствиеОрганизации.Подразделение = СписаниеТоваров.Ссылка.Подразделение
	|	И ТаблицаСоответствиеОрганизации.Организация = СписаниеТоваров.Ссылка.Организация
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	Константы КАК Константы
	|ПО ИСТИНА
	|ГДЕ
	|	СписаниеТоваров.Ссылка = &ТекущийДокумент
	|УПОРЯДОЧИТЬ ПО
	|	ТоварТара,
	|	НомерСтроки";

	ТаблицаЗатрат = УправлениеЗапасами.ПолучитьТаблицуЗатратДляПечатиИЗаполненияДокументов(Ссылка, ТекстЗапроса, СтруктраПараметров);
	
	Если ТаблицаЗатрат = Неопределено Тогда
		Возврат Неопределено;
	ИначеЕсли ТаблицаЗатрат.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Шапка = ТаблицаЗатрат[0];

	Макет       = ПолучитьОбщийМакет("ТОРГ16");
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СписаниеТоваров_ТОРГ16";

	// Выводим общие реквизиты шапки
	ОписаниеОрганизации = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(Шапка.Организация, Шапка.ДатаДокумента);
	ОбластьМакета       = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Заполнить(Шапка);
	ОбластьМакета.Параметры.ДатаДокумента            = Шапка.ДатаДокумента;
	ОбластьМакета.Параметры.ПредставлениеОрганизации = ФормированиеПечатныхФорм.ОписаниеОрганизации(ОписаниеОрганизации);
	ОбластьМакета.Параметры.ОрганизацияПоОКПО        = ОписаниеОрганизации.КодПоОКПО;

	Руководители = РегламентированнаяОтчетность.ОтветственныеЛицаОрганизаций(Шапка.Руководители, Шапка.ДатаДокумента,);
	Руководитель = Руководители.Руководитель;
	Бухгалтер    = Руководители.ГлавныйБухгалтер;
	ОбластьМакета.Параметры.ФИОРуководителя = Руководитель;

	ТабДокумент.Вывести(ОбластьМакета);

	СтрокНаСтранице = 23;
	СтрокШапки      = 11;
	СтрокПодвала    = 0;
	НомерСтраницы   = 1;
	Ном             = 0;

	КоличествоСтрок = ТаблицаЗатрат.Количество();

	Если КоличествоСтрок = 1 Тогда
		ПереноситьПоследнююСтроку = 0;
	Иначе
		ЦелыхСтраницСПодвалом     = Цел((СтрокШапки + КоличествоСтрок + СтрокПодвала) / СтрокНаСтранице);
		ЦелыхСтраницБезПодвала    = Цел((СтрокШапки + КоличествоСтрок - 1) / СтрокНаСтранице);
		ПереноситьПоследнююСтроку = ЦелыхСтраницСПодвалом - ЦелыхСтраницБезПодвала;
	КонецЕсли;

	// Выводим заголовок таблицы
	ЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицыПервогоЛиста");
	ТабДокумент.Вывести(ЗаголовокТаблицы);

	ОбластьМакета = Макет.ПолучитьОбласть("СтрокаПервогоЛиста");

	// Товары
	Для Каждого ВыборкаСтрокТовары Из ТаблицаЗатрат Цикл
		Если НЕ ВыборкаСтрокТовары.ТоварТара = 1 Тогда
			Продолжить;
		КонецЕсли;

		Если НЕ ЗначениеЗаполнено(ВыборкаСтрокТовары.Номенклатура) Тогда
			Сообщить("В одной из строк не заполнено значение номенклатуры - строка при печати пропущена.", СтатусСообщения.Важное);
			Продолжить;
		КонецЕсли;

		Ном = Ном + 1;

		//Начинаем новую страницу, если предыдущая строка была последней на странице
		//или пора переносить последнюю строку на последнюю страницу с подвалом.
		ЦелаяСтраница = (СтрокШапки + Ном - 1) / СтрокНаСтранице;
		Если (ЦелаяСтраница = Цел(ЦелаяСтраница))
		 или ((ПереноситьПоследнююСтроку = 1) и (Ном = КоличествоСтрок)) Тогда

			ИтогоСтоимостьПоСтранице = 0;
			НомерСтраницы            = НомерСтраницы + 1;
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			ТабДокумент.Вывести(ЗаголовокТаблицы);
		КонецЕсли;

		ОбластьМакета.Параметры.Заполнить(ВыборкаСтрокТовары);
		
		ОбластьМакета.Параметры.ДатаСписанияТовара = Шапка.ДатаДокумента;

		ТабДокумент.Вывести(ОбластьМакета);
	КонецЦикла;

	// Тара
	Для Каждого ВыборкаСтрокТара Из ТаблицаЗатрат Цикл
		Если НЕ ВыборкаСтрокТара.ТоварТара = 2 Тогда
			Продолжить;
		КонецЕсли;

		Если НЕ ЗначениеЗаполнено(ВыборкаСтрокТара.Номенклатура) Тогда
			Сообщить("В одной из строк не заполнено значение номенклатуры - строка при печати пропущена.", СтатусСообщения.Важное);
			Продолжить;
		КонецЕсли;

		Ном = Ном + 1;

		//Начинаем новую страницу, если предыдущая строка была последней на странице
		//или пора переносить последнюю строку на последнюю страницу с подвалом.
		ЦелаяСтраница = (СтрокШапки + Ном - 1) / СтрокНаСтранице;
		Если (ЦелаяСтраница = Цел(ЦелаяСтраница))
		 или ((ПереноситьПоследнююСтроку = 1) и (Ном = КоличествоСтрок)) Тогда

			ИтогоСтоимостьПоСтранице = 0;
			НомерСтраницы            = НомерСтраницы + 1;
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			ТабДокумент.Вывести(ЗаголовокТаблицы);
		КонецЕсли;

		ОбластьМакета.Параметры.Заполнить(ВыборкаСтрокТара);
		ОбластьМакета.Параметры.ДатаСписанияТовара = Шапка.ДатаДокумента;

		ТабДокумент.Вывести(ОбластьМакета);
	КонецЦикла;

	СтрокНаСтранице = 27;
	СтрокШапки      = 5;
	СтрокПодвала    = 11;
	НомерСтраницы   = 1;
	Ном             = 0;

	ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();

	НомерСтраницы                            = НомерСтраницы + 1;
	ЗаголовокТаблицы                         = Макет.ПолучитьОбласть("ЗаголовокТаблицыВторогоЛиста");
	ЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы; 
	ТабДокумент.Вывести(ЗаголовокТаблицы);

	Если КоличествоСтрок = 1 Тогда
		ПереноситьПоследнююСтроку = 0;
	Иначе
		ЦелыхСтраницСПодвалом     = Цел((СтрокШапки + КоличествоСтрок + СтрокПодвала) / СтрокНаСтранице);
		ЦелыхСтраницБезПодвала    = Цел((СтрокШапки + КоличествоСтрок - 1) / СтрокНаСтранице);
		ПереноситьПоследнююСтроку = ЦелыхСтраницСПодвалом - ЦелыхСтраницБезПодвала;
	КонецЕсли;

	ИтогоСтоимость = 0;

	// Выводим многострочную часть докмента
	ОбластьМакета = Макет.ПолучитьОбласть("СтрокаВторогоЛиста");

	// Товары
	Для Каждого ВыборкаСтрокТовары Из ТаблицаЗатрат Цикл
		Если НЕ ВыборкаСтрокТовары.ТоварТара = 1 Тогда
			Продолжить;
		КонецЕсли;

		Если НЕ ЗначениеЗаполнено(ВыборкаСтрокТовары.Номенклатура) Тогда
			Сообщить("В одной из строк не заполнено значение номенклатуры - строка при печати пропущена.", СтатусСообщения.Важное);
			Продолжить;
		КонецЕсли;

		Ном = Ном + 1;

		//Начинаем новую страницу, если предыдущая строка была последней на странице
		//или пора переносить последнюю строку на последнюю страницу с подвалом.
		ЦелаяСтраница = (СтрокШапки + Ном - 1) / СтрокНаСтранице;
		
		Если (ЦелаяСтраница = Цел(ЦелаяСтраница))
		 или ((ПереноситьПоследнююСтроку = 1) и (Ном = КоличествоСтрок)) Тогда

			ИтогоСтоимостьПоСтранице = 0;
			НомерСтраницы            = НомерСтраницы + 1;
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			ЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы;
			ТабДокумент.Вывести(ЗаголовокТаблицы);
		КонецЕсли;
	
		ОбластьМакета.Параметры.Заполнить(ВыборкаСтрокТовары);

		КоличествоМест = Формат(ВыборкаСтрокТовары.Количество, "ЧЦ=15; ЧДЦ=3");

		Если ВыборкаСтрокТовары.КоличествоМест <> 0 И ЗначениеЗаполнено(ВыборкаСтрокТовары.ЕдиницаИзмеренияМестНаименование) Тогда
			КоличествоМест = КоличествоМест 
			                 + Символы.ПС
			                 + " (" + Формат(ВыборкаСтрокТовары.КоличествоМест,"ЧЦ=15; ЧДЦ=0")
			                 + " "
			                 + ВыборкаСтрокТовары.ЕдиницаИзмеренияМестНаименование
			                 + ")";
		КонецЕсли;

		ОбластьМакета.Параметры.КоличествоМест    = КоличествоМест;
		ОбластьМакета.Параметры.ТоварНаименование = ВыборкаСтрокТовары.ТоварНаименование + ФормированиеПечатныхФорм.ПредставлениеСерий(ВыборкаСтрокТовары);
		МассаНетто                                = ВыборкаСтрокТовары.МассаОдногоМеста * ВыборкаСтрокТовары.КоличествоМест;
		ОбластьМакета.Параметры.МассаНетто        = МассаНетто;
		ОбластьМакета.Параметры.Цена = ?(ВыборкаСтрокТовары.Количество = 0, ВыборкаСтрокТовары.Стоимость, Окр(ВыборкаСтрокТовары.Стоимость / ВыборкаСтрокТовары.Количество, 2, 1));
		
		ТабДокумент.Вывести(ОбластьМакета);

		// Обновим итоги по документу
		ИтогоСтоимость = ИтогоСтоимость + ВыборкаСтрокТовары.Стоимость;
	КонецЦикла;

	// Тара
	Для Каждого ВыборкаСтрокТара Из ТаблицаЗатрат Цикл
		Если НЕ ВыборкаСтрокТара.ТоварТара = 2 Тогда
			Продолжить;
		КонецЕсли;

		Если НЕ ЗначениеЗаполнено(ВыборкаСтрокТара.Номенклатура) Тогда
			Сообщить("В одной из строк не заполнено значение номенклатуры - строка при печати пропущена.", СтатусСообщения.Важное);
			Продолжить;
		КонецЕсли;

		Ном = Ном + 1;

		//Начинаем новую страницу, если предыдущая строка была последней на странице
		//или пора переносить последнюю строку на последнюю страницу с подвалом.
		ЦелаяСтраница = (СтрокШапки + Ном - 1) / СтрокНаСтранице;
		
		Если (ЦелаяСтраница = Цел(ЦелаяСтраница))
		 или ((ПереноситьПоследнююСтроку = 1) и (Ном = КоличествоСтрок)) Тогда

			ИтогоСтоимостьПоСтранице = 0;
			НомерСтраницы            = НомерСтраницы + 1;
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			ЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы;
			ТабДокумент.Вывести(ЗаголовокТаблицы);
		КонецЕсли;
	
		ОбластьМакета.Параметры.Заполнить(ВыборкаСтрокТара);

		ОбластьМакета.Параметры.КоличествоМест    = Формат(ВыборкаСтрокТара.Количество, "ЧЦ=15; ЧДЦ=3");
		ОбластьМакета.Параметры.ТоварНаименование = ВыборкаСтрокТара.ТоварНаименование + " (возвратная тара)";
		ОбластьМакета.Параметры.Цена = ?(ВыборкаСтрокТовары.Количество = 0, ВыборкаСтрокТовары.Стоимость, Окр(ВыборкаСтрокТовары.Стоимость / ВыборкаСтрокТовары.Количество, 2, 1));
		
		ТабДокумент.Вывести(ОбластьМакета);

		// Обновим итоги по документу
		ИтогоСтоимость = ИтогоСтоимость + ВыборкаСтрокТара.Стоимость;
	КонецЦикла;

	// Выводим итоги по документу в общем
	ОбластьМакета = Макет.ПолучитьОбласть("Итого");
	ОбластьМакета.Параметры.Итого = ИтогоСтоимость;
	ТабДокумент.Вывести(ОбластьМакета);
	
	ВалютаПечати = мВалютаРегламентированногоУчета;

	// Выводим подвал документа
	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	ОбластьМакета.Параметры.Заполнить(Шапка);
	ОбластьМакета.Параметры.СуммаСписанияПрописью = ОбщегоНазначения.СформироватьСуммуПрописью(ИтогоСтоимость, ВалютаПечати);
	ТабДокумент.Вывести(ОбластьМакета);

	// Зададим параметры макета
	ТабДокумент.ПолеСверху = 0;
	ТабДокумент.ПолеСлева  = 0;
	ТабДокумент.ПолеСнизу  = 0;
	ТабДокумент.ПолеСправа = 0;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;

	Возврат ТабДокумент;

КонецФункции // ПечатьТОРГ16()

// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходимое количество копий.
//
//  Название макета печати передается в качестве параметра,
// по переданному названию находим имя макета в соответствии.
//
// Параметры:
//  НазваниеМакета - строка, название макета.
//
Процедура Печать(ИмяМакета, КоличествоЭкземпляров = 1, НаПринтер = Ложь) Экспорт

	Если Не Проведен Тогда
		Предупреждение("Документ можно распечатать только после его проведения!");
		Возврат;
	ИначеЕсли Не УправлениеДопПравамиПользователей.РазрешитьПечатьНепроведенныхДокументов(Проведен) Тогда
		Предупреждение("Недостаточно полномочий для печати непроведенного документа!");
		Возврат;
	КонецЕсли;

	Если Не РаботаСДиалогами.ПроверитьМодифицированность(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;

	// Получить экземпляр документа на печать
	Если ИмяМакета = "Ведомость" Тогда
		ТабДокумент = ПечатьСписаниеТоваров();
	ИначеЕсли ИмяМакета = "ТОРГ16" тогда
		ТабДокумент = ПечатьТОРГ16();
		
	ИначеЕсли ТипЗнч(ИмяМакета) = Тип("ДвоичныеДанные") Тогда

		ТабДокумент = УниверсальныеМеханизмы.НапечататьВнешнююФорму(Ссылка, ИмяМакета);
		
		Если ТабДокумент = Неопределено Тогда
			Возврат
		КонецЕсли; 
		
	КонецЕсли; 

	УниверсальныеМеханизмы.НапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер, ОбщегоНазначения.СформироватьЗаголовокДокумента(ЭтотОбъект, ЭтотОбъект.Метаданные().Представление()), Ссылка);

КонецПроцедуры // Печать

// Возвращает доступные варианты печати документа
//
// Возвращаемое значение:
//  Структура, каждая строка которой соответствует одному из вариантов печати
//  
Функция ПолучитьСтруктуруПечатныхФорм() Экспорт
	
	СтруктураМакетов =  Новый Структура("Ведомость", "Акт списания товаров");
	СтруктураМакетов.Вставить("ТОРГ16", "ТОРГ-16 (Акт о списании товаров)");
	
	Возврат СтруктураМакетов;
	
КонецФункции // ПолучитьСтруктуруПечатныхФорм()

#КонецЕсли

// Процедура выполняет заполниение табличной части по инвентаризации товаров.
//
// Параметры:
//  ДокументОснование - ссылка на документ основание (инвентаризация товаров).
//
Процедура ЗаполнитьТоварыПоИнвентаризацииТоваровУпр(ДокументОснование, СкладЗаполнения = Неопределено) Экспорт

#Если ВнешнееСоединение Тогда
	Если Товары.Количество() > 0 Тогда
		Товары.Очистить();
	КонецЕсли;
#КонецЕсли

	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ДокументСсылка",          Ссылка);
	Запрос.УстановитьПараметр("ДокументОснованиеСсылка", ДокументОснование);

	Запрос.Текст =
	"ВЫБРАТЬ
	|	МИНИМУМ(Док.НомерСтроки) КАК НомерСтроки,
	/// Кунов О.В., 26.06.2014 - 28394
	|	Док.Ссылка,
	///	
	|	Док.Номенклатура,
	|	Док.ЕдиницаИзмерения,
	|	Док.ЕдиницаИзмеренияМест,
	|	Док.Ссылка.Склад                              КАК Склад,
	|	МАКСИМУМ(Док.КоличествоУчет - Док.Количество) КАК КоличествоОтклонение,
	|	ВЫБОР
	|		КОГДА СУММА(ВложенныйЗапрос.Количество) ЕСТЬ NULL ТОГДА
	|			0
	|		ИНАЧЕ
	|			СУММА(ВложенныйЗапрос.Количество)
	|	КОНЕЦ КАК КоличествоСписанное,
	|	Док.ЦенаВРознице КАК Цена,
	|	Док.ХарактеристикаНоменклатуры,
	|	Док.СерияНоменклатуры,
	|	Док.Качество
	|ИЗ
	|	Документ.ИнвентаризацияТоваровНаСкладе.Товары КАК Док
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	(ВЫБРАТЬ 
	|       ДокСписание.Номенклатура,
	|       ДокСписание.ХарактеристикаНоменклатуры,
	|       ДокСписание.СерияНоменклатуры,
	|       ДокСписание.Ссылка.Склад                 КАК Склад,
	|		ДокСписание.Количество                   КАК Количество,
	|       ДокСписание.Цена
	|	 ИЗ
	|       Документ.СписаниеТоваров.Товары КАК ДокСписание
	|    ГДЕ
	|       ДокСписание.Ссылка <> &ДокументСсылка
	|       И ДокСписание.Ссылка.Проведен
	|       И ДокСписание.Ссылка.ИнвентаризацияТоваровНаСкладе = &ДокументОснованиеСсылка
	|
	|    ОБЪЕДИНИТЬ ВСЕ
	|
	|	 ВЫБРАТЬ 
	|       ДокОтчетККМ.Номенклатура,
	|       ДокОтчетККМ.ХарактеристикаНоменклатуры,
	|       ДокОтчетККМ.СерияНоменклатуры,
	|       ДокОтчетККМ.Склад                        КАК Склад,
	|		ДокОтчетККМ.Количество                   КАК Количество,
	|       ДокОтчетККМ.Цена
	|	 ИЗ
	|       Документ.ОтчетОРозничныхПродажах.Товары КАК ДокОтчетККМ
	|    ГДЕ
	|         ДокОтчетККМ.Ссылка.Проведен
	|       И ДокОтчетККМ.Ссылка.ИнвентаризацияТоваровНаСкладе = &ДокументОснованиеСсылка) КАК ВложенныйЗапрос
	|ПО
	|      Док.Номенклатура               = ВложенныйЗапрос.Номенклатура
	|    И Док.ХарактеристикаНоменклатуры = ВложенныйЗапрос.ХарактеристикаНоменклатуры
	|    И Док.СерияНоменклатуры          = ВложенныйЗапрос.СерияНоменклатуры
	|    И Док.Ссылка.Склад               = ВложенныйЗапрос.Склад " +
	?(Склад.ВидСклада = Перечисления.ВидыСкладов.НТТ, " 
	|    И Док.Цена                       = ВложенныйЗапрос.Цена ", "") + "
	|
	|ГДЕ
	|	  Док.Ссылка = &ДокументОснованиеСсылка 
	|   И Док.КоличествоУчет - Док.Количество > 0
	|" + ?(Склад.ВидСклада = Перечисления.ВидыСкладов.НТТ, " И Док.ЦенаВРознице > 0 ", "") + "
	/// Кунов О.В., 01.10.2014 - 32066
	|" + ? (СкладЗаполнения = Неопределено, "", "И Док.ПРГ_Склад = &Склад") + "
	///
	|
	|СГРУППИРОВАТЬ ПО
	/// Кунов О.В., 26.06.2014 - 28394
	|	Док.Ссылка,
	///	
	|	Док.Ссылка.Склад,
	|	Док.Номенклатура,
	|	Док.ЕдиницаИзмерения,
	|	Док.ЕдиницаИзмеренияМест,
	|	Док.ЦенаВРознице,
	|	Док.ХарактеристикаНоменклатуры,
	|	Док.СерияНоменклатуры,
	|	Док.Качество
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";

	/// Кунов О.В., 01.10.2014 - 32066
	Если СкладЗаполнения <> Неопределено Тогда
		Запрос.УстановитьПараметр("Склад", СкладЗаполнения);
		Склад = СкладЗаполнения;
	Иначе
		Склад = ДокументОснование.Склад;
	КонецЕсли;
	///
	
	РезультатЗапроса = Запрос.Выполнить();

	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		/// Кунов О.В., 26.06.2014 - 28394
		КоличествоНедостачи = Выборка.Ссылка.Пересортица.Выгрузить(Новый Структура("СерияНедостача", Выборка.СерияНоменклатуры)).Итог("Количество");

		КоличествоСписать = Выборка.КоличествоОтклонение - Выборка.КоличествоСписанное - КоличествоНедостачи;
		///

		Если КоличествоСписать <= 0 Тогда
			Продолжить;
		КонецЕсли;

		СтрокаТабличнойЧасти = Товары.Добавить();

		СтрокаТабличнойЧасти.Номенклатура               = Выборка.Номенклатура;
		СтрокаТабличнойЧасти.ЕдиницаИзмерения           = Выборка.ЕдиницаИзмерения;
		СтрокаТабличнойЧасти.ЕдиницаИзмеренияМест       = Выборка.ЕдиницаИзмеренияМест;
		СтрокаТабличнойЧасти.Коэффициент                = Выборка.ЕдиницаИзмерения.Коэффициент;
		СтрокаТабличнойЧасти.Качество                   = Выборка.Качество;
		СтрокаТабличнойЧасти.Количество                 = КоличествоСписать;
		СтрокаТабличнойЧасти.ХарактеристикаНоменклатуры = Выборка.ХарактеристикаНоменклатуры;
		СтрокаТабличнойЧасти.СерияНоменклатуры          = Выборка.СерияНоменклатуры;

		ОбработкаТабличныхЧастей.РассчитатьКоличествоМестТабЧасти(СтрокаТабличнойЧасти, ЭтотОбъект);

		Если Склад.ВидСклада = Перечисления.ВидыСкладов.НТТ Тогда

			СтрокаТабличнойЧасти.Цена = Выборка.Цена;
			ОбработкаТабличныхЧастей.РассчитатьСуммуТабЧасти(СтрокаТабличнойЧасти, ЭтотОбъект);

		КонецЕсли;

		ЗаполнитьСчетаУчетаВСтрокеТабЧастиРегл(СтрокаТабличнойЧасти, "Товары", ОтражатьВБухгалтерскомУчете, ОтражатьВНалоговомУчете);

	КонецЦикла;

	#Если Клиент Тогда

	Если Товары.Количество() = 0 Тогда
		Сообщить("В документе """ + ДокументОснование.Метаданные().Представление() + """ № " + ДокументОснование.Номер +" отсутствуют товары учетное количество которых превышает фактическое.");
	КонецЕсли;

	#КонецЕсли

КонецПроцедуры // ЗаполнитьТоварыПоИнвентаризацииТоваров()

//Выполняет заполнение счетов учета в переданной строке табличной части
//
Процедура ЗаполнитьСчетаУчетаВСтрокеТабЧастиРегл(СтрокаТЧ, ИмяТабЧасти, ЗаполнятьБУ, ЗаполнятьНУ) Экспорт

	ЗаполнитьСчетаУчетаВТабЧасти(СтрокаТЧ, ИмяТабЧасти, ЗаполнятьБУ, ЗаполнятьНУ);
	
КонецПроцедуры // ЗаполнитьСчетаУчетаВСтрокеТабЧастиРегл()

// Процедура заполняет счета БУ и НУ в указанной табличной части.
//
Процедура ЗаполнитьСчетаУчетаВТабЧасти(ТабличнаяЧасть, ИмяТабЧасти, ЗаполнятьБУ, ЗаполнятьНУ) Экспорт

	СчетаУчетаВДокументах.ЗаполнитьСчетаУчетаТабличнойЧасти(ИмяТабЧасти, ТабличнаяЧасть, ЭтотОбъект, ЗаполнятьБУ, ЗаполнятьНУ);

КонецПроцедуры // ЗаполнитьСчетаУчетаВТабЧасти()

// Заолняет табличную часть по сериям при оперативном проведении
//
Процедура ЗаполнитьПоСериям() Экспорт

	ТоварыТабличнойЧасти = Товары.Выгрузить();
	ТоварыТабличнойЧасти.Свернуть("Номенклатура"); 

	МассивНоменклатуры = ТоварыТабличнойЧасти.ВыгрузитьКолонку("Номенклатура");
	ТаблицаРезервовПоСериям = новый ТаблицаЗначений;
	ТаблицаСерий   = УправлениеЗапасами.ПолучитьТаблицуОстатковПоСериям(Склад, Организация, МассивНоменклатуры,,,ТаблицаРезервовПоСериям);
	
	ТоварыТабличнойЧасти=Товары.Выгрузить();
	
	Товары.Очистить();

	КолонкиТабЧасти = ТоварыТабличнойЧасти.Колонки;
    ИспользоватьУказаниеСерийНоменклатурыПриРезервировании = глЗначениеПеременной("ИспользоватьУказаниеСерийНоменклатурыПриРезервировании");
	Для Каждого ИсходнаяСтрока ИЗ ТоварыТабличнойЧасти Цикл

		СтуктураПоиска   = Новый Структура;
		СтуктураПоиска.Вставить("Номенклатура",               ИсходнаяСтрока.Номенклатура);
		СтуктураПоиска.Вставить("ХарактеристикаНоменклатуры", ИсходнаяСтрока.ХарактеристикаНоменклатуры);
		СтуктураПоиска.Вставить("Качество",                   ИсходнаяСтрока.Качество);

		флЗаполнятьИзРезервов = ложь;
		Если ТипЗнч(ИсходнаяСтрока.ДокументРезерва)=Тип("ДокументСсылка.ЗаказПокупателя") И ИспользоватьУказаниеСерийНоменклатурыПриРезервировании Тогда
			Если ИсходнаяСтрока.ДокументРезерва.ДоговорКонтрагента.ОбособленныйУчетТоваровПоЗаказамПокупателей Тогда
				СтуктураПоиска.Вставить("ДокументРезерва",    ИсходнаяСтрока.ДокументРезерва);
				флЗаполнятьИзРезервов = истина;
			КонецЕсли;
		КонецЕсли;
		Если флЗаполнятьИзРезервов Тогда
			НайденныеСтроки  = ТаблицаРезервовПоСериям.НайтиСтроки(СтуктураПоиска);
		Иначе
			НайденныеСтроки  = ТаблицаСерий.НайтиСтроки(СтуктураПоиска);
		КонецЕсли;

		СтрокаСПустойСерией = Неопределено;
		КоличествоОсталосьПогасить = ИсходнаяСтрока.Количество;

		Для Каждого Строка Из НайденныеСтроки Цикл

			Если КоличествоОсталосьПогасить<=0 Тогда
				Прервать;
			КонецЕсли;

			Если Строка.Остаток <= 0 Тогда
				Продолжить;
			КонецЕсли;

			ОстатокВЕдиницахДокумента = Строка.Остаток * ИсходнаяСтрока.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент / ИсходнаяСтрока.ЕдиницаИзмерения.Коэффициент;

			Если ОстатокВЕдиницахДокумента >= КоличествоОсталосьПогасить Тогда
				КоэффСписания = КоличествоОсталосьПогасить/ОстатокВЕдиницахДокумента;
			Иначе
				КоэффСписания = 1
			КонецЕсли;
			
			СписанноеКоличество = Окр(ОстатокВЕдиницахДокумента * КоэффСписания, 3, РежимОкругления.Окр15как20);
			НоваяСтрока = Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ИсходнаяСтрока);
			НоваяСтрока.СерияНоменклатуры = Строка.СерияНоменклатуры;
			НоваяСтрока.Количество        = СписанноеКоличество; 
			НоваяСтрока.КоличествоМест    = НоваяСтрока.Количество / НоваяСтрока.Коэффициент;
			
			ОбработкаТабличныхЧастей.РассчитатьСуммуТабЧасти(НоваяСтрока, ЭтотОбъект);
			
			Если НЕ ЗначениеЗаполнено(НоваяСтрока.СерияНоменклатуры) Тогда
				СтрокаСПустойСерией = НоваяСтрока;
			КонецЕсли;

			КоличествоОсталосьПогасить = КоличествоОсталосьПогасить-СписанноеКоличество;
			Строка.Остаток = Строка.Остаток - СписанноеКоличество* ИсходнаяСтрока.ЕдиницаИзмерения.Коэффициент / ИсходнаяСтрока.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент;            

		КонецЦикла;

		Если КоличествоОсталосьПогасить>0 Тогда
			Если СтрокаСПустойСерией = Неопределено Тогда
				НоваяСтрока = Товары.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ИсходнаяСтрока);
				НоваяСтрока.Количество     = КоличествоОсталосьПогасить;
				НоваяСтрока.КоличествоМест = НоваяСтрока.Количество / НоваяСтрока.Коэффициент;
				ОбработкаТабличныхЧастей.РассчитатьСуммуТабЧасти(НоваяСтрока, ЭтотОбъект);
			Иначе
				СтрокаСПустойСерией.Количество = СтрокаСПустойСерией.Количество+КоличествоОсталосьПогасить;
				ОбработкаТабличныхЧастей.РассчитатьСуммуТабЧасти(СтрокаСПустойСерией, ЭтотОбъект);
			КонецЕсли;

		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

/////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

// Выгружает результат запроса в табличную часть, добавляет ей необходимые колонки для проведения.
//
// Параметры: 
//  РезультатЗапросаПоТоварам - результат запроса по табличной части "Товары",
//  СтруктураШапкиДокумента   - выборка по результату запроса по шапке документа.
//
// Возвращаемое значение:
//  Сформированная таблица значений.
//
Функция ПодготовитьТаблицуТоваров(РезультатЗапросаПоТоварам, СтруктураШапкиДокумента)

	ТаблицаТоваров = РезультатЗапросаПоТоварам.Выгрузить();

	ПодготовитьТаблицуТоваровУпр(ТаблицаТоваров, СтруктураШапкиДокумента);

	Возврат ТаблицаТоваров;

КонецФункции // ПодготовитьТаблицуТоваров()

Процедура ПодготовитьТаблицуТоваровУпр(ТаблицаТоваров, СтруктураШапкиДокумента)

	Если Склад.ВидСклада = Перечисления.ВидыСкладов.НТТ Тогда
		ТаблицаТоваров.Колонки.Цена.Имя = "ЦенаВРознице";
	КонецЕсли;

КонецПроцедуры

// Выгружает результат запроса в табличную часть, добавляет ей необходимые колонки для проведения.
//
// Параметры: 
//  РезультатЗапросаПоТаре    - результат запроса по табличной части "ВозвратнаяТара",
//  СтруктураШапкиДокумента   - выборка по результату запроса по шапке документа.
//
// Возвращаемое значение:
//  Сформированная таблица значений.
//
Функция ПодготовитьТаблицуТары(РезультатЗапросаПоТаре, СтруктураШапкиДокумента)

	ТаблицаТоваров = РезультатЗапросаПоТаре.Выгрузить();

	Возврат ТаблицаТоваров;

КонецФункции // ПодготовитьТаблицуТары()

// Дополняет список обязательных параметров шапки
// регл. параметрами
Процедура ДополнитьОбязательныеРеквизитыШапкиРегл(Реквизиты)
	
	Реквизиты = Реквизиты + ?(ПустаяСтрока(Реквизиты), "", ", ")
			  + "НДСвСтоимостиТоваров";
			  
КонецПроцедуры // ДополнитьОбязательныеРеквизитыШапкиРегл()

// Проверяет правильность заполнения шапки документа.
// Если какой-то из реквизитов шапки, влияющий на проведение, не заполнен или
// заполнен не корректно, то выставляется флаг отказа в проведении.
// Проверяется также правильность заполнения реквизитов ссылочных полей документа.
// Проверка выполняется по объекту и по выборке из результата запроса по шапке.
//
// Параметры: 
//  СтруктураШапкиДокумента - выборка из результата запроса по шапке документа,
//  Отказ                   - флаг отказа в проведении,
//  Заголовок               - строка, заголовок сообщения об ошибке проведения.
//
Процедура ПроверитьЗаполнениеШапки(СтруктураШапкиДокумента, Отказ, Заголовок)

	// Укажем, что надо проверить:
	ОбязательныеРеквизитыШапки = "Организация, Склад";
	Если СтруктураШапкиДокумента.ОтражатьВБухгалтерскомУчете Тогда
		ДополнитьОбязательныеРеквизитыШапкиРегл(ОбязательныеРеквизитыШапки);
	КонецЕсли;
	
	СтруктураОбязательныхПолей = 
	Новый Структура(ОбязательныеРеквизитыШапки);

	// Теперь вызовем общую процедуру проверки.
	ЗаполнениеДокументов.ПроверитьЗаполнениеШапкиДокумента(ЭтотОбъект, СтруктураОбязательныхПолей, Отказ, Заголовок);
	
	Если СтруктураШапкиДокумента.ВидОперации = Перечисления.ВидыОперацийСписаниеТоваров.Оборудование
	   И СтруктураШапкиДокумента.ВидСклада   = Перечисления.ВидыСкладов.НТТ Тогда
		ОбщегоНазначения.СообщитьОбОшибке("При операции с оборудованием склад не может быть НТТ.", Отказ, Заголовок);
	КонецЕсли;

КонецПроцедуры // ПроверитьЗаполнениеШапки()

// Проверяет правильность заполнения строк табличной части "Товары".
//
// Параметры:
// Параметры: 
//  ТаблицаПоТоварам        - таблица значений, содержащая данные для проведения и проверки ТЧ Товары
//  СтруктураШапкиДокумента - выборка из результата запроса по шапке документа,
//  Отказ                   - флаг отказа в проведении.
//  Заголовок               - строка, заголовок сообщения об ошибке проведения.
//
Процедура ПроверитьЗаполнениеТабличнойЧастиТовары(ТаблицаПоТоварам, СтруктураШапкиДокумента, Отказ, Заголовок)

	ИмяТабличнойЧасти = "Товары";

	// Укажем, что надо проверить:
	СтруктураОбязательныхПолей = Новый Структура("Номенклатура, Количество");
	Если ВидОперации = Перечисления.ВидыОперацийСписаниеТоваров.ТоварыПродукция Тогда
		СтруктураОбязательныхПолей.Вставить("Качество")
	КонецЕсли;

	УправлениеЗапасами.КорректировкаСтруктурыОбязательныхПолей(СтруктураОбязательныхПолей, СтруктураШапкиДокумента.ВидСклада);

	// Теперь вызовем общую процедуру проверки.
	ЗаполнениеДокументов.ПроверитьЗаполнениеТабличнойЧасти(ЭтотОбъект, "Товары",СтруктураОбязательныхПолей, Отказ, Заголовок);

	// Здесь услуг быть не должно.
	УправлениеЗапасами.ПроверитьЧтоНетУслуг(ЭтотОбъект, "Товары", ТаблицаПоТоварам, Отказ, Заголовок);

	// Здесь наборов быть не должно.
	УправлениеЗапасами.ПроверитьЧтоНетНаборов(ЭтотОбъект, "Товары", ТаблицаПоТоварам, Отказ, Заголовок);

	// Здесь комплектов быть не должно.
	УправлениеЗапасами.ПроверитьЧтоНетКомплектов(ЭтотОбъект, "Товары", ТаблицаПоТоварам, Отказ, Заголовок);

	//{25.03.2015 Островерхий заявка №38069 
	Для каждого Строка Из Товары Цикл
		
		СтрокаНачалаСообщенияОбОшибке = Нстр("ru = 'В строке номер """+ СокрЛП(Строка.НомерСтроки) +
			                               """ табличной части """ + ИмяТабличнойЧасти + """: '");
										   
		Если ВидОперации = Перечисления.ВидыОперацийСписаниеТоваров.ПеремещениеТарыНаСкладСолво
			И Строка.Номенклатура.ВидНоменклатуры.НСИ_ОбменССолво Тогда
			
			// неправильная номенклатура (можно использовать только номенклатуру не доступную для обмена с солво)
			ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + Нстр("ru = 'для вида операции Перемещение тары на склад солво нельзя использовать номенклатуру выгружаемую в солво'") , Отказ, Заголовок);
		
		КонецЕсли;  
		
		Если ВидОперации = Перечисления.ВидыОперацийСписаниеТоваров.ТоварыПродукция
			И НЕ Строка.Номенклатура.ВидНоменклатуры.НСИ_ОбменССолво
			И Склад.НСИ_ПодключенКSolvo Тогда
		
			// неправильная номенклатура (можно использовать только номенклатуру не доступную для обмена с солво)
			ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + Нстр("ru = 'для вида операции товары продукция нельзя использовать номенклатуру не выгружаемую в солво'") , Отказ, Заголовок);
		
		КонецЕсли;
		
	КонецЦикла;  
	//25.03.2015 Островерхий} 
	
КонецПроцедуры // ПроверитьЗаполнениеТабличнойЧастиТовары()

// Проверяет правильность заполнения строк табличной части "Товары".
//
// Параметры:
// Параметры: 
//  ТаблицаПоТаре           - таблица значений, содержащая данные для проведения и проверки ТЧ 
//  СтруктураШапкиДокумента - выборка из результата запроса по шапке документа,
//  Отказ                   - флаг отказа в проведении.
//  Заголовок               - строка, заголовок сообщения об ошибке проведения.
//
Процедура ПроверитьЗаполнениеТабличнойЧастиВозвратнаяТара(ТаблицаПоТаре, СтруктураШапкиДокумента, Отказ, Заголовок)

	// Укажем, что надо проверить:
	СтруктураОбязательныхПолей = 
	Новый Структура("Номенклатура, Количество");

	// Теперь вызовем общую процедуру проверки.
	ЗаполнениеДокументов.ПроверитьЗаполнениеТабличнойЧасти(ЭтотОбъект, "ВозвратнаяТара",СтруктураОбязательныхПолей, Отказ, Заголовок);

	// Здесь услуг быть не должно.
	УправлениеЗапасами.ПроверитьЧтоНетУслуг(ЭтотОбъект, "ВозвратнаяТара", ТаблицаПоТаре, Отказ, Заголовок);

	// Здесь наборов быть не должно.
	УправлениеЗапасами.ПроверитьЧтоНетНаборов(ЭтотОбъект, "ВозвратнаяТара", ТаблицаПоТаре, Отказ, Заголовок);

	// Здесь комплектов быть не должно.
	УправлениеЗапасами.ПроверитьЧтоНетКомплектов(ЭтотОбъект, "ВозвратнаяТара", ТаблицаПоТаре, Отказ, Заголовок);

	// По возвратной таре не должен вестись учет по характеристикам.
	УправлениеЗапасами.ПроверитьЧтоНетНоменклатурыСХарактеристиками(ЭтотОбъект, "ВозвратнаяТара", ТаблицаПоТаре, Отказ, Заголовок);

КонецПроцедуры // ПроверитьЗаполнениеТабличнойЧастиТовары()

// По результату запроса по шапке документа формируем движения по регистрам.
//
// Параметры: 
//  РежимПроведения           - режим проведения документа (оперативный или неоперативный),
//  СтруктураШапкиДокумента   - выборка из результата запроса по шапке документа,
//  ТаблицаПоТоварам          - таблица значений, содержащая данные для проведения и проверки ТЧ Товары
//  ТаблицаПоТаре             - таблица значений, содержащая данные для проведения и проверки ТЧ "Возвратная тара",
//  Отказ                     - флаг отказа в проведении,
//  Заголовок                 - строка, заголовок сообщения об ошибке проведения.
//
Процедура ДвиженияПоРегистрам(РежимПроведения, СтруктураШапкиДокумента, ТаблицаПоТоварам, ТаблицаПоТаре, Отказ, Заголовок);

	ДвиженияПоРегистрамУпр(РежимПроведения, СтруктураШапкиДокумента, ТаблицаПоТоварам, ТаблицаПоТаре, Отказ, Заголовок);
	ДвиженияПоТоварамОрганизаций(РежимПроведения, СтруктураШапкиДокумента, ТаблицаПоТоварам, ТаблицаПоТаре, Отказ, Заголовок);
	ДвиженияПоСписаннымТоварам(РежимПроведения, СтруктураШапкиДокумента, ТаблицаПоТоварам, ТаблицаПоТаре, Отказ, Заголовок);

	
	Если ТаблицаПоТоварам.Количество() + ТаблицаПоТаре.Количество() >0 тогда
		УправлениеЗапасами.ЗарегистрироватьДокументВПоследовательностяхПартионногоУчета(ЭтотОбъект, Дата, СтруктураШапкиДокумента.Организация,ОтражатьВУправленческомУчете,СтруктураШапкиДокумента.ОтражатьВБухгалтерскомУчете,СтруктураШапкиДокумента.ОтражатьВНалоговомУчете,СтруктураШапкиДокумента.СпособВеденияПартионногоУчетаПоОрганизации);
	КонецЕсли;	
		

	// При списании партий также делаются записи в регистры бухгалтерского учета.
	Если СтруктураШапкиДокумента.ОтражатьВБухгалтерскомУчете Тогда
		Если Движения.Хозрасчетный.Модифицированность() Тогда
			Движения.Хозрасчетный.Записать();
		КонецЕсли;
	КонецЕсли;
	
	Если СтруктураШапкиДокумента.ОтражатьВНалоговомУчете Тогда
		Если Движения.Налоговый.Модифицированность() Тогда
			Движения.Налоговый.Записать();
		КонецЕсли;
	КонецЕсли;

	//начало изменений Ожиганов 29.05.2015 немножко оптимизируем 
	Если ПРГ_ДопФункцииКлиентСервер.ПРГПроводитьПоПартиям(СтруктураШапкиДокумента) Тогда	
		УправлениеЗапасамиПартионныйУчет.ДвижениеПартийТоваров(Ссылка, Движения.СписанныеТовары.Выгрузить());
		ДвиженияПоРегистрамУСНРегл(РежимПроведения, СтруктураШапкиДокумента,Отказ, Заголовок);
	КонецЕсли;	
	//конец изменений 

КонецПроцедуры // ДвиженияПоРегистрам()

Процедура ДвиженияПоРегистрамУСНРегл(РежимПроведения, СтруктураШапкиДокумента,Отказ, Заголовок)
	
	Если НЕ СтруктураШапкиДокумента.ОтражатьВНалоговомУчетеУСН Тогда
		Возврат;
	КонецЕсли;
	
	НалоговыйУчетУСН.ДвиженияУСН(Ссылка, РежимПроведения);
		
КонецПроцедуры

Процедура ДвиженияПоРегистрамУпр(РежимПроведения, СтруктураШапкиДокумента, ТаблицаПоТоварам, ТаблицаПоТаре, Отказ, Заголовок)

	// Движения по документу.

	Если СтруктураШапкиДокумента.ОтражатьВУправленческомУчете Тогда
	
		// ТОВАРЫ ПО РЕГИСТРУ ТоварыНаСкладах или ТоварыВНТТ.
		Если СтруктураШапкиДокумента.ВидСклада = Перечисления.ВидыСкладов.НТТ Тогда
			НаборДвижений = Движения.ТоварыВНТТ;
			
			// Контроль остатков товара
			Если Товары.Количество() <> 0 Тогда
				ПроцедурыКонтроляОстатков.ТоварыВНТТКонтрольОстатков("Товары", СтруктураШапкиДокумента, Отказ, Заголовок, РежимПроведения);
			КонецЕсли;
			Если ВозвратнаяТара.Количество() <> 0 Тогда
				ПроцедурыКонтроляОстатков.ТоварыВНТТКонтрольОстатков("ВозвратнаяТара", СтруктураШапкиДокумента, Отказ, Заголовок, РежимПроведения);
			КонецЕсли;
			
		ИначеЕсли СтруктураШапкиДокумента.ВидСклада = Перечисления.ВидыСкладов.Розничный Тогда
			НаборДвижений = Движения.ТоварыВРознице;
			
			// Контроль остатков товара
			Если Товары.Количество() <> 0 Тогда
				ПроцедурыКонтроляОстатков.ТоварыВРозницеКонтрольОстатков("Товары", СтруктураШапкиДокумента, Отказ, Заголовок, РежимПроведения);
			КонецЕсли;
			Если ВозвратнаяТара.Количество() <> 0 Тогда
				ПроцедурыКонтроляОстатков.ТоварыВРозницеКонтрольОстатков("ВозвратнаяТара", СтруктураШапкиДокумента, Отказ, Заголовок, РежимПроведения);
			КонецЕсли;
			
		Иначе
			НаборДвижений = Движения.ТоварыНаСкладах;
			
			// Контроль остатков товара
			Если Товары.Количество() <> 0 Тогда
				ПроцедурыКонтроляОстатков.ТоварыНаСкладахКонтрольОстатков("Товары", СтруктураШапкиДокумента, Отказ, Заголовок, РежимПроведения);
			КонецЕсли;
			Если ВозвратнаяТара.Количество() <> 0 Тогда
				ПроцедурыКонтроляОстатков.ТоварыНаСкладахКонтрольОстатков("ВозвратнаяТара", СтруктураШапкиДокумента, Отказ, Заголовок, РежимПроведения);
			КонецЕсли;
			
		КонецЕсли;

		Если НЕ Отказ Тогда
		
			СтруктТаблицДокумента = Новый Структура;
			СтруктТаблицДокумента.Вставить("ТаблицаПоТоварам", ТаблицаПоТоварам);
			СтруктТаблицДокумента.Вставить("ТаблицаПоТаре",    ТаблицаПоТаре);
				
			ТаблицыДанныхДокумента = ОбщегоНазначения.ЗагрузитьТаблицыДокументаВСтруктуру(НаборДвижений, СтруктТаблицДокумента);
				
			ОбщегоНазначения.УстановитьЗначениеВТаблицыДокумента(ТаблицыДанныхДокумента, "Склад", Склад);
			
			Если СтруктураШапкиДокумента.ВидСклада = Перечисления.ВидыСкладов.НТТ Тогда
			
				ОбщегоНазначения.УстановитьЗначениеВТаблицыДокумента(ТаблицыДанныхДокумента, "ТоварТара",    Перечисления.ТоварТара.Товар, "ТаблицаПоТоварам");
				ОбщегоНазначения.УстановитьЗначениеВТаблицыДокумента(ТаблицыДанныхДокумента, "ТоварТара",    Перечисления.ТоварТара.Тара,  "ТаблицаПоТаре");
				ОбщегоНазначения.УстановитьЗначениеВТаблицыДокумента(ТаблицыДанныхДокумента, "ЦенаВРознице", 0,                            "ТаблицаПоТаре");
				
			Иначе
			
				ОбщегоНазначения.УстановитьЗначениеВТаблицыДокумента(ТаблицыДанныхДокумента, "Качество",     Справочники.Качество.Новый,   "ТаблицаПоТаре");

				Если СтруктураШапкиДокумента.ВидСклада = Перечисления.ВидыСкладов.Розничный Тогда
			
					// ТОВАР
					ТаблицаПоЦенам = УправлениеРозничнойТорговлей.СформироватьЗапросПоПродажнымЦенам(Дата, Склад, ТаблицыДанныхДокумента.ТаблицаПоТоварам.ВыгрузитьКолонку("Номенклатура")).Выгрузить();
					УправлениеРозничнойТорговлей.ЗаполнитьКолонкуСуммаПродажная(ТаблицыДанныхДокумента.ТаблицаПоТоварам, ТаблицаПоЦенам);
					
					// ТАРА
					ТаблицаПоЦенам = УправлениеРозничнойТорговлей.СформироватьЗапросПоПродажнымЦенам(Дата, Склад, ТаблицыДанныхДокумента.ТаблицаПоТаре   .ВыгрузитьКолонку("Номенклатура")).Выгрузить();
					УправлениеРозничнойТорговлей.ЗаполнитьКолонкуСуммаПродажная(ТаблицыДанныхДокумента.ТаблицаПоТаре,    ТаблицаПоЦенам);
					
				КонецЕсли;
				
			КонецЕсли;
				
			ОбщегоНазначения.ЗаписатьТаблицыДокументаВРегистр(НаборДвижений, ВидДвиженияНакопления.Расход, ТаблицыДанныхДокумента, Дата);
		
		КонецЕсли;
				
		// Списание резерва по товарам
		ТабРезервТовары = УдалитьСтрокиНеТребующиеСписаниеИзРезерва(ТаблицаПоТоварам);	
		ТабРезервТара   = УдалитьСтрокиНеТребующиеСписаниеИзРезерва(ТаблицаПоТаре);	
		
		Если ТабРезервТовары.Количество() > 0 ИЛИ ТабРезервТара.Количество() > 0 Тогда
			
			// Контроль остатков товара
			Если Товары.Количество() <> 0 Тогда
				ПроцедурыКонтроляОстатков.ТоварыВРезервеНаСкладахКонтрольОстатков("Товары", СтруктураШапкиДокумента, Отказ, Заголовок, РежимПроведения);
			КонецЕсли;
			Если ВозвратнаяТара.Количество() <> 0 Тогда
				ПроцедурыКонтроляОстатков.ТоварыВРезервеНаСкладахКонтрольОстатков("ВозвратнаяТара", СтруктураШапкиДокумента, Отказ, Заголовок, РежимПроведения);
			КонецЕсли;
			
			Если НЕ Отказ Тогда
				
				СтруктТаблицДокумента = Новый Структура;
				СтруктТаблицДокумента.Вставить("ТаблицаПоТоварам", ТабРезервТовары);
				СтруктТаблицДокумента.Вставить("ТаблицаПоТаре",    ТабРезервТара);
				
				ТаблицыДанныхДокумента = ОбщегоНазначения.ЗагрузитьТаблицыДокументаВСтруктуру(Движения.ТоварыВРезервеНаСкладах, СтруктТаблицДокумента);
				ОбщегоНазначения.УстановитьЗначениеВТаблицыДокумента(ТаблицыДанныхДокумента, "Склад", Склад);

				ОбщегоНазначения.ЗаписатьТаблицыДокументаВРегистр(Движения.ТоварыВРезервеНаСкладах, ВидДвиженияНакопления.Расход, ТаблицыДанныхДокумента, Дата);
				
			КонецЕсли;
			
		КонецЕсли;	

		//Товары к передаче со складов
		ТабКПередачеТовары = УдалитьСтрокиНеТребующиеСписаниеИзТоваровКПередаче(ТаблицаПоТоварам);	
		ТабКПередачеТара   = УдалитьСтрокиНеТребующиеСписаниеИзТоваровКПередаче(ТаблицаПоТаре);	
		
		Если ТабКПередачеТовары.Количество() > 0 ИЛИ ТабКПередачеТара.Количество() > 0 Тогда
			НаборДвижений = Движения.ТоварыКПередачеСоСкладов;
            ТабКПередачеТовары.Колонки.ДокументРезерва.Имя = "ДокументПередачи";
            ТабКПередачеТара.Колонки.ДокументРезерва.Имя = "ДокументПередачи";

			// Контроль остатков товара
			Если Товары.Количество() <> 0 Тогда
				ПроцедурыКонтроляОстатков.ТоварыКПередачеСоСкладовКонтрольОстатков("Товары", СтруктураШапкиДокумента, Отказ, Заголовок, РежимПроведения);
			КонецЕсли;
			Если ВозвратнаяТара.Количество() <> 0 Тогда
				ПроцедурыКонтроляОстатков.ТоварыКПередачеСоСкладовКонтрольОстатков("ВозвратнаяТара", СтруктураШапкиДокумента, Отказ, Заголовок, РежимПроведения);
			КонецЕсли;
			
			Если НЕ Отказ Тогда
				
				СтруктТаблицДокумента = Новый Структура;
				СтруктТаблицДокумента.Вставить("ТаблицаПоТоварам", ТабКПередачеТовары);
				СтруктТаблицДокумента.Вставить("ТаблицаПоТаре",    ТабКПередачеТара);
				
				ТаблицыДанныхДокумента = ОбщегоНазначения.ЗагрузитьТаблицыДокументаВСтруктуру(Движения.ТоварыКПередачеСоСкладов, СтруктТаблицДокумента);
				ОбщегоНазначения.УстановитьЗначениеВТаблицыДокумента(ТаблицыДанныхДокумента, "Склад", Склад);
                ОбщегоНазначения.УстановитьЗначениеВТаблицыДокумента(ТаблицыДанныхДокумента, "СтатусПартии", Перечисления.СтатусыПартийТоваров.Купленный);

				ОбщегоНазначения.ЗаписатьТаблицыДокументаВРегистр(Движения.ТоварыКПередачеСоСкладов, ВидДвиженияНакопления.Расход, ТаблицыДанныхДокумента, Дата);
				
			КонецЕсли;
			
		КонецЕсли;	
   КонецЕсли;
КонецПроцедуры

Функция  УдалитьСтрокиНеТребующиеСписаниеИзРезерва(ТабТовары)
	ТабРезерв = ТабТовары.Скопировать();
	
	// Удалим записи таблицы в которых не надо списывать из резерва или не указан заказ покупателя.
	Сч = 0;
	Пока Сч < ТабРезерв.Количество() Цикл
		СтрокаТаблицы = ТабРезерв.Получить(Сч);
		Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.ДокументРезерва) 
		 ИЛИ (ТипЗнч(СтрокаТаблицы.ДокументРезерва) <> Тип("ДокументСсылка.ЗаказПокупателя")
			   И ТипЗнч(СтрокаТаблицы.ДокументРезерва) <> Тип("ДокументСсылка.ВнутреннийЗаказ")
			   И ТипЗнч(СтрокаТаблицы.ДокументРезерва) <> Тип("ДокументСсылка.ЗаказНаПроизводство")
			   И ТипЗнч(СтрокаТаблицы.ДокументРезерва) <> Тип("ДокументСсылка.ПриходныйОрдерНаТовары"))Тогда
			ТабРезерв.Удалить(СтрокаТаблицы);
		Иначе
			Если ТипЗнч(СтрокаТаблицы.ДокументРезерва) = Тип("ДокументСсылка.ПриходныйОрдерНаТовары")
				И СтрокаТаблицы.ДокументРезерва.БезПраваПродажи Тогда
				ТабРезерв.Удалить(СтрокаТаблицы);
			Иначе	
				Сч = Сч + 1;
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
	Возврат ТабРезерв;

КонецФункции

Функция  УдалитьСтрокиНеТребующиеСписаниеИзТоваровКПередаче(ТабТовары)
	ТабРезерв = ТабТовары.Скопировать();
	
	Сч = 0;
	Пока Сч < ТабРезерв.Количество() Цикл
		СтрокаТаблицы = ТабРезерв.Получить(Сч);
		Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.ДокументРезерва) 
		 ИЛИ (ТипЗнч(СтрокаТаблицы.ДокументРезерва) <> Тип("ДокументСсылка.РеализацияТоваровУслуг")
			   И ТипЗнч(СтрокаТаблицы.ДокументРезерва) <> Тип("ДокументСсылка.ПередачаТоваров")
			   И ТипЗнч(СтрокаТаблицы.ДокументРезерва) <> Тип("ДокументСсылка.ПеремещениеТоваров"))Тогда
			ТабРезерв.Удалить(СтрокаТаблицы);
		Иначе
			Сч = Сч + 1;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ТабРезерв;

КонецФункции



Процедура ДвиженияПоТоварамОрганизаций(РежимПроведения, СтруктураШапкиДокумента, ТаблицаПоТоварам, ТаблицаПоТаре, Отказ, Заголовок)

	// ТОВАРЫ ПО РЕГИСТРУ ТоварыОрганизаций.
	Если СтруктураШапкиДокумента.ОтражатьВРегламентированномУчете Тогда

		НаборДвижений = Движения.ТоварыОрганизаций;

		// Проверка остатков при оперативном проведении.
		НаборДвижений.КонтрольОстатков(ЭтотОбъект, "Товары", СтруктураШапкиДокумента, Отказ, Заголовок, РежимПроведения);
		
		Если НЕ Отказ Тогда
		
			СтруктТаблицДокумента = Новый Структура;
			СтруктТаблицДокумента.Вставить("ТаблицаПоТоварам", ТаблицаПоТоварам);
			//начало изменений
			Если Дата >= ПРГ_ДопФункцииКлиентСервер.ПолучитьДатуНовогоАлгоритмовВСтомости() Тогда
				Если ТаблицаПоТаре.Колонки.Найти("Качество") = Неопределено Тогда
					ТаблицаПоТаре.Колонки.Добавить("Качество");
					ТаблицаПоТаре.ЗаполнитьЗначения(Справочники.Качество.Новый,"Качество")
				КонецЕсли;	
				СтруктТаблицДокумента.Вставить("ТаблицаПоТаре", ТаблицаПоТаре);
			КонецЕсли;	
			//конец изменений 			
						                                       
			ТаблицыДанныхДокумента = ОбщегоНазначения.ЗагрузитьТаблицыДокументаВСтруктуру(НаборДвижений, СтруктТаблицДокумента);
			
			ЗначСклад = ?(СтруктураШапкиДокумента.ВестиУчетТоваровОрганизацийВРазрезеСкладов, Склад, Неопределено);
			ОбщегоНазначения.УстановитьЗначениеВТаблицыДокумента(ТаблицыДанныхДокумента, "Организация", Организация);
			ОбщегоНазначения.УстановитьЗначениеВТаблицыДокумента(ТаблицыДанныхДокумента, "Комиссионер", Неопределено);
			ОбщегоНазначения.УстановитьЗначениеВТаблицыДокумента(ТаблицыДанныхДокумента, "Склад",       ЗначСклад);
					
			ОбщегоНазначения.ЗаписатьТаблицыДокументаВРегистр(НаборДвижений, ВидДвиженияНакопления.Расход, ТаблицыДанныхДокумента, Дата);
			
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

Процедура ДвиженияПоСписаннымТоварам(РежимПроведения, СтруктураШапкиДокумента, ТаблицаПоТоварам, ТаблицаПоТаре, Отказ, Заголовок)

	// ТОВАРЫ ПО РЕГИСТРУ СписанныеТовары.

	НаборДвижений = Движения.СписанныеТовары;

	// Получим таблицу значений, совпадающую со структурой набора записей регистра.
	ТаблицаДвижений = НаборДвижений.Выгрузить();
	ТаблицаДвижений.Очистить();

	// Заполним таблицу движений.

	ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(ТаблицаПоТоварам, ТаблицаДвижений);

	// Недостающие поля.
	Инд = 0;
	Для каждого Строка Из ТаблицаДвижений Цикл
		Инд = Инд + 1;
		Строка.НомерСтрокиДокумента = Инд;
		Строка.ВидТабличнойЧасти = Перечисления.ВидыТабличныхЧастей.Товары;
	КонецЦикла;
	
	
	Для каждого СтрокаТоваров из ТаблицаПоТоварам Цикл

		Если ЗначениеЗаполнено(СтрокаТоваров.ДокументРезерва) тогда

			Если СтрокаТоваров.ДокументРезерва.Метаданные().Имя = "ПриходныйОрдерНаТовары" тогда

				ТаблицаДвижений[СтрокаТоваров.НомерСтроки-1].НеСписывать = Истина;
				
			ИначеЕсли СтрокаТоваров.ДокументРезерва.Метаданные().Имя = "ЗаказПокупателя"  тогда

				ТаблицаДвижений[СтрокаТоваров.НомерСтроки-1].ЗаказСписания = СтрокаТоваров.ДокументРезерва;

			КонецЕсли;

			Если ТипЗНЧ(СтрокаТоваров.ДокументРезерва) = Тип("ДокументСсылка.ЗаказПокупателя")
			  И СтрокаТоваров.ДокументРезерва.ВидОперации = Перечисления.ВидыОперацийЗаказПокупателя.Переработка Тогда
				  ТаблицаДвижений[СтрокаТоваров.НомерСтроки-1].ДоговорКонтрагента = СтрокаТоваров.ДокументРезерва.ДоговорКонтрагента;
			КонецЕсли;
		КонецЕсли;

	КонецЦикла;

	ТаблицаДвижений.ЗаполнитьЗначения(Склад,"Склад");

	ТаблицаДвижений.ЗаполнитьЗначения(Дата,   "Период");
	ТаблицаДвижений.ЗаполнитьЗначения(Ссылка, "Регистратор");
	ТаблицаДвижений.ЗаполнитьЗначения(Истина, "Активность");
	
	//начало изменений
	Если Дата >= ПРГ_ДопФункцииКлиентСервер.ПолучитьДатуНовогоАлгоритмовВСтомости() Тогда
		Если СтруктураШапкиДокумента.ОтражатьВБухгалтерскомУчете и ЗначениеЗаполнено(СчетЗатратШапка) Тогда
			 ТаблицаДвижений.ЗаполнитьЗначения(СчетЗатратШапка,"ПРГ_КорСчетБУ");
			 ТаблицаДвижений.ЗаполнитьЗначения(СубконтоШапка1,"ПРГ_СубконтоБУ1");
			 ТаблицаДвижений.ЗаполнитьЗначения(СубконтоШапка2,"ПРГ_СубконтоБУ2");
			 ТаблицаДвижений.ЗаполнитьЗначения(СубконтоШапка3,"ПРГ_СубконтоБУ3");
		КонецЕсли;	                  
		Если СтруктураШапкиДокумента.ОтражатьВНалоговомУчете и ЗначениеЗаполнено(СчетЗатратНУШапка) Тогда
			 ТаблицаДвижений.ЗаполнитьЗначения(СчетЗатратНУШапка,"ПРГ_КорСчетНУ");
			 ТаблицаДвижений.ЗаполнитьЗначения(СубконтоНУШапка1,"ПРГ_СубконтоНУ1");
			 ТаблицаДвижений.ЗаполнитьЗначения(СубконтоНУШапка2,"ПРГ_СубконтоНУ2");
			 ТаблицаДвижений.ЗаполнитьЗначения(СубконтоНУШапка3,"ПРГ_СубконтоНУ3");
		КонецЕсли;	
	КонецЕсли;	
	//конец изменений 

	ТаблицаДвижений.ЗаполнитьЗначения(Перечисления.КодыОперацийПартииТоваров.СписаниеПоИнвентаризации,"КодОперацииПартииТоваров");
	ТаблицаДвижений.ЗаполнитьЗначения(Организация,"Организация");

	ЗаполнитьКолонкиРегистраСписанныеТоварыПоТоварамУпр(ТаблицаДвижений, СтруктураШапкиДокумента, ТаблицаПоТоварам);
	ЗаполнитьКолонкиРегистраСписанныеТоварыПоТоварамРегл(ТаблицаДвижений, СтруктураШапкиДокумента, ТаблицаПоТоварам);

	НаборДвижений.мПериод = Дата;
	НаборДвижений.мТаблицаДвижений = ТаблицаДвижений;

	Если Не Отказ Тогда
		Движения.СписанныеТовары.ВыполнитьДвижения();
	КонецЕсли;

	// ТАРА ПО РЕГИСТРУ СписанныеТовары.

	НаборДвижений = Движения.СписанныеТовары;

	// Получим таблицу значений, совпадающую со структурой набора записей регистра.
	ТаблицаДвижений = НаборДвижений.Выгрузить();
	ТаблицаДвижений.Очистить();

	// Заполним таблицу движений.
	ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(ТаблицаПоТаре, ТаблицаДвижений);

	// Недостающие поля.
	Для каждого Строка Из ТаблицаДвижений Цикл
		Инд = Инд + 1;
		Строка.НомерСтрокиДокумента = Инд;
		Строка.ВидТабличнойЧасти = Перечисления.ВидыТабличныхЧастей.Тара;
	КонецЦикла;

	// Если ПУ по складам
	ТаблицаДвижений.ЗаполнитьЗначения(Склад,"Склад");

	ТаблицаДвижений.ЗаполнитьЗначения(Дата,"Период");
	ТаблицаДвижений.ЗаполнитьЗначения(Ссылка,"Регистратор");
	ТаблицаДвижений.ЗаполнитьЗначения(Истина,"Активность");

	ТаблицаДвижений.ЗаполнитьЗначения(Организация, "Организация");

	ЗаполнитьКолонкиРегистраСписанныеТоварыПоТареУпр(ТаблицаДвижений, СтруктураШапкиДокумента,  ТаблицаПоТоварам);
	ЗаполнитьКолонкиРегистраСписанныеТоварыПоТареРегл(ТаблицаДвижений, СтруктураШапкиДокумента, ТаблицаПоТоварам);
	
	НаборДвижений.мПериод = Дата;
	НаборДвижений.мТаблицаДвижений = ТаблицаДвижений;

	Если Не Отказ Тогда
		Движения.СписанныеТовары.ВыполнитьДвижения();
	КонецЕсли;
	
	Движения.СписанныеТовары.Записать(Истина);

КонецПроцедуры//ДвиженияПоРегиструСписанныеТовары

Процедура ЗаполнитьКолонкиРегистраСписанныеТоварыПоТоварамУпр(ТаблицаДвижений, СтруктураШапкиДокумента, ТаблицаПоТоварам)

	ТаблицаДвижений.ЗаполнитьЗначения(ОтражатьВУправленческомУчете, "ОтражатьВУправленческомУчете");
	Если ВидОперации = Перечисления.ВидыОперацийСписаниеТоваров.Оборудование Тогда
		ТаблицаДвижений.ЗаполнитьЗначения(Перечисления.СтатусыПартийТоваров.Оборудование, "ДопустимыйСтатус1");
	Иначе
		ТаблицаДвижений.ЗаполнитьЗначения(Перечисления.СтатусыПартийТоваров.Купленный,    "ДопустимыйСтатус1");
		ТаблицаДвижений.ЗаполнитьЗначения(Перечисления.СтатусыПартийТоваров.Продукция,    "ДопустимыйСтатус2");
		ТаблицаДвижений.ЗаполнитьЗначения(Перечисления.СтатусыПартийТоваров.НаКомиссию,   "ДопустимыйСтатус3");
		ТаблицаДвижений.ЗаполнитьЗначения(Перечисления.СтатусыПартийТоваров.ВПереработку, "ДопустимыйСтатус4");
	КонецЕсли;
	ТаблицаДвижений.ЗаполнитьЗначения(Подразделение, "Подразделение");

КонецПроцедуры

Процедура ЗаполнитьКолонкиРегистраСписанныеТоварыПоТоварамРегл(ТаблицаДвижений, СтруктураШапкиДокумента, ТаблицаПоТоварам)

	ТаблицаДвижений.ЗаполнитьЗначения(СтруктураШапкиДокумента.ОтражатьВБухгалтерскомУчете, "ОтражатьВБухгалтерскомУчете");
	ТаблицаДвижений.ЗаполнитьЗначения(СтруктураШапкиДокумента.ОтражатьВНалоговомУчете,     "ОтражатьВНалоговомУчете");
	ТаблицаДвижений.ЗаполнитьЗначения(ПланыСчетов.Хозрасчетный.НедостачиИПотериОтПорчиЦенностей, "КорСчетБУ");
	ТаблицаДвижений.ЗаполнитьЗначения(ПланыСчетов.Налоговый.НедостачиИПотериОтПорчиЦенностей,    "КорСчетНУ");

	// Кор.Счет для начисления задолженности перед комитентом, если вдруг продадим комиссионный товар
	ТаблицаДвижений.ЗаполнитьЗначения(ПланыСчетов.Хозрасчетный.НедостачиИПотериОтПорчиЦенностей, "КорСчетЗадолженностиБУ")	;

	Для каждого Строка Из ТаблицаДвижений Цикл

		СтрокаТЧ = ТаблицаПоТоварам.Получить(ТаблицаДвижений.Индекс(Строка));

		// Принятые на комиссию товары не учитываются в налоговом учете:
		Если НЕ ЗначениеЗаполнено(СтрокаТЧ.СчетУчетаНУ) Тогда
			Строка.ОтражатьВНалоговомУчете = Ложь;

		Иначе
			Строка.ОтражатьВНалоговомУчете = СтруктураШапкиДокумента.ОтражатьВНалоговомУчете;

		КонецЕсли;
		Если СтруктураШапкиДокумента.ОтражатьВНалоговомУчетеУСН Тогда
			Строка.ОтражатьВНалоговомУчете 	= Истина;
			Строка.СчетУчетаНУ				= СтрокаТЧ.СчетУчетаБУ;
			Строка.ПринятыеСчетУчетаНУ 		= СтрокаТЧ.ПринятыеСчетУчетаБУ;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

Процедура ЗаполнитьКолонкиРегистраСписанныеТоварыПоТареУпр(ТаблицаДвижений, СтруктураШапкиДокумента, ТаблицаПоТоварам)

	ТаблицаДвижений.ЗаполнитьЗначения(ОтражатьВУправленческомУчете, "ОтражатьВУправленческомУчете");
	ТаблицаДвижений.ЗаполнитьЗначения(Перечисления.СтатусыПартийТоваров.ВозвратнаяТара, "ДопустимыйСтатус1");
	ТаблицаДвижений.ЗаполнитьЗначения(Подразделение, "Подразделение");

КонецПроцедуры

Процедура ЗаполнитьКолонкиРегистраСписанныеТоварыПоТареРегл(ТаблицаДвижений, СтруктураШапкиДокумента, ТаблицаПоТоварам)

	ТаблицаДвижений.ЗаполнитьЗначения(СтруктураШапкиДокумента.ОтражатьВБухгалтерскомУчете, "ОтражатьВБухгалтерскомУчете");
	ТаблицаДвижений.ЗаполнитьЗначения(СтруктураШапкиДокумента.ОтражатьВНалоговомУчете,     "ОтражатьВНалоговомУчете");
	ТаблицаДвижений.ЗаполнитьЗначения(ПланыСчетов.Хозрасчетный.НедостачиИПотериОтПорчиЦенностей, "КорСчетБУ");
	ТаблицаДвижений.ЗаполнитьЗначения(ПланыСчетов.Налоговый.НедостачиИПотериОтПорчиЦенностей,    "КорСчетНУ");

КонецПроцедуры

//начало изменений 
Функция ПРГ_ПолучитьНомерСубконто(Счет,ВидСубконто)
	Для к = 1 По Счет.ВидыСубконто.Количество() Цикл
		Если Счет.ВидыСубконто[к-1].ВидСубконто = ВидСубконто Тогда
			возврат к;
		КонецЕсли;	
	КонецЦикла;	
	возврат 0;
КонецФункции	
//конец изменений 

Процедура ДополнитьСтруктуруПолейТабличнойЧастиТоварыРегл(СтруктураПолей)

	СтруктураПолей.Вставить("СчетУчетаБУ"        , "СчетУчетаБУ");
	СтруктураПолей.Вставить("СчетУчетаНУ"        , "СчетУчетаНУ");
	СтруктураПолей.Вставить("ПринятыеСчетУчетаБУ", "ПринятыеСчетУчетаБУ");
	
	//начало изменений 
	Счет94 = ПланыСчетов.Хозрасчетный.НедостачиИПотериОтПорчиЦенностей;
	НС = ПРГ_ПолучитьНомерСубконто(Счет94,ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ПРГ_ВидыТМЦ);
	Если НС > 0 Тогда
		СтруктураПолей.Вставить("КорСубконтоБУ"+НС, "ВидТмц");
	КонецЕсли;	
	НС = ПРГ_ПолучитьНомерСубконто(Счет94,ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.РаботникиОрганизации);
	Если НС > 0 Тогда
		СтруктураПолей.Вставить("КорСубконтоБУ"+НС, "Ссылка.ФизЛицо");
	КонецЕсли;	
	//конец изменений 

КонецПроцедуры

Процедура ДополнитьСтруктуруПолейТабличнойЧастиВозвратнаяТараРегл(СтруктураПолей)

	СтруктураПолей.Вставить("СчетУчетаБУ", "СчетУчетаБУ");
	СтруктураПолей.Вставить("СчетУчетаНУ", "СчетУчетаНУ");
	
	//начало изменений 
	Счет94 = ПланыСчетов.Хозрасчетный.НедостачиИПотериОтПорчиЦенностей;
	НС = ПРГ_ПолучитьНомерСубконто(Счет94,ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ПРГ_ВидыТМЦ);
	Если НС > 0 Тогда
		СтруктураПолей.Вставить("КорСубконто"+НС, "ВидТмц");
	КонецЕсли;	
	НС = ПРГ_ПолучитьНомерСубконто(Счет94,ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.РаботникиОрганизации);
	Если НС > 0 Тогда
		СтруктураПолей.Вставить("КорСубконто"+НС, "Ссылка.ФизЛицо");
	КонецЕсли;	
	//конец изменений 
	

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Процедура - обработчик события "ОбработкаЗаполнения".
//
Процедура ОбработкаЗаполнения(Основание)

	Если ТипЗнч(Основание) = Тип("ДокументСсылка.ИнвентаризацияТоваровНаСкладе") Тогда

		// Заполним реквизиты из стандартного набора по документу основанию.
		ЗаполнениеДокументов.ЗаполнитьШапкуДокументаПоОснованию(ЭтотОбъект, Основание);

		/// Кунов О.В., 01.10.2014 - 32066
		Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ИнвентаризацияТоваровНаСкладеТовары.ПРГ_Склад КАК Склад
		|ИЗ
		|	Документ.ИнвентаризацияТоваровНаСкладе.Товары КАК ИнвентаризацияТоваровНаСкладеТовары
		|ГДЕ
		|	ИнвентаризацияТоваровНаСкладеТовары.Ссылка = &Основание");
		Запрос.УстановитьПараметр("Основание", Основание);
		
		СкладыОснованияМ = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Склад");
		СкладыОснования = Новый СписокЗначений;
		СкладыОснования.ЗагрузитьЗначения(СкладыОснованияМ);
		
		Если СкладыОснования.Количество() > 1 Тогда
			ВыбранныйСклад = СкладыОснования.ВыбратьЭлемент("Выберите склад");
			Если ВыбранныйСклад <> Неопределено Тогда
				СкладОснования = ВыбранныйСклад.Значение;
			Иначе
				Возврат;
			КонецЕсли;
		Иначе
			СкладОснования = Основание.Склад;
		КонецЕсли;
		
		Склад = СкладОснования;
		///
		
		ИнвентаризацияТоваровНаСкладе = Основание;
		
		НДСвСтоимостиТоваров = Перечисления.ДействиеНДСВСтоимостиТоваров.НеИзменять;

		ЗаполнитьТоварыПоИнвентаризацииТоваровУпр(Основание, СкладОснования);

		ЗаполнитьСчетаУчетаВТабЧасти(Товары,         "Товары",         Истина, Истина);
		ЗаполнитьСчетаУчетаВТабЧасти(ВозвратнаяТара, "ВозвратнаяТара", Истина, Истина);

		//++ Spl_Апроф 15.01.2015 (k.russkih@a-prof.ru)
		СП_НеОтражатьВСолво = Истина;
	//++ Spl_Апроф 15.01.2015 (k.russkih@a-prof.ru)
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ПР_Претензия") Тогда
	
		// <- Шевченков 37871
		Формировать2документа      = Ложь;
		ЕстьРешениеНеПеревыставить = Ложь;
		ЕстьРешениеПеревыставить   = Ложь;
		МассивРешений              = Новый Массив;
		Для каждого Строка Из Основание.Товары Цикл
			Если МассивРешений.Найти(Строка.РешениеПоПретензии) = Неопределено Тогда
				МассивРешений.Добавить(Строка.РешениеПоПретензии);
			КонецЕсли;											
		КонецЦикла;   
		Если МассивРешений.Найти(Справочники.ПР_РешениеПоПретензиям.НайтиПоКоду("000000005")) <> Неопределено
			Или МассивРешений.Найти(Справочники.ПР_РешениеПоПретензиям.НайтиПоКоду("000000008")) <> Неопределено Тогда	
			ЕстьРешениеПеревыставить = Истина;
		КонецЕсли;
		Если МассивРешений.Найти(Справочники.ПР_РешениеПоПретензиям.НайтиПоКоду("000000003")) <> Неопределено 
			Или МассивРешений.Найти(Справочники.ПР_РешениеПоПретензиям.НайтиПоКоду("000000002")) <> Неопределено
			Или МассивРешений.Найти(Справочники.ПР_РешениеПоПретензиям.НайтиПоКоду("000000001")) <> Неопределено
			Или МассивРешений.Найти(Справочники.ПР_РешениеПоПретензиям.НайтиПоКоду("000000004")) <> Неопределено
			Или МассивРешений.Найти(Справочники.ПР_РешениеПоПретензиям.НайтиПоКоду("000000006")) <> Неопределено
			Или МассивРешений.Найти(Справочники.ПР_РешениеПоПретензиям.НайтиПоКоду("000000007")) <> Неопределено
			Или МассивРешений.Найти(Справочники.ПР_РешениеПоПретензиям.НайтиПоКоду("000000009")) <> Неопределено Тогда
			ЕстьРешениеНеПеревыставить = Истина;				
		КонецЕсли;
		// Дальше добавил возможность фармировать 2 документа если есть решения выставлять и не выставлять
		Если ЕстьРешениеНеПеревыставить И ЕстьРешениеПеревыставить Тогда			
			Формировать2документа = Истина;
			
			НовыйДокТЭК = Документы.СписаниеТоваров.СоздатьДокумент();
			ЗаполнениеДокументов.ЗаполнитьШапкуДокументаПоОснованию(НовыйДокТЭК, Основание);
			НовыйДокТЭК.СуммаДокумента = 0;
			НовыйДокТЭК.ДокументОснование = Основание;
			НовыйДокТЭК.Склад = Основание.СкладПретензии;
			НовыйДокТЭК.ФизЛицо = ПолучитьОтветственноеЛицо();
			НовыйДокТЭК.НДСвСтоимостиТоваров = Перечисления.ДействиеНДСВСтоимостиТоваров.НеИзменять;
			НовыйДокТЭК.КП_Отделение = Основание.КП_Отделение;
			//СчетЗатратШапка = ПланыСчетов.Хозрасчетный.НайтиПоКоду("76.02.1");
				
			Для каждого Строка Из Основание.Товары Цикл
				Если Формировать2документа Тогда
					Если Строка.КоличествоРекламации > 0 И Строка.РешениеПоПретензии.ПеревыставитьНаТЭК Тогда
						ЗаполнитьЗначенияСвойств(НовыйДокТЭК.Товары.Добавить(), Строка,,"Цена, Сумма");
					КонецЕсли; 
				КонецЕсли;			
			КонецЦикла;	
			
			Если НЕ ЕстьРешениеПеревыставить Тогда
				НовыйДокТЭК.СчетЗатратШапка   = ПланыСчетов.Хозрасчетный.НайтиПоКоду("91.02.1"); // Шевченков 37868
				НовыйДокТЭК.СчетЗатратНУШапка = ПланыСчетов.Налоговый.НайтиПоКоду("91.02.7");  // Шевченков 37868
				//СчетЗатратШапка = ПланыСчетов.Хозрасчетный.НайтиПоКоду("76.02.1");
				НовыйДокТЭК.СубконтоШапка1   = Справочники.ПрочиеДоходыИРасходы.НайтиПоКоду("Р-4.3.169"); // Шевченков 37868
				НовыйДокТЭК.СубконтоНУШапка1 = Справочники.ПрочиеДоходыИРасходы.НайтиПоКоду("Р-4.3.169"); // Шевченков 37868
				НовыйДокТЭК.Подразделение    = Склад.НСИ_Отделение.Подразделение; // Шевченков 37868
				НовыйДокТЭК.СубконтоШапка2 = ПолучитьПодразделениеОрганизации(НовыйДокТЭК.Склад.НСИ_Отделение.Подразделение); // Шевченков 37868
				//СубконтоШапка1 = Основание.ТЭК;
			Иначе
				НовыйДокТЭК.СчетЗатратШапка   = ПланыСчетов.Хозрасчетный.НайтиПоКоду("76.02.1"); // Шевченков 37868
				//НовыйДокТЭК.СчетЗатратНУШапка = ПланыСчетов.Налоговый.НайтиПоКоду("91.02.7");  // Шевченков 37868
				//СчетЗатратШапка = ПланыСчетов.Хозрасчетный.НайтиПоКоду("76.02.1");
				НовыйДокТЭК.СубконтоШапка1   = Основание.ТЭК; // Шевченков 37868
				//НовыйДокТЭК.СубконтоНУШапка1 = Справочники.ПрочиеДоходыИРасходы.НайтиПоКоду("Р-4.3.169"); // Шевченков 37868
				НовыйДокТЭК.Подразделение    = Склад.НСИ_Отделение.Подразделение; // Шевченков 37868
				НовыйДокТЭК.СубконтоШапка2 = Основание.ТЭК.ОсновнойДоговорКонтрагента; // Шевченков 37868
			КонецЕсли;
		
			НовыйДокТЭК.ПР_СтатусПриходногоОрдера = Перечисления.СП_СтатусыПриходногоОрдераСолво.Открыто;
			НовыйДокТЭК.Комментарий = Основание.Комментарий; // Шевченков 37895		
			ЗаполнитьСчетаУчетаВТабЧасти(НовыйДокТЭК.Товары, "Товары", Истина, Истина);
			
			НовыйДокТЭКФормаДокумента = НовыйДокТЭК.ПолучитьФорму("ФормаДокумента");
			НовыйДокТЭКФормаДокумента.Открыть();
			
		КонецЕсли;
		// ->  Ниже тоже есть изменения по строкам, не комментирую
		
		ЗаполнениеДокументов.ЗаполнитьШапкуДокументаПоОснованию(ЭтотОбъект, Основание);
		СуммаДокумента = 0;
		ДокументОснование = Основание;
		Склад = Основание.СкладПретензии;
		ФизЛицо = ПолучитьОтветственноеЛицо();
		НДСвСтоимостиТоваров = Перечисления.ДействиеНДСВСтоимостиТоваров.НеИзменять;
		КП_Отделение = Основание.КП_Отделение;
		//СчетЗатратШапка = ПланыСчетов.Хозрасчетный.НайтиПоКоду("76.02.1");
		ЕстьРешениеПеревыставить   = Ложь;	
		Для каждого Строка Из Основание.Товары Цикл
			Если Формировать2документа Тогда
				Если Строка.КоличествоРекламации > 0 И не Строка.РешениеПоПретензии.ПеревыставитьНаТЭК Тогда
					НоваяСтрока = Товары.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка,,"Цена, Сумма");
				КонецЕсли; 
			Иначе
				Если Строка.КоличествоРекламации > 0 Тогда
					ЗаполнитьЗначенияСвойств(Товары.Добавить(), Строка,,"Цена, Сумма");
				КонецЕсли;
				Если Строка.РешениеПоПретензии.ПеревыставитьНаТЭК Тогда
					ЕстьРешениеПеревыставить = Истина;
				КонецЕсли;				
			КонецЕсли;			
		КонецЦикла;	
		
		Если НЕ ЕстьРешениеПеревыставить Тогда
			СчетЗатратШапка   = ПланыСчетов.Хозрасчетный.НайтиПоКоду("91.02.1"); // Шевченков 37868
			СчетЗатратНУШапка = ПланыСчетов.Налоговый.НайтиПоКоду("91.02.7");  // Шевченков 37868
			//СчетЗатратШапка = ПланыСчетов.Хозрасчетный.НайтиПоКоду("76.02.1");
			СубконтоШапка1   = Справочники.ПрочиеДоходыИРасходы.НайтиПоКоду("Р-4.3.169"); // Шевченков 37868
			СубконтоНУШапка1 = Справочники.ПрочиеДоходыИРасходы.НайтиПоКоду("Р-4.3.169"); // Шевченков 37868
			Подразделение    = Склад.НСИ_Отделение.Подразделение; // Шевченков 37868
			ЭтотОбъект.СубконтоШапка2 = ПолучитьПодразделениеОрганизации(Склад.НСИ_Отделение.Подразделение); // Шевченков 37868
			//СубконтоШапка1 = Основание.ТЭК;
		Иначе
			СчетЗатратШапка   = ПланыСчетов.Хозрасчетный.НайтиПоКоду("76.02.1"); // Шевченков 37868
			СубконтоШапка1   = Основание.ТЭК; // Шевченков 37868
			Подразделение    = Склад.НСИ_Отделение.Подразделение; // Шевченков 37868
			СубконтоШапка2   = Основание.ТЭК.ОсновнойДоговорКонтрагента; // Шевченков 37868
		КонецЕсли;
		
		ПР_СтатусПриходногоОрдера = Перечисления.СП_СтатусыПриходногоОрдераСолво.Открыто;
		Комментарий = Основание.Комментарий; // Шевченков 37895		
		ЗаполнитьСчетаУчетаВТабЧасти(Товары, "Товары", Истина, Истина);
			
	КонецЕсли; 

КонецПроцедуры // ОбработкаЗаполнения()

// Процедура вызывается перед записью документа 
//
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка  Тогда
		Возврат;
	КонецЕсли;

	//начало изменений Ожиганов 28.05.2015 немножко оптимизируем 
	ПРГ_ДопФункцииКлиентСервер.ДобавитьВОбъектСвойстваДляУдаленияДвижения(ЭтотОбъект,РежимЗаписи);
	//конец изменений 	
	// Проверка заполнения единицы измерения мест и количества мест
	ОбработкаТабличныхЧастей.ПриЗаписиПроверитьЕдиницуИзмеренияМест(Товары);
	//начало изменений  
	Если Дата >= '20140401' Тогда
		Если ОтражатьВБухгалтерскомУчете Тогда
			ПРГ_ТаблСчетов = ПРГ_ДопФункцииКлиентСервер.ПолучитьТаблСчетовВидовТмц();
			Для Каждого ТекСтрока  Из Товары Цикл
				Если Не ЗначениеЗаполнено(ТекСтрока.ВидТМЦ) Тогда
					найдзнч = ПРГ_ТаблСчетов.Найти(ТекСтрока.СчетУчетаБУ,"Счет");
					Если найдзнч  <> Неопределено Тогда
						ТекСтрока.ВидТМЦ = найдзнч.ВидТмц;
					КонецЕсли;	
				КонецЕсли;	
			КонецЦикла;	
			
			Для Каждого ТекСтрока  Из ВозвратнаяТара Цикл
				Если Не ЗначениеЗаполнено(ТекСтрока.ВидТМЦ) Тогда
					найдзнч = ПРГ_ТаблСчетов.Найти(ТекСтрока.СчетУчетаБУ,"Счет");
					Если найдзнч  <> Неопределено Тогда
						ТекСтрока.ВидТМЦ = найдзнч.ВидТмц;
					КонецЕсли;	
				КонецЕсли;	
			КонецЦикла;	
			Если Не ЗначениеЗаполнено(ФизЛицо) Тогда
				ФизЛицо = ПолучитьОтветственноеЛицо();
			КонецЕсли;	
		КонецЕсли;		
	КонецЕсли;	
	//конец изменений 

	// Посчитать суммы документа и записать ее в соответствующий реквизит шапки для показа в журналах
	СуммаДокумента = Товары.Итог("Сумма");

	// Почистим неиспользуемые реквизиты.
	Если ВидОперации = Перечисления.ВидыОперацийСписаниеТоваров.Оборудование Тогда 

		Для каждого СтрокаТаблицы Из ЭтотОбъект.Товары Цикл

			Если ЗначениеЗаполнено(СтрокаТаблицы.ДокументРезерва) Тогда
				СтрокаТаблицы.ДокументРезерва = Неопределено;
			КонецЕсли;

			Если СтрокаТаблицы.Качество <> Справочники.Качество.Новый Тогда
				СтрокаТаблицы.Качество = Справочники.Качество.Новый;
			КонецЕсли;

		КонецЦикла;
	Иначе
		ОбработкаТабличныхЧастей.ЗаполнитьКачествоПоУмолчанию(Товары);
	КонецЕсли;

	мУдалятьДвижения = НЕ ЭтоНовый();
	//начало изменений
	Если ОтражатьВНалоговомУчете и ЗначениеЗаполнено(СчетЗатратШапка)
		и не ЗначениеЗаполнено(СчетЗатратНУШапка)
		Тогда
		Если СчетЗатратШапка = ПланыСчетов.Хозрасчетный.РасчетыПоВозмещениюМатериальногоУщерба
		 или СчетЗатратШапка = ПланыСчетов.Хозрасчетный.РасчетыПоПрочимОперациям Тогда
		     СчетЗатратНУШапка = ПланыСчетов.Налоговый.ПоступлениеИВыбытиеИмуществаРаботУслугПрав;
		ИначеЕсли СчетЗатратШапка = ПланыСчетов.Хозрасчетный.ПрочиеРасходыНеОблагаемыеЕНВД Тогда
			 СчетЗатратНУШапка = ПланыСчетов.Налоговый.ВнереализационныеРасходы;
			 СубконтоНУШапка1  = СубконтоШапка1;
		КонецЕсли; 
	КонецЕсли;	
	//конец изменений
	
	//m.ionov@a-prof.ru 08.10.2014
	Если НЕ ДоступенСтатусДокумента() Тогда
		ПР_СтатусПриходногоОрдера = Перечисления.СП_СтатусыПриходногоОрдераСолво.ПустаяСсылка();
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью

// Процедура - обработчик события "ПриЗаписи"
//
Процедура ПриЗаписи(Отказ)
	Если ОбменДанными.Загрузка  Тогда
		Возврат;
	КонецЕсли;
	
	//++ Spl_Апроф 07.11.2014 (k.russkih@a-prof.ru)
	Если Не Отказ Тогда	
		ЗаписатьИзменениеСтатусов(Отказ);			
	КонецЕсли; 
	//-- Spl_Апроф

КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	// Дерево значений, содержащее имена необходимых полей в запросе по шапке.
	Перем ДеревоПолейЗапросаПоШапке;

	
	Если мУдалятьДвижения Тогда
		ОбщегоНазначения.УдалитьДвиженияРегистратора(ЭтотОбъект, Отказ, Истина, РежимПроведения);
	КонецЕсли;
	
	Если Не РольДоступна("ПолныеПрава") Тогда
		Если ОтражатьВБухгалтерскомУчете и ЗначениеЗаполнено(СчетЗатратШапка) и  ВозвратнаяТара.Количество() > 0 Тогда
			 ОбщегоНазначения.СообщитьОбОшибке("Не поддерживается дополнительный счет для проводки для тары",Отказ);
			 возврат;
		КонецЕсли;	
	КонецЕсли;	

	// Заголовок для сообщений об ошибках проведения.
	Заголовок = "Проведение документа """ + СокрЛП(Ссылка) + """: ";

	СтруктураШапкиДокумента = ОбщегоНазначения.СформироватьСтруктуруШапкиДокументаИПроверитьОтражениеВУчете(ЭтотОбъект, Отказ, Заголовок);

	//{24.03.2015 Островерхий заявка №38069 
	Если СтруктураШапкиДокумента.ВидОперации = Перечисления.ВидыОперацийСписаниеТоваров.ПеремещениеТарыНаСкладСолво Тогда
		
		СтруктураШапкиДокумента.ВидОперации = Перечисления.ВидыОперацийСписаниеТоваров.ТоварыПродукция;
	
	КонецЕсли;  
	//24.03.2015 Островерхий}
	
	// Заполним по шапке документа дерево параметров, нужных при проведении.
	ДеревоПолейЗапросаПоШапке = УправлениеЗапасами.СформироватьДеревоПолейЗапросаПоШапке();
	УправлениеЗапасами.ДобавитьСтрокуВДеревоПолейЗапросаПоШапке(ДеревоПолейЗапросаПоШапке, "Константы"       , "ВалютаУправленческогоУчета", "ВалютаУправленческогоУчета");
	УправлениеЗапасами.ДобавитьСтрокуВДеревоПолейЗапросаПоШапке(ДеревоПолейЗапросаПоШапке, "Константы"       , "КурсВалютыУправленческогоУчета"    , "КурсВалютыУправленческогоУчета");
	УправлениеЗапасами.ДобавитьСтрокуВДеревоПолейЗапросаПоШапке(ДеревоПолейЗапросаПоШапке, "УчетнаяПолитика" , "ВестиПартионныйУчетПоСкладам"      , "ВестиПартионныйУчетПоСкладам");
	УправлениеЗапасами.ДобавитьСтрокуВДеревоПолейЗапросаПоШапке(ДеревоПолейЗапросаПоШапке, "Склад"           , "ВидСклада",                      "ВидСклада");
	УправлениеЗапасами.ДобавитьСтрокуВДеревоПолейЗапросаПоШапке(ДеревоПолейЗапросаПоШапке, "Организация"     , "ОтражатьВРегламентированномУчете"      , "ОтражатьВРегламентированномУчете");
	УправлениеЗапасами.ДобавитьСтрокуВДеревоПолейЗапросаПоШапке(ДеревоПолейЗапросаПоШапке, "УчетнаяПолитика"     , "ВестиУчетТоваровОрганизацийВРазрезеСкладов"   , "ВестиУчетТоваровОрганизацийВРазрезеСкладов");
    УправлениеЗапасами.ДобавитьСтрокуВДеревоПолейЗапросаПоШапке(ДеревоПолейЗапросаПоШапке, "НастройкаСпособовВеденияУправленческогоПартионногоУчета", "СпособВеденияПартионногоУчетаПоОрганизации", "СпособВеденияПартионногоУчетаПоОрганизации");
	// Сформируем запрос на дополнительные параметры, нужные при проведении, по данным шапки документа
	СтруктураШапкиДокумента = УправлениеЗапасами.СформироватьЗапросПоДеревуПолей(ЭтотОбъект, ДеревоПолейЗапросаПоШапке, СтруктураШапкиДокумента, мВалютаРегламентированногоУчета);

	// Проверим правильность заполнения шапки документа
	ПроверитьЗаполнениеШапки(СтруктураШапкиДокумента, Отказ, Заголовок);

	// Получим необходимые данные для проведения и проверки заполнения данные по табличной части "Товары".
	СтруктураПолей = Новый Структура();
	СтруктураПолей.Вставить("Номенклатура"              , "Номенклатура");
	СтруктураПолей.Вставить("Услуга"                    , "Номенклатура.Услуга");
	СтруктураПолей.Вставить("Набор"                     , "Номенклатура.Набор");
	СтруктураПолей.Вставить("Комплект"                  , "Номенклатура.Комплект");
	СтруктураПолей.Вставить("Количество"                , "Количество * Коэффициент /Номенклатура.ЕдиницаХраненияОстатков.Коэффициент");
	СтруктураПолей.Вставить("Цена"                      , "Цена");
	СтруктураПолей.Вставить("Сумма"                     , "Сумма");
	СтруктураПолей.Вставить("Качество"                  , "Качество");
	СтруктураПолей.Вставить("ХарактеристикаНоменклатуры", "ХарактеристикаНоменклатуры");
	СтруктураПолей.Вставить("СерияНоменклатуры"         , "СерияНоменклатуры");
	СтруктураПолей.Вставить("ЗаказПартии"               , "ДокументРезерва");
	СтруктураПолей.Вставить("ДокументРезерва"           , "ДокументРезерва");
	СтруктураПолей.Вставить("ВидСклада"           		, "Ссылка.Склад.ВидСклада");

	ДополнитьСтруктуруПолейТабличнойЧастиТоварыРегл(СтруктураПолей);

	РезультатЗапросаПоТоварам = УправлениеЗапасами.СформироватьЗапросПоТабличнойЧасти(ЭтотОбъект, "Товары", СтруктураПолей);

	// Подготовим таблицу товаров для проведения.
	ТаблицаПоТоварам = ПодготовитьТаблицуТоваров(РезультатЗапросаПоТоварам, СтруктураШапкиДокумента);

	// Проверить заполнение ТЧ "Товары".
	ПроверитьЗаполнениеТабличнойЧастиТовары(ТаблицаПоТоварам, СтруктураШапкиДокумента, Отказ, Заголовок);

	// Получим необходимые данные для проведения и проверки заполнения данные по табличной части "ВозвратнаяТара".
	СтруктураПолей = Новый Структура();
	СтруктураПолей.Вставить("Номенклатура"              , "Номенклатура");
	СтруктураПолей.Вставить("Услуга"                    , "Номенклатура.Услуга");
	СтруктураПолей.Вставить("Набор"                     , "Номенклатура.Набор");
	СтруктураПолей.Вставить("Комплект"                  , "Номенклатура.Комплект");
	СтруктураПолей.Вставить("Количество"                , "Количество");
	СтруктураПолей.Вставить("ДокументРезерва"           , "ДокументРезерва");
	СтруктураПолей.Вставить("ВестиУчетПоХарактеристикам", "Номенклатура.ВестиУчетПоХарактеристикам");
	СтруктураПолей.Вставить("ДоговорКонтрагента"		, "ДокументРезерва.ДоговорКонтрагента");
	СтруктураПолей.Вставить("ВидСклада"           		, "Ссылка.Склад.ВидСклада");
	
	ДополнитьСтруктуруПолейТабличнойЧастиВозвратнаяТараРегл(СтруктураПолей);

	РезультатЗапросаПоТаре = УправлениеЗапасами.СформироватьЗапросПоТабличнойЧасти(ЭтотОбъект, "ВозвратнаяТара", СтруктураПолей);

	// Подготовим таблицу товаров для проведения.
	ТаблицаПоТаре = ПодготовитьТаблицуТары(РезультатЗапросаПоТаре, СтруктураШапкиДокумента);

	// Проверить заполнение ТЧ "ВозвратнаяТара".
	ПроверитьЗаполнениеТабличнойЧастиВозвратнаяТара(ТаблицаПоТаре, СтруктураШапкиДокумента, Отказ, Заголовок);

	//Заполнение и проверка заполнения счетов учета номенклатуры и затрат
	СчетаУчетаВДокументах.ЗаполнитьИПроверитьЗаполнениеСчетовУчетаТабличнойЧасти("Товары", 		 	ТаблицаПоТоварам, 	СтруктураШапкиДокумента, Отказ, Заголовок);
	СчетаУчетаВДокументах.ЗаполнитьИПроверитьЗаполнениеСчетовУчетаТабличнойЧасти("ВозвратнаяТара", 	ТаблицаПоТаре, 		СтруктураШапкиДокумента, Отказ, Заголовок);
	
	// Движения по документу
	Если Не Отказ Тогда
		ДвиженияПоРегистрам(РежимПроведения, СтруктураШапкиДокумента, ТаблицаПоТоварам, ТаблицаПоТаре, Отказ, Заголовок);
	КонецЕсли;

	//Сделаем переменные доступными из подписок на события
	ДополнительныеСвойства.Вставить("СтруктураШапкиДокумента", СтруктураШапкиДокумента);
	ДополнительныеСвойства.Вставить("СтруктураТабличныхЧастей", Новый Структура("ТаблицаПоТоварам, ТаблицаПоТаре", ТаблицаПоТоварам, ТаблицаПоТаре));
	
	//{26.01.2016 Островерхий заявка №46457 
	Если РежимПроведения = РежимПроведенияДокумента.Неоперативный Тогда
		ПолныеПрава.ЗаписатьНаборЗаписейНаСервере("СвободныеОстатки", Ссылка,, "РегистрНакопления");
	КонецЕсли; 
	//26.01.2016 Островерхий}
	
КонецПроцедуры // ОбработкаПроведения()

//начало изменений
Функция ПолучитьОтветственноеЛицо() Экспорт 
	
	Если Не ЗначениеЗаполнено(Склад) Тогда
		возврат Справочники.Склады.ПустаяСсылка();
	КонецЕсли;	

	Отбор = Новый Структура("СтруктурнаяЕдиница");
	Отбор.СтруктурнаяЕдиница = Склад;
	СрезПоследних   = РегистрыСведений.ОтветственныеЛица.СрезПоследних(Дата, Отбор);

	Если СрезПоследних.Количество() < 1 Тогда
		возврат Справочники.Склады.ПустаяСсылка();
	Иначе
		возврат СрезПоследних[0].ФизическоеЛицо
	КонецЕсли;

КонецФункции // ПрочитатьОтветственноеЛицо()
//конец изменений 

//++ Spl_Апроф 06.11.2014 (k.russkih@a-prof.ru)
Процедура ЗаписатьИзменениеСтатусов(Отказ) Экспорт
	
	//Движения по регистру накопления "АП_СтатусыДокументов"
	ДанныеПоСтатусу = РегистрыСведений.АП_СтатусыДокументов.СрезПоследних(ТекущаяДата(), Новый Структура("Документ", Ссылка));
	
	Если ДанныеПоСтатусу.Количество() = 0 Тогда
		СформируемЗаписьПоРегиструСтатусы(Отказ);
	ИначеЕсли НЕ ДанныеПоСтатусу[0].Статус = ПР_СтатусПриходногоОрдера Тогда
	    СформируемЗаписьПоРегиструСтатусы(Отказ);
	КонецЕсли; 
	
КонецПроцедуры

Процедура СформируемЗаписьПоРегиструСтатусы(Отказ)

	мДатаЗаписи = ТекущаяДата();
	
	НаборЗаписей = РегистрыСведений.АП_СтатусыДокументов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Документ.Установить(Ссылка);
	НаборЗаписей.Отбор.Период.Установить(мДатаЗаписи);
	
	НаборЗаписей.Прочитать();
	
	НоваяСтрока = НаборЗаписей.Добавить();
	НоваяСтрока.Период = мДатаЗаписи;
	НоваяСтрока.Документ = Ссылка;
	НоваяСтрока.Статус = ПР_СтатусПриходногоОрдера;
	НоваяСтрока.Ответственный = глЗначениеПеременной("глТекущийПользователь");
	
	Попытка		
		НаборЗаписей.Записать();		
	Исключение
		ОбщегоНазначения.СообщитьОбОшибке(ОписаниеОшибки(), Отказ);
	КонецПопытки;
	
КонецПроцедуры 

Функция МожноИнтрактивноМенятьДокумент() Экспорт
	
	МожноМенять = Истина;
	
	Если ЭтоНовый() 
		ИЛИ РольДоступна("АП_Администратор")// ИЛИ РольДоступна("ПолныеПрава")
		ИЛИ АП_ОбщегоНазначенияСервер.ДоступнаРоль(глЗначениеПеременной("глТекущийПользователь"), Справочники.АП_РолиПользователей.СотрудникБухгалтерии) Тогда
		Возврат МожноМенять;
	КонецЕсли;
	
	Если МожноМенять И Не ЭтоНовый() Тогда
		Если Склад.НСИ_ПодключенКSolvo
			И Не ПР_СтатусПриходногоОрдера = Перечисления.СП_СтатусыПриходногоОрдераСолво.Открыто 
			И Не ПР_СтатусПриходногоОрдера = Перечисления.СП_СтатусыПриходногоОрдераСолво.Отменено
			//И Не АП_ОбщегоНазначенияСервер.ДоступнаРоль(глЗначениеПеременной("глТекущийПользователь"), Справочники.АП_РолиПользователей.СотрудникСклада) //Пока в тестовом режиме, потом не должно быть не достуно
			Тогда
			
			МожноМенять = Ложь; //Вне изменения из SOLVO
			
		КонецЕсли;
	КонецЕсли;
	
	Возврат МожноМенять;
	
КонецФункции

Функция МожноМенятьСтроку(ТекСтрока) Экспорт
	МожноМенять = Истина;
	
	Если ЭтоНовый() 
		ИЛИ РольДоступна("АП_Администратор") Тогда// ИЛИ РольДоступна("ПолныеПрава")
		//ИЛИ АП_ОбщегоНазначенияСервер.ДоступнаРоль(глЗначениеПеременной("глТекущийПользователь"), Справочники.АП_РолиПользователей.СотрудникБухгалтерии) Тогда
		Возврат МожноМенять;
	КонецЕсли;
	
	Если МожноМенять Тогда
		Если Склад.НСИ_ПодключенКSolvo
			И Не ПР_СтатусПриходногоОрдера = Перечисления.СП_СтатусыПриходногоОрдераСолво.Открыто 
			И Не ПР_СтатусПриходногоОрдера = Перечисления.СП_СтатусыПриходногоОрдераСолво.Отменено Тогда	
			
			МожноМенять = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат МожноМенять;
	
КонецФункции

Функция МожноМенятьСоставСтрок() Экспорт
	Если РольДоступна("АП_Администратор") Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если Не Склад.НСИ_ПодключенКSolvo Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если Не Проведен Тогда
		Возврат Истина;
	ИначеЕсли Не ПР_СтатусПриходногоОрдера = Перечисления.СП_СтатусыПриходногоОрдераСолво.Открыто 
		И Не ПР_СтатусПриходногоОрдера = Перечисления.СП_СтатусыПриходногоОрдераСолво.Отменено Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;	
КонецФункции

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если Не МожноМенятьСоставСтрок() Тогда
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

Функция ДоступенСтатусДокумента() Экспорт

	Доступен = Ложь;
	
	Если ВидОперации = Перечисления.ВидыОперацийСписаниеТоваров.ТоварыПродукция И Склад.НСИ_ПодключенКSolvo Тогда	
		Доступен = Истина;	
	КонецЕсли; 
	
	Возврат Доступен;

КонецФункции // ()

//++ Spl_Апроф 29.01.2015 (k.russkih@a-prof.ru)
Процедура ПриКопировании(ОбъектКопирования)
	ДокументОснование = Неопределено;
КонецПроцедуры

// <- Шевченков 37868
Функция ПолучитьПодразделениеОрганизации(Подразделение)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СоответствиеПодразделенийИПодразделенийОрганизаций.ПодразделениеОрганизации
	|ИЗ
	|	РегистрСведений.СоответствиеПодразделенийИПодразделенийОрганизаций КАК СоответствиеПодразделенийИПодразделенийОрганизаций
	|ГДЕ
	|	СоответствиеПодразделенийИПодразделенийОрганизаций.Подразделение = &Подразделение";
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Рез = Запрос.Выполнить().Выгрузить();
	
	Возврат ?(Рез.Количество()=1, Рез[0].ПодразделениеОрганизации, Справочники.ПодразделенияОрганизаций.ПустаяСсылка());
	
КонецФункции
// ->

мВалютаРегламентированногоУчета = глЗначениеПеременной("ВалютаРегламентированногоУчета");

Процедура ОбработкаПроведения(Отказ, Режим)
	
	// сделаем проверку на 1 документ
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	ПРГ_ФактическоеКоличествоДобытойВоды.Ссылка
	                      |ИЗ
	                      |	Документ.ПРГ_ФактическоеКоличествоДобытойВоды КАК ПРГ_ФактическоеКоличествоДобытойВоды
	                      |ГДЕ
	                      |	ПРГ_ФактическоеКоличествоДобытойВоды.Организация = &Организация
	                      |	И ПРГ_ФактическоеКоличествоДобытойВоды.Ссылка <> &Ссылка
	                      |	И ПРГ_ФактическоеКоличествоДобытойВоды.Проведен
	                      |	И ПРГ_ФактическоеКоличествоДобытойВоды.Дата МЕЖДУ &Дата1 И &Дата2");
						  
	Запрос.УстановитьПараметр("Дата1",НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("Дата2",КонецМесяца(Дата));
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	Запрос.УстановитьПараметр("Организация",Организация);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ОбщегоНазначения.СообщитьОбОшибке("В одном месяце допускается ввод однового документа по воде",Отказ);
	КонеЦесли;	
	
	//проверим потреблено не может меньше всего
	Для Каждого Текстрока Из  Вода Цикл
		Если Текстрока.КоличествоПотреблено > Текстрока.Количество Тогда
			ОбщегоНазначения.СообщитьОбОшибке("В строке № "+Текстрока.НомерСтроки+" не может быть задано всего произведенной воды меньше, чем произведено !!!",Отказ);
	    КонецЕсли;		
	КонецЦикла;	
	
КонецПроцедуры

#Если Клиент Тогда
// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходимое количество копий.
//
//  Название макета печати передается в качестве параметра,
// по переданному названию находим имя макета в соответствии.
//
// Параметры:
//  НазваниеМакета - строка, название макета.
//
Процедура Печать(ИмяМакета, КоличествоЭкземпляров = 1, НаПринтер = Ложь) Экспорт

	Если ЭтоНовый() Тогда
		Предупреждение("Документ можно распечатать только после его записи");
		Возврат;
	ИначеЕсли Не УправлениеДопПравамиПользователей.РазрешитьПечатьНепроведенныхДокументов(Проведен) Тогда
		Предупреждение("Недостаточно полномочий для печати непроведенного документа!");
		Возврат;
	КонецЕсли;

	Если ТипЗнч(ИмяМакета) = Тип("ДвоичныеДанные") Тогда
		ТабДокумент = УниверсальныеМеханизмы.НапечататьВнешнююФорму(Ссылка, ИмяМакета);

		Если ТабДокумент = Неопределено Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;

	Если ИмяМакета = "ОтчетПоВоде" Тогда
		ТабДокумент = ПолучитьОтчет();
	КонецЕсли; 
	
	УниверсальныеМеханизмы.НапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер, ОбщегоНазначения.СформироватьЗаголовокДокумента(ЭтотОбъект, ЭтотОбъект.Метаданные().Представление()), Ссылка);

КонецПроцедуры // Печать()

Функция ПолучитьОтчет()

	ТД = Новый ТабличныйДокумент;
	
	Макет = ПолучитьМакет("ОтчетПоВоде");
	
	СтруктураОбластей = Новый Структура(
		"Заголовок, Шапка, Строка, ДопСтроки, Итого, Подпись",
		"Заголовок", "ШапкаТаблицы", "СтрокаТаблицы", "ДополнительныеСтроки", "Итого", "Подпись"
	);
	
	Для Каждого КлючЗначение Из СтруктураОбластей Цикл
		СтруктураОбластей[КлючЗначение.Ключ] = Макет.ПолучитьОбласть(КлючЗначение.Значение);
	КонецЦикла;
	
	СтруктураОбластей.Заголовок.Параметры.Заполнить(
		Новый Структура("Организация, ИНН, ПериодСтр, Номер, Дата", Организация.Наименование, Организация.ИНН,
			ПредставлениеПериода(НачалоМесяца(Дата), КонецМесяца(Дата), "ФП = Истина"), ПРГ_ДопФункцииКлиентСервер.УдалитьЛидирующиеНули(Номер), Формат(Дата, "ДФ=dd.MM.yyyy")
	));
	
	ТД.Вывести(СтруктураОбластей.Заголовок);
	
	ТД.Вывести(СтруктураОбластей.Шапка);
	
	Для Каждого СтрокаНоменклатуры Из ЭтотОбъект.Вода Цикл 
		СтруктураОбластей.Строка.Параметры.Заполнить(
			Новый Структура("Номер, Номенклатура, КоличествоЛитров, КоличествоКубометров",
				СтрокаНоменклатуры.НомерСтроки, СтрокаНоменклатуры.Номенклатура.Наименование,
				СтрокаНоменклатуры.Количество, Окр(СтрокаНоменклатуры.Количество / 1000, 0)
		));
		ТД.Вывести(СтруктураОбластей.Строка);
		Если СтрокаНоменклатуры.Номенклатура.Наименование = "Вода минеральная" Тогда
			СтруктураОбластей.ДопСтроки.Параметры.Заполнить(
				Новый Структура("КоличествоВПределахНорм, КоличествоСверхНорм, КоличествоВПродукцию",
				СтрокаНоменклатуры.Количество, 0, СтрокаНоменклатуры.Количество
			));
			ТД.Вывести(СтруктураОбластей.ДопСтроки);
		КонецЕсли;
	КонецЦикла;
	
	СтруктураОбластей.Итого.Параметры.Заполнить(
		Новый Структура("ИтогоЛитры, ИтогоКубометры",
			Вода.Итог("Количество"), Окр(Вода.Итог("Количество")/1000, 0)
	));
	ТД.Вывести(СтруктураОбластей.Итого);
	
	ТД.Вывести(СтруктураОбластей.Подпись);
	
	Возврат ТД;

КонецФункции // ПолучитьОтчет()
 

// Возвращает доступные варианты печати документа.
//
// Возвращаемое значение:
//  Структура, каждая строка которой соответствует одному из вариантов печати.
//
Функция ПолучитьСтруктуруПечатныхФорм() Экспорт

	СтруктураМакетов = Новый Структура();

	СтруктураМакетов.Вставить("ОтчетПоВоде", "Отчет");
	
	Возврат СтруктураМакетов;

КонецФункции // ПолучитьСтруктуруПечатныхФорм()

#КонецЕсли

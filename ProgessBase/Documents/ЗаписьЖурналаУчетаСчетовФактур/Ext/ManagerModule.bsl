                                                                                         // ОБРАБОТЧИКИ ОБНОВЛЕНИЯ

// Обработчик обновления
//
// Устанавливает признак сводных счетов-фактур по комиссии
Процедура УстановитьСводныйКомиссионный() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗаписьЖурналаУчетаСчетовФактурПосредническаяДеятельность.Ссылка
	|ИЗ
	|	Документ.ЗаписьЖурналаУчетаСчетовФактур.ПосредническаяДеятельность КАК ЗаписьЖурналаУчетаСчетовФактурПосредническаяДеятельность
	|ГДЕ
	|	ЗаписьЖурналаУчетаСчетовФактурПосредническаяДеятельность.Ссылка.Дата >= ДАТАВРЕМЯ(2015, 1, 1)
	|	И НЕ ЗаписьЖурналаУчетаСчетовФактурПосредническаяДеятельность.Ссылка.СводныйКомиссионный
	|	И ЗаписьЖурналаУчетаСчетовФактурПосредническаяДеятельность.Ссылка.Проведен
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаписьЖурналаУчетаСчетовФактурПосредническаяДеятельность.Ссылка
	|
	|ИМЕЮЩИЕ
	|	МАКСИМУМ(ЗаписьЖурналаУчетаСчетовФактурПосредническаяДеятельность.НомерСтроки) > 1";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ЗаписьЖурналаУчетаСчетовФактур = Выборка.Ссылка.ПолучитьОбъект();
		ЗаписьЖурналаУчетаСчетовФактур.СводныйКомиссионный = Истина;
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(ЗаписьЖурналаУчетаСчетовФактур);
	КонецЦикла;
	
КонецПроцедуры
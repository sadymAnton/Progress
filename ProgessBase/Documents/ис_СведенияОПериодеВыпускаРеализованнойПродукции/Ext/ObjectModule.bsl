
Процедура ЗаполнитьСведения() Экспорт 

	Сведения.Очистить();
	
	лЗапросДанные = Новый Запрос;
	лЗапросДанные.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЕСТЬNULL(СУММА(ПартииТоваровНаСкладахМеждународныйУчетОстатки.КоличествоОстаток), 0) КАК Количество
	|ИЗ
	|	РегистрНакопления.ПартииТоваровНаСкладахМеждународныйУчет.Остатки(
	|			&НачалоМесяца,
	|			Организация = &Организация
	|				И Номенклатура = &Номенклатура
	|				И ХарактеристикаНоменклатуры = &ХарактеристикаНоменклатуры) КАК ПартииТоваровНаСкладахМеждународныйУчетОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЕСТЬNULL(СУММА(ис_ПериодВыпускаРеализованнойПродукцииОбороты.КоличествоОборот), 0) КАК Количество
	|ИЗ
	|	РегистрНакопления.ис_ПериодВыпускаРеализованнойПродукции.Обороты(
	|			&НачалоМесяца,
	|			&ОкончаниеПоследнегоМесяца,
	|			Период,
	|			Организация = &Организация
	|				И Номенклатура = &Номенклатура
	|				И ХарактеристикаНоменклатуры = &ХарактеристикаНоменклатуры
	|				И МесяцВыпуска < &НачалоМесяца) КАК ис_ПериодВыпускаРеализованнойПродукцииОбороты";
	лЗапросДанные.УстановитьПараметр("ОкончаниеПоследнегоМесяца", МесяцРеализации-1);
	
	лЗапросСведения = Новый Запрос;
	лЗапросСведения.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПродажиОбороты.Номенклатура КАК Номенклатура,
	|	ПродажиОбороты.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ПродажиОбороты.ДокументПродажи.МоментВремени КАК МоментВремени,
	|	ПродажиОбороты.ДокументПродажи КАК ДокументРеализации,
	|	ПродажиОбороты.Контрагент,
	|	ПродажиОбороты.ДоговорКонтрагента КАК Договор,
	|	СУММА(ПродажиОбороты.КоличествоОборот) КАК Количество
	|ПОМЕСТИТЬ втПродажи
	|ИЗ
	|	РегистрНакопления.Продажи.Обороты(&НачалоПериода, &ОкончаниеПериода, Период, Организация = &Организация) КАК ПродажиОбороты
	|
	|СГРУППИРОВАТЬ ПО
	|	ПродажиОбороты.Номенклатура,
	|	ПродажиОбороты.ХарактеристикаНоменклатуры,
	|	ПродажиОбороты.ДокументПродажи.МоментВремени,
	|	ПродажиОбороты.ДокументПродажи,
	|	ПродажиОбороты.Контрагент,
	|	ПродажиОбороты.ДоговорКонтрагента
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	ХарактеристикаНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	втПродажи.Номенклатура КАК Номенклатура,
	|	втПродажи.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	СУММА(втПродажи.Количество) КАК Количество
	|ИЗ
	|	втПродажи КАК втПродажи
	|
	|СГРУППИРОВАТЬ ПО
	|	втПродажи.Номенклатура,
	|	втПродажи.ХарактеристикаНоменклатуры
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номенклатура,
	|	ХарактеристикаНоменклатуры
	|АВТОУПОРЯДОЧИВАНИЕ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	втПродажи.Номенклатура КАК Номенклатура,
	|	втПродажи.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	втПродажи.МоментВремени КАК МоментВремени,
	|	втПродажи.ДокументРеализации КАК ДокументРеализации,
	|	втПродажи.Контрагент,
	|	втПродажи.Договор,
	|	втПродажи.Количество
	|ИЗ
	|	втПродажи КАК втПродажи
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номенклатура,
	|	ХарактеристикаНоменклатуры,
	|	МоментВремени,
	|	ДокументРеализации
	|АВТОУПОРЯДОЧИВАНИЕ";
	лЗапросСведения.УстановитьПараметр("Организация", Организация);
	лЗапросСведения.УстановитьПараметр("НачалоПериода", НачалоМесяца(МесяцРеализации));
	лЗапросСведения.УстановитьПараметр("ОкончаниеПериода", КонецМесяца(МесяцРеализации));
	лРезультатыЗапроса = лЗапросСведения.ВыполнитьПакет();
	лПодробныеСведения = лРезультатыЗапроса[2].Выгрузить();
	лВыборкаНоменклатура = лРезультатыЗапроса[1].Выбрать();
	Пока лВыборкаНоменклатура.Следующий() Цикл 
		лНачалоМесяца = МесяцРеализации;
		лИндексМестаВТабЧасти = Сведения.Количество();
		лКоличествоПродаж = лВыборкаНоменклатура.Количество;
		
		лОтбор = Новый Структура("Номенклатура, ХарактеристикаНоменклатуры");
		ЗаполнитьЗначенияСвойств(лОтбор, лВыборкаНоменклатура);
		лСтрокиПодробныхСведений = лПодробныеСведения.НайтиСтроки(лОтбор);
				
		// Получение данных по свободным остаткам и фиксация непоместившихся в остатках документов выпуском текущего месяца
		Пока лКоличествоПродаж > 0 Цикл  
			// Получение остатков с учетом сведенией о выпусках прошлых месяцев
			лКоличествоОстаток = 0;
			лЗапросДанные.УстановитьПараметр("Организация", Организация);
			лЗапросДанные.УстановитьПараметр("Номенклатура", лВыборкаНоменклатура.Номенклатура);
			лЗапросДанные.УстановитьПараметр("ХарактеристикаНоменклатуры", лВыборкаНоменклатура.ХарактеристикаНоменклатуры);
			лЗапросДанные.УстановитьПараметр("НачалоМесяца", лНачалоМесяца);
			лРезультатыЗапроса = лЗапросДанные.ВыполнитьПакет();
			лВыборка = лРезультатыЗапроса[0].Выбрать(); // Остатки по складам
			Если лВыборка.Следующий() Тогда 
				лКоличествоОстаток = лВыборка.Количество;
			КонецЕсли;
			лВыборка = лРезультатыЗапроса[1].Выбрать(); // Сведения о выпусках прошлых месяцев
			Если лВыборка.Следующий() Тогда 
				лКоличествоОстаток = лКоличествоОстаток - лВыборка.Количество;
			КонецЕсли;
			Если лКоличествоОстаток <= 0 Тогда 
				лКоличествоОстаток = 0;
			КонецЕсли;
			// Фиксация непоместившихся в остатках документов выпуском текущего месяца
			Если лКоличествоПродаж > лКоличествоОстаток Тогда 
				лКоличествоВыпускаМесяца = лКоличествоПродаж - лКоличествоОстаток;
				Пока лКоличествоВыпускаМесяца > 0 Цикл 
					лИндексСтроки = лСтрокиПодробныхСведений.Количество()-1;
					лСтрокаПодробныхСведений = лСтрокиПодробныхСведений[лИндексСтроки];
					лКоличество = Мин(лКоличествоВыпускаМесяца, лСтрокаПодробныхСведений.Количество);
					лСтрокаСведений = Сведения.Добавить();
					ЗаполнитьЗначенияСвойств(лСтрокаСведений, лСтрокаПодробныхСведений);
					лСтрокаСведений.МесяцВыпуска = лНачалоМесяца;
					лСтрокаСведений.Количество = лКоличество;
					лСтрокаПодробныхСведений.Количество = лСтрокаПодробныхСведений.Количество - лКоличество;
					Если лСтрокаПодробныхСведений.Количество = 0 Тогда 
						лСтрокиПодробныхСведений.Удалить(лИндексСтроки);
					КонецЕсли;
					лКоличествоВыпускаМесяца = лКоличествоВыпускаМесяца - лКоличество;
					лКоличествоПродаж = лКоличествоПродаж - лКоличество;
				КонецЦикла;
			КонецЕсли;
			// Итерация на предыдущем месяце 
			лНачалоМесяца = НачалоМесяца(лНачалоМесяца-1);
		КонецЦикла; // По месяцам
		
		// Остатки без месяцев выпуска (заглушка на всякий случай)
		Для Каждого лСтрокаПодробныхСведений Из лСтрокиПодробныхСведений Цикл 
			лСтрокаСведений = Сведения.Добавить();
			ЗаполнитьЗначенияСвойств(лСтрокаСведений, лСтрокаПодробныхСведений);
		КонецЦикла;
		
	КонецЦикла; // По номенклатуре
	
КонецПроцедуры


Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда 
		Возврат;
	КонецЕсли;
	
	лЗапрос = Новый Запрос;
	лЗапрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ис_СведенияОПериодеВыпускаРеализованнойПродукции.Ссылка КАК СведенияОПериодеВыпуска
	|ИЗ
	|	Документ.ис_СведенияОПериодеВыпускаРеализованнойПродукции КАК ис_СведенияОПериодеВыпускаРеализованнойПродукции
	|ГДЕ
	|	ис_СведенияОПериодеВыпускаРеализованнойПродукции.ПометкаУдаления = ЛОЖЬ
	|	И ис_СведенияОПериодеВыпускаРеализованнойПродукции.Организация = &Организация
	|	И ис_СведенияОПериодеВыпускаРеализованнойПродукции.МесяцРеализации = &МесяцРеализации
	|	И ис_СведенияОПериодеВыпускаРеализованнойПродукции.Ссылка <> &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	СведенияОПериодеВыпуска
	|АВТОУПОРЯДОЧИВАНИЕ";
	лЗапрос.УстановитьПараметр("Ссылка", Ссылка);
	лЗапрос.УстановитьПараметр("Организация", Организация);
	лЗапрос.УстановитьПараметр("МесяцРеализации", МесяцРеализации);
	лВыборка = лЗапрос.Выполнить().Выбрать();
	Пока лВыборка.Следующий() Цикл 
		Отказ = Истина;
		Сообщить("Для организации <"+Организация+"> и месяца реализации <"+Формат(МесяцРеализации, "ДФ=MMMM.yyyy")+"> уже существует документ: "+лВыборка.СведенияОПериодеВыпуска, СтатусСообщения.Внимание);
	КонецЦикла;
	
	лЗапрос = Новый Запрос;
	лЗапрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	МИНИМУМ(ГраницыЗапретаИзмененияДанных.ГраницаЗапретаИзменений) КАК ГраницаЗапретаИзменений
	|ИЗ
	|	РегистрСведений.ГраницыЗапретаИзмененияДанных КАК ГраницыЗапретаИзмененияДанных
	|ГДЕ
	|	(ГраницыЗапретаИзмененияДанных.Организация = &Организация
	|			ИЛИ ГраницыЗапретаИзмененияДанных.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))";
	лЗапрос.УстановитьПараметр("Организация", Организация);
	лВыборка = лЗапрос.Выполнить().Выбрать();
	Если лВыборка.Следующий() 
	 И ЗначениеЗаполнено(лВыборка.ГраницаЗапретаИзменений) Тогда 
	 	Если КонецДня(лВыборка.ГраницаЗапретаИзменений) < КонецМесяца(МесяцРеализации) Тогда 
		 	Сообщить("Период <"+Формат(МесяцРеализации, "ДФ=MMMM.yyyy")+"> еще не закрыт. Данные могут быть изменены", СтатусСообщения.Внимание);
		КонецЕсли; 
	Иначе
		Сообщить("Не установлен период запрета изменений. Данные могут быть изменены", СтатусСообщения.Внимание);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.ис_ПериодВыпускаРеализованнойПродукции.Очистить();
	
	лЗапрос = Новый Запрос;
	лЗапрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ис_СведенияОПериодеВыпускаРеализованнойПродукции.МесяцРеализации КАК Период,
	|	ис_СведенияОПериодеВыпускаРеализованнойПродукции.Организация,
	|	ис_СведенияОПериодеВыпускаРеализованнойПродукцииСведения.Номенклатура,
	|	ис_СведенияОПериодеВыпускаРеализованнойПродукцииСведения.ХарактеристикаНоменклатуры,
	|	ис_СведенияОПериодеВыпускаРеализованнойПродукцииСведения.ДокументРеализации,
	|	ис_СведенияОПериодеВыпускаРеализованнойПродукцииСведения.Контрагент,
	|	ис_СведенияОПериодеВыпускаРеализованнойПродукцииСведения.Договор,
	|	ис_СведенияОПериодеВыпускаРеализованнойПродукцииСведения.МесяцВыпуска,
	|	СУММА(ис_СведенияОПериодеВыпускаРеализованнойПродукцииСведения.Количество) КАК Количество
	|ИЗ
	|	Документ.ис_СведенияОПериодеВыпускаРеализованнойПродукции КАК ис_СведенияОПериодеВыпускаРеализованнойПродукции
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ис_СведенияОПериодеВыпускаРеализованнойПродукции.Сведения КАК ис_СведенияОПериодеВыпускаРеализованнойПродукцииСведения
	|		ПО ис_СведенияОПериодеВыпускаРеализованнойПродукции.Ссылка = ис_СведенияОПериодеВыпускаРеализованнойПродукцииСведения.Ссылка
	|ГДЕ
	|	ис_СведенияОПериодеВыпускаРеализованнойПродукции.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ис_СведенияОПериодеВыпускаРеализованнойПродукции.МесяцРеализации,
	|	ис_СведенияОПериодеВыпускаРеализованнойПродукции.Организация,
	|	ис_СведенияОПериодеВыпускаРеализованнойПродукцииСведения.Номенклатура,
	|	ис_СведенияОПериодеВыпускаРеализованнойПродукцииСведения.ХарактеристикаНоменклатуры,
	|	ис_СведенияОПериодеВыпускаРеализованнойПродукцииСведения.ДокументРеализации,
	|	ис_СведенияОПериодеВыпускаРеализованнойПродукцииСведения.Контрагент,
	|	ис_СведенияОПериодеВыпускаРеализованнойПродукцииСведения.Договор,
	|	ис_СведенияОПериодеВыпускаРеализованнойПродукцииСведения.МесяцВыпуска";
	лЗапрос.УстановитьПараметр("Ссылка", Ссылка);
	Движения.ис_ПериодВыпускаРеализованнойПродукции.Загрузить(лЗапрос.Выполнить().Выгрузить());
	
КонецПроцедуры

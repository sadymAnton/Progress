// Заполнение порядка списания НИОКР
//
Функция ЗаполнитьСпособПризнанияНИОКР() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|   ПринятиеКУчетуНМА.Ссылка КАК Док,
	|   ПринятиеКУчетуНМА.НачислятьАмортизациюНУ КАК АмортизацияНУ
	|ИЗ
	|   Документ.ПринятиеКУчетуНМА КАК ПринятиеКУчетуНМА
	|
	|СГРУППИРОВАТЬ ПО
	|   ПринятиеКУчетуНМА.НачислятьАмортизациюНУ,
	|   ПринятиеКУчетуНМА.Ссылка";
	
	Результат = Запрос.Выполнить();
	Выборка   = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ТекущийДокумент = Выборка.Док.ПолучитьОбъект();
		ТекущийДокумент.ПорядокСписанияНИОКРНаРасходыНУ =
			?(Выборка.АмортизацияНУ, Перечисления.ПорядокСписанияНИОКРНУ.Равномерно, Перечисления.ПорядокСписанияНИОКРНУ.НеСписывать);
		
		Попытка
			ТекущийДокумент.Записать(РежимЗаписиДокумента.Запись);
		Исключение
			ШаблонОшибки = НСтр("ru = 'Не удалось записать документ %1'");
			ТекстОшибки  = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонОшибки, ТекущийДокумент);
			ОбщегоНазначения.СообщитьОбОшибке(ТекстОшибки);
		КонецПопытки;
	КонецЦикла;
	
    Документы.УстановкаСоответствияСчетовБУиНУ.СоздатьНовыеСоответствияСчетовБУиНУ("ДляВидаРасходовНИОКРПоПеречнюПравительстваРФ");
	
КонецФункции


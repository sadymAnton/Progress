Перем мУдалятьДвижения;

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

Перем мСкопирован Экспорт; // хранит признак копирования документа

// Строки, хранят реквизиты имеющие смысл только для бух. учета и упр. соответственно
// в случае если документ не отражается в каком-то виде учета, делаются невидимыми
Перем мСтрокаРеквизитыБухУчета Экспорт; // (Регл)
Перем мСтрокаРеквизитыУпрУчета Экспорт; // (Упр)
Перем мСтрокаРеквизитыНалУчета Экспорт; // (Регл)
Перем ТабЗатрат;
Перем ПроводкиНУ;

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОКУМЕНТА

//Бирюков заявка № 31480
Функция ПолучитьСведенияОРуководителеПодразделения(ПодразделениеОрганизации, ДатаСреза)
	Отбор = Новый Структура;
	Отбор.Вставить("СтруктурнаяЕдиница", ПодразделениеОрганизации);
	Отбор.Вставить("ОтветственноеЛицо", Перечисления.ОтветственныеЛицаОрганизаций.Руководитель);
	
	Результат = Новый Структура("Должность, ФИО");
	
	СрезПоследних   = РегистрыСведений.ОтветственныеЛицаОрганизаций.СрезПоследних(ДатаСреза, Отбор);
	
	Если СрезПоследних.Количество() = 0 Тогда
		Возврат Результат;
	КонецЕсли;		
		
	Если ЗначениеЗаполнено(СрезПоследних[0].ФизическоеЛицо) Тогда
		Результат.Вставить("ФИО", ОбщегоНазначенияЗК.ФамилияИнициалыФизЛица(СрезПоследних[0].ФизическоеЛицо));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СрезПоследних[0].Должность) Тогда
		Результат.Вставить("Должность", СрезПоследних[0].Должность.Наименование);
	КонецЕсли;
	
	Возврат Результат;
КонецФункции // ПолучитьСведенияОРуководителеПодразделения(ВыборкаОС.ПодразделениеОрганизации)()
//%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

#Если Клиент Тогда

// Функция возращает параметры ОС
//
Функция ПолучитьПробегАвто( ОбъектОС,  НачДата, КонДата, ПечатьПоДаннымУпрУчета = Истина)
	
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ВыработкаОС.ОсновноеСредство КАК ОбъектОС,
	|	ВыработкаОС.ПараметрВыработки,
	|	ВыработкаОС.КоличествоОборот КАК Пробег
	|ИЗ
	|	РегистрСведений.ПервоначальныеСведенияОС.СрезПоследних(&КонДата, ОсновноеСредство = &ОС) КАК РегСведенияОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ВыработкаОС.Обороты(&НачГраница, &КонГраница, , ОсновноеСредство = &ОС) КАК ВыработкаОС
	|		ПО РегСведенияОС.ОсновноеСредство = ВыработкаОС.ОсновноеСредство И РегСведенияОС.ПараметрВыработки = ВыработкаОС.ПараметрВыработки";
	
	Если НЕ ПечатьПоДаннымУпрУчета тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"ПервоначальныеСведенияОС","ПервоначальныеСведенияОСБухгалтерскийУчет");
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	
	Запрос.УстановитьПараметр("КонДата", КонДата);
	Запрос.УстановитьПараметр("НачГраница", Новый Граница(НачДата, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("КонГраница", Новый Граница(КонДата, ВидГраницы.Включая));
	
	Запрос.УстановитьПараметр("ОС", ОбъектОС);
	
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	РезультатЗапроса.Следующий();
	
	Возврат РезультатЗапроса.Пробег;
	
КонецФункции // ПолучитьПробегАвто()

// Функция формирует табличный документ с печатной формой ОС4
// Возвращаемое значение:
//  Табличный документ - печатная форма накладной
//
Функция ПечатьСписаниеОС(ПечатьПоДаннымУпрУчета = Истина)

	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СписаниеОС_ОС4";

	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	СписаниеОС.Номер						  КАК НомерДок,
	|	СписаниеОС.Дата							  КАК ДатаДок,
	|	СписаниеОС.Событие						  КАК Событие,
	|	СписаниеОС.ПричинаСписания 				  КАК ПричинаСписания,
	//Бирюков заявка № 31480
	|	СписаниеОС.Комментарий 				  	  КАК Установлено,
	//%%%%%%%%%%%%%%%%%%%%%%%%%%%
	|	СписаниеОС.Организация   				  КАК Организация,
	|	СписаниеОС.Организация.КодПоОКПО		  КАК КодПоОКПО,
	|	СписаниеОС.Организация.НаименованиеПолное КАК НаимОрганизация
	|ИЗ
	|	Документ.СписаниеОС КАК СписаниеОС
	|
	|ГДЕ
	|	СписаниеОС.Ссылка = &Ссылка";
	СтруктураДока = Запрос.Выполнить().Выбрать();
	СтруктураДока.Следующий();
	
	СведенияОРуководителеГлавбухе = УправлениеВнеоборотнымиАктивами.ПолучитьСведенияОРуководителеГлавБухе(СтруктураДока.Организация,СтруктураДока.ДатаДок);
	
	// определимся с переменными, зависящими от типа учета
	НазваниеРегистраПервоначальныхСведений = "ПервоначальныеСведенияОС";
	НазваниеРегистраМестонахождения = "МестонахождениеОС";
	
	Запрос = Новый Запрос();
	
	Если ПечатьПоДаннымУпрУчета тогда
		кВидуУчета			= "управленческому";
		cВидаУчета 	 	  	= "управленческого";
		НазваниеАмортизации = "Амортизация";
		НазваниеАмортизацииЗаМесяц = "АмортизацияЗаМесяц";
		НазваниеСтоимости		   = "Стоимость";	
		ВалютаПечати = глЗначениеПеременной("ВалютаУправленческогоУчета");
		СоединениеПоСчетам = "";
		СтрокиСчетов	   = "";
	Иначе
		кВидуУчета 		 	= "бухгалтерскому";
		cВидаУчета 		 	= "бухгалтерского";
		ВалютаПечати 	 	= глЗначениеПеременной("ВалютаРегламентированногоУчета");
		НазваниеАмортизации = "АмортизацияБУ";
		НазваниеАмортизацииЗаМесяц = "АмортизацияЗаМесяцБУ";
		НазваниеСтоимости		   = "СтоимостьБУ";	
		НазваниеРегистраПервоначальныхСведений = НазваниеРегистраПервоначальныхСведений+"БухгалтерскийУчет";
		НазваниеРегистраМестонахождения        = НазваниеРегистраМестонахождения+"БухгалтерскийУчет";
		СтрокиСчетов	   = "	СчетаБухгалтерскогоУчетаОС.СчетУчета КАК СубСчет,
							  |	СчетаБухгалтерскогоУчетаОС.СчетНачисленияАмортизации,";
		СоединениеПоСчетам = "		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СчетаБухгалтерскогоУчетаОС.СрезПоследних(&Дата,
							 |Организация = &Организация) КАК СчетаБухгалтерскогоУчетаОС
							 |		ПО СписаниеОСОС.ОсновноеСредство = СчетаБухгалтерскогоУчетаОС.ОсновноеСредство";
		Запрос.УстановитьПараметр("Организация",СтруктураДока.Организация);
		Запрос.УстановитьПараметр("Дата",СтруктураДока.ДатаДок);

	КонецЕсли;	

	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СписаниеОСОС.НомерСтроки                                 КАК НП,
	|	СписаниеОСОС.ОсновноеСредство                            КАК ОС,
	|	СписаниеОСОС.ОсновноеСредство.Код                        КАК ИнвНомерУпр,
	|	СписаниеОСОС.ОсновноеСредство.ЗаводскойНомер             КАК ЗаводскойНомер,
	|	СписаниеОСОС.ОсновноеСредство.Автотранспорт              КАК Автотранспорт,
	|	СписаниеОСОС.ОсновноеСредство.ДатаВыпуска                КАК ГодВыпуска,
	|	СписаниеОСОС.ОсновноеСредство.НаименованиеПолное         КАК НаименованиеОС,
	|	СписаниеОСОС.ОсновноеСредство.ГруппаОС                   КАК Группа,
	//Бирюков заявка № 31480
	|	СписаниеОСОС.ПодразделениеОрганизации	 				 КАК ПодразделениеОрганизации,
	//%%%%%%%%%%%%%%%%%%%%%%%%%%
	|	ЕСТЬNULL(СписаниеОСОС."+НазваниеАмортизации+", 0)        КАК НачАмортизация,
	|	ЕСТЬNULL(СписаниеОСОС."+НазваниеСтоимости+", 0)          КАК Стоимость,
	|	ЕСТЬNULL(СписаниеОСОС."+НазваниеАмортизацииЗаМесяц+", 0) КАК АмортизацияЗаМесяц,
	|	ЕСТЬNULL(СписаниеОСОС.СписаноНаЗатраты, ЛОЖЬ)            КАК СписаноНаЗатраты,
	|	"+СтрокиСчетов+"
	|	ЕСТЬNULL(ПервоначальныеСведенияОбОсновныхСредствахСрезПоследних.ПервоначальнаяСтоимость, 0) КАК НачСтоимость,
	|	МестонахождениеОсновныхСредствСрезПоследних.МОЛ,
	|	МестонахождениеОсновныхСредствСрезПоследних.Местонахождение КАК НаимПодразделение,
	|	МестонахождениеОсновныхСредствСрезПоследних.МОЛ.Код КАК ТабНомерМОЛ
	|ИЗ
	|	Документ.СписаниеОС.ОС КАК СписаниеОСОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений."+НазваниеРегистраПервоначальныхСведений+".СрезПоследних КАК ПервоначальныеСведенияОбОсновныхСредствахСрезПоследних
	|		ПО СписаниеОСОС.ОсновноеСредство = ПервоначальныеСведенияОбОсновныхСредствахСрезПоследних.ОсновноеСредство
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений."+НазваниеРегистраМестонахождения+".СрезПоследних КАК МестонахождениеОсновныхСредствСрезПоследних
	|		ПО СписаниеОСОС.ОсновноеСредство = МестонахождениеОсновныхСредствСрезПоследних.ОсновноеСредство
	|		"+СоединениеПоСчетам+"
	|ГДЕ
	|	СписаниеОСОС.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	СписаниеОСОС.НомерСтроки";
	РезультатЗапросаПоОС = Запрос.Выполнить();
	
	Если РезультатЗапросаПоОС.Пустой() тогда
		// скорее всего в табличной части документа нет ни строчки
		// или нет записей в регистре
		Возврат Неопределено;
	КонецЕсли;
	
	ВыборкаОС = РезультатЗапросаПоОС.Выбрать();

	Если ВыборкаОС.Количество() = 1 Тогда

		ВыборкаОС.Следующий();

		Если ВыборкаОС.Автотранспорт Тогда 
			Макет = ПолучитьМакет("ОС4а");
		Иначе
			Макет = ПолучитьМакет("ОС4");
		КонецЕсли;

		Область = Макет.ПолучитьОбласть("Заголовок");
		Область.Параметры.Заполнить(СтруктураДока);
		Область.Параметры.Заполнить(ВыборкаОС);
		Область.Параметры.Заполнить(СведенияОРуководителеГлавбухе);
		//Бирюков заявка № 31480
		Область.Параметры.Заполнить(ПолучитьСведенияОРуководителеПодразделения(ВыборкаОС.ПодразделениеОрганизации, СтруктураДока.ДатаДок));
		Область.Параметры.ЗаводскойНомер = СокрЛП(ВыборкаОС.ЗаводскойНомер);
		//%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
		Область.Параметры.Валюта 	 = ВалютаПечати;
		Область.Параметры.кВидуУчета = кВидуУчета;
		Область.Параметры.cВидаУчета = cВидаУчета;

		ДокументПринятияКУчету     = "";
		ДокументВводаВЭксплуатацию = "";
		ПринятоКУчету              = "";
		ВведеноВЭксплуатацию       = "";

		// получим состояния ос 
		СтруктПараметров = УправлениеВнеоборотнымиАктивами.ПолучитьАтрибутыСостоянияОС(ВыборкаОС.ОС, Перечисления.СостоянияОС.ВведеноВЭксплуатацию,
														ПечатьПоДаннымУпрУчета,СтруктураДока.Организация);
		ДокументВводаВЭксплуатацию = СтруктПараметров["ИмяДок"];
		ВведеноВЭксплуатацию	   = СтруктПараметров["Дата"];

		СтруктПараметров = УправлениеВнеоборотнымиАктивами.ПолучитьАтрибутыСостоянияОС(ВыборкаОС.ОС, Перечисления.СостоянияОС.ПринятоКУчету,
														ПечатьПоДаннымУпрУчета,СтруктураДока.Организация);
		ДокументПринятияКУчету  = СтруктПараметров["ИмяДок"];
		ПринятоКУчету		  	= СтруктПараметров["Дата"];
		
		Если ВыборкаОС.Автотранспорт Тогда
			Область.Параметры.ВведеноВЭксплуатацию = ВведеноВЭксплуатацию;
			Если ДокументВводаВЭксплуатацию = Неопределено или ВведеноВЭксплуатацию = Неопределено Тогда
				Область.Параметры.Пробег = ПолучитьПробегАвто(ВыборкаОС.ОС, Дата, Дата, ПечатьПоДаннымУпрУчета);
			Иначе
				Область.Параметры.Пробег = ПолучитьПробегАвто(ВыборкаОС.ОС, ВведеноВЭксплуатацию, Дата, ПечатьПоДаннымУпрУчета);
			КонецЕсли;
		Иначе
			Область.Параметры.СрокЭкспл  = ?(ЗначениеЗаполнено(ВведеноВЭксплуатацию),
  			                                 УправлениеВнеоборотнымиАктивами.ОпределитьФактическийСрокИспользования(ВведеноВЭксплуатацию, Дата),
								             0); 
		КонецЕсли;

		СтоимостьОС   = ?(ВыборкаОС.СписаноНаЗатраты, ВыборкаОС.НачСтоимость, ВыборкаОС.Стоимость);
		АмортизацияОС = ?(ВыборкаОС.СписаноНаЗатраты, 0, ВыборкаОС.НачАмортизация + ВыборкаОС.АмортизацияЗаМесяц);
		
		Область.Параметры.ПринятоКУчету  = ПринятоКУчету;
		Область.Параметры.НачСтоимость   = СтоимостьОС;
		Область.Параметры.НачАмортизация = АмортизацияОС;
		Область.Параметры.ОстСтоимость   = ?(ВыборкаОС.СписаноНаЗатраты, 0, СтоимостьОС - АмортизацияОС);
		
		Если ПечатьПоДаннымУпрУчета тогда
			Область.Параметры.ИнвНомер = ВыборкаОС.ИнвНомерУпр;
		Иначе
			ВыборкаЗаписей = РегистрыСведений.ПервоначальныеСведенияОСБухгалтерскийУчет.ПолучитьПоследнее(СтруктураДока.ДатаДок,
											Новый Структура("ОсновноеСредство",ВыборкаОС.ОС));
			ИнвНомерБух    = ?(ВыборкаЗаписей.Количество() > 0,ВыборкаЗаписей.ИнвентарныйНомер,0);
			Область.Параметры.ИнвНомер = ИнвНомерБух;
		КонецЕсли;
		
		Если ПустаяСтрока(ВыборкаОС.НаименованиеОС) Тогда
			Область.Параметры.НаименованиеОС = СокрЛП(ВыборкаОС.ОС);
		КонецЕсли; 

		ТабДокумент.Вывести(Область);

	Иначе

		Макет  = ПолучитьМакет("ОС4б");

		Шапка = Макет.ПолучитьОбласть("Шапка0");
		Шапка.Параметры.Заполнить(СтруктураДока);
		Шапка.Параметры.Заполнить(СведенияОРуководителеГлавбухе);
		Шапка.Параметры.cВидаУчета = cВидаУчета;
		ТабДокумент.Вывести(Шапка);

		Шапка = Макет.ПолучитьОбласть("Шапка1");
		Шапка.Параметры.Заполнить(СтруктураДока);
		Шапка.Параметры.Валюта = ВалютаПечати;

		ТабДокумент.Вывести(Шапка);

		Строка1 = Макет.ПолучитьОбласть("Строка1");

		Пока ВыборкаОС.Следующий() Цикл

			Строка1.Параметры.Заполнить(ВыборкаОС);
			Строка1.Параметры.Заполнить(СтруктураДока);
			
			СтоимостьОС   = ?(ВыборкаОС.СписаноНаЗатраты, ВыборкаОС.НачСтоимость, ВыборкаОС.Стоимость);
			АмортизацияОС = ?(ВыборкаОС.СписаноНаЗатраты, 0, ВыборкаОС.НачАмортизация + ВыборкаОС.АмортизацияЗаМесяц);
			
			Строка1.Параметры.НачСтоимость   = СтоимостьОС;
			Строка1.Параметры.НачАмортизация = АмортизацияОС;
			Строка1.Параметры.ОстСтоимость   = ?(ВыборкаОС.СписаноНаЗатраты, 0, СтоимостьОС - АмортизацияОС);

			ДокументПринятияКУчету     = "";
			ДокументВводаВЭксплуатацию = "";
			ПринятоКУчету              = "";
			ВведеноВЭксплуатацию       = "";

			// получим состояния ос 
			СтруктПараметров = УправлениеВнеоборотнымиАктивами.ПолучитьАтрибутыСостоянияОС(ВыборкаОС.ОС, Перечисления.СостоянияОС.ВведеноВЭксплуатацию,
			ПечатьПоДаннымУпрУчета,СтруктураДока.Организация);
			ДокументВводаВЭксплуатацию = СтруктПараметров["ИмяДок"];
			ВведеноВЭксплуатацию	   = СтруктПараметров["Дата"];
			
			СтруктПараметров = УправлениеВнеоборотнымиАктивами.ПолучитьАтрибутыСостоянияОС(ВыборкаОС.ОС, Перечисления.СостоянияОС.ПринятоКУчету,
			ПечатьПоДаннымУпрУчета,СтруктураДока.Организация);
			ДокументПринятияКУчету  = СтруктПараметров["ИмяДок"];
			ПринятоКУчету		  	= СтруктПараметров["Дата"];

			Строка1.Параметры.СрокЭкспл  = ?(ЗначениеЗаполнено(ВведеноВЭксплуатацию),
  			                                 УправлениеВнеоборотнымиАктивами.ОпределитьФактическийСрокИспользования(ВведеноВЭксплуатацию, Дата),
								             0); 
			
			Если ПечатьПоДаннымУпрУчета тогда
				Строка1.Параметры.ИнвНомер = ВыборкаОС.ИнвНомерУпр;
			Иначе
				ВыборкаЗаписей = РегистрыСведений.ПервоначальныеСведенияОСБухгалтерскийУчет.ПолучитьПоследнее(СтруктураДока.ДатаДок,
															Новый Структура("ОсновноеСредство",ВыборкаОС.ОС));
				ИнвНомерБух    = ?(ВыборкаЗаписей.Количество() > 0,ВыборкаЗаписей.ИнвентарныйНомер,0);
				Строка1.Параметры.ИнвНомер = ИнвНомерБух;
			КонецЕсли;
			
			Если ПустаяСтрока(ВыборкаОС.НаименованиеОС) Тогда
				Строка1.Параметры.НаименованиеОС = СокрЛП(ВыборкаОС.ОС);
			КонецЕсли; 
			
			Строка1.Параметры.Причина = СтруктураДока.ПричинаСписания;

			ТабДокумент.Вывести(Строка1);

		КонецЦикла;

		Шапка = Макет.ПолучитьОбласть("Шапка2");
		Шапка.Параметры.Заполнить(СтруктураДока);
		Шапка.Параметры.Валюта = ВалютаПечати;
		Шапка.Параметры.Заполнить(СведенияОРуководителеГлавбухе);
		ТабДокумент.Вывести(Шапка);

	КонецЕсли;
	
	ТабДокумент.АвтоМасштаб = Истина;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ТабДокумент.ПолеСверху = 0;
	ТабДокумент.ПолеСнизу = 0;

	Возврат ТабДокумент;

КонецФункции // ПечатьСписаниеТоваров()

// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходимое количество копий.
//
//  Название макета печати передается в качестве параметра,
// по переданному названию находим имя макета в соответствии.
//
// Параметры:
//  НазваниеМакета - строка, название макета.
//
Процедура Печать(ИмяМакета, КоличествоЭкземпляров = 1, НаПринтер = Ложь) Экспорт

	Если ЭтоНовый() Тогда
		Предупреждение("Документ можно распечатать только после его записи");
		Возврат;
	ИначеЕсли Не УправлениеДопПравамиПользователей.РазрешитьПечатьНепроведенныхДокументов(Проведен) Тогда
		Предупреждение("Недостаточно полномочий для печати непроведенного документа!");
		Возврат;
	КонецЕсли;

	Если Не РаботаСДиалогами.ПроверитьМодифицированность(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;

	// Получить экземпляр документа на печать
	Если ИмяМакета = "ОС4упр" тогда
		ТабДокумент = ПечатьСписаниеОС();
	ИначеЕсли ИмяМакета = "ОС4бух" тогда
		ТабДокумент = ПечатьСписаниеОС(Ложь);
	ИначеЕсли ТипЗнч(ИмяМакета) = Тип("ДвоичныеДанные") Тогда

		ТабДокумент = УниверсальныеМеханизмы.НапечататьВнешнююФорму(Ссылка, ИмяМакета);
		
		Если ТабДокумент = Неопределено Тогда
			Возврат
		КонецЕсли; 
	
	КонецЕсли;

	УниверсальныеМеханизмы.НапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер, ОбщегоНазначения.СформироватьЗаголовокДокумента(ЭтотОбъект, ЭтотОбъект.Метаданные().Представление()), Ссылка);

КонецПроцедуры // Печать

// Возвращает доступные варианты печати документа
//
// Возвращаемое значение:
//  Структура, каждая строка которой соответствует одному из вариантов печати
//  
Функция ПолучитьСтруктуруПечатныхФорм() Экспорт
	
	СтруктураМакетов = Новый Структура;

	СтруктураМакетов.Вставить("ОС4упр","Форма ОС-4 (упр. учет)");
	СтруктураМакетов.Вставить("ОС4бух","Форма ОС-4 (бух. учет)");

	Возврат СтруктураМакетов;
	
КонецФункции // ПолучитьСтруктуруПечатныхФорм()

#КонецЕсли

// Процедура заполняет структуры именами реквизитов, которые имеют смысл
// только для определенного вида учета
//
Процедура ЗаполнитьСписокРеквизитовЗависимыхОтТиповУчета() Экспорт

	ЗаполнитьСписокРеквизитовЗависимыхОтТиповУчетаУпр();
	ЗаполнитьСписокРеквизитовЗависимыхОтТиповУчетаРегл();

КонецПроцедуры // ЗаполнитьСписокРеквизитовЗависимыхОтТиповУчета()

// Процедура заполняет структуры именами реквизитов, которые имеют смысл
// только для управленческого учета
//
Процедура ЗаполнитьСписокРеквизитовЗависимыхОтТиповУчетаУпр()

	мСтрокаРеквизитыУпрУчета = "Событие,ОС.Стоимость, ОС.Амортизация, ОС.АмортизацияЗаМесяц, ОС.ОстСтоимость, ОС.СписаноНаЗатраты";

КонецПроцедуры // ЗаполнитьСписокРеквизитовЗависимыхОтТиповУчетаУпр()

// Процедура заполняет структуры именами реквизитов, которые имеют смысл
// только для регламентированного учета
//
Процедура ЗаполнитьСписокРеквизитовЗависимыхОтТиповУчетаРегл()

	мСтрокаРеквизитыБухУчета = "СобытиеРегл,СчетСписанияБУ, СубконтоБУ, ОС.СтоимостьБУ, ОС.АмортизацияБУ, ОС.АмортизацияЗаМесяцБУ, ОС.ОстСтоимостьБУ, ОС.СписаноНаЗатратыБУ";
	мСтрокаРеквизитыНалУчета = "СчетСписанияНУ, СубконтоНУ, ОС.СтоимостьНУ, ОС.АмортизацияНУ, ОС.АмортизацияЗаМесяцНУ, ОС.ОстСтоимостьНУ, ОС.СуммаКапитальныхВложенийВключаемыхВРасходыНУ";

КонецПроцедуры // ЗаполнитьСписокРеквизитовЗависимыхОтТиповУчетаРегл()

///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

// Дополняет список обязательных параметров шапки
// упр. параметрами
Процедура ДополнитьОбязательныеРеквизитыШапкиУпр(Реквизиты)

	Реквизиты = Реквизиты + ?(ПустаяСтрока(Реквизиты), "", ", ") + "Событие";
	
КонецПроцедуры

// Дополняет список обязательных параметров шапки
// регл. параметрами
Процедура ДополнитьОбязательныеРеквизитыШапкиРегл(Реквизиты)

	Реквизиты = Реквизиты + ?(ПустаяСтрока(Реквизиты), "", ", ") + "СчетСписанияБУ,СчетСписанияНУ,СобытиеРегл";

КонецПроцедуры

// Проверяет правильность заполнения упр. реквизитов шапки
Процедура ПроверитьЗаполнениеШапкиУпр(СтруктураШапкиДокумента, Отказ, Заголовок)

	ПредставлениеРеквизита = ЭтотОбъект.Метаданные().Реквизиты.Событие.Представление();
	//начало изменений
	Если ЧастичнаяЛиквидация Тогда
		УправлениеВнеоборотнымиАктивами.ПроверкаЗаполненияСобытий(СтруктураШапкиДокумента.Событие.ВидСобытияОС,
								  Перечисления.ВидыСобытийОС.ЧастичнаяЛиквидация,
								  ПредставлениеРеквизита,Отказ);
	Иначе
		УправлениеВнеоборотнымиАктивами.ПроверкаЗаполненияСобытий(СтруктураШапкиДокумента.Событие.ВидСобытияОС,
								  Перечисления.ВидыСобытийОС.Списание,
								  ПредставлениеРеквизита,Отказ);								  
	КонецЕсли;								  
	//конец изменений 
	
КонецПроцедуры

// Проверяет правильность заполнения регл. реквизитов шапки
Процедура ПроверитьЗаполнениеШапкиРегл(СтруктураШапкиДокумента, Отказ, Заголовок)

	Если (НЕ СтруктураШапкиДокумента.ОтражатьВБухгалтерскомУчете) И (НЕ СтруктураШапкиДокумента.ОтражатьВНалоговомУчете) Тогда
		Возврат;
	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(СтруктураШапкиДокумента.Организация) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не заполнено поле Организация", Отказ,Заголовок);
	КонецЕсли;
	ПредставлениеРеквизита = ЭтотОбъект.Метаданные().Реквизиты.СобытиеРегл.Представление();
	//начало изменений
	Если ЧастичнаяЛиквидация Тогда
	УправлениеВнеоборотнымиАктивами.ПроверкаЗаполненияСобытий(СтруктураШапкиДокумента.СобытиеРегл.ВидСобытияОС,
								  Перечисления.ВидыСобытийОС.ЧастичнаяЛиквидация,
								  ПредставлениеРеквизита,Отказ);
		
	Иначе	
	УправлениеВнеоборотнымиАктивами.ПроверкаЗаполненияСобытий(СтруктураШапкиДокумента.СобытиеРегл.ВидСобытияОС,
								  Перечисления.ВидыСобытийОС.Списание,
								  ПредставлениеРеквизита,Отказ);
	КонецЕсли;
	//конец изменений								  


КонецПроцедуры

// Проверяет правильность заполнения шапки документа.
// Если какой-то из реквизитов шапки, влияющий на проведение, не заполнен или
// заполнен не корректно, то выставляется флаг отказа в проведении.
// Проверяется также правильность заполнения реквизитов ссылочных полей документа.
// Проверка выполняется по объекту и по выборке из результата запроса по шапке.
//
// Параметры: 
//  СтруктураШапкиДокумента - выборка из результата запроса по шапке документа,
//  Отказ                   - флаг отказа в проведении,
//  Заголовок               - строка, заголовок сообщения об ошибке проведения.
//
Процедура ПроверитьЗаполнениеШапки(СтруктураШапкиДокумента, Отказ, Заголовок)

	// Укажем, что надо проверить:
	ОбязательныеРеквизитыШапки = "Событие";
	ДополнитьОбязательныеРеквизитыШапкиУпр(ОбязательныеРеквизитыШапки);
	ДополнитьОбязательныеРеквизитыШапкиРегл(ОбязательныеРеквизитыШапки);

	УправлениеЗатратами.НепроверятьРеквизитыПоТипуУчета(ЭтотОбъект, ОбязательныеРеквизитыШапки, СтруктураШапкиДокумента, мСтрокаРеквизитыУпрУчета, мСтрокаРеквизитыБухУчета, мСтрокаРеквизитыНалУчета);

	СтруктураОбязательныхПолей = Новый Структура(ОбязательныеРеквизитыШапки);

	// Документ должен принадлежать хотя бы к одному виду учета (управленческий, бухгалтерский, налоговый)
	ОбщегоНазначения.ПроверитьПринадлежностьКВидамУчета(СтруктураШапкиДокумента, Отказ, Заголовок, Истина);

	// Теперь вызовем общую процедуру проверки.
	ЗаполнениеДокументов.ПроверитьЗаполнениеШапкиДокумента(ЭтотОбъект, СтруктураОбязательныхПолей, Отказ, Заголовок);

	ПроверитьЗаполнениеШапкиУпр(СтруктураШапкиДокумента, Отказ, Заголовок);
	ПроверитьЗаполнениеШапкиРегл(СтруктураШапкиДокумента, Отказ, Заголовок);

КонецПроцедуры // ПроверитьЗаполнениеШапки()

// Дополняет список обязательных параметров табл. части
// упр. параметрами
Процедура ДополнитьОбязательныеРеквизитыТабОСУпр(Реквизиты)

	Реквизиты = Реквизиты + ?(ПустаяСтрока(Реквизиты), "", ", ") + "Стоимость";

КонецПроцедуры

// Процедура проверяет правильность заполнения реквизитов документа
//
Процедура ПроверкаРеквизитовТЧ(РежимПроведения,Отказ, Заголовок, СтруктураШапкиДокумента) Экспорт

	РеквизитыТабОС = "ОсновноеСредство"; //через запятую
	ДополнитьОбязательныеРеквизитыТабОСУпр(РеквизитыТабОС);
	
	УправлениеЗатратами.НепроверятьРеквизитыПоТипуУчета(ЭтотОбъект, РеквизитыТабОС, СтруктураШапкиДокумента, мСтрокаРеквизитыУпрУчета, мСтрокаРеквизитыБухУчета, мСтрокаРеквизитыНалУчета, "ОС");

	ЗаполнениеДокументов.ПроверитьЗаполнениеТабличнойЧасти(ЭтотОбъект, "ОС", Новый Структура(РеквизитыТабОС), Отказ, Заголовок);
	
	Если РежимПроведения = РежимПроведенияДокумента.Оперативный Тогда
		// Проверим возможность снятия ОС с учета
		Для каждого СтрокаОС из ОС Цикл
			Если ОтражатьВБухгалтерскомУчете тогда
				УправлениеВнеоборотнымиАктивами.ПроверитьВозможностьСнятияСУчета(СтрокаОС.ОсновноеСредство,Дата,Отказ,Организация);
			КонецЕсли;
			//начало изменений БП 07 
			//Если ОтражатьВУправленческомУчете тогда
			Если СтруктураШапкиДокумента.ОтражатьВУправленческомУчете тогда
			//конец изменений БП 07	 	
				УправлениеВнеоборотнымиАктивами.ПроверитьВозможностьСнятияСУчета(СтрокаОС.ОсновноеСредство,Дата,Отказ);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры // ПроверкаРеквизитовТЧ()

// Выполняет движения по регистрам 
//
Процедура ДвиженияПоРегистрам(СтруктураШапкиДокумента,ТаблицаПоОС,ТаблицаНаправлений, Отказ, Заголовок)

	ДвиженияПоРегистрамУпр(СтруктураШапкиДокумента, ТаблицаПоОС,ТаблицаНаправлений, Отказ, Заголовок);
	ДвиженияПоРегистрамРегл(СтруктураШапкиДокумента, ТаблицаПоОС,ТаблицаНаправлений, Отказ, Заголовок)

КонецПроцедуры // ДвиженияПоРегистрам

Процедура ДвиженияПоРегистрамУпр(СтруктураШапкиДокумента, ТаблицаПоОС,ТаблицаНаправлений, Отказ, Заголовок)

	Если НЕ СтруктураШапкиДокумента.ОтражатьВУправленческомУчете Тогда
		Возврат;
	КонецЕсли;

	ДатаДока = Дата;

	АмортизацияОС              = Движения.СтоимостьОС;
	ТаблицаДвиженийАмортизация = АмортизацияОС.Выгрузить();
	ТаблицаДвиженийАмортизация.Очистить();

	ТабАмортизации = Новый ТаблицаЗначений;
	ТабАмортизации.Колонки.Добавить("НаправлениеАмортизации", Новый ОписаниеТипов("СправочникСсылка.СпособыОтраженияРасходовПоАмортизации"));
	ТабАмортизации.Колонки.Добавить("ОбъектУчета",            Новый ОписаниеТипов("СправочникСсылка.ОсновныеСредства"));
	ТабАмортизации.Колонки.Добавить("Сумма",                  Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла( 15, 2)));

	Для каждого СтрокаОС из ТаблицаПоОС Цикл

		Если СтрокаОС.АмортизацияЗаМесяц > 0 Тогда

			ТекНаправление = ТаблицаНаправлений.Найти(СтрокаОС.ОсновноеСредство,"ОС_УУ");

			НоваяСтрока    = ТабАмортизации.Добавить();

			НоваяСтрока.Сумма                  = СтрокаОС.АмортизацияЗаМесяц;
			НоваяСтрока.ОбъектУчета            = СтрокаОС.ОсновноеСредство;
			НоваяСтрока.НаправлениеАмортизации = ?(ТекНаправление = Неопределено, Неопределено, ТекНаправление.Направление_УУ);

			// Движения по регистру СтоимостьОС
			Движение = ТаблицаДвиженийАмортизация.Добавить();

			Движение.ОсновноеСредство = СтрокаОС.ОсновноеСредство;
			Движение.Амортизация      = СтрокаОС.АмортизацияЗаМесяц;
			Движение.Стоимость        = 0;

		КонецЕсли;

	КонецЦикла;

	// вызов процедуры списания амортизации по направлениям
	ТабЗатрат = УправлениеВнеоборотнымиАктивами.ПолучитьРаспределениеАмортизацииПоНаправлениямУпр(ЭтотОбъект, Отказ, Заголовок, ТабАмортизации, СтруктураШапкиДокумента);

	АмортизацияОС.мПериод          = ДатаДока;
	АмортизацияОС.мТаблицаДвижений = ТаблицаДвиженийАмортизация;
	Движения.СтоимостьОС.ВыполнитьПриход();

	СостояниеОС           = Движения.СостоянияОС;
	СобытиеОС             = Движения.СобытияОС;
	АмортизацияОС         = Движения.СтоимостьОС;
	ОССписанныеНаЗатраты  = Движения.ОССписанныеНаЗатраты;
	НачислениеАмортизации = Движения.НачислениеАмортизацииОС;

	ТаблицаДвиженийАмортизация = АмортизацияОС.Выгрузить();
	ТаблицаДвиженийАмортизация.Очистить();
	
	ТаблицаДвиженийОССписанныеНаЗатраты = ОССписанныеНаЗатраты.Выгрузить();

	Для каждого СтрокаТЧ Из ТаблицаПоОС Цикл

		ТекОС = СтрокаТЧ.ОсновноеСредство;

		// Движения по регистру НачислениеАмортизации
		//начало изменений 
		Если Не ЧастичнаяЛиквидация Тогда
			Движение = НачислениеАмортизации.Добавить();

			Движение.Период               = ДатаДока;
			Движение.ОсновноеСредство     = ТекОС;
			Движение.НачислятьАмортизацию = Ложь;
			Движение.НачислятьАмортизациюВТекущемМесяце = Ложь;
		КонецЕсли;

		// Движения по регистру СобытияОС
		Движение = СобытиеОС.Добавить();
		Движение.Период               = ДатаДока;
		Движение.ОсновноеСредство     = ТекОС;
		Движение.Событие              = СтруктураШапкиДокумента.Событие;
		Движение.НазваниеДокумента = Метаданные().Представление();
		Движение.НомерДокумента    = Номер;

		// Движения по регистру СостоянияОС
		//начало изменений 
		Если Не ЧастичнаяЛиквидация Тогда
			Движение = СостояниеОС.Добавить();
			Движение.Период               = ДатаДока;
			Движение.ОсновноеСредство     = ТекОС;
			Движение.Состояние            = Перечисления.СостоянияОС.СнятоСУчета;
			Движение.ДатаСостояния		  = ДатаДока;
		КонецЕсли;	
		//конец изменений

		Если НЕ СтрокаТЧ.СписаноНаЗатраты Тогда

			// Движения по регистру СтоимостьОС
			Движение = ТаблицаДвиженийАмортизация.Добавить();

			Движение.ОсновноеСредство = ТекОС;
			Движение.Амортизация      = СтрокаТЧ.Амортизация+СтрокаТЧ.АмортизацияЗаМесяц;
			Движение.Стоимость        = СтрокаТЧ.Стоимость;

		Иначе
			// Движения по регистру ОССписанныеНаЗатраты
			Движение = ТаблицаДвиженийОССписанныеНаЗатраты.Добавить();

			Движение.ОсновноеСредство = ТекОС;
			Движение.Стоимость        = СтрокаТЧ.Стоимость;

		КонецЕсли;

	КонецЦикла;

	АмортизацияОС.мПериод          = ДатаДока;
	АмортизацияОС.мТаблицаДвижений = ТаблицаДвиженийАмортизация;
	Движения.СтоимостьОС.ВыполнитьРасход();

	ОССписанныеНаЗатраты.мПериод   = ДатаДока;
	ОССписанныеНаЗатраты.мТаблицаДвижений = ТаблицаДвиженийОССписанныеНаЗатраты;
	Движения.ОССписанныеНаЗатраты.ВыполнитьРасход();

КонецПроцедуры

Процедура ДвиженияПоРегистрамРегл(СтруктураШапкиДокумента, ТаблицаПоОС,ТаблицаНаправлений, Отказ, Заголовок)

	Если (НЕ СтруктураШапкиДокумента.ОтражатьВБухгалтерскомУчете) И (НЕ СтруктураШапкиДокумента.ОтражатьВНалоговомУчете) Тогда
		Возврат;
	КонецЕсли;

	ДатаДока       = Дата;
	ТекОрганизация = СтруктураШапкиДокумента.Организация;

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТекОрганизация",  ТекОрганизация);
	Запрос.УстановитьПараметр("ТекДата",         ДатаДока);
	Запрос.УстановитьПараметр("ВнешнийИсточник", ТаблицаПоОС);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
   	|	ОсновноеСредство
	|ПОМЕСТИТЬ ОсновныеСредства
	|ИЗ &ВнешнийИсточник КАК ВнешнийИсточник
	|";
	Запрос.Выполнить();
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ОсновныеСредства.ОсновноеСредство                                              КАК ОсновноеСредство,
	|	СчетаБухгалтерскогоУчетаОсновныхСредствСрезПоследних.ОсновноеСредство          КАК ОС_БУ,
	|	СчетаБухгалтерскогоУчетаОсновныхСредствСрезПоследних.СчетУчета                 КАК СчетУчетаБУ,
	|	СчетаБухгалтерскогоУчетаОсновныхСредствСрезПоследних.СчетНачисленияАмортизации КАК СчетНачисленияАмортизацииБУ,
	|	СчетаНалоговогоУчетаОсновныхСредствСрезПоследних.СчетУчета                     КАК СчетУчетаНУ,
	|	СчетаНалоговогоУчетаОсновныхСредствСрезПоследних.ОсновноеСредство              КАК ОС_НУ,
	|	СчетаНалоговогоУчетаОсновныхСредствСрезПоследних.СчетНачисленияАмортизации     КАК СчетНачисленияАмортизацииНУ
	|ИЗ
	|	ОсновныеСредства
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СчетаБухгалтерскогоУчетаОС.СрезПоследних(&ТекДата, Организация = &ТекОрганизация И ОсновноеСредство В (ВЫБРАТЬ ОсновноеСредство ИЗ ОсновныеСредства)) КАК СчетаБухгалтерскогоУчетаОсновныхСредствСрезПоследних
	|	ПО ОсновныеСредства.ОсновноеСредство = СчетаБухгалтерскогоУчетаОсновныхСредствСрезПоследних.ОсновноеСредство
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СчетаНалоговогоУчетаОС.СрезПоследних(&ТекДата, Организация = &ТекОрганизация И ОсновноеСредство В (ВЫБРАТЬ ОсновноеСредство ИЗ ОсновныеСредства)) КАК СчетаНалоговогоУчетаОсновныхСредствСрезПоследних
	|	ПО ОсновныеСредства.ОсновноеСредство = СчетаНалоговогоУчетаОсновныхСредствСрезПоследних.ОсновноеСредство
	|";
	ТаблицаСчетов  = Запрос.Выполнить().Выгрузить();

	Если СтруктураШапкиДокумента.ОтражатьВБухгалтерскомУчете Тогда 
		// подготовим таблицу с данными по амортизации для списания амортизации по 
		// направлениям затрат

		АмортизацияОС  = Движения.СтоимостьОСБухгалтерскийУчет;

		ТаблицаДвиженийАмортизация = АмортизацияОС.Выгрузить();
		ТаблицаДвиженийАмортизация.Очистить();

		ТабАмортизации = Новый ТаблицаЗначений;
		ТабАмортизации.Колонки.Добавить("НаправлениеАмортизации", Новый ОписаниеТипов("СправочникСсылка.СпособыОтраженияРасходовПоАмортизации"));
		ТабАмортизации.Колонки.Добавить("ОбъектУчета",            Новый ОписаниеТипов("СправочникСсылка.ОсновныеСредства"));
		ТабАмортизации.Колонки.Добавить("Сумма",                  Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла( 15, 2)));
		ТабАмортизации.Колонки.Добавить("СчетАмортизации");
		ТабАмортизации.Колонки.Добавить("СчетАмортизацииНУ");
		ТабАмортизации.Колонки.Добавить("ИмяСубконто", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(16)));

		Для Каждого СтрокаОС Из ТаблицаПоОС Цикл

			Если НЕ (СтрокаОС.АмортизацияЗаМесяцБУ = 0) Тогда

				НоваяСтрока = ТабАмортизации.Добавить();

				НоваяСтрока.Сумма       = СтрокаОС.АмортизацияЗаМесяцБУ;
				НоваяСтрока.ОбъектУчета = СтрокаОС.ОсновноеСредство;
				НоваяСтрока.ИмяСубконто = "ОсновныеСредства";
				
				ТекНаправление = ТаблицаНаправлений.Найти(СтрокаОС.ОсновноеСредство,"ОС_БУ");
				НоваяСтрока.НаправлениеАмортизации = ?(ТекНаправление = Неопределено,Неопределено,ТекНаправление.Направление_БУ);

				СтрокаТЗ = ТаблицаСчетов.Найти(СтрокаОС.ОсновноеСредство,"ОС_БУ");
				Если СтрокаТЗ <> Неопределено Тогда
					
					НоваяСтрока.СчетАмортизации = СтрокаТЗ.СчетНачисленияАмортизацииБУ;
					
				Иначе
					
					ОбщегоНазначения.СообщитьОбОшибке("Основное средство " + СтрокаОС.ОсновноеСредство.Наименование +
					                    " не отражалось в бух. учете в организации " + ТекОрганизация, Отказ); 	
					Возврат;
					
				КонецЕсли;

				// Движения по регистру СтоимостьОС
				Движение = ТаблицаДвиженийАмортизация.Добавить();

				Движение.ОсновноеСредство = СтрокаОС.ОсновноеСредство;
				Движение.Организация      = ТекОрганизация;
				Движение.Амортизация      = СтрокаОС.АмортизацияЗаМесяцБУ;
				Движение.Стоимость        = 0;

			КонецЕсли;

		КонецЦикла;

		//вызов процедуры списания амортизации по направлениям
		ТабЗатрат = УправлениеВнеоборотнымиАктивами.ПолучитьРаспределениеАмортизацииПоНаправлениямРегл(ЭтотОбъект, Отказ, Заголовок, ТабАмортизации, СтруктураШапкиДокумента,"Бух","ОС");
        ТабАмортизацииБУ = ТабАмортизации;
		
		АмортизацияОС.мПериод          = ДатаДока;
		АмортизацияОС.мТаблицаДвижений = ТаблицаДвиженийАмортизация;
		Движения.СтоимостьОСБухгалтерскийУчет.ВыполнитьПриход();

		СобытиеОС             = Движения.СобытияОСОрганизаций;
		СостояниеОС			  = Движения.СостоянияОСОрганизаций;	
		АмортизацияОС         = Движения.СтоимостьОСБухгалтерскийУчет;
		ОССписанныеНаЗатраты  = Движения.ОССписанныеНаЗатратыОрганизаций;
		НачислениеАмортизации = Движения.НачислениеАмортизацииОСБухгалтерскийУчет;
		ПроводкиБУ            = Движения.Хозрасчетный;
		Проверка83ИСозданиеПроводок(ПроводкиБУ);

		ТаблицаДвиженийАмортизация = АмортизацияОС.Выгрузить();
		ТаблицаДвиженийАмортизация.Очистить();

		ТаблицаДвиженийОССписанныеНаЗатраты = ОССписанныеНаЗатраты.Выгрузить();

		Для Каждого СтрокаТЧ Из ТаблицаПоОС Цикл

			ТекОС = СтрокаТЧ.ОсновноеСредство;

			// Движения по регистру НачислениеАмортизации
			Если Не ЧастичнаяЛиквидация Тогда			
				Движение = НачислениеАмортизации.Добавить();
				Движение.Период               = ДатаДока;
				Движение.ОсновноеСредство     = ТекОС;
				Движение.Организация          = ТекОрганизация;
				Движение.НачислятьАмортизацию = Ложь;
			КонецЕсли;	

			// Движения по регистру СобытияОС
			Движение = СобытиеОС.Добавить();
			Движение.Период               = ДатаДока;
			Движение.ОсновноеСредство     = ТекОС;
			Движение.Организация          = ТекОрганизация;
			Движение.Событие              = СтруктураШапкиДокумента.СобытиеРегл;
			Движение.НазваниеДокумента 	  = Метаданные().Представление();
			Движение.НомерДокумента		  = Номер;
			
			// Движения по регистру СостоянияОС
			//начало изменений
			Если Не ЧастичнаяЛиквидация Тогда
				Движение = СостояниеОС.Добавить();
				Движение.Период               = ДатаДока;
				Движение.ОсновноеСредство     = ТекОС;
				Движение.Организация          = ТекОрганизация;
				Движение.Состояние            = Перечисления.СостоянияОС.СнятоСУчета;
				Движение.ДатаСостояния		  = ДатаДока;
			КонецЕсли;	
			//конец изменений
			
			Если НЕ СтрокаТЧ.СписаноНаЗатратыБУ И НЕ (СтрокаТЧ.СтоимостьБУ = 0) Тогда
				
				// Движения по регистру СтоимостьОС
				Движение = ТаблицаДвиженийАмортизация.Добавить();
				Движение.ОсновноеСредство = ТекОС;
				Движение.Организация      = ТекОрганизация;
				Движение.Амортизация      = СтрокаТЧ.АмортизацияБУ+СтрокаТЧ.АмортизацияЗаМесяцБУ;
				Движение.Стоимость        = СтрокаТЧ.СтоимостьБУ;
				Движение.ВидОперации      = Перечисления.ВидыСобытийОС.Списание;

				СтрокаТЗ = ТаблицаСчетов.Найти(ТекОС,"ОС_БУ");
				
				Если СтрокаТЗ <> Неопределено Тогда
					
					СчетУчетаСтоимости  = СтрокаТЗ.СчетУчетаБУ;
					СчетУчетаАмортизации= СтрокаТЗ.СчетНачисленияАмортизацииБУ;
					
				Иначе
					
					ОбщегоНазначения.СообщитьОбОшибке("Основное средство " + ТекОс.Наименование +
					                    " не отражалось в бух. учете в организации " + ТекОрганизация,Отказ); 	
					Возврат;
					
				КонецЕсли;

				// списание амортизации Д 02.01 К 01.09
				СуммаПроводки = СтрокаТЧ.АмортизацияБУ + СтрокаТЧ.АмортизацияЗаМесяцБУ;

				Если НЕ (СуммаПроводки = 0) Тогда

					Проводка = ПроводкиБУ.Добавить();

					Проводка.Период       = ДатаДока;
					Проводка.Организация  = ТекОрганизация;
					Проводка.Содержание   = "Списание ОС";
					Проводка.Сумма        = СуммаПроводки;
					Проводка.НомерЖурнала = "ОС";

					Проводка.СчетДт      = СчетУчетаАмортизации;
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "ОсновныеСредства", ТекОС);

					Проводка.СчетКт      = ПланыСчетов.Хозрасчетный.ВыбытиеОС;
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "ОсновныеСредства", ТекОС);

				КонецЕсли;

				// списание балансовой стоимости Д 01.09 К 01.01
				СуммаПроводки = СтрокаТЧ.СтоимостьБУ;

				Если НЕ (СуммаПроводки = 0) Тогда

					Проводка = ПроводкиБУ.Добавить();

					Проводка.Период       = ДатаДока;
					Проводка.Организация  = ТекОрганизация;
					Проводка.Сумма        = СтрокаТЧ.СтоимостьБУ;
					Проводка.Содержание   = "Списание ОС";
					Проводка.НомерЖурнала = "ОС";

					Проводка.СчетДт      = ПланыСчетов.Хозрасчетный.ВыбытиеОС;
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "ОсновныеСредства", ТекОС);

					Проводка.СчетКт      = СчетУчетаСтоимости;
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "ОсновныеСредства", ТекОС);

				КонецЕсли;

				// списание остаточной стоимости Д 99.01 (91.02) К 01.09
				СуммаПроводки = СтрокаТЧ.СтоимостьБУ - СтрокаТЧ.АмортизацияБУ - СтрокаТЧ.АмортизацияЗаМесяцБУ;

				Если НЕ (СуммаПроводки = 0) Тогда

					Проводка = ПроводкиБУ.Добавить();
					Проводка.Период       = ДатаДока;
					Проводка.Организация  = ТекОрганизация;
					Проводка.Содержание   = "Списание ОС";
					Проводка.Сумма        = СуммаПроводки;
					Проводка.НомерЖурнала = "ОС";

					Проводка.СчетДт = СтруктураШапкиДокумента.СчетСписанияБУ;
					Если ЗначениеЗаполнено(СтруктураШапкиДокумента.СубконтоБУ) Тогда
						БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 1, СтруктураШапкиДокумента.СубконтоБУ);
					КонецЕсли;
					//начало изменений БП 07 
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт,"ПодразделениеОрганизации", СтрокаТЧ.ПодразделениеОрганизации);
					//конец изменений БП 07	 

					Проводка.СчетКт      = ПланыСчетов.Хозрасчетный.ВыбытиеОС;
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "ОсновныеСредства", ТекОС);

				КонецЕсли;

			Иначе

				// Движения по регистру ОССписанныеНаЗатраты
				Движение = ТаблицаДвиженийОССписанныеНаЗатраты.Добавить();

				Движение.ОсновноеСредство = ТекОС;
				Движение.Организация      = ТекОрганизация;
				Движение.Стоимость        = СтрокаТЧ.СтоимостьБУ;

			КонецЕсли;

		КонецЦикла;

		АмортизацияОС.мПериод                    = ДатаДока;
		АмортизацияОС.мТаблицаДвижений           = ТаблицаДвиженийАмортизация;
		Движения.СтоимостьОСБухгалтерскийУчет.ВыполнитьРасход();

		ОССписанныеНаЗатраты.мПериод             = ДатаДока;
		ОССписанныеНаЗатраты.мТаблицаДвижений    = ТаблицаДвиженийОССписанныеНаЗатраты;
		Движения.ОССписанныеНаЗатратыОрганизаций.ВыполнитьРасход();

		ПроводкиБУ.Записать(Ложь);
			
	КонецЕсли;

	Если СтруктураШапкиДокумента.ОтражатьВНалоговомУчете Тогда 

		ПроводкиНУ = Движения.Налоговый;
	
		// подготовим таблицу с данными по амортизации для списания амортизации по 
		// направлениям затрат

		АмортизацияОСНУ = Движения.СтоимостьОСНалоговыйУчет;

		ТаблицаДвиженийАмортизацияНУ = АмортизацияОСНУ.Выгрузить();
		ТаблицаДвиженийАмортизацияНУ.Очистить();
		
		ТаблицаДвиженийСтоимостьНУ = ТаблицаДвиженийАмортизацияНУ.Скопировать();

		ТабАмортизации = Новый ТаблицаЗначений;
		ТабАмортизации.Колонки.Добавить("НаправлениеАмортизации", Новый ОписаниеТипов("СправочникСсылка.СпособыОтраженияРасходовПоАмортизации"));
		ТабАмортизации.Колонки.Добавить("ОбъектУчета",            Новый ОписаниеТипов("СправочникСсылка.ОсновныеСредства"));
		ТабАмортизации.Колонки.Добавить("Сумма",                  Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла( 15, 2)));
		ТабАмортизации.Колонки.Добавить("СчетУчетаНУ");
		ТабАмортизации.Колонки.Добавить("СчетАмортизации");
		ТабАмортизации.Колонки.Добавить("СчетАмортизацииНУ");
		ТабАмортизации.Колонки.Добавить("СуммаКапитальныхВложенийВключаемыхВРасходы", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
		ТабАмортизации.Колонки.Добавить("ИмяСубконто", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(16)));

		Для Каждого СтрокаОС Из ТаблицаПоОС Цикл

			 Если СтрокаТЧ.ПорядокВключенияСтоимостиВСоставРасходовНУ = Перечисления.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НачислениеАмортизации
			   И (СтруктураШапкиДокумента.МетодНачисленияАмортизацииНУ = Перечисления.МетодыНачисленияАмортизации.Нелинейный
			     И НЕ (((СтрокаТЧ.ОсновноеСредство.АмортизационнаяГруппа = Перечисления.АмортизационныеГруппы.ВосьмаяГруппа
				          ИЛИ СтрокаТЧ.ОсновноеСредство.АмортизационнаяГруппа = Перечисления.АмортизационныеГруппы.ДевятаяГруппа
				          ИЛИ СтрокаТЧ.ОсновноеСредство.АмортизационнаяГруппа = Перечисления.АмортизационныеГруппы.ДесятаяГруппа)
				        И (СтрокаТЧ.ОсновноеСредство.ГруппаОС = Перечисления.ГруппыОС.Здания
				           ИЛИ СтрокаТЧ.ОсновноеСредство.ГруппаОС = Перечисления.ГруппыОС.Сооружения))
				   ИЛИ СтрокаТЧ.ОсновноеСредство.АмортизационнаяГруппа = Перечисления.АмортизационныеГруппы.ОтдельнаяГруппа)) Тогда
				   Продолжить;
			КонецЕсли;
			
			Если НЕ (СтрокаОС.АмортизацияЗаМесяцНУ = 0) Тогда

				НоваяСтрока = ТабАмортизации.Добавить();

				НоваяСтрока.Сумма       = СтрокаОС.АмортизацияЗаМесяцНУ;
				НоваяСтрока.ОбъектУчета = СтрокаОС.ОсновноеСредство;
				НоваяСтрока.ИмяСубконто = "ОсновныеСредства";

				ТекНаправление = ТаблицаНаправлений.Найти(СтрокаОС.ОсновноеСредство,"ОС_НУ");
				НоваяСтрока.НаправлениеАмортизации = ?(ТекНаправление = Неопределено,Неопределено,ТекНаправление.Направление_НУ);

				СтрокаТЗ = ТаблицаСчетов.Найти(СтрокаОС.ОсновноеСредство,"ОС_НУ");
				Если СтрокаТЗ <> Неопределено Тогда
					НоваяСтрока.СчетАмортизацииНУ = СтрокаТЗ.СчетНачисленияАмортизацииНУ;
					НоваяСтрока.СчетУчетаНУ       = СтрокаТЗ.СчетУчетаНУ;
				Иначе
					ОбщегоНазначения.СообщитьОбОшибке("Основное средство " + СтрокаОС.ОсновноеСредство.Наименование +
					                    " не отражалось в налог. учете в организации " + ТекОрганизация, Отказ); 	
					Возврат;
				КонецЕсли;

				Движение = ТаблицаДвиженийАмортизацияНУ.Добавить();

				Движение.ОсновноеСредство = СтрокаОС.ОсновноеСредство;
				Движение.Организация      = ТекОрганизация;
				Движение.Амортизация      = СтрокаОС.АмортизацияЗаМесяцНУ;
				Движение.Стоимость        = 0;
				
				НоваяСтрока.СуммаКапитальныхВложенийВключаемыхВРасходы = СтрокаОС.СуммаКапитальныхВложенийВключаемыхВРасходыНУ;

			КонецЕсли;
			
		КонецЦикла;
		
		// Включение амортизационной премии в состав расходов.
		ТабКапитальныхВложений = ТабАмортизации.Скопировать();
		ТабКапитальныхВложений.Колонки.Добавить("СуммаБУ",        Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла( 15, 2)));
		ТабКапитальныхВложений.Колонки.Добавить("СуммаПР",        Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла( 15, 2)));
		ТабКапитальныхВложений.Колонки.Удалить("Сумма");
		
		// Формирование списка основных средств, по которым отражается амортизационная премия
		СписокОС = Новый Массив;
		Для Каждого СтрокаОС Из ТаблицаПоОС Цикл
			
			 Если НЕ (СтрокаТЧ.ПорядокВключенияСтоимостиВСоставРасходовНУ = Перечисления.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НачислениеАмортизации
			   И (СтруктураШапкиДокумента.МетодНачисленияАмортизацииНУ = Перечисления.МетодыНачисленияАмортизации.Нелинейный
			     И НЕ (((СтрокаТЧ.ОсновноеСредство.АмортизационнаяГруппа = Перечисления.АмортизационныеГруппы.ВосьмаяГруппа
				          ИЛИ СтрокаТЧ.ОсновноеСредство.АмортизационнаяГруппа = Перечисления.АмортизационныеГруппы.ДевятаяГруппа
				          ИЛИ СтрокаТЧ.ОсновноеСредство.АмортизационнаяГруппа = Перечисления.АмортизационныеГруппы.ДесятаяГруппа)
				        И (СтрокаТЧ.ОсновноеСредство.ГруппаОС = Перечисления.ГруппыОС.Здания
				           ИЛИ СтрокаТЧ.ОсновноеСредство.ГруппаОС = Перечисления.ГруппыОС.Сооружения))
				   ИЛИ СтрокаТЧ.ОсновноеСредство.АмортизационнаяГруппа = Перечисления.АмортизационныеГруппы.ОтдельнаяГруппа))) Тогда
				   
				СписокОС.Добавить(СтрокаОС.ОсновноеСредство);
				
			КонецЕсли;
			
		КонецЦикла;
		
		Если СписокОС.Количество() > 0 Тогда
			НалоговыйУчет.РасчетАмортизационнойПремии(ЭтотОбъект, СтруктураШапкиДокумента, СписокОС, ТабКапитальныхВложений, ПроводкиНУ, ТаблицаДвиженийСтоимостьНУ, Отказ, Заголовок);			
		КонецЕсли;
		
		//вызов процедуры списания амортизации по направлениям
		Если  СписокОС.Количество() > 0 
		   И  СтруктураШапкиДокумента.ПоддержкаПБУ18 Тогда
			НалоговыйУчет.РасчетРазницПоАмортизации(СтруктураШапкиДокумента, "ОС", СписокОС, ТабАмортизации, ТабЗатрат, ТабАмортизацииБУ);
		КонецЕсли;
		
		Если  СписокОС.Количество() > 0 Тогда
			
			ТабЗатрат = УправлениеВнеоборотнымиАктивами.ПолучитьРаспределениеАмортизацииПоНаправлениямРегл(ЭтотОбъект, Отказ, Заголовок, ТабАмортизации, СтруктураШапкиДокумента, "Нал", "ОС");
			ПроводкиНУ.Записать(Ложь);
		КонецЕсли;

		АмортизацияОСНУ.мПериод          = ДатаДока;
		АмортизацияОСНУ.мТаблицаДвижений = ТаблицаДвиженийСтоимостьНУ;
		Движения.СтоимостьОСНалоговыйУчет.ВыполнитьРасход();
		
		АмортизацияОСНУ.мПериод          = ДатаДока;
		АмортизацияОСНУ.мТаблицаДвижений = ТаблицаДвиженийАмортизацияНУ;
		Движения.СтоимостьОСНалоговыйУчет.ВыполнитьПриход();

		АмортизацияОСНУ          = Движения.СтоимостьОСНалоговыйУчет;
		НачислениеАмортизацииНУ  = Движения.НачислениеАмортизацииОСНалоговыйУчет;

		ТаблицаДвиженийАмортизацияНУ = АмортизацияОСНУ.Выгрузить();
		ТаблицаДвиженийАмортизацияНУ.Очистить();

		Для каждого СтрокаТЧ Из ТаблицаПоОС Цикл

			 Если СтрокаТЧ.ПорядокВключенияСтоимостиВСоставРасходовНУ = Перечисления.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НачислениеАмортизации
			   И (СтруктураШапкиДокумента.МетодНачисленияАмортизацииНУ = Перечисления.МетодыНачисленияАмортизации.Нелинейный
			     И НЕ (((СтрокаТЧ.ОсновноеСредство.АмортизационнаяГруппа = Перечисления.АмортизационныеГруппы.ВосьмаяГруппа
				          ИЛИ СтрокаТЧ.ОсновноеСредство.АмортизационнаяГруппа = Перечисления.АмортизационныеГруппы.ДевятаяГруппа
				          ИЛИ СтрокаТЧ.ОсновноеСредство.АмортизационнаяГруппа = Перечисления.АмортизационныеГруппы.ДесятаяГруппа)
				        И (СтрокаТЧ.ОсновноеСредство.ГруппаОС = Перечисления.ГруппыОС.Здания
				           ИЛИ СтрокаТЧ.ОсновноеСредство.ГруппаОС = Перечисления.ГруппыОС.Сооружения))
				   ИЛИ СтрокаТЧ.ОсновноеСредство.АмортизационнаяГруппа = Перечисления.АмортизационныеГруппы.ОтдельнаяГруппа)) Тогда
				   Продолжить;
			КонецЕсли;
			
			ТекОС = СтрокаТЧ.ОсновноеСредство;

			Если НЕ (СтрокаТЧ.СтоимостьНУ = 0) Тогда
				
				// Движения по регистру НачислениеАмортизации
				Если Не ЧастичнаяЛиквидация Тогда
					Движение = НачислениеАмортизацииНУ.Добавить();
					Движение.Организация          = ТекОрганизация;
					Движение.Период               = ДатаДока;
					Движение.ОсновноеСредство     = ТекОС;
					Движение.НачислятьАмортизацию = Ложь;
				КонецЕсли;	

				// Движения по регистру СтоимостьОС
				Движение = ТаблицаДвиженийАмортизацияНУ.Добавить();

				Движение.ОсновноеСредство = ТекОС;
				Движение.Организация      = ТекОрганизация;
				Движение.Амортизация      = СтрокаТЧ.АмортизацияНУ + СтрокаТЧ.АмортизацияЗаМесяцНУ;
				Движение.Стоимость        = СтрокаТЧ.СтоимостьНУ - СтрокаТЧ.СуммаКапитальныхВложенийВключаемыхВРасходыНУ;
				Движение.ВидОперации      = Перечисления.ВидыСобытийОС.Списание;

				СтрокаТЗ = ТаблицаСчетов.Найти(ТекОС,"ОС_НУ");
				Если СтрокаТЗ <> Неопределено Тогда

					СчетУчетаСтоимости   = СтрокаТЗ.СчетУчетаНУ;
					СчетУчетаАмортизации = СтрокаТЗ.СчетНачисленияАмортизацииНУ;

				КонецЕсли;

				// списание амортизации Д 02.01 К 01.09
				СуммаПроводки = СтрокаТЧ.АмортизацияНУ + СтрокаТЧ.АмортизацияЗаМесяцНУ;

				Если НЕ (СуммаПроводки = 0) Тогда

					Проводка = ПроводкиНУ.Добавить();

					Проводка.Период      = ДатаДока;
					Проводка.Организация = ТекОрганизация;
					Проводка.Содержание  = "Списание ОС";
					Проводка.Сумма       = СуммаПроводки;
					Проводка.НомерЖурнала = "ОС";

					Проводка.СчетДт      = СчетУчетаАмортизации;
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "ОсновныеСредства", ТекОС);

					Проводка.СчетКт      = ПланыСчетов.Налоговый.ВыбытиеОС;
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "ОсновныеСредства", ТекОС);

				КонецЕсли;

				// списание балансовой стоимости Д 01.09 К 01.01
				СуммаПроводки = СтрокаТЧ.СтоимостьНУ - СтрокаТЧ.СуммаКапитальныхВложенийВключаемыхВРасходыНУ;
				
				Если НЕ (СуммаПроводки = 0) Тогда

					Проводка = ПроводкиНУ.Добавить();

					Проводка.Период       = ДатаДока;
					Проводка.Организация  = ТекОрганизация;
					Проводка.Содержание   = "Списание ОС";
					Проводка.Сумма        = СуммаПроводки;
					Проводка.НомерЖурнала = "ОС";

					Проводка.СчетДт      = ПланыСчетов.Налоговый.ВыбытиеОС;
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "ОсновныеСредства", ТекОС);

					Проводка.СчетКт      = СчетУчетаСтоимости;
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "ОсновныеСредства", ТекОС);

				КонецЕсли;

				// списание остаточной стоимости Д 99.01 (91.02) К 01.09
				СуммаПроводки = СтрокаТЧ.СтоимостьНУ - СтрокаТЧ.АмортизацияНУ - СтрокаТЧ.АмортизацияЗаМесяцНУ - СтрокаТЧ.СуммаКапитальныхВложенийВключаемыхВРасходыНУ;

				Если НЕ (СуммаПроводки = 0) Тогда

					Проводка = ПроводкиНУ.Добавить();

					Проводка.Период      = ДатаДока;
					Проводка.Организация = ТекОрганизация;
					Проводка.Содержание  = "Списание ОС";
					Проводка.Сумма       = СуммаПроводки;
					Проводка.НомерЖурнала = "ОС";

					Проводка.СчетДт      = СтруктураШапкиДокумента.СчетСписанияНУ;
					Если ЗначениеЗаполнено(СтруктураШапкиДокумента.СубконтоНУ) Тогда
						БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 1, СтруктураШапкиДокумента.СубконтоНУ);
					КонецЕсли;

					Проводка.СчетКт      = ПланыСчетов.Налоговый.ВыбытиеОС;
					БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "ОсновныеСредства", ТекОС);

				КонецЕсли;

			КонецЕсли;

		КонецЦикла;

		АмортизацияОСНУ.мПериод          = ДатаДока;
		АмортизацияОСНУ.мТаблицаДвижений = ТаблицаДвиженийАмортизацияНУ;
		Движения.СтоимостьОСНалоговыйУчет.ВыполнитьРасход();

		// Рассчитаем разницы в амортизации на месяц списания
		Если СтруктураШапкиДокумента.ПоддержкаПБУ18 Тогда
			
			// Формирование массива основных средств, по которым рассчитываются разницы
			МассивОС = Новый Массив;
			Для Каждого СтрокаОС Из ТаблицаПоОС Цикл
				
				 Если НЕ (СтрокаТЧ.ПорядокВключенияСтоимостиВСоставРасходовНУ = Перечисления.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НачислениеАмортизации
			   И (СтруктураШапкиДокумента.МетодНачисленияАмортизацииНУ = Перечисления.МетодыНачисленияАмортизации.Нелинейный
			     И НЕ (((СтрокаТЧ.ОсновноеСредство.АмортизационнаяГруппа = Перечисления.АмортизационныеГруппы.ВосьмаяГруппа
				          ИЛИ СтрокаТЧ.ОсновноеСредство.АмортизационнаяГруппа = Перечисления.АмортизационныеГруппы.ДевятаяГруппа
				          ИЛИ СтрокаТЧ.ОсновноеСредство.АмортизационнаяГруппа = Перечисления.АмортизационныеГруппы.ДесятаяГруппа)
				        И (СтрокаТЧ.ОсновноеСредство.ГруппаОС = Перечисления.ГруппыОС.Здания
				           ИЛИ СтрокаТЧ.ОсновноеСредство.ГруппаОС = Перечисления.ГруппыОС.Сооружения))
				   ИЛИ СтрокаТЧ.ОсновноеСредство.АмортизационнаяГруппа = Перечисления.АмортизационныеГруппы.ОтдельнаяГруппа))) Тогда
					   
					МассивОС.Добавить(СтрокаОС.ОсновноеСредство);
					
				КонецЕсли;
				
			КонецЦикла;
			
			Если МассивОС.Количество() > 0 Тогда
				ДвиженияПоРазницам(СтруктураШапкиДокумента, МассивОС, ТаблицаСчетов);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если СтруктураШапкиДокумента.ОтражатьВНалоговомУчете И НЕ Отказ Тогда
		ПроводкиНУ.Записать(Ложь);
	КонецЕсли; 
	
КонецПроцедуры

Процедура ДвиженияПоРазницам(СтруктураШапкиДокумента, СписокОС, ТаблицаПоОС)
	
	Если Не СтруктураШапкиДокумента.ОтражатьВНалоговомУчете Тогда 
		Возврат;
	КонецЕсли;
		
	// Списание разниц счета 01 и 02
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	НалоговыйОстатки.Субконто1 КАК Субконто1,
	               |	НалоговыйОстатки.СуммаОстатокКт КАК Сумма02ПР,
	               |	0 КАК Сумма02ВР,
	               |	0 КАК Сумма01ПР,
	               |	0 КАК Сумма01ВР,
	               |	0 КАК Сумма01
	               |ИЗ
	               |	РегистрБухгалтерии.Налоговый.Остатки(
	               |			&КонДата,
	               |			Счет В ИЕРАРХИИ (&НалоговыйАмортизацияОС),
	               |			&ВидСубконто,
	               |			ВидУчета = &ВидУчетаПР
	               |				И Организация = &Организация
	               |				И Субконто1 В (&СписокОС)) КАК НалоговыйОстатки
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	НалоговыйОстатки.Субконто1,
	               |	0,
	               |	НалоговыйОстатки.СуммаОстатокКт,
	               |	0,
	               |	0,
	               |	0
	               |ИЗ
	               |	РегистрБухгалтерии.Налоговый.Остатки(
	               |			&КонДата,
	               |			Счет В ИЕРАРХИИ (&НалоговыйАмортизацияОС),
	               |			&ВидСубконто,
	               |			ВидУчета = &ВидУчетаВР
	               |				И Организация = &Организация
	               |				И Субконто1 В (&СписокОС)) КАК НалоговыйОстатки
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	НалоговыйОстатки.Субконто1,
	               |	0,
	               |	0,
	               |	НалоговыйОстатки.СуммаОстатокДт,
	               |	0,
	               |	0
	               |ИЗ
	               |	РегистрБухгалтерии.Налоговый.Остатки(
	               |			&КонДата,
	               |			Счет В ИЕРАРХИИ (&НалоговыйОСвОрганизации),
	               |			&ВидСубконто,
	               |			ВидУчета = &ВидУчетаПР
	               |				И Организация = &Организация
	               |				И Субконто1 В (&СписокОС)) КАК НалоговыйОстатки
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	НалоговыйОстатки.Субконто1,
	               |	0,
	               |	0,
	               |	0,
	               |	НалоговыйОстатки.СуммаОстатокДт,
	               |	0
	               |ИЗ
	               |	РегистрБухгалтерии.Налоговый.Остатки(
	               |			&КонДата,
	               |			Счет В ИЕРАРХИИ (&НалоговыйОСвОрганизации),
	               |			&ВидСубконто,
	               |			ВидУчета = &ВидУчетаВР
	               |				И Организация = &Организация
	               |				И Субконто1 В (&СписокОС)) КАК НалоговыйОстатки
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	НалоговыйОстатки.Субконто1,
	               |	0,
	               |	0,
	               |	0,
	               |	0,
	               |	НалоговыйОстатки.СуммаОстатокДт
	               |ИЗ
	               |	РегистрБухгалтерии.Хозрасчетный.Остатки(
	               |			&КонДата,
	               |			Счет В ИЕРАРХИИ (&ХозрасчетныйОСвОрганизации),
	               |			&ВидСубконто,
	               |			Организация = &Организация
	               |				И Субконто1 В (&СписокОС)) КАК НалоговыйОстатки
	               |ИТОГИ
	               |	СУММА(Сумма02ПР),
	               |	СУММА(Сумма02ВР),
	               |	СУММА(Сумма01ПР),
	               |	СУММА(Сумма01ВР),
	               |	СУММА(Сумма01)
	               |ПО
	               |	Субконто1";

	
                   Запрос.УстановитьПараметр("НачДата",  Новый Граница(НачалоМесяца(Дата),ВидГраницы.Включая));
                   Запрос.УстановитьПараметр("КонДата",  Новый Граница(КонецМесяца(Дата),ВидГраницы.Включая));
                   Запрос.УстановитьПараметр("Организация",Организация);
                   Запрос.УстановитьПараметр("ВидУчетаВР",Перечисления.ВидыУчетаПоПБУ18.ВР);
                   Запрос.УстановитьПараметр("ВидУчетаПР",Перечисления.ВидыУчетаПоПБУ18.ПР);
                   Запрос.УстановитьПараметр("ВидСубконто",ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства);
                   
                   Массив = Новый Массив;
                   Массив.Добавить(ПланыСчетов.Хозрасчетный.ОсновныеСредства);
                   Массив.Добавить(ПланыСчетов.Хозрасчетный.ДоходныеВложенияВ_МЦ);
                   Запрос.УстановитьПараметр("ХозрасчетныйОСвОрганизации", Массив);
                   Запрос.УстановитьПараметр("НалоговыйАмортизацияОС", ПланыСчетов.Налоговый.АмортизацияОсновныхСредств);
                   
                   Массив = Новый Массив;
                   Массив.Добавить(ПланыСчетов.Налоговый.ОсновныеСредства);
                   Массив.Добавить(ПланыСчетов.Налоговый.ДоходныеВложенияВ_МЦ);
                   Запрос.УстановитьПараметр("НалоговыйОСвОрганизации", Массив);
	Запрос.УстановитьПараметр("СписокОС",       СписокОС);
	Список = ОС.Выгрузить();
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Субконто1");
	Пока Выборка.Следующий() Цикл
		СтрокаТаблицы = Список.Найти(Выборка.Субконто1, "ОсновноеСредство");
		Если СтрокаТаблицы = NULL Тогда
			Продолжить;
		КонецЕсли;
		
		ПараметрыОС = ТаблицаПоОС.Найти(Выборка.Субконто1, "ОС_НУ");
		Если ПараметрыОС = Неопределено Тогда
			ПараметрыОС = ТаблицаПоОС.Найти(Выборка.Субконто1,"ОС_БУ");
			Если ПараметрыОС = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			СчетУчетаНУ                 = БухгалтерскийУчет.ПреобразоватьСчетаБУвСчетНУ(Новый Структура("СчетБУ",ПараметрыОС.СчетУчетаБУ), Ложь, СтруктураШапкиДокумента.Дата);
			СчетНачисленияАмортизацииНУ = БухгалтерскийУчет.ПреобразоватьСчетаБУвСчетНУ(Новый Структура("СчетБУ",ПараметрыОС.СчетНачисленияАмортизацииБУ), Ложь, СтруктураШапкиДокумента.Дата);
		Иначе
			Если Не ЗначениеЗаполнено(ПараметрыОС.СчетУчетаНУ) Тогда
				СчетУчетаНУ                 = БухгалтерскийУчет.ПреобразоватьСчетаБУвСчетНУ(Новый Структура("СчетБУ",ПараметрыОС.СчетУчетаБУ), Ложь, СтруктураШапкиДокумента.Дата);
			Иначе
				СчетУчетаНУ                 = ПараметрыОС.СчетУчетаНУ;
			КонецЕсли;
			Если Не ЗначениеЗаполнено(ПараметрыОС.СчетНачисленияАмортизацииНУ) Тогда
				СчетНачисленияАмортизацииНУ = БухгалтерскийУчет.ПреобразоватьСчетаБУвСчетНУ(Новый Структура("СчетБУ",ПараметрыОС.СчетНачисленияАмортизацииБУ), Ложь, СтруктураШапкиДокумента.Дата);
			Иначе
				СчетНачисленияАмортизацииНУ = ПараметрыОС.СчетНачисленияАмортизацииНУ;
			КонецЕсли;
		КонецЕсли;
		Если Выборка.Сумма01Вр <> 0 Тогда
			ПроводкаНУ              = ПроводкиНУ.Добавить();
			ПроводкаНУ.Период       = Дата;
			ПроводкаНУ.Организация  = СтруктураШапкиДокумента.Организация;
			
			ПроводкаНУ.СчетКт       = СчетУчетаНУ; 
			БухгалтерскийУчет.УстановитьСубконто(ПроводкаНУ.СчетКт,ПроводкаНУ.СубконтоКт, "ОсновныеСредства", Выборка.Субконто1);  
			ПроводкаНУ.ВидУчетаКт   = Перечисления.ВидыУчетаПоПБУ18.ВР;  
			ПроводкаНУ.Сумма        = Выборка.Сумма01Вр;
			ПроводкаНУ.Содержание   = "Списание ОС";
			ПроводкаНУ.СчетДт       = ПланыСчетов.Налоговый.ВыбытиеОС; 
			БухгалтерскийУчет.УстановитьСубконто(ПроводкаНУ.СчетДт,ПроводкаНУ.СубконтоДт, "ОсновныеСредства", Выборка.Субконто1);   			
			ПроводкаНУ.ВидУчетаДт   = Перечисления.ВидыУчетаПоПБУ18.ВР; 
			ПроводкаНУ.НомерЖурнала = "ВР";
		КонецЕсли;
		Если Выборка.Сумма01Вр - Выборка.Сумма02Вр <> 0 Тогда
			ПроводкаНУ              = ПроводкиНУ.Добавить();
			ПроводкаНУ.Период       = Дата;
			ПроводкаНУ.Организация  = СтруктураШапкиДокумента.Организация;
			
			ПроводкаНУ.СчетДт      = СчетСписанияНУ;
			БухгалтерскийУчет.УстановитьСубконто(ПроводкаНУ.СчетДт, ПроводкаНУ.СубконтоДт, 1, СубконтоНУ);
			БухгалтерскийУчет.УстановитьСубконто(ПроводкаНУ.Счетдт, ПроводкаНУ.СубконтоДт,"ОсновныеСредства", СтрокаТаблицы.ОсновноеСредство);
			ПроводкаНУ.ВидУчетаКт   = Перечисления.ВидыУчетаПоПБУ18.ВР;  
			ПроводкаНУ.Сумма        = Выборка.Сумма01Вр - Выборка.Сумма02Вр;
			ПроводкаНУ.Содержание   = "Списание ОС";
			ПроводкаНУ.СчетКт       = ПланыСчетов.Налоговый.ВыбытиеОС; 
			БухгалтерскийУчет.УстановитьСубконто(ПроводкаНУ.СчетКт,ПроводкаНУ.СубконтоКт, "ОсновныеСредства", Выборка.Субконто1);   			
			ПроводкаНУ.ВидУчетаДт   = Перечисления.ВидыУчетаПоПБУ18.ВР; 
			ПроводкаНУ.НомерЖурнала = "ВР";
		КонецЕсли;
		Если Выборка.Сумма02Вр <> 0 Тогда
			ПроводкаНУ              = ПроводкиНУ.Добавить();
			ПроводкаНУ.Период       = Дата;
			ПроводкаНУ.Организация  = СтруктураШапкиДокумента.Организация;
			
			ПроводкаНУ.СчетДт       = СчетНачисленияАмортизацииНУ; 
			БухгалтерскийУчет.УстановитьСубконто(ПроводкаНУ.СчетДт,ПроводкаНУ.СубконтоДт, "ОсновныеСредства", Выборка.Субконто1);  
			ПроводкаНУ.ВидУчетаКт   = Перечисления.ВидыУчетаПоПБУ18.ВР;  
			ПроводкаНУ.Сумма        = Выборка.Сумма02Вр;
			ПроводкаНУ.Содержание   = "Списание ОС";
			ПроводкаНУ.СчетКт       = ПланыСчетов.Налоговый.ВыбытиеОС; 
			БухгалтерскийУчет.УстановитьСубконто(ПроводкаНУ.СчетКт,ПроводкаНУ.СубконтоКт, "ОсновныеСредства", Выборка.Субконто1);   			
			ПроводкаНУ.ВидУчетаДт   = Перечисления.ВидыУчетаПоПБУ18.ВР; 
			ПроводкаНУ.НомерЖурнала = "ВР";
			
		КонецЕсли;
		
		Если Выборка.Сумма01Пр <> 0 Тогда
			ПроводкаНУ              = ПроводкиНУ.Добавить();
			ПроводкаНУ.Период       = Дата;
			ПроводкаНУ.Организация  = СтруктураШапкиДокумента.Организация;
			
			ПроводкаНУ.СчетКт       = СчетУчетаНУ; 
			БухгалтерскийУчет.УстановитьСубконто(ПроводкаНУ.СчетКт,ПроводкаНУ.СубконтоКт, "ОсновныеСредства", Выборка.Субконто1);  
			ПроводкаНУ.ВидУчетаКт   = Перечисления.ВидыУчетаПоПБУ18.ПР;  
			ПроводкаНУ.Сумма        = Выборка.Сумма01Пр;
			ПроводкаНУ.Содержание   = "Списание ОС";
			ПроводкаНУ.СчетДт       = ПланыСчетов.Налоговый.ВыбытиеОС; 
			БухгалтерскийУчет.УстановитьСубконто(ПроводкаНУ.СчетДт,ПроводкаНУ.СубконтоДт, "ОсновныеСредства", Выборка.Субконто1);   			
			ПроводкаНУ.ВидУчетаДт   = Перечисления.ВидыУчетаПоПБУ18.ПР; 
			ПроводкаНУ.НомерЖурнала = "ПР";
		КонецЕсли;
		Если Выборка.Сумма01Пр - Выборка.Сумма02Пр <> 0 Тогда
			ПроводкаНУ              = ПроводкиНУ.Добавить();
			ПроводкаНУ.Период       = Дата;
			ПроводкаНУ.Организация  = СтруктураШапкиДокумента.Организация;
			ПроводкаНУ.СчетДт      = СчетСписанияНУ;
			БухгалтерскийУчет.УстановитьСубконто(ПроводкаНУ.СчетДт, ПроводкаНУ.СубконтоДт, 1, СубконтоНУ);
			БухгалтерскийУчет.УстановитьСубконто(ПроводкаНУ.Счетдт, ПроводкаНУ.СубконтоДт,"ОсновныеСредства", СтрокаТаблицы.ОсновноеСредство);
			ПроводкаНУ.ВидУчетаКт   = Перечисления.ВидыУчетаПоПБУ18.ПР;  
			ПроводкаНУ.Сумма        = Выборка.Сумма01Пр - Выборка.Сумма02Пр;
			ПроводкаНУ.Содержание   = "Списание ОС";
			ПроводкаНУ.СчетКт       = ПланыСчетов.Налоговый.ВыбытиеОС; 
			БухгалтерскийУчет.УстановитьСубконто(ПроводкаНУ.СчетКт,ПроводкаНУ.СубконтоКт, "ОсновныеСредства", Выборка.Субконто1);   			
			ПроводкаНУ.ВидУчетаДт   = Перечисления.ВидыУчетаПоПБУ18.ПР; 
			ПроводкаНУ.НомерЖурнала = "ПР";
		КонецЕсли;
		Если Выборка.Сумма02Пр <> 0 Тогда		
			ПроводкаНУ              = ПроводкиНУ.Добавить();
			ПроводкаНУ.Период       = Дата;
			ПроводкаНУ.Организация  = СтруктураШапкиДокумента.Организация;
			
			ПроводкаНУ.СчетДт       = СчетНачисленияАмортизацииНУ; 
			БухгалтерскийУчет.УстановитьСубконто(ПроводкаНУ.СчетДт,ПроводкаНУ.СубконтоДт, "ОсновныеСредства", Выборка.Субконто1);  
			ПроводкаНУ.ВидУчетаКт   = Перечисления.ВидыУчетаПоПБУ18.ПР;  
			ПроводкаНУ.Сумма        = Выборка.Сумма02Пр;
			ПроводкаНУ.Содержание   = "Списание ОС";
			ПроводкаНУ.СчетКт       = ПланыСчетов.Налоговый.ВыбытиеОС; 
			БухгалтерскийУчет.УстановитьСубконто(ПроводкаНУ.СчетКт,ПроводкаНУ.СубконтоКт, "ОсновныеСредства", Выборка.Субконто1);   			
			ПроводкаНУ.ВидУчетаДт   = Перечисления.ВидыУчетаПоПБУ18.ПР; 
			ПроводкаНУ.НомерЖурнала = "ПР";
		КонецЕсли;
	КонецЦикла;
	
	ПроводкиНУ.Записать(Ложь);
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ
// Процедура - обработчик события "ОбработкаЗаполнения".
//
Процедура ОбработкаЗаполнения(Основание)

	Если ТипЗнч(Основание) = Тип("СправочникСсылка.ОсновныеСредства") Тогда

		СтрокаТабличнойЧасти = ОС.Добавить();

		СтрокаТабличнойЧасти.ОсновноеСредство = Основание.Ссылка;

	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ИнвентаризацияОС") Тогда
		
		Организация = Основание.Организация;
		
		Для Каждого ТекСтрокаОС Из Основание.ОС Цикл
			
			Если ТекСтрокаОС.НаличиеПоДаннымУчета И НЕ ТекСтрокаОС.НаличиеФактическое Тогда
				
				НоваяСтрока = ОС.Добавить();
				НоваяСтрока.ОсновноеСредство = ТекСтрокаОС.ОсновноеСредство;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры // ОбработкаЗаполнения()

// Процедура - обработчик события "Обработка проведения" 
//
Процедура ОбработкаПроведения(Отказ,РежимПроведения)

	
	Если мУдалятьДвижения Тогда
		ОбщегоНазначения.УдалитьДвиженияРегистратора(ЭтотОбъект, Отказ);
	КонецЕсли;

	ЗаполнитьСписокРеквизитовЗависимыхОтТиповУчета();

	Заголовок = ОбщегоНазначения.ПредставлениеДокументаПриПроведении(Ссылка);

	// Сформируем структуру реквизитов шапки документа
	СтруктураШапкиДокумента = ОбщегоНазначения.СформироватьСтруктуруШапкиДокумента(ЭтотОбъект);
	//начало изменений БП 07 
	СтруктураШапкиДокумента.ОтражатьВУправленческомУчете = Не ПараметрыСеанса.НеведетсяУПРУчетВЧастиЗатратИОС;
	//конец изменений БП 07	 
	СтруктураШапкиДокумента.Вставить("ПериодРегистрации", КонецМесяца(Дата));
	Если СтруктураШапкиДокумента.ОтражатьВБухгалтерскомУчете ИЛИ СтруктураШапкиДокумента.ОтражатьВНалоговомУчете Тогда
		СтруктураПолейУчетнойПолитикиНУ = Новый Структура("ПоддержкаПБУ18, МетодНачисленияАмортизацииНУ");
		ОбщегоНазначения.ДополнитьПоложениямиУчетнойПолитики(СтруктураШапкиДокумента, СтруктураШапкиДокумента.Дата, Отказ, СтруктураШапкиДокумента.Организация, "Бух", СтруктураПолейУчетнойПолитикиНУ);
	КонецЕсли;
	
	// Проверим правильность заполнения шапки документа
	ПроверитьЗаполнениеШапки(СтруктураШапкиДокумента, Отказ, Заголовок);

	ПроверкаРеквизитовТЧ(РежимПроведения,Отказ, Заголовок, СтруктураШапкиДокумента);

	СтруктураПолей = Новый Структура;
	СтруктураПолей.Вставить("ОсновноеСредство",     "ОсновноеСредство");
	СтруктураПолей.Вставить("Стоимость",            "Стоимость");
	СтруктураПолей.Вставить("Амортизация",          "Амортизация");
	СтруктураПолей.Вставить("АмортизацияЗаМесяц",   "АмортизацияЗаМесяц");
	СтруктураПолей.Вставить("СписаноНаЗатраты",     "СписаноНаЗатраты");

	СтруктураПолей.Вставить("СтоимостьБУ",          "СтоимостьБУ");
	СтруктураПолей.Вставить("АмортизацияБУ",        "АмортизацияБУ");
	СтруктураПолей.Вставить("АмортизацияЗаМесяцБУ", "АмортизацияЗаМесяцБУ");
	СтруктураПолей.Вставить("СписаноНаЗатратыБУ",   "СписаноНаЗатратыБУ");

	СтруктураПолей.Вставить("СтоимостьНУ",          "СтоимостьНУ");
	СтруктураПолей.Вставить("АмортизацияНУ",        "АмортизацияНУ");
	СтруктураПолей.Вставить("АмортизацияЗаМесяцНУ", "АмортизацияЗаМесяцНУ");
	//начало изменений БП 07 
	СтруктураПолей.Вставить("ПодразделениеОрганизации", "ПодразделениеОрганизации");
	//конец изменений БП 07	 

	СтруктураПолей.Вставить("СуммаКапитальныхВложенийВключаемыхВРасходыНУ", "СуммаКапитальныхВложенийВключаемыхВРасходыНУ");
	
	РезультатЗапросаПоОС = УправлениеЗапасами.СформироватьЗапросПоТабличнойЧасти(ЭтотОбъект, "ОС", СтруктураПолей);
	ТаблицаПоОС          = РезультатЗапросаПоОС.Выгрузить();

	УправлениеВнеоборотнымиАктивами.ПроверитьДубли(ТаблицаПоОС, "Основные средства", "ОсновноеСредство", "Основное средство", Отказ, Заголовок);	
	
	// подготовка таблицы для списания
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТекОрганизация",  СтруктураШапкиДокумента.Организация);
	Запрос.УстановитьПараметр("ТекПериод",       Дата);
	Запрос.УстановитьПараметр("ВнешнийИсточник", ТаблицаПоОС);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
   	|	ОсновноеСредство
	|ПОМЕСТИТЬ ОсновныеСредства
	|ИЗ &ВнешнийИсточник КАК ВнешнийИсточник
	|";
	Запрос.Выполнить();
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОсновныеСредства.ОсновноеСредство                                                                         КАК ОсновноеСредство,
	|	СпособыОтраженияРасходовПоАмортизацииБухгалтерскийУчетСрезПоследних.ОсновноеСредство                      КАК ОС_БУ,
	|	СпособыОтраженияРасходовПоАмортизацииНалоговыйУчетСрезПоследних.ОсновноеСредство                          КАК ОС_НУ,
	|	СпособыОтраженияРасходовПоАмортизацииСрезПоследних.ОсновноеСредство                                       КАК ОС_УУ,
	|	СпособыОтраженияРасходовПоАмортизацииБухгалтерскийУчетСрезПоследних.СпособыОтраженияРасходовПоАмортизации КАК Направление_БУ,
	|	СпособыОтраженияРасходовПоАмортизацииНалоговыйУчетСрезПоследних.СпособыОтраженияРасходовПоАмортизации     КАК Направление_НУ,
	|	СпособыОтраженияРасходовПоАмортизацииСрезПоследних.СпособыОтраженияРасходовПоАмортизации                  КАК Направление_УУ,
	|	ПервоначальныеСведенияОСНалоговыйУчетСрезПоследних.ПорядокВключенияСтоимостиВСоставРасходов    КАК ПорядокВключенияСтоимостиВСоставРасходовНУ
	|ИЗ
	|	ОсновныеСредства
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СпособыОтраженияРасходовПоАмортизацииОСБухгалтерскийУчет.СрезПоследних(&ТекПериод, ОсновноеСредство В (ВЫБРАТЬ ОсновноеСредство ИЗ ОсновныеСредства) И Организация = &ТекОрганизация) КАК СпособыОтраженияРасходовПоАмортизацииБухгалтерскийУчетСрезПоследних
	|	ПО ОсновныеСредства.ОсновноеСредство = СпособыОтраженияРасходовПоАмортизацииБухгалтерскийУчетСрезПоследних.ОсновноеСредство
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СпособыОтраженияРасходовПоАмортизацииОСНалоговыйУчет.СрезПоследних(&ТекПериод, ОсновноеСредство В (ВЫБРАТЬ ОсновноеСредство ИЗ ОсновныеСредства) И Организация = &ТекОрганизация) КАК СпособыОтраженияРасходовПоАмортизацииНалоговыйУчетСрезПоследних
	|	ПО ОсновныеСредства.ОсновноеСредство = СпособыОтраженияРасходовПоАмортизацииНалоговыйУчетСрезПоследних.ОсновноеСредство
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СпособыОтраженияРасходовПоАмортизацииОС.СрезПоследних(&ТекПериод, ОсновноеСредство В (ВЫБРАТЬ ОсновноеСредство ИЗ ОсновныеСредства)) КАК СпособыОтраженияРасходовПоАмортизацииСрезПоследних
	|	ПО ОсновныеСредства.ОсновноеСредство = СпособыОтраженияРасходовПоАмортизацииСрезПоследних.ОсновноеСредство
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОСНалоговыйУчет.СрезПоследних(&ТекПериод, ОсновноеСредство В (ВЫБРАТЬ ОсновноеСредство ИЗ ОсновныеСредства) И Организация = &ТекОрганизация) КАК ПервоначальныеСведенияОСНалоговыйУчетСрезПоследних
	|	ПО ОсновныеСредства.ОсновноеСредство = ПервоначальныеСведенияОСНалоговыйУчетСрезПоследних.ОсновноеСредство";
	ТаблицаНаправлений = Запрос.Выполнить().Выгрузить();

	ТаблицаПООс.Колонки.Добавить("ПорядокВключенияСтоимостиВСоставРасходовНУ");
	
	Для Каждого СтрокаОС ИЗ ТаблицаПоОС Цикл
		СтрокаНаправлений = ТаблицаНаправлений.Найти(СтрокаОС.ОсновноеСредство, "ОсновноеСредство");
		СтрокаОС.ПорядокВключенияСтоимостиВСоставРасходовНУ = ?(СтрокаНаправлений = Неопределено, Ложь, СтрокаНаправлений.ПорядокВключенияСтоимостиВСоставРасходовНУ);
	КонецЦикла;
	
	Если Не Отказ Тогда
		ДвиженияПоРегистрам(СтруктураШапкиДокумента,ТаблицаПоОС,ТаблицаНаправлений, Отказ, Заголовок);
	КонецЕсли;

КонецПроцедуры // ОбработкаПроведения()

// Процедура - обработчик события "При копировании" 
//
Процедура ПриКопировании(ОбъектКопирования)
	
	мСкопирован = Истина;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка  Тогда
		Возврат;
	КонецЕсли;


	 
	мУдалятьДвижения = НЕ ЭтоНовый();
	
	//начало изменений БП 07 
	Если ПараметрыСеанса.НеведетсяУПРУчетВЧастиЗатратИОС Тогда
		Событие = СобытиеРегл;
	КонецЕсли;	
	//конец изменений БП 07 
КонецПроцедуры // ПередЗаписью

Процедура ПриЗаписи(Отказ)
	Если ОбменДанными.Загрузка  Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры

///Вадим 09.10.2013 11:29:40
Процедура Проверка83ИСозданиеПроводок(ПроводкиБУ)
    счет83=ПланыСчетов.Хозрасчетный.ПриростСтоимостиИмуществаПоПереоценке;
	счет84=ПланыСчетов.Хозрасчетный.ПрибыльПодлежащаяРаспределению;
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ХозрасчетныйОстатки.Счет,
		|	ХозрасчетныйОстатки.Субконто1,
		|	СУММА(ХозрасчетныйОстатки.СуммаОстаток) КАК Сумма
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Остатки(&Дата, Счет = &Счет, , Субконто1 В (&ОС)) КАК ХозрасчетныйОстатки
		|
		|СГРУППИРОВАТЬ ПО
		|	ХозрасчетныйОстатки.Субконто1,
		|	ХозрасчетныйОстатки.Счет";

	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("ОС", ОС.ВыгрузитьКолонку("ОсновноеСредство"));
	Запрос.УстановитьПараметр("Счет", счет83);

	Результат = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = Результат.Выбрать();
 	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Проводка = ПроводкиБУ.Добавить();
		
		Проводка.Период       = Дата;
		Проводка.Организация  = Организация;
		Проводка.Содержание   = "Сумма дооценки";
		Проводка.Сумма        = -ВыборкаДетальныеЗаписи.Сумма;
		Проводка.НомерЖурнала = "ОС";
		//
		Проводка.СчетДт      = счет83;
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "ОсновныеСредства", ВыборкаДетальныеЗаписи.Субконто1);
		//
		Проводка.СчетКт      = счет84;
		//БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "ОсновныеСредства", ТекОС);
	КонецЦикла;


	

КонецПроцедуры
//ВадимКонец


мСкопирован = Ложь;


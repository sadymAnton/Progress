// Процедура печатает карточки депонентов, зарегистрированные указанным документом
//
// Параметры:
//	Документ - документ-объект, депонировавший зарплату
//	ДепонируемыеСуммы - таблица значений с колонками:
//		Физлицо
//		Ведомость
//		Сумма
//
Функция ПечатьКарточкиДепонента(Документ, ДепонируемыеСуммы = Неопределено) Экспорт

	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ПолеСлева = 0;
	ТабДокумент.ПолеСправа = 0;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ДепонированиеОрганизаций_КарточкиДепонента";
	
	Макет = ПолучитьМакет("КарточкаДепонента");
	
	// получаем данные для печати
	Шапка = Новый Структура;
	Шапка.Вставить("Номер",		Документ.Номер);
	Шапка.Вставить("ДатаДок",	Документ.Дата);
	Шапка.Вставить("НазваниеОрганизации", СокрЛП(Документ.Организация.НаименованиеПолное));
	
	Депоненты	= ДепонентыПоДокументу(Документ, ДепонируемыеСуммы);
	
	ОбластьМакета = Макет.ПолучитьОбласть("Карточка");
	
	// выводим общие данные
	ОбластьМакета.Параметры.Заполнить(Шапка); // Шапка документа.
	
	ВыведеноСтрок = 0;
	ВалютаРасчетов = Константы.ВалютаРегламентированногоУчета.Получить();
	// выводим данные по строкам документа.
	Для Каждого Депонент Из Депоненты Цикл
	
		ВыведеноСтрок = ВыведеноСтрок + 1;
		ОбластьМакета.Параметры.Заполнить(Депонент);
		ОбластьМакета.Параметры.НомерКарточки = "" + Шапка.Номер + "/" + ВыведеноСтрок;
		ОбластьМакета.Параметры.СуммаПрописью = ОбщегоНазначенияЗК.СформироватьСуммуПрописью(Депонент.Сумма, ВалютаРасчетов);
		ТабДокумент.Вывести(ОбластьМакета);
		
		// разбиение на страницы
		Если ВыведеноСтрок % 2 Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;

	КонецЦикла;
	
	Возврат ТабДокумент

КонецФункции

Функция ДепонентыПоДокументу(Документ, ДепонируемыеСуммы = Неопределено)
	
	Запрос = Новый Запрос;

	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДатаДепонирования",	Документ.Дата);
	Запрос.УстановитьПараметр("Организация", 		ОбщегоНазначенияЗК.ГоловнаяОрганизация(Документ.Организация));

	Если ДепонируемыеСуммы = Неопределено Тогда
		Запрос.УстановитьПараметр("Регистратор", Документ.Ссылка);
		ТекстЗапросаДепонируемыхСумм = 
		"ВЫБРАТЬ
		|	ВзаиморасчетыСДепонентамиОрганизаций.Физлицо,
		|	ВзаиморасчетыСДепонентамиОрганизаций.Ведомость,
		|	ВзаиморасчетыСДепонентамиОрганизаций.Сумма
		|ПОМЕСТИТЬ ВТДепонированныеСуммы
		|ИЗ
		|	РегистрНакопления.ВзаиморасчетыСДепонентамиОрганизаций КАК ВзаиморасчетыСДепонентамиОрганизаций
		|ГДЕ
		|	ВзаиморасчетыСДепонентамиОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	И ВзаиморасчетыСДепонентамиОрганизаций.Регистратор = &Регистратор"
	Иначе
		Запрос.УстановитьПараметр("ДепонируемыеСуммы", ДепонируемыеСуммы);
		ТекстЗапросаДепонируемыхСумм = 
		"ВЫБРАТЬ
		|	ДепонируемыеСуммы.Физлицо,
		|	ДепонируемыеСуммы.Ведомость,
		|	ДепонируемыеСуммы.Сумма
		|ПОМЕСТИТЬ ВТДепонированныеСуммы
		|ИЗ
		|	&ДепонируемыеСуммы КАК ДепонируемыеСуммы"
	КонецЕсли;
	
	Запрос.Текст = 
	ТекстЗапросаДепонируемыхСумм +
    Символы.ПС +
	";
	|
	|////////////////////////////////////////////////////////////////////////////////" +
    Символы.ПС +
	"ВЫБРАТЬ
	|	ДепонированныеСуммы.Ведомость.Номер КАК НомерВедомости,
	|	ДепонированныеСуммы.Ведомость.Дата КАК ДатаВедомости,
	|	ДепонированныеСуммы.Сумма,
	|	ЕСТЬNULL(ФИОФизЛицСрезПоследних.Фамилия + ВЫБОР
	|			КОГДА ПОДСТРОКА(ФИОФизЛицСрезПоследних.Имя, 1, 1) <> """"
	|				ТОГДА "" "" + ПОДСТРОКА(ФИОФизЛицСрезПоследних.Имя, 1, 1) + "".""
	|			ИНАЧЕ """"
	|		КОНЕЦ + ВЫБОР
	|			КОГДА ПОДСТРОКА(ФИОФизЛицСрезПоследних.Отчество, 1, 1) <> """"
	|				ТОГДА "" "" + ПОДСТРОКА(ФИОФизЛицСрезПоследних.Отчество, 1, 1) + "".""
	|			ИНАЧЕ """"
	|		КОНЕЦ, ДепонированныеСуммы.Физлицо.Наименование) КАК ФИОРаботника,
	|	РаботникиОрганизаций.Сотрудник.Код КАК ТабельныйНомер
	|ИЗ
	|	ВТДепонированныеСуммы КАК ДепонированныеСуммы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(
	|				&ДатаДепонирования,
	|				Физлицо В
	|					(ВЫБРАТЬ
	|						ДепонированныеСуммы.Физлицо
	|					ИЗ
	|						ВТДепонированныеСуммы КАК ДепонированныеСуммы)) КАК ФИОФизЛицСрезПоследних
	|		ПО ДепонированныеСуммы.Физлицо = ФИОФизЛицСрезПоследних.ФизЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций.СрезПоследних(
	|				&ДатаДепонирования,
	|				Сотрудник.Физлицо В
	|						(ВЫБРАТЬ
	|							ДепонированныеСуммы.Физлицо
	|						ИЗ
	|							ВТДепонированныеСуммы КАК ДепонированныеСуммы)
	|					И Сотрудник.ВидЗанятости <> ЗНАЧЕНИЕ(Перечисление.ВидыЗанятостиВОрганизации.ВнутреннееСовместительство)
	|					И Организация = &Организация) КАК РаботникиОрганизаций
	|		ПО (РаботникиОрганизаций.ПричинаИзмененияСостояния <> ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.Увольнение))
	|			И ДепонированныеСуммы.Физлицо = РаботникиОрганизаций.Сотрудник.Физлицо
	|
	|УПОРЯДОЧИТЬ ПО
	|	ФИОРаботника";
	
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции	
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОКУМЕНТА

#Если ТолстыйКлиентОбычноеПриложение Тогда

// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходимое количество копий.
//
//  Название макета печати передается в качестве параметра,
// по переданному названию находим имя макета в соответствии.
//
// Параметры:
//  НазваниеМакета - строка, название макета.
//
Функция Печать(ИмяМакета, КоличествоЭкземпляров = 1, НаПринтер = Ложь) Экспорт
	
	// Получить экземпляр документа на печать
	Если ИмяМакета = "КарточкаДепонента" тогда
		
		ТабДокумент = Документы.ДепонированиеОрганизаций.ПечатьКарточкиДепонента(ЭтотОбъект, ДепонируемыеСуммыДляПечати()); 	
		УниверсальныеМеханизмы.НапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер, ОбщегоНазначенияЗК.СформироватьЗаголовокДокумента(ЭтотОбъект,"Карточки депонентов"));
		
	КонецЕсли;

КонецФункции // Печать

#КонецЕсли

// Возвращает доступные варианты печати документа
//
// Возвращаемое значение:
//  Структура, каждая строка которой соответствует одному из вариантов печати
//  
Функция ПолучитьСтруктуруПечатныхФорм() Экспорт
	
	Возврат Новый Структура("КарточкаДепонента","Карточка депонента");

КонецФункции // ПолучитьСтруктуруПечатныхФорм()

// Определяет сумму неполученной заработной платы по платежной ведомости
//
// Параметры
//  Ведомость - ДокументСсылка.ЗарплатаКВыплатеОрганизации - обрабатываемая платежная ведомость
//
// Возвращаемое значение:
//   число - сумма к депонированию
//
Функция ПолучитьДанныеПлатежнойВедомостиДляДепонирования(Ведомости) Экспорт
	
	ТаблицаВыплат = ЗарплатаКВыплатеОрганизацийДополнительный.ПолучитьДанныеДляВыплаты(Ведомости, Перечисления.ВыплаченностьЗарплаты.Задепонировано, Ссылка);
	
	ТаблицаВыплат.Свернуть("Ведомость", "СуммаКВыплате");
	
	ТаблицаВыплат.Колонки.Ведомость.Имя		= "Ссылка";
	ТаблицаВыплат.Колонки.СуммаКВыплате.Имя	= "Сумма";
	
	Возврат ТаблицаВыплат;
	
КонецФункции // ПолучитьДанныеПлатежнойВедомостиДляДепонирования()

// Выполняет автоматическое заполнение документа 
// 
// Параметры: 
//  нет
//
// Возвращаемое значение:
//  нет
//
Процедура Автозаполнение() Экспорт

	Запрос = Новый Запрос();
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ЗарплатаКВыплатеОрганизацииЗарплата.Ссылка КАК Ведомость,
	|	СУММА(ЗарплатаКВыплатеОрганизацииЗарплата.Сумма + ЗарплатаКВыплатеОрганизацииЗарплата.КомпенсацияЗаЗадержкуЗарплаты) КАК Сумма
	|ИЗ
	|	Документ.ЗарплатаКВыплатеОрганизаций.Зарплата КАК ЗарплатаКВыплатеОрганизацииЗарплата
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОВыплатахРаботникамОрганизацийПоПлатежнымВедомостям КАК РанееВыплаченныеСуммы
	|		ПО ЗарплатаКВыплатеОрганизацииЗарплата.Ссылка = РанееВыплаченныеСуммы.Ведомость
	|			И ЗарплатаКВыплатеОрганизацииЗарплата.Физлицо = РанееВыплаченныеСуммы.Физлицо
	|			И (РанееВыплаченныеСуммы.Регистратор <> &Регистратор)
	|ГДЕ
	|	(НЕ ЗарплатаКВыплатеОрганизацииЗарплата.Ссылка В
	|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					ЗарплатаКВыплатеОрганизацииЗарплата.Ссылка
	|				ИЗ
	|					Документ.ЗарплатаКВыплатеОрганизаций.Зарплата КАК ЗарплатаКВыплатеОрганизацииЗарплата
	|				ГДЕ
	|					ЗарплатаКВыплатеОрганизацииЗарплата.Ссылка.СпособВыплаты = ЗНАЧЕНИЕ(Перечисление.СпособыВыплатыЗарплаты.ЧерезКассу)
	|					И ЗарплатаКВыплатеОрганизацииЗарплата.ВыплаченностьЗарплаты = ЗНАЧЕНИЕ(Перечисление.ВыплаченностьЗарплаты.НеВыплачено)
	|					И ЗарплатаКВыплатеОрганизацииЗарплата.Ссылка.Организация = &Организация))
	|	И ЗарплатаКВыплатеОрганизацииЗарплата.Ссылка.Организация = &Организация
	|	И ЗарплатаКВыплатеОрганизацииЗарплата.Ссылка.СпособВыплаты = ЗНАЧЕНИЕ(Перечисление.СпособыВыплатыЗарплаты.ЧерезКассу)
	|	И ЗарплатаКВыплатеОрганизацииЗарплата.ВыплаченностьЗарплаты = ЗНАЧЕНИЕ(Перечисление.ВыплаченностьЗарплаты.Задепонировано)
	|	И ЗарплатаКВыплатеОрганизацииЗарплата.Ссылка.Дата <= &Дата
	|	И РанееВыплаченныеСуммы.Физлицо ЕСТЬ NULL 
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗарплатаКВыплатеОрганизацииЗарплата.Ссылка";
	
	Запрос.УстановитьПараметр("Организация",Организация);
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	Запрос.УстановитьПараметр("Дата",?(ЭтоНовый(),КонецДня(Дата),Дата));
	ДепонированиеЗаработнойПлаты.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры //  Автозаполнение

////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
//

Процедура ЗаполнитьПоЗарплатеКВыплатеОрганизации(Основание)
	
	ШапкаОснования = ЗарплатаКВыплатеОрганизацийДополнительный.ПолучитьДанныеДокумента(Основание);
	
	Если НЕ ШапкаОснования.Проведен Тогда
		ВызватьИсключение "Платежная ведомость не проведена! Заполнение не выполнялось.";
	КонецЕсли;
	
	Если ШапкаОснования.СпособВыплаты = Перечисления.СпособыВыплатыЗарплаты.ЧерезБанк Тогда
		ВызватьИсключение "Платежная ведомость оформлена на перечисление в банк! Заполнение не выполнялось.";
	КонецЕсли;
	
	// Получим подлежащую депонированию заработную плату
	ТаблицаВыплат = ЗарплатаКВыплатеОрганизацийДополнительный.ПолучитьДанныеДляВыплаты(Основание.Ссылка, Перечисления.ВыплаченностьЗарплаты.Задепонировано);

	Если ТаблицаВыплат.Количество() = 0 Тогда
		ВызватьИсключение "По текущей ведомости нет сумм к депонированию!";
	КонецЕсли;	
	
	// Заполним реквизиты по документу-основанию.
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ШапкаОснования,, "Проведен, ПометкаУдаления");

	ТаблицаРаботников = ТаблицаВыплат.Скопировать();
	
	ТаблицаВыплат.Свернуть("Ведомость", "СуммаКВыплате");
	
	СтрокаВедомость = ДепонированиеЗаработнойПлаты.Добавить();
	СтрокаВедомость.Ведомость	= ТаблицаВыплат[0].Ведомость;
	СтрокаВедомость.Сумма		= ТаблицаВыплат[0].СуммаКВыплате;

	СуммаДокумента = ТаблицаВыплат[0].СуммаКВыплате;
	
	ТаблицаРаботников.Свернуть("Физлицо", "СуммаКВыплате");
	ТаблицаРаботников.Колонки.СуммаКВыплате.Имя = "Сумма";

	Работники.Загрузить(ТаблицаРаботников);
	
КонецПроцедуры

Функция ДепонируемыеСуммыДляПечати()

	Запрос = Новый Запрос;

	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка",   Ссылка);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗарплатаКВыплатеОрганизацииЗарплата.Физлицо КАК Физлицо,
	|	ДепонированиеОрганизацийДепонированиеЗаработнойПлаты.Ведомость КАК Ведомость,
	|	СУММА(ЗарплатаКВыплатеОрганизацииЗарплата.Сумма + ЗарплатаКВыплатеОрганизацииЗарплата.КомпенсацияЗаЗадержкуЗарплаты) КАК Сумма
	|ИЗ
	|	Документ.ДепонированиеОрганизаций.ДепонированиеЗаработнойПлаты КАК ДепонированиеОрганизацийДепонированиеЗаработнойПлаты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗарплатаКВыплатеОрганизаций.Зарплата КАК ЗарплатаКВыплатеОрганизацииЗарплата
	|		ПО ДепонированиеОрганизацийДепонированиеЗаработнойПлаты.Ведомость = ЗарплатаКВыплатеОрганизацииЗарплата.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОВыплатахРаботникамОрганизацийПоПлатежнымВедомостям КАК РанееВыплаченныеСуммы
	|		ПО ДепонированиеОрганизацийДепонированиеЗаработнойПлаты.Ведомость = РанееВыплаченныеСуммы.Ведомость
	|			И (ЗарплатаКВыплатеОрганизацииЗарплата.Физлицо = РанееВыплаченныеСуммы.Физлицо)
	|			И (РанееВыплаченныеСуммы.Регистратор <> &ДокументСсылка)
	|ГДЕ
	|	ДепонированиеОрганизацийДепонированиеЗаработнойПлаты.Ссылка = &ДокументСсылка
	|	И ЗарплатаКВыплатеОрганизацииЗарплата.ВыплаченностьЗарплаты = ЗНАЧЕНИЕ(Перечисление.ВыплаченностьЗарплаты.Задепонировано)
	|	И РанееВыплаченныеСуммы.Физлицо ЕСТЬ NULL 
	|
	|СГРУППИРОВАТЬ ПО
	|	ДепонированиеОрганизацийДепонированиеЗаработнойПлаты.Ведомость,
	|	ЗарплатаКВыплатеОрганизацииЗарплата.Физлицо";

	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции // ДепонируемыеСуммыДляПечати()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

Функция НадоФормироватьНалоговыеПроводки(ФормироватьНалоговыеПроводки = Неопределено)
	
	Если ФормироватьНалоговыеПроводки = Неопределено Тогда
		// Определим необходимость формирования налоговых проводок
		Если ОтражатьВНалоговомУчете Тогда
			УчетнаяПолитика = ОбщегоНазначения.ПолучитьПараметрыУчетнойПолитикиРегл(КонецКвартала(Дата), Организация);
			ФормироватьНалоговыеПроводки =
				ЗначениеЗаполнено(УчетнаяПолитика) И (НЕ УчетнаяПолитика.УСН) И УчетнаяПолитика.ПоддержкаПБУ18;
		Иначе
			ФормироватьНалоговыеПроводки = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ФормироватьНалоговыеПроводки;
	
КонецФункции

// Формирует запрос по шапке документа
//
// Параметры: 
//  Режим - режим проведения
//
// Возвращаемое значение:
//  Результат запроса
//
Функция СформироватьЗапросПоШапке(ФормироватьНалоговыеПроводки = Неопределено)
	
	ФормироватьНалоговыеПроводки = НадоФормироватьНалоговыеПроводки(ФормироватьНалоговыеПроводки);
	
	Запрос = Новый Запрос;

	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка" , Ссылка);

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДепонированиеОрганизаций.Дата,
	|	ДепонированиеОрганизаций.Ссылка,
	|	ДепонированиеОрганизаций.Организация,
	|	ВЫБОР
	|		КОГДА ДепонированиеОрганизаций.Организация.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ДепонированиеОрганизаций.Организация
	|		ИНАЧЕ ДепонированиеОрганизаций.Организация.ГоловнаяОрганизация
	|	КОНЕЦ КАК ГоловнаяОрганизация";
	Если ФормироватьНалоговыеПроводки Тогда
		ТекстЗапроса = ТекстЗапроса + ",
	|	ДепонированиеОрганизаций.СчетУчетаРасходов,
	|	ЕСТЬNULL(ОбщиеСуммыПоВедомости.КомпенсацияЗаЗадержкуЗарплаты, 0) КАК КомпенсацияЗаЗадержкуЗарплаты";
	КонецЕсли;
	ТекстЗапроса = ТекстЗапроса + "
	|ИЗ
	|	Документ.ДепонированиеОрганизаций КАК ДепонированиеОрганизаций";
	Если ФормироватьНалоговыеПроводки Тогда
		ТекстЗапроса = ТекстЗапроса + "
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			СУММА(ЗарплатаКВыплатеОрганизацииЗарплата.КомпенсацияЗаЗадержкуЗарплаты) КАК КомпенсацияЗаЗадержкуЗарплаты
	|		ИЗ
	|			Документ.ДепонированиеОрганизаций.ДепонированиеЗаработнойПлаты КАК ДепонированиеОрганизацийДепонированиеЗаработнойПлаты
	|				ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗарплатаКВыплатеОрганизаций.Зарплата КАК ЗарплатаКВыплатеОрганизацииЗарплата
	|				ПО ДепонированиеОрганизацийДепонированиеЗаработнойПлаты.Ведомость = ЗарплатаКВыплатеОрганизацииЗарплата.Ссылка
	|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОВыплатахРаботникамОрганизацийПоПлатежнымВедомостям КАК РанееВыплаченныеСуммы
	|				ПО ДепонированиеОрганизацийДепонированиеЗаработнойПлаты.Ведомость = РанееВыплаченныеСуммы.Ведомость
	|					И ЗарплатаКВыплатеОрганизацииЗарплата.Физлицо = РанееВыплаченныеСуммы.Физлицо 
	|					И РанееВыплаченныеСуммы.Регистратор <> &ДокументСсылка
	|		ГДЕ
	|			ДепонированиеОрганизацийДепонированиеЗаработнойПлаты.Ссылка = &ДокументСсылка
	|			И РанееВыплаченныеСуммы.Физлицо ЕСТЬ NULL 
	|			И ЗарплатаКВыплатеОрганизацииЗарплата.ВыплаченностьЗарплаты = ЗНАЧЕНИЕ(Перечисление.ВыплаченностьЗарплаты.Задепонировано)) КАК ОбщиеСуммыПоВедомости
	|		ПО (ИСТИНА)";
	КонецЕсли;
	ТекстЗапроса = ТекстЗапроса + "
	|ГДЕ
	|	ДепонированиеОрганизаций.Ссылка = &ДокументСсылка";

	Запрос.Текст = ТекстЗапроса;
	Возврат Запрос.Выполнить();

КонецФункции // СформироватьЗапросПоШапке()

// Формирует запрос по табличной части документа
//
// Параметры: 
//  нет
//
// Возвращаемое значение:
//  Результат запроса.
//
Функция СформироватьЗапросПоДепонированиеЗаработнойПлаты()

	Запрос = Новый Запрос;

	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка", Ссылка);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДепонированиеОрганизацийДепонированиеЗаработнойПлаты.НомерСтроки КАК НомерСтроки,
	|	ДепонированиеОрганизацийДепонированиеЗаработнойПлаты.Ведомость КАК Ведомость,
	|	ДепонированиеОрганизацийДепонированиеЗаработнойПлаты.Ведомость.ПериодРегистрации КАК ПериодРегистрацииВедомости,
	|	ДепонированиеОрганизацийДепонированиеЗаработнойПлаты.Ведомость.СпособВыплаты КАК СпособВыплатыВедомости,
	|	ДепонированиеОрганизацийДепонированиеЗаработнойПлаты.Ведомость.Представление КАК ПредставлениеВедомости,
	|	ДепонированиеОрганизацийДепонированиеЗаработнойПлаты.Ведомость.Дата КАК ДатаВедомости,
	|	СУММА(ВЫБОР
	|			КОГДА ЗарплатаКВыплатеОрганизацииЗарплата.ВыплаченностьЗарплаты = ЗНАЧЕНИЕ(Перечисление.ВыплаченностьЗарплаты.НеВыплачено)
	|				ТОГДА ЗарплатаКВыплатеОрганизацииЗарплата.Сумма + ЗарплатаКВыплатеОрганизацииЗарплата.КомпенсацияЗаЗадержкуЗарплаты
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК НеПомеченнаяСумма,
	|	ВЫБОР
	|		КОГДА ДепонированиеОрганизацийДепонированиеЗаработнойПлаты.Сумма <> ЕСТЬNULL(СУММА(ВЫБОР
	|						КОГДА ЗарплатаКВыплатеОрганизацииЗарплата.ВыплаченностьЗарплаты = ЗНАЧЕНИЕ(Перечисление.ВыплаченностьЗарплаты.Задепонировано)
	|							ТОГДА ЗарплатаКВыплатеОрганизацииЗарплата.Сумма + ЗарплатаКВыплатеОрганизацииЗарплата.КомпенсацияЗаЗадержкуЗарплаты
	|						ИНАЧЕ 0
	|					КОНЕЦ), 0)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК СуммыСоответствуютВедомостям,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЗарплатаКВыплатеОрганизацииЗарплата.Физлицо <> ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|				ТОГДА ЛОЖЬ
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ) КАК НеУказаныФизлица
	|ИЗ
	|	Документ.ДепонированиеОрганизаций.ДепонированиеЗаработнойПлаты КАК ДепонированиеОрганизацийДепонированиеЗаработнойПлаты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗарплатаКВыплатеОрганизаций.Зарплата КАК ЗарплатаКВыплатеОрганизацииЗарплата
	|		ПО ДепонированиеОрганизацийДепонированиеЗаработнойПлаты.Ведомость = ЗарплатаКВыплатеОрганизацииЗарплата.Ссылка
	|ГДЕ
	|	ДепонированиеОрганизацийДепонированиеЗаработнойПлаты.Ссылка = &ДокументСсылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ДепонированиеОрганизацийДепонированиеЗаработнойПлаты.Ведомость,
	|	ДепонированиеОрганизацийДепонированиеЗаработнойПлаты.Сумма,
	|	ДепонированиеОрганизацийДепонированиеЗаработнойПлаты.НомерСтроки
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";

	Возврат Запрос.Выполнить();

КонецФункции // СформироватьЗапросПоДепонированиеЗаработнойПлаты()

// Формирует запрос по табличной части документа
//
// Параметры: 
//  нет
//
// Возвращаемое значение:
//  Результат запроса.
//
Функция СформироватьЗапросПоВедомостямЗаработнойПлаты()

	Запрос = Новый Запрос;

	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка", Ссылка);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДепонированиеОрганизацийДепонированиеЗаработнойПлаты.НомерСтроки КАК НомерСтроки,
	|	ДепонированиеОрганизацийДепонированиеЗаработнойПлаты.Ведомость КАК Ведомость,
	|	ДепонированиеОрганизацийДепонированиеЗаработнойПлаты.Ведомость.ПериодРегистрации КАК ПериодРегистрацииВедомости,
	|	ДепонированиеОрганизацийДепонированиеЗаработнойПлаты.Ведомость.Дата КАК Дата,
	|	ДепонированиеОрганизацийДепонированиеЗаработнойПлаты.Ведомость.Номер КАК Номер,
	|	ДепонированиеОрганизацийДепонированиеЗаработнойПлаты.Ведомость.ХарактерВыплаты КАК ХарактерВыплаты,
	|	ЗарплатаКВыплатеОрганизацииЗарплата.Физлицо КАК Физлицо,
	|	СУММА(ЕСТЬNULL(ЗарплатаКВыплатеОрганизацииЗарплата.Сумма + ЗарплатаКВыплатеОрганизацииЗарплата.КомпенсацияЗаЗадержкуЗарплаты, 0)) КАК СуммаКДепонированию,
	|	СУММА(ЕСТЬNULL(ЗарплатаКВыплатеОрганизацииЗарплата.Сумма, 0)) КАК Сумма,
	|	СУММА(ЕСТЬNULL(ЗарплатаКВыплатеОрганизацииЗарплата.КомпенсацияЗаЗадержкуЗарплаты, 0)) КАК КомпенсацияЗаЗадержкуЗарплаты
	|ИЗ
	|	Документ.ДепонированиеОрганизаций.ДепонированиеЗаработнойПлаты КАК ДепонированиеОрганизацийДепонированиеЗаработнойПлаты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗарплатаКВыплатеОрганизаций.Зарплата КАК ЗарплатаКВыплатеОрганизацииЗарплата
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОВыплатахРаботникамОрганизацийПоПлатежнымВедомостям КАК РанееВыплаченныеСуммы
	|			ПО ЗарплатаКВыплатеОрганизацииЗарплата.Ссылка = РанееВыплаченныеСуммы.Ведомость
	|				И ЗарплатаКВыплатеОрганизацииЗарплата.Физлицо = РанееВыплаченныеСуммы.Физлицо
	|				И (РанееВыплаченныеСуммы.Регистратор <> &ДокументСсылка)
	|		ПО ДепонированиеОрганизацийДепонированиеЗаработнойПлаты.Ведомость = ЗарплатаКВыплатеОрганизацииЗарплата.Ссылка
	|			И (ЗарплатаКВыплатеОрганизацииЗарплата.ВыплаченностьЗарплаты = ЗНАЧЕНИЕ(Перечисление.ВыплаченностьЗарплаты.Задепонировано))
	|ГДЕ
	|	ДепонированиеОрганизацийДепонированиеЗаработнойПлаты.Ссылка = &ДокументСсылка
	|	И РанееВыплаченныеСуммы.Физлицо ЕСТЬ NULL 
	|
	|СГРУППИРОВАТЬ ПО
	|	ДепонированиеОрганизацийДепонированиеЗаработнойПлаты.НомерСтроки,
	|	ДепонированиеОрганизацийДепонированиеЗаработнойПлаты.Ведомость,
	|	ЗарплатаКВыплатеОрганизацииЗарплата.Физлицо
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ведомость,
	|	Физлицо";

	Возврат Запрос.Выполнить();

КонецФункции // СформироватьЗапросПоВедомостямЗаработнойПлаты()

// Проверяет правильность заполнения шапки документа.
// Если какой-то из реквизитов шапки, влияющий на проведение не заполнен или
// заполнен не корректно, то выставляется флаг отказа в проведении.
// Проверка выполняется по выборке из результата запроса по шапке,
// все проверяемые реквизиты должны быть включены в выборку по шапке.
//
// Параметры: 
//  ВыборкаПоШапкеДокумента	- выборка из результата запроса по шапке документа,
//  Отказ 		 - флаг отказа в проведении,
//	Заголовок	 - Заголовок для сообщений об ошибках проведения.
//
Процедура ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ, Заголовок)

	Если НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.Организация) Тогда
		ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке(ОбщегоНазначенияЗК.ПреобразоватьСтрокуИнтерфейса("Не указана организация!"), Отказ, Заголовок);
	КонецЕсли;
	
КонецПроцедуры // ПроверитьЗаполнениеШапки()

// Проверяет правильность заполнения строки документа.
Процедура ПроверитьЗаполнениеСтрокиДепонированиеЗаработнойПлаты(ВыборкаПоДепонированиеЗаработнойПлаты, Отказ, Заголовок)

	НачалоСообщения = "В строке № """+ СокрЛП(ВыборкаПоДепонированиеЗаработнойПлаты.НомерСтроки) +
					  """ табл. части: ";
	
	Если НЕ ЗначениеЗаполнено(ВыборкаПоДепонированиеЗаработнойПлаты.Ведомость) Тогда
		ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке(НачалоСообщения + "не указана платежная ведомость!", Отказ, Заголовок);
	Иначе
		Если ВыборкаПоДепонированиеЗаработнойПлаты.СпособВыплатыВедомости <> Перечисления.СпособыВыплатыЗарплаты.ЧерезКассу Тогда
			ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке(НачалоСообщения + "указан документ, не предназначенный для оплаты через кассу!", Отказ, Заголовок);
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(ВыборкаПоДепонированиеЗаработнойПлаты.ПериодРегистрацииВедомости) Тогда
			ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке(НачалоСообщения + "указана платежная ведомость (" + ВыборкаПоДепонированиеЗаработнойПлаты.ПредставлениеВедомости + "), в которой не указан месяц начисления выплачиваемой зарплаты!", Отказ, Заголовок);
			
		Иначе
			Если Дата < ВыборкаПоДепонированиеЗаработнойПлаты.ПериодРегистрацииВедомости Тогда
				ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке(НачалоСообщения + "указана платежная ведомость (" + ВыборкаПоДепонированиеЗаработнойПлаты.ПредставлениеВедомости + "), месяц начисления которой больше даты депонирования!",Отказ,Заголовок);
			КонецЕсли;
			Если Дата < ВыборкаПоДепонированиеЗаработнойПлаты.ДатаВедомости Тогда
				ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке(НачалоСообщения + "указана платежная ведомость (" + ВыборкаПоДепонированиеЗаработнойПлаты.ПредставлениеВедомости + "), которая оформлена позже этого депонирования!",Отказ,Заголовок);
			КонецЕсли;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ВыборкаПоДепонированиеЗаработнойПлаты.НеПомеченнаяСумма) Тогда
			ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке(НачалоСообщения + "указана платежная ведомость (" + ВыборкаПоДепонированиеЗаработнойПлаты.ПредставлениеВедомости + "), в которой есть строки с невыплаченной зарплатой!", Отказ, Заголовок);
		КонецЕсли;
		
		Если ВыборкаПоДепонированиеЗаработнойПлаты.НеУказаныФизлица Тогда
			ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке(НачалоСообщения + "указана платежная ведомость (" + ВыборкаПоДепонированиеЗаработнойПлаты.ПредставлениеВедомости + ") со строками, в которых нет сотрудников!", Отказ, Заголовок);
		КонецЕсли;
		
		Если Не ВыборкаПоДепонированиеЗаработнойПлаты.СуммыСоответствуютВедомостям Тогда
			ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке(НачалоСообщения + "сумма по строке документа не совпадает с суммой к депонированию по платежной ведомость!", Отказ, Заголовок);
		КонецЕсли;
	КонецЕсли;
	
	
КонецПроцедуры // ПроверитьЗаполнениеСтрокиДепонированиеЗаработнойПлаты()

// По строке выборки результата запроса по документу формируем движения по регистрам
//
// Параметры: 
//  ВыборкаПоШапкеДокумента                  - выборка из результата запроса по шапке документа
//  СтруктураПроведенияПоРегистрамНакопления - структура, содержащая имена регистров 
//                                             накопления по которым надо проводить документ
//  СтруктураПараметров                      - структура параметров проведения.
//
// Возвращаемое значение:
//  Нет.
//
Процедура ДобавитьСтрокуВДвиженияПоРегистрамНакопления(ВыборкаПоШапкеДокумента, ВыборкаПоТЧ)
	
	Движение = Движения.ВзаиморасчетыСРаботникамиОрганизаций.Добавить();
	// Свойства
	Движение.Период           				= ВыборкаПоШапкеДокумента.Дата;
	Движение.ВидДвижения					= ВидДвиженияНакопления.Расход;
	// Измерения
	Движение.Организация					= ВыборкаПоШапкеДокумента.Организация;
	Движение.ФизЛицо                		= ВыборкаПоТЧ.ФизЛицо;
	Движение.ПериодВзаиморасчетов       	= ВыборкаПоТЧ.ПериодРегистрацииВедомости;
	
	// Ресурсы
	Движение.СуммаВзаиморасчетов			= ВыборкаПоТЧ.Сумма; 
	// Реквизиты
	Движение.ХарактерВыплаты				= ВыборкаПоТЧ.ХарактерВыплаты; 
	Движение.СпособВыплаты					= Перечисления.СпособыВыплатыЗарплаты.ЧерезКассу; 
	Движение.Ведомость  					= ВыборкаПоТЧ.Ведомость; 
	
	Движение = Движения.ВзаиморасчетыСДепонентамиОрганизаций.Добавить();
	// Свойства
	Движение.Период							= ВыборкаПоШапкеДокумента.Дата;
	Движение.ВидДвижения					= ВидДвиженияНакопления.Приход;
	// Измерения
	Движение.Организация					= ВыборкаПоШапкеДокумента.Организация;
	Движение.ФизЛицо						= ВыборкаПоТЧ.ФизЛицо;
	Движение.Ведомость						= ВыборкаПоТЧ.Ведомость;
	
	// Ресурсы
	Движение.Сумма							= ВыборкаПоТЧ.СуммаКДепонированию; 
	
КонецПроцедуры // ДобавитьСтрокуВДвиженияПоРегистрамНакопления

// По строке выборки результата запроса по документу формируем движения по регистрам
//
// Параметры: 
//  ВыборкаПоШапкеДокумента                  - выборка из результата запроса по шапке документа
//  СтруктураПроведенияПоРегистрамНакопления - структура, содержащая имена регистров 
//                                             накопления по которым надо проводить документ
//  СтруктураПараметров                      - структура параметров проведения.
//
// Возвращаемое значение:
//  Нет.
//
Процедура ДобавитьСтрокуВДвиженияПоРегистрамСведений(ВыборкаПоТЧ)
	
	Движение = Движения.СведенияОВыплатахРаботникамОрганизацийПоПлатежнымВедомостям.Добавить();
	
	// Измерения
	Движение.ФизЛицо   = ВыборкаПоТЧ.ФизЛицо;
	Движение.Ведомость = ВыборкаПоТЧ.Ведомость;
	
	// Ресурсы
	
КонецПроцедуры // ДобавитьСтрокуВДвиженияПоРегистрамСведений

Процедура СформироватьРасчетыПоНДФЛ(ВыборкаПоШапкеДокумента = Неопределено) Экспорт
	
	Если ВыборкаПоШапкеДокумента = Неопределено Тогда
		ВыборкаПоШапкеДокумента = СформироватьЗапросПоШапке().Выбрать();
		ВыборкаПоШапкеДокумента.Следующий()
	КонецЕсли;
	
	СтруктураДокумента = Новый Структура("Ссылка,Период,ОбособленноеПодразделение,Организация",ВыборкаПоШапкеДокумента.Ссылка,ВыборкаПоШапкеДокумента.Дата,ВыборкаПоШапкеДокумента.Организация,ВыборкаПоШапкеДокумента.ГоловнаяОрганизация);
	
	ТекстТаблицыВыплаты = 
	"ВЫБРАТЬ
	|	ЗарплатаКВыплатеОрганизацииЗарплата.Физлицо КАК Физлицо,
	|	СУММА(ЗарплатаКВыплатеОрганизацииЗарплата.Сумма) КАК Сумма
	|ПОМЕСТИТЬ ВТВыплатыФизлицам
	|ИЗ
	|	Документ.ДепонированиеОрганизаций.ДепонированиеЗаработнойПлаты КАК ДепонированиеОрганизацийДепонированиеЗаработнойПлаты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗарплатаКВыплатеОрганизаций.Зарплата КАК ЗарплатаКВыплатеОрганизацииЗарплата
	|		ПО ДепонированиеОрганизацийДепонированиеЗаработнойПлаты.Ведомость = ЗарплатаКВыплатеОрганизацииЗарплата.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОВыплатахРаботникамОрганизацийПоПлатежнымВедомостям КАК РанееВыплаченныеСуммы
	|		ПО ДепонированиеОрганизацийДепонированиеЗаработнойПлаты.Ведомость = РанееВыплаченныеСуммы.Ведомость
	|			И (ЗарплатаКВыплатеОрганизацииЗарплата.Физлицо = РанееВыплаченныеСуммы.Физлицо)
	|			И (РанееВыплаченныеСуммы.Регистратор <> &ДокументСсылка)
	|ГДЕ
	|	ДепонированиеОрганизацийДепонированиеЗаработнойПлаты.Ссылка = &ДокументСсылка
	|	И ЗарплатаКВыплатеОрганизацииЗарплата.ВыплаченностьЗарплаты = ЗНАЧЕНИЕ(Перечисление.ВыплаченностьЗарплаты.Задепонировано)
	|	И РанееВыплаченныеСуммы.Физлицо ЕСТЬ NULL 
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗарплатаКВыплатеОрганизацииЗарплата.Физлицо
	|
	|ИМЕЮЩИЕ
	|	СУММА(ЗарплатаКВыплатеОрганизацииЗарплата.Сумма) <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Физлицо";
	
	Запрос = Новый Запрос;
	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка" , ВыборкаПоШапкеДокумента.Ссылка);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ПлДок.Ссылка.Дата > КОНЕЦПЕРИОДА(МАКСИМУМ(ПлДок.Ведомость.ПериодРегистрации), МЕСЯЦ)
	|			ТОГДА ПлДок.Ссылка.Дата
	|		ИНАЧЕ КОНЕЦПЕРИОДА(МАКСИМУМ(ПлДок.Ведомость.ПериодРегистрации), МЕСЯЦ)
	|	КОНЕЦ КАК ДатаНДФЛ
	|ИЗ
	|	Документ.ДепонированиеОрганизаций.ДепонированиеЗаработнойПлаты КАК ПлДок
	|ГДЕ
	|	ПлДок.Ссылка = &ДокументСсылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ПлДок.Ссылка.Дата";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	СтруктураДокумента.Вставить("ДатаНДФЛ",Выборка.ДатаНДФЛ);
	СтруктураДокумента.Вставить("ХарактерВыплаты", Перечисления.ХарактерВыплатыЗарплаты.Зарплата);
	
	ПроведениеРасчетовПереопределяемый.СформироватьРасчетыПоНДФЛПриВыплате(СтруктураДокумента, ТекстТаблицыВыплаты, Движения.НДФЛРасчетыСБюджетом, Движения.РасчетыНалоговыхАгентовСБюджетомПоНДФЛ);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Процедура осуществляет заполнение на основании документа 
//	ЗарплатаКВыплатеОрганизаций
//
// Параметры:
//  Основание - документ основание, из которого брать данные.
//
Процедура ОбработкаЗаполнения(Основание)
	
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.ЗарплатаКВыплатеОрганизаций") Тогда
		
		ЗаполнитьПоЗарплатеКВыплатеОрганизации(Основание);
		
	КонецЕсли;
	
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		Запрос = Новый Запрос;

		// Установим параметры запроса
		Запрос.УстановитьПараметр("ДокументСсылка" , Ссылка);
		Запрос.УстановитьПараметр("Ведомости", ДепонированиеЗаработнойПлаты.ВыгрузитьКолонку("Ведомость"));
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗарплатаКВыплатеОрганизацииЗарплата.Физлицо,
		|	ЗарплатаКВыплатеОрганизацииЗарплата.Физлицо.Наименование КАК ФизлицоНаименование,
		|	СУММА(ЗарплатаКВыплатеОрганизацииЗарплата.Сумма + ЗарплатаКВыплатеОрганизацииЗарплата.КомпенсацияЗаЗадержкуЗарплаты) КАК Сумма
		|ИЗ
		|	Документ.ЗарплатаКВыплатеОрганизаций.Зарплата КАК ЗарплатаКВыплатеОрганизацииЗарплата
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОВыплатахРаботникамОрганизацийПоПлатежнымВедомостям КАК РанееВыплаченныеСуммы
		|		ПО ЗарплатаКВыплатеОрганизацииЗарплата.Ссылка = РанееВыплаченныеСуммы.Ведомость
		|			И ЗарплатаКВыплатеОрганизацииЗарплата.Физлицо = РанееВыплаченныеСуммы.Физлицо
		|			И (РанееВыплаченныеСуммы.Регистратор <> &ДокументСсылка)
		|ГДЕ
		|	ЗарплатаКВыплатеОрганизацииЗарплата.Ссылка В(&Ведомости)
		|	И ЗарплатаКВыплатеОрганизацииЗарплата.Сумма + ЗарплатаКВыплатеОрганизацииЗарплата.КомпенсацияЗаЗадержкуЗарплаты <> 0
		|	И ЗарплатаКВыплатеОрганизацииЗарплата.ВыплаченностьЗарплаты = ЗНАЧЕНИЕ(Перечисление.ВыплаченностьЗарплаты.Задепонировано)
		|	И РанееВыплаченныеСуммы.Физлицо ЕСТЬ NULL 
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗарплатаКВыплатеОрганизацииЗарплата.Физлицо
		|
		|УПОРЯДОЧИТЬ ПО
		|	ФизлицоНаименование";

		Работники.Загрузить(Запрос.Выполнить().Выгрузить());
		
	ИначеЕсли РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
		
		Работники.Очистить();
		
	КонецЕсли;
	
	// Посчитать суммы документа и записать ее в соответствующий реквизит шапки для показа в журналах
	СуммаДокумента = ДепонированиеЗаработнойПлаты.Итог("Сумма");
	
	МассивТЧ = Новый Массив();
	МассивТЧ.Добавить(Работники);
	
	КраткийСоставДокумента = ПроцедурыУправленияПерсоналом.ЗаполнитьКраткийСоставДокумента(МассивТЧ, "Физлицо");

КонецПроцедуры // ПередЗаписью

Процедура ОбработкаПроведения(Отказ, Режим)
	
	ОбработкаКомментариев = глЗначениеПеременной("глОбработкаСообщений");
	ОбработкаКомментариев.УдалитьСообщения();
	
	// Заголовок для сообщений об ошибках проведения.
	Заголовок = ОбщегоНазначения.ПредставлениеДокументаПриПроведении(Ссылка);
	
	УпрощенныйУчетВзаиморасчетов = ПроцедурыУправленияПерсоналом.ЗначениеУчетнойПолитикиПоПерсоналуОрганизации(глЗначениеПеременной("глУчетнаяПолитикаПоПерсоналуОрганизации"), Организация, "УпрощенныйУчетВзаиморасчетов");
	
	Если УпрощенныйУчетВзаиморасчетов Тогда
		СтрокаСообщения = 
		"В организации ведется упрощенный учет взаиморасчетов, проведение документа запрещено.
		|Депонирование заработной платы производится при проведении документов «Зарплата к выплате организаций»";
		
		ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке(СтрокаСообщения, Отказ, Заголовок);
	КонецЕсли;
	
	ФормироватьНалоговыеПроводки = НадоФормироватьНалоговыеПроводки();
	
	// Получим реквизиты шапки из запроса
	ВыборкаПоШапкеДокумента = СформироватьЗапросПоШапке(ФормироватьНалоговыеПроводки).Выбрать();
	
	Если ВыборкаПоШапкеДокумента.Следующий() Тогда
		
		//Надо позвать проверку заполнения реквизитов шапки
		ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ, Заголовок);
		
		// Движения стоит добавлять, если в проведении еще не отказано (отказ = ложь)
		Если НЕ Отказ Тогда
			
			// получим реквизиты табличной части
			РезультатЗапроса = СформироватьЗапросПоДепонированиеЗаработнойПлаты();
			
			Если РезультатЗапроса.Пустой() Тогда
				ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке("По указанным документам на выплату зарплаты не обнаружено не получивших деньги сотрудников, на которых ещё не оформлен документ ""Депонирование""!", Отказ, Заголовок);
			Иначе
				
				Если ФормироватьНалоговыеПроводки Тогда
					ФормироватьНалоговыеПроводки = ВыборкаПоШапкеДокумента.КомпенсацияЗаЗадержкуЗарплаты > 0;
				КонецЕсли;
				Если ФормироватьНалоговыеПроводки Тогда
					СчетНУСоответствующийСчетуБУ = БухгалтерскийУчет.ПреобразоватьСчетаБУвСчетНУ(Новый Структура("СчетБУ",СчетУчетаРасходов), Ложь, Дата);
					Если СчетНУСоответствующийСчетуБУ.Пустая() Тогда
						ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке("Постоянные разницы в налоговом учете по компенсации за задержку выплаты зарплаты сформированы не будут!",,Заголовок);
						ФормироватьНалоговыеПроводки = Ложь;
					КонецЕсли;
				КонецЕсли;
				
				// ведется ли учет задолженности в разрезе периодов возникновения задолженности
				УчетЗадолженностиПоМесяцам	= ПроцедурыУправленияПерсоналом.ЗначениеУчетнойПолитикиПоПерсоналуОрганизации(глЗначениеПеременной("глУчетнаяПолитикаПоПерсоналуОрганизации"), Организация, "УчетЗадолженностиПоМесяцам");
				// Определим способ ведения учета расчетов с персоналом - сводно/подробно
				ВестиРеглУчетРасчетовСПерсоналомПодробно = Не глЗначениеПеременной("ВестиРеглУчетРасчетовСПерсоналомСводно");

				ДанныеПоВедомостямЗаработнойПлаты = СформироватьЗапросПоВедомостямЗаработнойПлаты().Выгрузить();
				ДанныеПоВедомостямЗаработнойПлаты.Индексы.Добавить("НомерСтроки");
				СтруктураПоиска = Новый Структура("НомерСтроки");
				
				ВыборкаПоДепонированиеЗаработнойПлаты = РезультатЗапроса.Выбрать();
				Пока ВыборкаПоДепонированиеЗаработнойПлаты.Следующий() Цикл 
					
					// проверим очередную строку табличной части
					ПроверитьЗаполнениеСтрокиДепонированиеЗаработнойПлаты(ВыборкаПоДепонированиеЗаработнойПлаты, Отказ, Заголовок);
					
					Если Не Отказ Тогда // Заполним записи в наборах записей регистров
						ЗаполнитьЗначенияСвойств(СтруктураПоиска,ВыборкаПоДепонированиеЗаработнойПлаты);
						СтрокиВедомости = ДанныеПоВедомостямЗаработнойПлаты.НайтиСтроки(СтруктураПоиска);
						Для каждого СтрокаВедомости Из СтрокиВедомости Цикл
							ДобавитьСтрокуВДвиженияПоРегистрамСведений(СтрокаВедомости);
							Если СтрокаВедомости.СуммаКДепонированию > 0 Тогда
								ДобавитьСтрокуВДвиженияПоРегистрамНакопления(ВыборкаПоШапкеДокумента, СтрокаВедомости);
								ДобавитьСтрокуПроводки(ВыборкаПоШапкеДокумента, СтрокаВедомости, ФормироватьНалоговыеПроводки,  СчетНУСоответствующийСчетуБУ, ВестиРеглУчетРасчетовСПерсоналомПодробно);
							КонецЕсли;
						КонецЦикла;
					КонецЕсли;
					
				КонецЦикла;
				
				ПроведениеРасчетов.ПроверитьХронологиюУчетаУдержанногоНДФЛ(Организация, ДанныеПоВедомостямЗаработнойПлаты.ВыгрузитьКолонку("ФизЛицо"), Дата, Отказ, Заголовок);
				
				Если Не Отказ Тогда // регистрируем НДФЛ удержанный
					СформироватьРасчетыПоНДФЛ(ВыборкаПоШапкеДокумента)
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	ОбработкаКомментариев.ПоказатьСообщения();
	
КонецПроцедуры

// По строке выборки результата запроса по документу формируем движения по регистрам
//
// Параметры: 
//  ВыборкаПоШапкеДокумента                  - выборка из результата запроса по шапке документа
//  СтруктураПроведенияПоРегистрамНакопления - структура, содержащая имена регистров 
//                                             накопления по которым надо проводить документ
//  СтруктураПараметров                      - структура параметров проведения.
//
// Возвращаемое значение:
//  Нет.
//
Процедура ДобавитьСтрокуПроводки(ВыборкаПоШапкеДокумента, ВыборкаПоТЧ, ФормироватьНалоговыеПроводки,СчетНУСоответствующийСчетуБУ,ВестиРеглУчетРасчетовСПерсоналомПодробно) 
	
	// Бухгалтерские проводки документа
	ПроводкиБУ = Движения.Хозрасчетный;
	
	Если ВыборкаПоТЧ.Сумма <> 0 Тогда
		Проводка = ПроводкиБУ.Добавить();
		
		Проводка.Период      = ВыборкаПоШапкеДокумента.Дата;
		Проводка.Активность  = Истина;
		Проводка.Организация = ВыборкаПоШапкеДокумента.Организация;
		Проводка.Сумма       = ВыборкаПоТЧ.Сумма;
		
		Проводка.СчетДт      = ПланыСчетов.Хозрасчетный.РасчетыСПерсоналомПоОплатеТруда;
		Если ВестиРеглУчетРасчетовСПерсоналомПодробно Тогда
			Проводка.СубконтоДт.РаботникиОрганизации = ВыборкаПоТЧ.ФизЛицо;
		КонецЕсли;
		
		Проводка.Содержание  = "Депонирование зарплаты по ведомости №" + ВыборкаПоТЧ.Номер + " от " + ВыборкаПоТЧ.Дата;
		Проводка.СчетКт		 = ПланыСчетов.Хозрасчетный.РасчетыПоДепонированнымСуммам;
		Если ВестиРеглУчетРасчетовСПерсоналомПодробно Тогда
			Проводка.СубконтоКт.РаботникиОрганизации = ВыборкаПоТЧ.ФизЛицо;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ВыборкаПоТЧ.КомпенсацияЗаЗадержкуЗарплаты <> 0 Тогда
		Проводка = ПроводкиБУ.Добавить();
		
		Проводка.Период      = ВыборкаПоШапкеДокумента.Дата;
		Проводка.Активность  = Истина;
		Проводка.Организация = ВыборкаПоШапкеДокумента.Организация;
		Проводка.Сумма       = ВыборкаПоТЧ.КомпенсацияЗаЗадержкуЗарплаты;
		
		Проводка.СчетДт      = СчетУчетаРасходов;
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 1, Субконто1);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 2, Субконто2);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 3, Субконто3);
		
		Проводка.Содержание  = "Депонирование компенсации за задержку зарплаты по ведомости №" + ВыборкаПоТЧ.Номер + " от " + ВыборкаПоТЧ.Дата;
		Проводка.СчетКт		= ПланыСчетов.Хозрасчетный.РасчетыПоДепонированнымСуммам;
		Если ВестиРеглУчетРасчетовСПерсоналомПодробно Тогда
			Проводка.СубконтоКт.РаботникиОрганизации = ВыборкаПоТЧ.ФизЛицо;
		КонецЕсли;
		
		Если ФормироватьНалоговыеПроводки Тогда  // фиксируем постоянные разницы по компенсации за задержку выплаты зарплаты

			Проводка = Движения.Налоговый.Добавить();

			Проводка.Период      = ВыборкаПоШапкеДокумента.Дата;
			Проводка.Организация = ВыборкаПоШапкеДокумента.Организация;
			Проводка.Сумма       = ВыборкаПоТЧ.КомпенсацияЗаЗадержкуЗарплаты;
			Проводка.Содержание  = "Депонирование компенсации за задержку зарплаты по ведомости №" + ВыборкаПоТЧ.Номер + " от " + ВыборкаПоТЧ.Дата;

			Проводка.СчетДт      = СчетНУСоответствующийСчетуБУ;
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 1, Субконто1);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 2, Субконто2);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 3, Субконто3);
			Проводка.ВидУчетаДт = Перечисления.ВидыУчетаПоПБУ18.ПР;
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ДобавитьСтрокуПроводки


// Возвращает доступные варианты печати документа
//
// Возвращаемое значение:
//  Структура, каждая строка которой соответствует одному из вариантов печати
//
Функция ПолучитьСтруктуруПечатныхФорм() Экспорт
	Возврат Новый Структура("ЗаявлениеВФССДополнительныеДниОтпуска_2011, ЗаявлениеВФССДополнительныеДниОтпуска_2012", "Заявление о возмещении расходов (форма 2011 г.)", "Заявление о возмещении расходов (форма 2012 г.)");
КонецФункции // ПолучитьСтруктуруПечатныхФорм()

#Если ТолстыйКлиентОбычноеПриложение Тогда

//Процедура вывода сведений на печать
Функция Печать(ИмяМакета, КоличествоЭкземпляров = 1, НаПринтер = Ложь) Экспорт
	
	Если Лев(ИмяМакета,37) = "ЗаявлениеВФССДополнительныеДниОтпуска" Тогда
		
		ОбработкаКомментариев = глЗначениеПеременной("глОбработкаСообщений");
		ОбработкаКомментариев.УдалитьСообщения();
		
		ВыборкаПоШапкеДокумента = СформироватьЗапросПоШапкеДокумента().Выбрать();
		ВыборкаПоШапкеДокумента.Следующий();
		
		ВыборкаПоТЧРаботникиОрганизации = СформироватьЗапросПоТЧРаботникиОрганизации().Выбрать();
	
		Отказ = Ложь;
	
		Заголовок = ОбщегоНазначенияЗК.ПредставлениеДокументаПриПроведении(Ссылка);
		
		ПроверкаЗаполненияШапкиДокумента(ВыборкаПоШапкеДокумента, Отказ, Заголовок);
		
		Пока ВыборкаПоТЧРаботникиОрганизации.Следующий() Цикл
		
			ПроверитьЗаполнениеСтрокиТЧРаботникиОрганизации(ВыборкаПоТЧРаботникиОрганизации, Отказ, Заголовок);
		
		КонецЦикла; 
		
		Если Отказ Тогда
	
			ОбработкаКомментариев.ПоказатьСообщения();
			
			Возврат Неопределено;
	
		КонецЕсли; 
		
		ВыборкаПоТЧРаботникиОрганизации.Сбросить();
		
		ТабДокумент = Новый ТабличныйДокумент;
		
		ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ЗаявлениеВФССДополнительныеДниОтпуска";
		
		Если ИмяМакета = "ЗаявлениеВФССДополнительныеДниОтпуска_2011" Тогда
			
			Макет = ПолучитьМакет("ЗаявлениеВФССДополнительныеДниОтпуска_2011");
			
			ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
			
			ДлиныСтрок = Новый Массив();
			ДлиныСтрок.Добавить(27);
			
			НаПечатьНаименованиеТерриториальногоОрганаФСС = ОбменСведениямиОПособияхСФСС.РазбитьСтрокуНаПодСтроки(ВыборкаПоШапкеДокумента.НаименованиеТерриториальногоОрганаФСС, ДлиныСтрок);
			ФормированиеПечатныхФормЗК.ВывестиДанныеПоБуквенно(НаПечатьНаименованиеТерриториальногоОрганаФСС, ОбластьШапка, "НаименованиеФСС_", 135);
			
			НаПечатьОрганизация = ВыборкаПоШапкеДокумента.ОрганизацияНаименование;
			
			НаПечатьОрганизация = ОбменСведениямиОПособияхСФСС.РазбитьСтрокуНаПодСтроки(НаПечатьОрганизация, ДлиныСтрок);
			ФормированиеПечатныхФормЗК.ВывестиДанныеПоБуквенно(НаПечатьОрганизация, ОбластьШапка, "Страхователь_", 81);
			
			ТабДокумент.Вывести(ОбластьШапка);
			
			Пока ВыборкаПоТЧРаботникиОрганизации.Следующий() Цикл
				
				Если ВыборкаПоТЧРаботникиОрганизации.НомерСтроки > 1 Тогда
					
					ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
					
					ТабДокумент.Вывести(ОбластьСтрока);
					
				КонецЕсли; 
				
				НПП = Формат(ВыборкаПоТЧРаботникиОрганизации.НомерСтроки, "ЧЦ=2");
				
				НПП = Лев("  ", 2 - СтрДлина(НПП)) + НПП;
				
				ДлиныСтрок = Новый Массив();
				ДлиныСтрок.Добавить(28);
				
				ФИО = ОбменСведениямиОПособияхСФСС.РазбитьСтрокуНаПодСтроки(ОбщегоНазначенияЗК.ФамилияИнициалыФизЛица(ВыборкаПоТЧРаботникиОрганизации.ФИОСотрудника), ДлиныСтрок);
				
				Статус = ВыборкаПоТЧРаботникиОрганизации.Статус;
				
				СреднедневнойЗаработок = Формат(ВыборкаПоТЧРаботникиОрганизации.СреднедневнойЗаработок, "ЧЦ=15; ЧДЦ=2; ЧН=; ЧГ=");
				
				СреднедневнойЗаработок = Лев("        ", 8 - СтрДлина(СреднедневнойЗаработок)) + СреднедневнойЗаработок;
				
				ПерваяСтрокаВыводилась = Ложь;
				
				Пока СтрДлина(ФИО) > 0 ИЛИ НЕ ПерваяСтрокаВыводилась Цикл
					
					ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
					
					Если НЕ ПерваяСтрокаВыводилась Тогда
						
						ПерваяСтрокаВыводилась = Истина;
						
						ФормированиеПечатныхФормЗК.ВывестиДанныеПоБуквенно(НПП, ОбластьСтрока, "НПП_", 2);
						
						ФормированиеПечатныхФормЗК.ВывестиДанныеПоБуквенно(СреднедневнойЗаработок, ОбластьСтрока, "СреднедневнойЗаработок_", 8);
						
						ОбластьСтрока.Области["Статус"].Текст = Статус;
						
					КонецЕсли;
					
					Если СтрДлина(ФИО) <= 28 Тогда
						
						СтрокаФИО = ФИО;
						
						ФИО = "";
						
					Иначе
						
						СтрокаФИО = Лев(ФИО, 28);
						
						ФИО = Сред(ФИО, 29);
						
					КонецЕсли;
					
					ФормированиеПечатныхФормЗК.ВывестиДанныеПоБуквенно(СтрокаФИО, ОбластьСтрока, "ФИОПолучателя_", 28);
					
					ТабДокумент.Вывести(ОбластьСтрока);
					
				КонецЦикла;
				
			КонецЦикла;
			
			ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
			
			ИтогоСреднеДневнойЗаработок = РаботникиОрганизации.Итог("СреднедневнойЗаработок");
			
			СтрокаИтогоСреднеДневнойЗаработок = Формат(ИтогоСреднеДневнойЗаработок, "ЧДЦ=2; ЧН=; ЧГ=");
			
			СтрокаИтогоСреднеДневнойЗаработок = Лев("        ", 8 - СтрДлина(СтрокаИтогоСреднеДневнойЗаработок)) + СтрокаИтогоСреднеДневнойЗаработок; 
			
			ФормированиеПечатныхФормЗК.ВывестиДанныеПоБуквенно(СтрокаИтогоСреднеДневнойЗаработок, ОбластьПодвал, "ИтогоСреднеДневнойЗаработок_", 8);
			
			ИтогоРазмерПособия = ВыборкаПоШапкеДокумента.СуммаПособий;
			
			Рубли = Формат(Цел(ИтогоРазмерПособия), "ЧН=; ЧГ=");
			
			Рубли = Лев("        ", 8 - СтрДлина(Рубли)) + Рубли;
			
			ФормированиеПечатныхФормЗК.ВывестиДанныеПоБуквенно(Рубли, ОбластьПодвал, "ИтогоРазмерПособияРубли_", 8);
			
			Копейки = Формат((ИтогоРазмерПособия - Цел(ИтогоРазмерПособия)) * 100, "ЧЦ=2; ЧН=; ЧВН=");
			
			ФормированиеПечатныхФормЗК.ВывестиДанныеПоБуквенно(Копейки, ОбластьПодвал, "ИтогоРазмерПособияКопейки_", 2);
			
			СтрокаКоличествоСтраниц = Формат(ВыборкаПоШапкеДокумента.КоличествоСтраниц, "ЧЦ=2");
			
			СтрокаКоличествоСтраниц = Лев("  ", 2 - СтрДлина(СтрокаКоличествоСтраниц)) + СтрокаКоличествоСтраниц;
			
			ФормированиеПечатныхФормЗК.ВывестиДанныеПоБуквенно(СтрокаКоличествоСтраниц, ОбластьПодвал, "КоличествоСтраниц_", 2);
			ФормированиеПечатныхФормЗК.ВывестиДанныеПоБуквенно(ВыборкаПоШапкеДокумента.РегистрационныйНомерФСС, ОбластьПодвал, "РегистрационныйНомерФСС_", 10);
			ФормированиеПечатныхФормЗК.ВывестиДанныеПоБуквенно(ВыборкаПоШапкеДокумента.ДополнительныйКодФСС, ОбластьПодвал, "ДополнительныйКодФСС_", 10);
			ФормированиеПечатныхФормЗК.ВывестиДанныеПоБуквенно(ВыборкаПоШапкеДокумента.КодПодчиненностиФСС, ОбластьПодвал, "КодПодчиненностиФСС_", 5);
			ФормированиеПечатныхФормЗК.ВывестиДанныеПоБуквенно(ВыборкаПоШапкеДокумента.ИНН, ОбластьПодвал, "ИНН_", 12);
			ФормированиеПечатныхФормЗК.ВывестиДанныеПоБуквенно(ВыборкаПоШапкеДокумента.КПП, ОбластьПодвал, "КПП_", 9);
			
			ДлиныСтрок = Новый Массив();
			ДлиныСтрок.Добавить(24);
			ДлиныСтрок.Добавить(43);
			ДлиныСтрок.Добавить(43);
			
			Адрес = ОбменСведениямиОПособияхСФСС.РазбитьСтрокуНаПодСтроки(УправлениеКонтактнойИнформацией.ПолучитьПредставлениеАдресаПоСтрока(ВыборкаПоШапкеДокумента.АдресОрганизации), ДлиныСтрок);
			ДлинаАдреса = ?(ОбластьПодвал.Области.Найти("Адрес_68") = Неопределено, 67, 110);
			ФормированиеПечатныхФормЗК.ВывестиДанныеПоБуквенно(Адрес, ОбластьПодвал, "Адрес_", ДлинаАдреса);
			
			ОбластьПодвал.Области["ДолжностьФИОРуководителя"].Текст = "" + ВыборкаПоШапкеДокумента.ДолжностьРуководителя + ", " + ВыборкаПоШапкеДокумента.ФИОРуководителя;
			
			ТабДокумент.Вывести(ОбластьПодвал);
		КонецЕсли;
		Если ИмяМакета = "ЗаявлениеВФССДополнительныеДниОтпуска_2012" Тогда
			
			ТабДокумент.ПолеСверху 	= 0;
			ТабДокумент.ПолеСлева 	= 0;
			ТабДокумент.ПолеСнизу 	= 0;
			ТабДокумент.ПолеСправа 	= 0;
			
			Макет = Неопределено;
			ВыведеноСтрок = 0;
			ИтогоПособия = 0;
			ИтогоСреднедневнойЗаработок = 0;
			ВсегоСтраниц = 0;
			Пока ВыборкаПоТЧРаботникиОрганизации.Следующий() Цикл
				
				ВыведеноСтрок = (ВыведеноСтрок + 1) % 4;
				Если ВыведеноСтрок = 1 Тогда
					Если Макет <> Неопределено Тогда
						ОбменСведениямиОПособияхСФСС.ВывестиСуммуВРубляхКопейкахВЯчейки(ИтогоСреднедневнойЗаработок, Макет, "ИтогоРазмерПособия_", 9);
						ОбменСведениямиОПособияхСФСС.ВывестиСуммуВРубляхКопейкахВЯчейки(ИтогоПособия, Макет, "ИтогоРазмерПособия2_", 9);
						ФормированиеПечатныхФормЗК.ВывестиДанныеПоБуквенно(Строка(ВсегоСтраниц), Макет, "КоличествоСтраниц_", 2);
						ТабДокумент.Вывести(Макет);
						ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
					КонецЕсли;
					ИтогоПособия = 0;
					ВсегоСтраниц = 0;
					Макет = ОбменСведениямиОПособияхСФСС.МакетЗаявленияОВозмещении2012СЗаполненнымиШапкойИПодвалом(ВыборкаПоШапкеДокумента, ПолучитьМакет("ЗаявлениеВФССДополнительныеДниОтпуска_2012"))
				КонецЕсли;
				
				Фамилия = ВыборкаПоТЧРаботникиОрганизации.Фамилия;
				Имя = ВыборкаПоТЧРаботникиОрганизации.Имя;
				Отчество = ВыборкаПоТЧРаботникиОрганизации.Отчество;
				Если Не ЗначениеЗаполнено(Фамилия) Тогда
					ОбщегоНазначенияЗК.ФамилияИнициалыФизЛица(ВыборкаПоТЧРаботникиОрганизации.ФИОСотрудника, Фамилия, Имя, Отчество);
				КонецЕсли;
				
				НомерСтроки = ?(ВыведеноСтрок = 0, 4, ВыведеноСтрок);
				ПрефиксСтроки = "ФИО_" + Формат(НомерСтроки,"ЧЦ=2; ЧВН=") + "_";
				ФормированиеПечатныхФормЗК.ВывестиДанныеПоБуквенно(ВРег(Фамилия), Макет, ПрефиксСтроки, 24);
				ФормированиеПечатныхФормЗК.ВывестиДанныеПоБуквенно(ВРег(Имя), 	Макет, ПрефиксСтроки, 24, 25);
				ФормированиеПечатныхФормЗК.ВывестиДанныеПоБуквенно(ВРег(Отчество),Макет, ПрефиксСтроки, 24, 49);
				
				ОбменСведениямиОПособияхСФСС.ВывестиСуммуВРубляхКопейкахВЯчейки(ВыборкаПоТЧРаботникиОрганизации.СреднедневнойЗаработок, Макет, "РазмерПособия" + НомерСтроки + "_", 8);
				Макет.Области["Статус" + НомерСтроки].Текст = ВыборкаПоТЧРаботникиОрганизации.Статус;
				
				ИтогоСреднедневнойЗаработок = ИтогоСреднедневнойЗаработок + ВыборкаПоТЧРаботникиОрганизации.СреднедневнойЗаработок;
				ИтогоПособия = ИтогоПособия + ВыборкаПоТЧРаботникиОрганизации.СуммаПособия;
				ВсегоСтраниц = ВсегоСтраниц + ВыборкаПоТЧРаботникиОрганизации.КоличествоСтраниц;
				
			КонецЦикла;
			
			ОбменСведениямиОПособияхСФСС.ВывестиСуммуВРубляхКопейкахВЯчейки(ИтогоСреднедневнойЗаработок, Макет, "ИтогоРазмерПособия_", 9);
			ОбменСведениямиОПособияхСФСС.ВывестиСуммуВРубляхКопейкахВЯчейки(ИтогоПособия, Макет, "ИтогоРазмерПособия2_", 9);
			ФормированиеПечатныхФормЗК.ВывестиДанныеПоБуквенно(Строка(ВсегоСтраниц), Макет, "КоличествоСтраниц_", 2);
			ТабДокумент.Вывести(Макет);
			
		КонецЕсли;
		
		Возврат УниверсальныеМеханизмы.НапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер, "Заявление о возмещении расходов на оплату выходных дней", , ТабДокумент);
		
	КонецЕсли;
	
КонецФункции // Печать() 

#КонецЕсли

// Процедура заполняет табличную часть "РаботникиОрганизации"
//			Предварительно ее очистив.
//
// Параметры
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
Процедура Автозаполнение() Экспорт

	РаботникиОрганизации.Очистить();
	
	СуммаПособий = 0;
		
	ДатаПередачиФССВыплатыПособий = ПроцедурыУправленияПерсоналом.ЗначениеУчетнойПолитикиПоПерсоналуОрганизации(глЗначениеПеременной("глУчетнаяПолитикаПоПерсоналуОрганизации"), Организация, "ДатаПередачиФССВыплатыПособий");
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПособияСоциальномуСтрахованию.Сотрудник,
	|	ПособияСоциальномуСтрахованию.Сотрудник.Физлицо КАК Физлицо,
	|	ПособияСоциальномуСтрахованию.ДокументОснование,
	|	ПособияСоциальномуСтрахованию.СуммаВсего
	|ПОМЕСТИТЬ ВТПолучателиПособия
	|ИЗ
	|	РегистрНакопления.ПособияСоциальномуСтрахованию КАК ПособияСоциальномуСтрахованию
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ЗаявлениеВФССНаДетейИнвалидовРаботникиОрганизации.Ссылка КАК Ссылка,
	|			ЗаявлениеВФССНаДетейИнвалидовРаботникиОрганизации.Сотрудник КАК Сотрудник,
	|			ЗаявлениеВФССНаДетейИнвалидовРаботникиОрганизации.ДокументОснование КАК ДокументОснование
	|		ИЗ
	|			Документ.ЗаявлениеВФССНаДетейИнвалидов.РаботникиОрганизации КАК ЗаявлениеВФССНаДетейИнвалидовРаботникиОрганизации
	|		ГДЕ
	|			ЗаявлениеВФССНаДетейИнвалидовРаботникиОрганизации.Ссылка.Проведен = ИСТИНА
	|			И ЗаявлениеВФССНаДетейИнвалидовРаботникиОрганизации.Ссылка.Ссылка <> &Ссылка
	|			И ЗаявлениеВФССНаДетейИнвалидовРаботникиОрганизации.Ссылка.Организация = &Организация) КАК РанееВведенныеДокументы
	|		ПО ПособияСоциальномуСтрахованию.Сотрудник = РанееВведенныеДокументы.Сотрудник
	|			И ПособияСоциальномуСтрахованию.ДокументОснование = РанееВведенныеДокументы.ДокументОснование
	|ГДЕ
	|	ПособияСоциальномуСтрахованию.ВидПособияСоциальногоСтрахования = ЗНАЧЕНИЕ(Перечисление.ВидыПособийСоциальногоСтрахования.ДополнительныеВыходныеДниПоУходуЗаДетьмиИнвалидами)
	|	И РанееВведенныеДокументы.Ссылка ЕСТЬ NULL 
	|	И ПособияСоциальномуСтрахованию.Период >= &ДатаПередачиФССВыплатыПособий
	|	И ПособияСоциальномуСтрахованию.Организация = &Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетСреднегоЗаработка.Сотрудник,
	|	ПолучателиПособия.ДокументОснование,
	|	РасчетСреднегоЗаработка.ВидРасчета,
	|	РасчетСреднегоЗаработка.Результат,
	|	РасчетСреднегоЗаработка.ОтработаноДней,
	|	РасчетСреднегоЗаработка.ОтработаноЧасов,
	|	РасчетСреднегоЗаработка.НормаПоПятидневке,
	|	РасчетСреднегоЗаработка.ОтработаноПоПятидневке,
	|	РасчетСреднегоЗаработка.ЧислоМесяцев,
	|	РасчетСреднегоЗаработка.КоэффициентИндексации,
	|	РасчетСреднегоЗаработка.БазовыйПериодНачало,
	|	РасчетСреднегоЗаработка.БазовыйПериодКонец,
	|	РасчетСреднегоЗаработка.ПериодРасчетаСреднегоЗаработка
	|ПОМЕСТИТЬ ВТРасчетСреднегоЗаработка
	|ИЗ
	|	ВТПолучателиПособия КАК ПолучателиПособия
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрРасчета.РасчетСреднегоЗаработка КАК РасчетСреднегоЗаработка
	|		ПО ПолучателиПособия.Сотрудник = РасчетСреднегоЗаработка.Сотрудник
	|			И ПолучателиПособия.ДокументОснование = РасчетСреднегоЗаработка.Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетСреднегоЗаработка.Сотрудник,
	|	РасчетСреднегоЗаработка.ДокументОснование,
	|	СУММА(ВЫРАЗИТЬ(ВЫБОР
	|				КОГДА РасчетСреднегоЗаработка.ВидРасчета = ЗНАЧЕНИЕ(ПланВидовРасчета.СреднийЗаработок.ПоЗаработкуИндексируемые)
	|					ТОГДА РасчетСреднегоЗаработка.Результат * ВЫБОР
	|							КОГДА РасчетСреднегоЗаработка.КоэффициентИндексации < 1
	|								ТОГДА 1
	|							ИНАЧЕ РасчетСреднегоЗаработка.КоэффициентИндексации
	|						КОНЕЦ
	|				КОГДА РасчетСреднегоЗаработка.ВидРасчета = ЗНАЧЕНИЕ(ПланВидовРасчета.СреднийЗаработок.ПоЗаработкуНеИндексируемые)
	|					ТОГДА РасчетСреднегоЗаработка.Результат
	|				КОГДА РасчетСреднегоЗаработка.ЧислоМесяцев = 0
	|					ТОГДА 0
	|				ИНАЧЕ ВЫБОР
	|						КОГДА РасчетСреднегоЗаработка.ВидРасчета В (&ПоПремиям)
	|							ТОГДА РасчетСреднегоЗаработка.Результат * ВЫБОР
	|									КОГДА РасчетСреднегоЗаработка.КоэффициентИндексации < 1
	|										ТОГДА 1
	|									ИНАЧЕ РасчетСреднегоЗаработка.КоэффициентИндексации
	|								КОНЕЦ
	|						КОГДА РасчетСреднегоЗаработка.ВидРасчета В (&ПоФиксПремиямНеИндексируемые)
	|							ТОГДА РасчетСреднегоЗаработка.Результат * ВЫБОР
	|									КОГДА РасчетСреднегоЗаработка.НормаПоПятидневке = 0
	|										ТОГДА 0
	|									ИНАЧЕ РасчетСреднегоЗаработка.ОтработаноПоПятидневке / РасчетСреднегоЗаработка.НормаПоПятидневке
	|								КОНЕЦ
	|						ИНАЧЕ РасчетСреднегоЗаработка.Результат
	|					КОНЕЦ * ВЫБОР
	|						КОГДА РасчетСреднегоЗаработка.ЧислоМесяцев > РасчетСреднегоЗаработка.ПериодРасчетаСреднегоЗаработка
	|							ТОГДА РасчетСреднегоЗаработка.ПериодРасчетаСреднегоЗаработка / РасчетСреднегоЗаработка.ЧислоМесяцев
	|						ИНАЧЕ 1
	|					КОНЕЦ
	|			КОНЕЦ КАК ЧИСЛО(15, 2))) КАК СреднийЗаработок,
	|	СУММА(ВЫБОР
	|			КОГДА РасчетСреднегоЗаработка.ВидРасчета = ЗНАЧЕНИЕ(ПланВидовРасчета.СреднийЗаработок.ПоЗаработкуИндексируемые)
	|					ИЛИ РасчетСреднегоЗаработка.ВидРасчета = ЗНАЧЕНИЕ(ПланВидовРасчета.СреднийЗаработок.ПоЗаработкуНеИндексируемые)
	|				ТОГДА РасчетСреднегоЗаработка.ОтработаноДней
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОтработаноДней,
	|	СУММА(ВЫБОР
	|			КОГДА РасчетСреднегоЗаработка.ВидРасчета = ЗНАЧЕНИЕ(ПланВидовРасчета.СреднийЗаработок.ПоЗаработкуИндексируемые)
	|					ИЛИ РасчетСреднегоЗаработка.ВидРасчета = ЗНАЧЕНИЕ(ПланВидовРасчета.СреднийЗаработок.ПоЗаработкуНеИндексируемые)
	|				ТОГДА РасчетСреднегоЗаработка.ОтработаноЧасов
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОтработаноЧасов
	|ПОМЕСТИТЬ ВТСреднийЗаработок
	|ИЗ
	|	ВТРасчетСреднегоЗаработка КАК РасчетСреднегоЗаработка
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетСреднегоЗаработка.Сотрудник,
	|	РасчетСреднегоЗаработка.ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПолучателиПособия.Сотрудник КАК Сотрудник,
	|	ПолучателиПособия.Физлицо,
	|	ПолучателиПособия.ДокументОснование,
	|	ПолучателиПособия.СуммаВсего КАК СуммаПособия,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ДанныеСреднегоЗаработка.ОтработаноДней = 0
	|				ТОГДА 0
	|			ИНАЧЕ ДанныеСреднегоЗаработка.СреднийЗаработок / ДанныеСреднегоЗаработка.ОтработаноДней
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СреднедневнойЗаработок,
	|	1 КАК КоличествоСтраниц
	|ИЗ
	|	ВТПолучателиПособия КАК ПолучателиПособия
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСреднийЗаработок КАК ДанныеСреднегоЗаработка
	|		ПО ПолучателиПособия.Сотрудник = ДанныеСреднегоЗаработка.Сотрудник
	|			И ПолучателиПособия.ДокументОснование = ДанныеСреднегоЗаработка.ДокументОснование
	|
	|УПОРЯДОЧИТЬ ПО
	|	Сотрудник";
	
	ПоПремиям = Новый Массив(2);
	ПоПремиям[0] = ПланыВидовРасчета.СреднийЗаработок.ПоПремиямИндексируемые;
	ПоПремиям[1] = ПланыВидовРасчета.СреднийЗаработок.ПоГодовойПремииИндексируемые;
	ПоФиксПремиямНеИндексируемые = Новый Массив(2);
	ПоФиксПремиямНеИндексируемые[0] = ПланыВидовРасчета.СреднийЗаработок.ПоФиксГодовойПремииНеИндексируемые;
	ПоФиксПремиямНеИндексируемые[1] = ПланыВидовРасчета.СреднийЗаработок.ПоФиксПремиямНеИндексируемые;
	
	Запрос.УстановитьПараметр("ДатаПередачиФССВыплатыПособий", ДатаПередачиФССВыплатыПособий);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ПоПремиям", ПоПремиям);
	Запрос.УстановитьПараметр("ПоФиксПремиямНеИндексируемые", ПоФиксПремиямНеИндексируемые);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			НоваяСтрока = РаботникиОрганизации.Добавить();
		
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			
			СуммаПособий = СуммаПособий + Выборка.СуммаПособия;
		
		КонецЦикла; 
	
	КонецЕсли; 
	
КонецПроцедуры // Автозаполнение()


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Собирает сведения по щапке документа для вывода напечать и проверки заполнения
//
// Параметры
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
// Возвращаемое значение:
//   <Тип.Вид>   - <описание возвращаемого значения>
//
Функция СформироватьЗапросПоШапкеДокумента()

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗаявлениеВФССНаДетейИнвалидов.Ссылка,
	|	ЗаявлениеВФССНаДетейИнвалидов.Номер,
	|	ЗаявлениеВФССНаДетейИнвалидов.Дата,
	|	ЗаявлениеВФССНаДетейИнвалидов.Организация КАК Организация,
	|	ЗаявлениеВФССНаДетейИнвалидов.НаименованиеТерриториальногоОрганаФСС,
	|	ЗаявлениеВФССНаДетейИнвалидов.РегистрационныйНомерФСС,
	|	ЗаявлениеВФССНаДетейИнвалидов.ДополнительныйКодФСС,
	|	ЗаявлениеВФССНаДетейИнвалидов.КодПодчиненностиФСС,
	|	ЗаявлениеВФССНаДетейИнвалидов.АдресОрганизации,
	|	ЗаявлениеВФССНаДетейИнвалидов.Руководитель,
	|	ЗаявлениеВФССНаДетейИнвалидов.ДолжностьРуководителя,
	|	ЗаявлениеВФССНаДетейИнвалидов.КраткийСоставДокумента,
	|	ЗаявлениеВФССНаДетейИнвалидов.СуммаПособий,
	|	ЗаявлениеВФССНаДетейИнвалидов.КоличествоСтраниц,
	|	ЗаявлениеВФССНаДетейИнвалидов.ТелефонСоставителя,
	|	ЗаявлениеВФССНаДетейИнвалидов.НаименованиеБанка,
	|	ЗаявлениеВФССНаДетейИнвалидов.НомерЛицевогоСчета,
	|	ЗаявлениеВФССНаДетейИнвалидов.НомерСчета,
	|	ЗаявлениеВФССНаДетейИнвалидов.БИКБанка,
	|	ЗаявлениеВФССНаДетейИнвалидов.КоррСчетБанка
	|ПОМЕСТИТЬ ВТДанныеДокумента
	|ИЗ
	|	Документ.ЗаявлениеВФССНаДетейИнвалидов КАК ЗаявлениеВФССНаДетейИнвалидов
	|ГДЕ
	|	ЗаявлениеВФССНаДетейИнвалидов.Ссылка = &ДокументСсылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВТДанныеДокумента.Ссылка,
	|	ВТДанныеДокумента.Номер,
	|	ВТДанныеДокумента.Дата,
	|	ВТДанныеДокумента.Организация,
	|	ВТДанныеДокумента.Организация.НаименованиеПолное КАК ОрганизацияНаименование,
	|	ВТДанныеДокумента.НаименованиеТерриториальногоОрганаФСС,
	|	ВТДанныеДокумента.РегистрационныйНомерФСС,
	|	ВТДанныеДокумента.ДополнительныйКодФСС,
	|	ВТДанныеДокумента.КодПодчиненностиФСС,
	|	ВТДанныеДокумента.АдресОрганизации,
	|	ЕСТЬNULL(ФИОФизЛицСрезПоследних.Фамилия + "" "" + ФИОФизЛицСрезПоследних.Имя + "" "" + ФИОФизЛицСрезПоследних.Отчество, ВТДанныеДокумента.Руководитель.Наименование) КАК ФИОРуководителя,
	|	ВТДанныеДокумента.ДолжностьРуководителя,
	|	ВТДанныеДокумента.КраткийСоставДокумента,
	|	ВТДанныеДокумента.СуммаПособий,
	|	ВТДанныеДокумента.КоличествоСтраниц,
	|	ВТДанныеДокумента.Организация.ИНН КАК ИНН,
	|	ВТДанныеДокумента.Организация.КПП КАК КПП,
	|	ВТДанныеДокумента.ТелефонСоставителя,
	|	ВТДанныеДокумента.НаименованиеБанка,
	|	ВТДанныеДокумента.НомерЛицевогоСчета,
	|	ВТДанныеДокумента.НомерСчета,
	|	ВТДанныеДокумента.БИКБанка,
	|	ВТДанныеДокумента.КоррСчетБанка
	|ИЗ
	|	ВТДанныеДокумента КАК ВТДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(
	|				&Дата,
	|				ФизЛицо В
	|					(ВЫБРАТЬ
	|						ВТДанныеДокумента.Руководитель
	|					ИЗ
	|						ВТДанныеДокумента КАК ВТДанныеДокумента)) КАК ФИОФизЛицСрезПоследних
	|		ПО ВТДанныеДокумента.Руководитель = ФИОФизЛицСрезПоследних.ФизЛицо";

	
	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка", Ссылка);
	Запрос.УстановитьПараметр("Дата", Дата);
	
	Возврат Запрос.Выполнить();	

КонецФункции // СформироватьЗапросПоШапкеДокумента()

// Запрос по табличной части РаботникиОрганизации для вывода на печать и проверки корректности заполнения
//
// Параметры
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
// Возвращаемое значение:
//   <Тип.Вид>   - <описание возвращаемого значения>
//
Функция СформироватьЗапросПоТЧРаботникиОрганизации()

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗаявлениеВФССНаДетейИнвалидовРаботникиОрганизации.НомерСтроки,
	|	ЗаявлениеВФССНаДетейИнвалидовРаботникиОрганизации.ФизЛицо,
	|	ЗаявлениеВФССНаДетейИнвалидовРаботникиОрганизации.Статус,
	|	ЗаявлениеВФССНаДетейИнвалидовРаботникиОрганизации.СреднедневнойЗаработок,
	|	ЗаявлениеВФССНаДетейИнвалидовРаботникиОрганизации.КоличествоСтраниц,
	|	ЗаявлениеВФССНаДетейИнвалидовРаботникиОрганизации.СуммаПособия
	|ПОМЕСТИТЬ ВТТЧРаботникиОрганизации
	|ИЗ
	|	Документ.ЗаявлениеВФССНаДетейИнвалидов.РаботникиОрганизации КАК ЗаявлениеВФССНаДетейИнвалидовРаботникиОрганизации
	|ГДЕ
	|	ЗаявлениеВФССНаДетейИнвалидовРаботникиОрганизации.Ссылка = &ДокументСсылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТЧРаботникиОрганизации.НомерСтроки,
	|	ЕСТЬNULL(ФИОФизЛицСрезПоследних.Фамилия + "" "" + ФИОФизЛицСрезПоследних.Имя + "" "" + ФИОФизЛицСрезПоследних.Отчество, ТЧРаботникиОрганизации.ФизЛицо.Наименование) КАК ФИОСотрудника,
	|	ТЧРаботникиОрганизации.Статус,
	|	ТЧРаботникиОрганизации.СреднедневнойЗаработок,
	|	ТЧРаботникиОрганизации.КоличествоСтраниц,
	|	ТЧРаботникиОрганизации.СуммаПособия,
	|	ФИОФизЛицСрезПоследних.Фамилия,
	|	ФИОФизЛицСрезПоследних.Имя,
	|	ФИОФизЛицСрезПоследних.Отчество
	|ИЗ
	|	ВТТЧРаботникиОрганизации КАК ТЧРаботникиОрганизации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(
	|				&Дата,
	|				ФизЛицо В
	|					(ВЫБРАТЬ
	|						ТЧРаботникиОрганизации.ФизЛицо
	|					ИЗ
	|						ВТТЧРаботникиОрганизации КАК ТЧРаботникиОрганизации)) КАК ФИОФизЛицСрезПоследних
	|		ПО ТЧРаботникиОрганизации.ФизЛицо = ФИОФизЛицСрезПоследних.ФизЛицо";
	
	
	Запрос.УстановитьПараметр("ДокументСсылка", Ссылка);
	Запрос.УстановитьПараметр("Дата", Дата);
	
	Возврат Запрос.Выполнить();

КонецФункции // СформироватьЗапросПоТЧРаботникиОрганизации()
 
// Проверяет правильность заполнения шапки документа.
// Если какой-то из реквизитов шапки, влияющий на проведение, не заполнен или
// заполнен некорректно, то выставляется флаг отказа в проведении.
// Проверка выполняется по выборке из результата запроса по шапке,
//
// Параметры: 
//	ВыборкаПоШапкеДокумента	- выборка из результата запрос
//	Отказ 					- флаг отказа
//	Заголовок				- Заголовок для сообщений об ошибках
Процедура ПроверкаЗаполненияШапкиДокумента(ВыборкаПоШапкеДокумента, Отказ, Заголовок)

	Если Не ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.Организация) Тогда
		ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке(ОбщегоНазначенияЗК.ПреобразоватьСтрокуИнтерфейса("Не указана организация!"), Отказ, Заголовок);
	КонецЕсли;

	Если Не ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.АдресОрганизации) Тогда
		ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке(ОбщегоНазначенияЗК.ПреобразоватьСтрокуИнтерфейса("Не указан адрес организации!"), Отказ, Заголовок);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.НаименованиеТерриториальногоОрганаФСС) Тогда
		ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке("Не указано наименование территориального органа ФСС!", Отказ, Заголовок);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.РегистрационныйНомерФСС) Тогда
		ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке("Не указан регистрационный номер в ФСС!", Отказ, Заголовок);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.КодПодчиненностиФСС) Тогда
		ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке("Не указан код подчиненности ФСС!", Отказ, Заголовок);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.СуммаПособий) Тогда
		ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке("Не указана сумма пособий!", Отказ, Заголовок);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.КоличествоСтраниц) Тогда
		ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке("Не указано количество страниц!", Отказ, Заголовок);
	КонецЕсли;
	
КонецПроцедуры // ПроверкаЗаполненияШапкиДокумента()

// Проверяет заполненность полей табличной части "РаботникиОрганизации"
// Проверка ведется по строке выборки по табличной части
//
// Параметры
// Параметры: 
//	СтрокаВыборкиПоТЧРаботникиОрганизации	- выборка из результата запрос
//	Отказ 									- флаг отказа
//	Заголовок								- Заголовок для сообщений об ошибках
//
Процедура ПроверитьЗаполнениеСтрокиТЧРаботникиОрганизации(СтрокаВыборкиПоТЧРаботникиОрганизации, Отказ, Заголовок)

	ТекстОшибкиСНомеромСтроки = НСтр("ru='В строке номер %НомерСтроки% табличной части ""Работники организации"": '");
	ТекстОшибкиСНомеромСтроки = СтрЗаменить(ТекстОшибкиСНомеромСтроки, "%НомерСтроки%", СтрокаВыборкиПоТЧРаботникиОрганизации.НомерСтроки);
	
	Если Не ЗначениеЗаполнено(СтрокаВыборкиПоТЧРаботникиОрганизации.ФИОСотрудника) Тогда
		ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке(ОбщегоНазначенияЗК.ПреобразоватьСтрокуИнтерфейса(ТекстОшибкиСНомеромСтроки + "Не указан сотрудник!"), Отказ, Заголовок);
	КонецЕсли;

	Если Не ЗначениеЗаполнено(СтрокаВыборкиПоТЧРаботникиОрганизации.Статус) Тогда
		ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке(ОбщегоНазначенияЗК.ПреобразоватьСтрокуИнтерфейса(ТекстОшибкиСНомеромСтроки + "Не указан статус!"), Отказ, Заголовок);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(СтрокаВыборкиПоТЧРаботникиОрганизации.СреднедневнойЗаработок) Тогда
		ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке(ОбщегоНазначенияЗК.ПреобразоватьСтрокуИнтерфейса(ТекстОшибкиСНомеромСтроки + "Не указан среднедневной заработок!"), Отказ, Заголовок);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(СтрокаВыборкиПоТЧРаботникиОрганизации.СуммаПособия) Тогда
		ОбщегоНазначенияЗК.ВывестиИнформациюОбОшибке(ОбщегоНазначенияЗК.ПреобразоватьСтрокуИнтерфейса(ТекстОшибкиСНомеромСтроки + "Не указана сумма пособия!"), Отказ, Заголовок);
	КонецЕсли;
	
КонецПроцедуры // ПроверитьЗаполнениеСтрокиТЧРаботникиОрганизации()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	МассивТЧ = Новый Массив();
	МассивТЧ.Добавить(РаботникиОрганизации);

	КраткийСоставДокумента = ПроцедурыУправленияПерсоналом.ЗаполнитьКраткийСоставДокумента(МассивТЧ);
	
КонецПроцедуры


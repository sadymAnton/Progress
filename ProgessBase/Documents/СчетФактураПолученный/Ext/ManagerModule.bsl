
Процедура СобратьДанныеДляПечати(Ссылка, ДанныеДляПечати)
	
	мВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	Корректировочный = Ссылка.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыПолученного.Корректировочный;

	Если Ссылка.СформированПриВводеНачальныхОстатковНДС Тогда
		Возврат;
	КонецЕсли;
	
	Если Корректировочный Тогда
		КорректируемыеСчетаФактуры = Новый ТаблицаЗначений;
		КорректируемыеСчетаФактуры.Колонки.Добавить("НомерСчетаФактуры");
		КорректируемыеСчетаФактуры.Колонки.Добавить("ДатаСчетаФактуры");
		КорректируемыеСчетаФактуры.Колонки.Добавить("НомерИсправления");
		КорректируемыеСчетаФактуры.Колонки.Добавить("ДатаИсправления");
	КонецЕсли;
	
	Для каждого СтрДокОснования Из Ссылка.ДокументыОснования Цикл
		// Получить экземпляр документа на печать
		
		Если НЕ ЗначениеЗаполнено(СтрДокОснования.ДокументОснование) Тогда
			Продолжить;
		КонецЕсли;
		
		ЭкземплярДанныхДляПечати = Неопределено;
		ТипОснования = ТипЗнч(СтрДокОснования.ДокументОснование);
		
		Если ТипОснования      = Тип("ДокументСсылка.ПоступлениеТоваровУслуг") Тогда
			ЭкземплярДанныхДляПечати = СобратьДанныеПоПоступлениюТоваров(СтрДокОснования.ДокументОснование, Ссылка);
		ИначеЕсли ТипОснования      = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя") Тогда
			ЭкземплярДанныхДляПечати = СобратьДанныеПоВозвратуТоваровОтПокупателя(СтрДокОснования.ДокументОснование, Ссылка);
		ИначеЕсли ТипОснования      = Тип("ДокументСсылка.ВозвратТоваровПоставщику") Тогда
			ЭкземплярДанныхДляПечати = СобратьДанныеПоВозвратуТоваров(СтрДокОснования.ДокументОснование, Ссылка);
		ИначеЕсли ТипОснования = Тип("ДокументСсылка.ПоступлениеТоваровУслугВНТТ") Тогда
			ДанныеДляПечати    = СобратьДанныеПоПоступлениюТоваровУслугВНТТ(СтрДокОснования.ДокументОснование, Ссылка);
		ИначеЕсли ТипОснования = Тип("ДокументСсылка.ОтчетКомиссионераОПродажах") Тогда
			ЭкземплярДанныхДляПечати = СобратьДанныеПоОтчетОПродажахКомиссионера(СтрДокОснования.ДокументОснование, Ссылка);
		ИначеЕсли ТипОснования = Тип("ДокументСсылка.ПоступлениеДопРасходов") Тогда
			ЭкземплярДанныхДляПечати = СобратьДанныеДопРасходам(СтрДокОснования.ДокументОснование, Ссылка);
		ИначеЕсли ТипОснования = Тип("ДокументСсылка.ПоступлениеНМА") Тогда
			ЭкземплярДанныхДляПечати = СобратьДанныеПоПоступлениюНМА(СтрДокОснования.ДокументОснование, Ссылка);
		ИначеЕсли ТипОснования = Тип("ДокументСсылка.ПолучениеУслугПоПереработке") Тогда
			ЭкземплярДанныхДляПечати = СобратьДанныеПолучениеУслугПоПереработке(СтрДокОснования.ДокументОснование, Ссылка)
		ИначеЕсли ТипОснования = Тип("ДокументСсылка.ОтражениеПоступленияТоваровИУслугНДС") Тогда
			ЭкземплярДанныхДляПечати = СобратьДанныеПоОтражениеПоступленияТоваровУслугНДС(СтрДокОснования.ДокументОснование, Ссылка)
		ИначеЕсли ТипОснования = Тип("ДокументСсылка.АвансовыйОтчет") Тогда
			ЭкземплярДанныхДляПечати = СобратьДанныеПоАвансовыйОтчет(СтрДокОснования.ДокументОснование, Ссылка)
		ИначеЕсли ТипОснования = Тип("ДокументСсылка.КорректировкаПоступления") Тогда
			Если Корректировочный Тогда
				ЭкземплярДанныхДляПечати = СобратьДанныеДляПечатиКорректировочногоСчетаФактуры(СтрДокОснования.ДокументОснование, Ссылка);
			ИначеЕсли Ссылка.ВидСчетаФактуры <> Перечисления.ВидСчетаФактурыПолученного.НаАванс Тогда
				ЭкземплярДанныхДляПечати = СобратьДанныеДляПечатиИсправленияСчетаФактуры(СтрДокОснования.ДокументОснование, Ссылка);
			КонецЕсли;
		КонецЕсли;
		
		Если ЭкземплярДанныхДляПечати = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		// Заполнение данных для печати
		Если ДанныеДляПечати = Неопределено Тогда
			ДанныеДляПечати = ЭкземплярДанныхДляПечати;
		Иначе
			
			Для каждого СтрДанных Из ЭкземплярДанныхДляПечати Цикл
				
				Если СтрДанных.Ключ = "ТабличнаяЧасть" Тогда
					СтараяТабЧасть = ДанныеДляПечати.ТабличнаяЧасть;
					НоваяТабЧасть  = СтрДанных.Значение;
					
					Для каждого НоваяСтрокаТабЧасти Из НоваяТабЧасть Цикл
						СтрокаТабЧасти = СтараяТабЧасть.Добавить();
						Для каждого ТекКол Из НоваяТабЧасть.Колонки Цикл
							Если СтараяТабЧасть.Колонки.Найти(ТекКол.Имя) <> Неопределено Тогда
								СтрокаТабЧасти[ТекКол.Имя] = НоваяСтрокаТабЧасти[ТекКол.Имя];
							КонецЕсли;
						КонецЦикла;
					КонецЦикла;
					
				ИначеЕсли НЕ ДанныеДляПечати.Свойство(СтрДанных.Ключ) Тогда
					// Если данный параметр для печати шапки документа еще не  определен - то определяем его
					ДанныеДляПечати.Вставить(СтрДанных.Ключ, СтрДанных.Значение);
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
		Если Корректировочный Тогда
			МассивСтрокОснований = Ссылка.ДокументыОснования.НайтиСтроки(Новый Структура("ДокументОснование", СтрДокОснования.ДокументОснование));
			Для Каждого СтрокаМассива ИЗ МассивСтрокОснований Цикл
				НовоеОснование = КорректируемыеСчетаФактуры.Добавить();	
				// Номер и дата исходного счета-фактуры
				НовоеОснование.НомерСчетаФактуры 	= СокрЛП(СтрокаМассива.НомерИсходногоДокумента);
				НовоеОснование.ДатаСчетаФактуры 	= Формат(СтрокаМассива.ДатаИсходногоДокумента, "ДЛФ='ДД'");
				// Номер и дата исправления исходного счета-фактуры
				Если ЗначениеЗаполнено(СтрокаМассива.НомерИсправленияИсходногоДокумента) Тогда
					НовоеОснование.НомерИсправления = СокрЛП(СтрокаМассива.НомерИсправленияИсходногоДокумента);
					НовоеОснование.ДатаИсправления	= Формат(СтрокаМассива.ДатаИсправленияИсходногоДокумента, "ДЛФ='ДД'");
				Иначе
					НовоеОснование.НомерИсправления	= "--";
					НовоеОснование.ДатаИсправления	= "--";
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
	
	Если Корректировочный И ДанныеДляПечати <> Неопределено Тогда
		
		КорректируемыеСчетаФактуры.Свернуть("НомерСчетаФактуры,ДатаСчетаФактуры,НомерИсправления,ДатаИсправления");
		ДанныеДляПечати.Вставить("КорректируемыеСчетаФактуры", КорректируемыеСчетаФактуры);
		
		КолонкиГруппировок = ""
		+"Номенклатура,"
		+"НаименованиеТовара,"
		+"НаименованиеЕдиницыИзмерения,"
		+"ЕдиницаИзмеренияКод,"
		+"ЦенаДоИзменения,"	
		+"ЦенаПослеИзменения,"
		+"СтавкаНДС";
		
		КолонкиСуммирования = ""
		+"СуммаНДСДоИзменения,"
		+"СуммаНДСПослеИзменения,"
		+"СтоимостьСНДСДоИзменения,"
		+"СтоимостьСНДСПослеИзменения,"
		+"РазницаБезНДСУвеличение,"
		+"РазницаБезНДСУменьшение,"
		+"РазницаНДСУвеличение,"
		+"РазницаНДСУменьшение,"
		+"РазницаСНДСУвеличение,"
		+"РазницаСНДСУменьшение,"
		+"СтоимостьБезНДСДоИзменения,"
		+"СтоимостьБезНДСПослеИзменения,"
		+"КоличествоДоИзменения,"
		+"КоличествоПослеИзменения";
		
		ДанныеДляПечати.ТабличнаяЧасть.Свернуть(КолонкиГруппировок, КолонкиСуммирования);
		
	КонецЕсли; 	
	
КонецПроцедуры

// Функция собирает данные по документу-основанию КорректировкаПоступления и
// возвращает типизированную структуру с данными для печати исправленного счета-фактуры
// 
Функция СобратьДанныеДляПечатиИсправленияСчетаФактуры(ДокОснование, Ссылка)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументОснование", ДокОснование);
	Запрос.УстановитьПараметр("ПустойКонтрагент", Справочники.Контрагенты.ПустаяСсылка());
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КорректировкаПоступления.Организация,
	|	КорректировкаПоступления.Организация КАК Покупатель,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(КорректировкаПоступления.Грузополучатель, &ПустойКонтрагент) = &ПустойКонтрагент
	|			ТОГДА КорректировкаПоступления.Организация
	|		ИНАЧЕ КорректировкаПоступления.Грузополучатель
	|	КОНЕЦ КАК Грузополучатель,
	|	КорректировкаПоступления.Контрагент КАК Поставщик,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(КорректировкаПоступления.Грузоотправитель, &ПустойКонтрагент) = &ПустойКонтрагент
	|			ТОГДА ""он же""
	|		ИНАЧЕ КорректировкаПоступления.Грузоотправитель
	|	КОНЕЦ КАК Грузоотправитель,
	|	КорректировкаПоступления.СуммаДокумента КАК Сумма,
	|	КорректировкаПоступления.ВалютаДокумента КАК Валюта,
	|	КорректировкаПоступления.УчитыватьНДС КАК УчитыватьНДС,
	|	КорректировкаПоступления.СуммаВключаетНДС КАК СуммаВключаетНДС
	|ИЗ
	|	Документ.КорректировкаПоступления КАК КорректировкаПоступления
	|ГДЕ
	|	КорректировкаПоступления.Ссылка = &ДокументОснование";

	ЗапросПоТоварам = Новый Запрос();
	ЗапросПоТоварам.УстановитьПараметр("ДокументОснование", ДокОснование);
	ЗапросПоТоварам.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаПоТоварам.НомерСтроки КАК НомерСтроки,
	|	ТаблицаПоТоварам.Номенклатура КАК Товар,
	|	ТаблицаПоТоварам.СерияНоменклатуры КАК СерияНоменклатуры,
	|	ТаблицаПоТоварам.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ТаблицаПоТоварам.СерияНоменклатуры.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ТаблицаПоТоварам.СерияНоменклатуры.НомерГТД КАК НомерГТД,
	|	ТаблицаПоТоварам.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	СУММА(ТаблицаПоТоварам.Количество) КАК Количество,
	|	ТаблицаПоТоварам.Цена КАК Цена,
	|	ТаблицаПоТоварам.Сумма КАК Сумма,
	|	ТаблицаПоТоварам.СуммаНДС КАК СуммаНДС,
	|	ТаблицаПоТоварам.СтавкаНДС КАК СтавкаНДС
	|ПОМЕСТИТЬ ТаблицаТовары
	|ИЗ
	|	Документ.КорректировкаПоступления.Товары КАК ТаблицаПоТоварам
	|ГДЕ
	|	ТаблицаПоТоварам.Ссылка = &ДокументОснование
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаПоТоварам.НомерСтроки,
	|	ТаблицаПоТоварам.Номенклатура,
	|	ТаблицаПоТоварам.СтавкаНДС,
	|	ТаблицаПоТоварам.Цена,
	|	ТаблицаПоТоварам.СерияНоменклатуры,
	|	ТаблицаПоТоварам.ХарактеристикаНоменклатуры,
	|	ТаблицаПоТоварам.СерияНоменклатуры.СтранаПроисхождения,
	|	ТаблицаПоТоварам.СерияНоменклатуры.НомерГТД,
	|	ТаблицаПоТоварам.Сумма,
	|	ТаблицаПоТоварам.СуммаНДС,
	|	ТаблицаПоТоварам.ЕдиницаИзмерения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТовары.Товар,
	|	ВЫРАЗИТЬ(ТаблицаТовары.Товар.НаименованиеПолное КАК СТРОКА(1000)) КАК ТоварНаименование,
	|	ТаблицаТовары.СерияНоменклатуры КАК Серия,
	|	ТаблицаТовары.ХарактеристикаНоменклатуры КАК Характеристика,
	|	ТаблицаТовары.СтранаПроисхождения,
	|	ТаблицаТовары.СтранаПроисхождения.Наименование КАК ПредставлениеСтраны,
	|	ТаблицаТовары.СтранаПроисхождения.Код КАК СтранаПроисхожденияКод,
	|	ТаблицаТовары.НомерГТД,
	|	ТаблицаТовары.НомерГТД.Представление КАК ПредставлениеГТД,
	|	ТаблицаТовары.ЕдиницаИзмерения.Представление КАК ЕдиницаИзмерения,
	|	ТаблицаТовары.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код КАК ЕдиницаИзмеренияКод,
	|	ТаблицаТовары.Количество,
	|	ТаблицаТовары.Цена,
	|	ТаблицаТовары.Сумма,
	|	ТаблицаТовары.СуммаНДС,
	|	ТаблицаТовары.СтавкаНДС,
	|	""Товары"" КАК ВидПоступления
	|ИЗ
	|	ТаблицаТовары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Сумма <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаПоУслугам.НомерСтроки,
	|	ТаблицаПоУслугам.Номенклатура,
	|	ВЫРАЗИТЬ(ТаблицаПоУслугам.Содержание КАК СТРОКА(1000)),
	|	NULL,
	|	NULL,
	|	""Россия"",
	|	""Россия"",
	|	"""",
	|	"""",
	|	"""",
	|	ТаблицаПоУслугам.Номенклатура.ЕдиницаХраненияОстатков,
	|	ТаблицаПоУслугам.Номенклатура.ЕдиницаХраненияОстатков.ЕдиницаПоКлассификатору.Код,
	|	ТаблицаПоУслугам.Количество,
	|	ТаблицаПоУслугам.Цена,
	|	ТаблицаПоУслугам.Сумма,
	|	ТаблицаПоУслугам.СуммаНДС,
	|	ТаблицаПоУслугам.СтавкаНДС,
	|	""Услуги""
	|ИЗ
	|	Документ.КорректировкаПоступления.Услуги КАК ТаблицаПоУслугам
	|ГДЕ
	|	ТаблицаПоУслугам.Ссылка = &ДокументОснование
	|	И ТаблицаПоУслугам.Сумма <> 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВидПоступления,
	|	НомерСтроки";

	Шапка = Запрос.Выполнить().Выбрать();
	ВыборкаСтрокТовары = ЗапросПоТоварам.Выполнить().Выбрать();
    	
	Шапка.Следующий();
	
	ДанныеДляПечати = Новый Структура();
	
	Покупатель = Шапка.Покупатель;
	
	Если ЗначениеЗаполнено(Покупатель.ГоловнаяОрганизация) Тогда 
		Покупатель = Покупатель.ГоловнаяОрганизация;
	КонецЕсли;
	
	РеквизитыСФ = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(
		Ссылка, "Дата,НомерВходящегоДокумента,ДатаВходящегоДокумента");
    	
	ДанныеДляПечати.Вставить("Организация",      Шапка.Организация);
	ДанныеДляПечати.Вставить("Номер",            РеквизитыСФ.НомерВходящегоДокумента);
	ДанныеДляПечати.Вставить("Дата",             РеквизитыСФ.ДатаВходящегоДокумента);
	ДанныеДляПечати.Вставить("Поставщик",        Шапка.Поставщик);
	ДанныеДляПечати.Вставить("Грузоотправитель", Шапка.Грузоотправитель);
	ДанныеДляПечати.Вставить("Покупатель",       Покупатель);
	ДанныеДляПечати.Вставить("Грузополучатель",  Шапка.Грузополучатель);
	ДанныеДляПечати.Вставить("Сумма",            Шапка.Сумма);
	ДанныеДляПечати.Вставить("Валюта",           Шапка.Валюта);
	ДанныеДляПечати.Вставить("УчитыватьНДС",     Шапка.УчитыватьНДС);
	ДанныеДляПечати.Вставить("СуммаВключаетНДС", Шапка.СуммаВключаетНДС);
	ДанныеДляПечати.Вставить("ФИОРуководителя",       );
	ДанныеДляПечати.Вставить("ФИОГлавногоБухгалтера", );

	Товары = ИнициализацияТаблицыСтрок();

	ЕстьТовары = Ложь;
	
	Пока ВыборкаСтрокТовары.Следующий() Цикл
		
		ЕстьТовары = ЕстьТовары ИЛИ ВыборкаСтрокТовары.ВидПоступления <> "Услуги";
		Строчка    = Товары.Добавить();
		ЗаполнитьЗначенияСвойств(Строчка, ВыборкаСтрокТовары);
		
		Строчка.ТоварНаименование = СокрЛП(ВыборкаСтрокТовары.ТоварНаименование) + ФормированиеПечатныхФормСервер.ПредставлениеСерий(ВыборкаСтрокТовары);
		Строчка.СуммаВключаетНДС = Шапка.СуммаВключаетНДС;

	КонецЦикла;

	Если НЕ ЕстьТовары Тогда
		ДанныеДляПечати.Грузоотправитель = "";
		ДанныеДляПечати.Грузополучатель = "";		
	КонецЕсли;
	
	ДанныеДляПечати.Вставить("ТабличнаяЧасть", Товары);

	Возврат ДанныеДляПечати;
		
КонецФункции	

// Функция собирает данные по документу-основанию КорректировкаПоступления и
// возвращает типизированную структуру с данными для печати корректировочного 
// счета-фактуры
// 
Функция СобратьДанныеДляПечатиКорректировочногоСчетаФактуры(ДокОснование, Ссылка)
	
	Перем ПодразделениеОтветственныхЛиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ДокументОснование", ДокОснование);
	Запрос.УстановитьПараметр("ПустойКонтрагент", Справочники.Контрагенты.ПустаяСсылка());
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КорректировкаПоступления.Организация,
	|	КорректировкаПоступления.Организация КАК Покупатель,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(КорректировкаПоступления.Грузополучатель, &ПустойКонтрагент) = &ПустойКонтрагент
	|			ТОГДА КорректировкаПоступления.Организация
	|		ИНАЧЕ КорректировкаПоступления.Грузополучатель
	|	КОНЕЦ КАК Грузополучатель,
	|	КорректировкаПоступления.Контрагент КАК Поставщик,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(КорректировкаПоступления.Грузоотправитель, &ПустойКонтрагент) = &ПустойКонтрагент
	|			ТОГДА ""он же""
	|		ИНАЧЕ КорректировкаПоступления.Грузоотправитель
	|	КОНЕЦ КАК Грузоотправитель,
	|	КорректировкаПоступления.СуммаДокумента КАК Сумма,
	|	КорректировкаПоступления.ВалютаДокумента КАК Валюта,
	|	КорректировкаПоступления.УчитыватьНДС КАК УчитыватьНДС,
	|	КорректировкаПоступления.СуммаВключаетНДС КАК СуммаВключаетНДС
	|ПОМЕСТИТЬ ВТИсходныйДокумент
	|ИЗ
	|	Документ.КорректировкаПоступления КАК КорректировкаПоступления
	|ГДЕ
	|	КорректировкаПоступления.Ссылка = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТИсходныйДокумент.Организация,
	|	ВТИсходныйДокумент.Покупатель,
	|	ВТИсходныйДокумент.Грузополучатель,
	|	ВТИсходныйДокумент.Поставщик,
	|	ВТИсходныйДокумент.Грузоотправитель,
	|	ВТИсходныйДокумент.Сумма,
	|	ВТИсходныйДокумент.Валюта,
	|	ВТИсходныйДокумент.УчитыватьНДС,
	|	ВТИсходныйДокумент.СуммаВключаетНДС
	|ИЗ
	|	ВТИсходныйДокумент КАК ВТИсходныйДокумент";
	
	Шапка = Запрос.Выполнить().Выбрать();
	Шапка.Следующий();
	
	ДанныеДляПечати = Новый Структура();
	
	Покупатель = Шапка.Покупатель;
	
	Если ЗначениеЗаполнено(Покупатель.ГоловнаяОрганизация) Тогда 
		Покупатель = Покупатель.ГоловнаяОрганизация;
	КонецЕсли;
	
	РеквизитыСФ = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(Ссылка, 
		"НомерВходящегоДокумента,ДатаВходящегоДокумента,
		|Исправление,НомерИсправления,ДатаИсправления,
		|НомерИсходногоДокумента,ДатаИсходногоДокумента");
    	
	ДанныеДляПечати.Вставить("Организация",       Шапка.Организация);
	ДанныеДляПечати.Вставить("Номер",             РеквизитыСФ.НомерВходящегоДокумента);
	ДанныеДляПечати.Вставить("Дата",              Формат(РеквизитыСФ.ДатаВходящегоДокумента, "ДФ='дд ММММ гггг'") + " г.");
	ДанныеДляПечати.Вставить("Поставщик",         Шапка.Поставщик);
	ДанныеДляПечати.Вставить("Валюта",			  Шапка.Валюта);
	ДанныеДляПечати.Вставить("Покупатель",        Покупатель);
	ДанныеДляПечати.Вставить("СуммаВключаетНДС",  Шапка.СуммаВключаетНДС);
	
	ДанныеДляПечати.Вставить("НомерСчетаФактуры", РеквизитыСФ.НомерИсходногоДокумента);
	ДанныеДляПечати.Вставить("ДатаСчетаФактуры",  Формат(РеквизитыСФ.ДатаИсходногоДокумента, "ДФ='дд ММММ гггг'") + " г.");
	ДанныеДляПечати.Вставить("НомерИсправленияКорректировочного", РеквизитыСФ.НомерИсправления);
	ДанныеДляПечати.Вставить("ДатаИсправленияКорректировочного",  РеквизитыСФ.ДатаИсправления);
	ДанныеДляПечати.Вставить("ФИОРуководителя",       );
	ДанныеДляПечати.Вставить("ФИОГлавногоБухгалтера", );
		
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КорректировкаПоступленияТовары.Ссылка,
	|	КорректировкаПоступленияТовары.НомерСтроки,
	|	КорректировкаПоступленияТовары.Номенклатура,
	|	ВЫРАЗИТЬ(КорректировкаПоступленияТовары.Номенклатура.НаименованиеПолное КАК СТРОКА(1000)) КАК НаименованиеТовара,
	|	КорректировкаПоступленияТовары.Номенклатура.БазоваяЕдиницаИзмерения КАК НаименованиеЕдиницыИзмерения,
	|	КорректировкаПоступленияТовары.Номенклатура.БазоваяЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияКод,
	|	КорректировкаПоступленияТовары.СтавкаНДС,
	|	КорректировкаПоступленияТовары.СуммаНДС КАК СуммаНДСПослеИзменения,
	|	КорректировкаПоступленияТовары.СуммаНДСДоИзменения,
	|	КорректировкаПоступленияТовары.Количество КАК КоличествоПослеИзменения,
	|	КорректировкаПоступленияТовары.КоличествоДоИзменения,
	|	ВЫРАЗИТЬ(ВЫБОР
	|		КОГДА КорректировкаПоступленияТовары.Количество = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ИсходныйДокумент.СуммаВключаетНДС
	|					ТОГДА (КорректировкаПоступленияТовары.Сумма - КорректировкаПоступленияТовары.СуммаНДС) / КорректировкаПоступленияТовары.Количество
	|				ИНАЧЕ КорректировкаПоступленияТовары.Сумма / КорректировкаПоступленияТовары.Количество
	|			КОНЕЦ
	|	КОНЕЦ КАК ЧИСЛО(15, 2)) КАК ЦенаПослеИзменения,
	|	ВЫРАЗИТЬ(ВЫБОР
	|		КОГДА КорректировкаПоступленияТовары.КоличествоДоИзменения = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ИсходныйДокумент.СуммаВключаетНДС
	|					ТОГДА (КорректировкаПоступленияТовары.СуммаДоИзменения - КорректировкаПоступленияТовары.СуммаНДСДоИзменения) / КорректировкаПоступленияТовары.КоличествоДоИзменения
	|				ИНАЧЕ КорректировкаПоступленияТовары.СуммаДоИзменения / КорректировкаПоступленияТовары.КоличествоДоИзменения
	|			КОНЕЦ
	|	КОНЕЦ КАК ЧИСЛО(15, 2)) КАК ЦенаДоИзменения,
	|	ВЫБОР
	|		КОГДА ИсходныйДокумент.СуммаВключаетНДС
	|			ТОГДА КорректировкаПоступленияТовары.Сумма - КорректировкаПоступленияТовары.СуммаНДС
	|		ИНАЧЕ КорректировкаПоступленияТовары.Сумма
	|	КОНЕЦ КАК СтоимостьБезНДСПослеИзменения,
	|	ВЫБОР
	|		КОГДА ИсходныйДокумент.СуммаВключаетНДС
	|			ТОГДА КорректировкаПоступленияТовары.СуммаДоИзменения - КорректировкаПоступленияТовары.СуммаНДСДоИзменения
	|		ИНАЧЕ КорректировкаПоступленияТовары.СуммаДоИзменения
	|	КОНЕЦ КАК СтоимостьБезНДСДоИзменения,
	|	ВЫБОР
	|		КОГДА ИсходныйДокумент.СуммаВключаетНДС
	|			ТОГДА КорректировкаПоступленияТовары.Сумма
	|		ИНАЧЕ КорректировкаПоступленияТовары.Сумма + КорректировкаПоступленияТовары.СуммаНДС
	|	КОНЕЦ КАК СтоимостьСНДСПослеИзменения,
	|	ВЫБОР
	|		КОГДА ИсходныйДокумент.СуммаВключаетНДС
	|			ТОГДА КорректировкаПоступленияТовары.СуммаДоИзменения
	|		ИНАЧЕ КорректировкаПоступленияТовары.СуммаДоИзменения + КорректировкаПоступленияТовары.СуммаНДСДоИзменения
	|	КОНЕЦ КАК СтоимостьСНДСДоИзменения,
	|	ВЫБОР
	|		КОГДА КорректировкаПоступленияТовары.СуммаНДСДоИзменения - КорректировкаПоступленияТовары.СуммаНДС > 0
	|			ТОГДА КорректировкаПоступленияТовары.СуммаНДСДоИзменения - КорректировкаПоступленияТовары.СуммаНДС
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РазницаНДСУменьшение,
	|	ВЫБОР
	|		КОГДА КорректировкаПоступленияТовары.СуммаНДСДоИзменения - КорректировкаПоступленияТовары.СуммаНДС < 0
	|			ТОГДА (КорректировкаПоступленияТовары.СуммаНДСДоИзменения - КорректировкаПоступленияТовары.СуммаНДС) * -1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РазницаНДСУвеличение,
	|	ВЫБОР
	|		КОГДА ИсходныйДокумент.СуммаВключаетНДС
	|			ТОГДА ВЫБОР
	|					КОГДА КорректировкаПоступленияТовары.СуммаДоИзменения - КорректировкаПоступленияТовары.СуммаНДСДоИзменения - (КорректировкаПоступленияТовары.Сумма - КорректировкаПоступленияТовары.СуммаНДС) > 0
	|						ТОГДА КорректировкаПоступленияТовары.СуммаДоИзменения - КорректировкаПоступленияТовары.СуммаНДСДоИзменения - (КорректировкаПоступленияТовары.Сумма - КорректировкаПоступленияТовары.СуммаНДС)
	|					ИНАЧЕ 0
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА КорректировкаПоступленияТовары.СуммаДоИзменения - КорректировкаПоступленияТовары.Сумма > 0
	|					ТОГДА КорректировкаПоступленияТовары.СуммаДоИзменения - КорректировкаПоступленияТовары.Сумма
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ КАК РазницаБезНДСУменьшение,
	|	ВЫБОР
	|		КОГДА ИсходныйДокумент.СуммаВключаетНДС
	|			ТОГДА ВЫБОР
	|					КОГДА КорректировкаПоступленияТовары.СуммаДоИзменения - КорректировкаПоступленияТовары.СуммаНДСДоИзменения - (КорректировкаПоступленияТовары.Сумма - КорректировкаПоступленияТовары.СуммаНДС) < 0
	|						ТОГДА (КорректировкаПоступленияТовары.СуммаДоИзменения - КорректировкаПоступленияТовары.СуммаНДСДоИзменения - (КорректировкаПоступленияТовары.Сумма - КорректировкаПоступленияТовары.СуммаНДС)) * -1
	|					ИНАЧЕ 0
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА КорректировкаПоступленияТовары.СуммаДоИзменения - КорректировкаПоступленияТовары.Сумма < 0
	|					ТОГДА (КорректировкаПоступленияТовары.СуммаДоИзменения - КорректировкаПоступленияТовары.Сумма) * -1
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ КАК РазницаБезНДСУвеличение,
	|	ВЫБОР
	|		КОГДА ИсходныйДокумент.СуммаВключаетНДС
	|			ТОГДА ВЫБОР
	|					КОГДА КорректировкаПоступленияТовары.СуммаДоИзменения - КорректировкаПоступленияТовары.Сумма > 0
	|						ТОГДА КорректировкаПоступленияТовары.СуммаДоИзменения - КорректировкаПоступленияТовары.Сумма
	|					ИНАЧЕ 0
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА КорректировкаПоступленияТовары.СуммаДоИзменения + КорректировкаПоступленияТовары.СуммаНДСДоИзменения - (КорректировкаПоступленияТовары.Сумма + КорректировкаПоступленияТовары.СуммаНДС) > 0
	|					ТОГДА КорректировкаПоступленияТовары.СуммаДоИзменения + КорректировкаПоступленияТовары.СуммаНДСДоИзменения - (КорректировкаПоступленияТовары.Сумма + КорректировкаПоступленияТовары.СуммаНДС)
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ КАК РазницаСНДСУменьшение,
	|	ВЫБОР
	|		КОГДА ИсходныйДокумент.СуммаВключаетНДС
	|			ТОГДА ВЫБОР
	|					КОГДА КорректировкаПоступленияТовары.СуммаДоИзменения - КорректировкаПоступленияТовары.Сумма < 0
	|						ТОГДА (КорректировкаПоступленияТовары.СуммаДоИзменения - КорректировкаПоступленияТовары.Сумма) * -1
	|					ИНАЧЕ 0
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА КорректировкаПоступленияТовары.СуммаДоИзменения + КорректировкаПоступленияТовары.СуммаНДСДоИзменения - (КорректировкаПоступленияТовары.Сумма + КорректировкаПоступленияТовары.СуммаНДС) < 0
	|					ТОГДА (КорректировкаПоступленияТовары.СуммаДоИзменения + КорректировкаПоступленияТовары.СуммаНДСДоИзменения - (КорректировкаПоступленияТовары.Сумма + КорректировкаПоступленияТовары.СуммаНДС)) * -1
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ КАК РазницаСНДСУвеличение
	|ИЗ
	|	Документ.КорректировкаПоступления.Товары КАК КорректировкаПоступленияТовары,
	|	ВТИсходныйДокумент КАК ИсходныйДокумент
	|ГДЕ
	|	КорректировкаПоступленияТовары.Ссылка = &ДокументОснование
	|	И (КорректировкаПоступленияТовары.Количество <> КорректировкаПоступленияТовары.КоличествоДоИзменения
	|			ИЛИ КорректировкаПоступленияТовары.Сумма <> КорректировкаПоступленияТовары.СуммаДоИзменения
	|			ИЛИ КорректировкаПоступленияТовары.СуммаНДС <> КорректировкаПоступленияТовары.СуммаНДСДоИзменения
	|			ИЛИ КорректировкаПоступленияТовары.Цена <> КорректировкаПоступленияТовары.ЦенаДоИзменения)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КорректировкаПоступленияУслуги.Ссылка,
	|	КорректировкаПоступленияУслуги.НомерСтроки,
	|	КорректировкаПоступленияУслуги.Номенклатура,
	|	ВЫРАЗИТЬ(КорректировкаПоступленияУслуги.Содержание КАК СТРОКА(1000)),
	|	КорректировкаПоступленияУслуги.Номенклатура.БазоваяЕдиницаИзмерения,
	|	КорректировкаПоступленияУслуги.Номенклатура.БазоваяЕдиницаИзмерения.Код,
	|	КорректировкаПоступленияУслуги.СтавкаНДС,
	|	КорректировкаПоступленияУслуги.СуммаНДС,
	|	КорректировкаПоступленияУслуги.СуммаНДСДоИзменения,
	|	КорректировкаПоступленияУслуги.Количество,
	|	КорректировкаПоступленияУслуги.КоличествоДоИзменения,
	|	ВЫРАЗИТЬ(ВЫБОР
	|		КОГДА КорректировкаПоступленияУслуги.Количество = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ИсходныйДокумент.СуммаВключаетНДС
	|					ТОГДА (КорректировкаПоступленияУслуги.Сумма - КорректировкаПоступленияУслуги.СуммаНДС) / КорректировкаПоступленияУслуги.Количество
	|				ИНАЧЕ КорректировкаПоступленияУслуги.Сумма / КорректировкаПоступленияУслуги.Количество
	|			КОНЕЦ
	|	КОНЕЦ КАК ЧИСЛО(15, 2)),
	|	ВЫРАЗИТЬ(ВЫБОР
	|		КОГДА КорректировкаПоступленияУслуги.КоличествоДоИзменения = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ИсходныйДокумент.СуммаВключаетНДС
	|					ТОГДА (КорректировкаПоступленияУслуги.СуммаДоИзменения - КорректировкаПоступленияУслуги.СуммаНДСДоИзменения) / КорректировкаПоступленияУслуги.КоличествоДоИзменения
	|				ИНАЧЕ КорректировкаПоступленияУслуги.СуммаДоИзменения / КорректировкаПоступленияУслуги.КоличествоДоИзменения
	|			КОНЕЦ
	|	КОНЕЦ КАК ЧИСЛО(15, 2)),
	|	ВЫБОР
	|		КОГДА ИсходныйДокумент.СуммаВключаетНДС
	|			ТОГДА КорректировкаПоступленияУслуги.Сумма - КорректировкаПоступленияУслуги.СуммаНДС
	|		ИНАЧЕ КорректировкаПоступленияУслуги.Сумма
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ИсходныйДокумент.СуммаВключаетНДС
	|			ТОГДА КорректировкаПоступленияУслуги.СуммаДоИзменения - КорректировкаПоступленияУслуги.СуммаНДСДоИзменения
	|		ИНАЧЕ КорректировкаПоступленияУслуги.СуммаДоИзменения
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ИсходныйДокумент.СуммаВключаетНДС
	|			ТОГДА КорректировкаПоступленияУслуги.Сумма
	|		ИНАЧЕ КорректировкаПоступленияУслуги.Сумма + КорректировкаПоступленияУслуги.СуммаНДС
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ИсходныйДокумент.СуммаВключаетНДС
	|			ТОГДА КорректировкаПоступленияУслуги.СуммаДоИзменения
	|		ИНАЧЕ КорректировкаПоступленияУслуги.СуммаДоИзменения + КорректировкаПоступленияУслуги.СуммаНДСДоИзменения
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА КорректировкаПоступленияУслуги.СуммаНДСДоИзменения - КорректировкаПоступленияУслуги.СуммаНДС > 0
	|			ТОГДА КорректировкаПоступленияУслуги.СуммаНДСДоИзменения - КорректировкаПоступленияУслуги.СуммаНДС
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА КорректировкаПоступленияУслуги.СуммаНДСДоИзменения - КорректировкаПоступленияУслуги.СуммаНДС < 0
	|			ТОГДА (КорректировкаПоступленияУслуги.СуммаНДСДоИзменения - КорректировкаПоступленияУслуги.СуммаНДС) * -1
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ИсходныйДокумент.СуммаВключаетНДС
	|			ТОГДА ВЫБОР
	|					КОГДА КорректировкаПоступленияУслуги.СуммаДоИзменения - КорректировкаПоступленияУслуги.СуммаНДСДоИзменения - КорректировкаПоступленияУслуги.Сумма - КорректировкаПоступленияУслуги.СуммаНДС > 0
	|						ТОГДА КорректировкаПоступленияУслуги.СуммаДоИзменения - КорректировкаПоступленияУслуги.СуммаНДСДоИзменения - КорректировкаПоступленияУслуги.Сумма - КорректировкаПоступленияУслуги.СуммаНДС
	|					ИНАЧЕ 0
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА КорректировкаПоступленияУслуги.СуммаДоИзменения - КорректировкаПоступленияУслуги.Сумма > 0
	|					ТОГДА КорректировкаПоступленияУслуги.СуммаДоИзменения - КорректировкаПоступленияУслуги.Сумма
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ИсходныйДокумент.СуммаВключаетНДС
	|			ТОГДА ВЫБОР
	|					КОГДА КорректировкаПоступленияУслуги.СуммаДоИзменения - КорректировкаПоступленияУслуги.СуммаНДСДоИзменения - КорректировкаПоступленияУслуги.Сумма - КорректировкаПоступленияУслуги.СуммаНДС < 0
	|						ТОГДА (КорректировкаПоступленияУслуги.СуммаДоИзменения - КорректировкаПоступленияУслуги.СуммаНДСДоИзменения - КорректировкаПоступленияУслуги.Сумма - КорректировкаПоступленияУслуги.СуммаНДС) * -1
	|					ИНАЧЕ 0
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА КорректировкаПоступленияУслуги.СуммаДоИзменения - КорректировкаПоступленияУслуги.Сумма < 0
	|					ТОГДА (КорректировкаПоступленияУслуги.СуммаДоИзменения - КорректировкаПоступленияУслуги.Сумма) * -1
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ИсходныйДокумент.СуммаВключаетНДС
	|			ТОГДА ВЫБОР
	|					КОГДА КорректировкаПоступленияУслуги.СуммаДоИзменения - КорректировкаПоступленияУслуги.Сумма > 0
	|						ТОГДА КорректировкаПоступленияУслуги.СуммаДоИзменения - КорректировкаПоступленияУслуги.Сумма
	|					ИНАЧЕ 0
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА КорректировкаПоступленияУслуги.СуммаДоИзменения + КорректировкаПоступленияУслуги.СуммаНДСДоИзменения - (КорректировкаПоступленияУслуги.Сумма + КорректировкаПоступленияУслуги.СуммаНДС) > 0
	|					ТОГДА КорректировкаПоступленияУслуги.СуммаДоИзменения + КорректировкаПоступленияУслуги.СуммаНДСДоИзменения - (КорректировкаПоступленияУслуги.Сумма + КорректировкаПоступленияУслуги.СуммаНДС)
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ИсходныйДокумент.СуммаВключаетНДС
	|			ТОГДА ВЫБОР
	|					КОГДА КорректировкаПоступленияУслуги.СуммаДоИзменения - КорректировкаПоступленияУслуги.Сумма < 0
	|						ТОГДА (КорректировкаПоступленияУслуги.СуммаДоИзменения - КорректировкаПоступленияУслуги.Сумма) * -1
	|					ИНАЧЕ 0
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА КорректировкаПоступленияУслуги.СуммаДоИзменения + КорректировкаПоступленияУслуги.СуммаНДСДоИзменения - (КорректировкаПоступленияУслуги.Сумма + КорректировкаПоступленияУслуги.СуммаНДС) < 0
	|					ТОГДА (КорректировкаПоступленияУслуги.СуммаДоИзменения + КорректировкаПоступленияУслуги.СуммаНДСДоИзменения - (КорректировкаПоступленияУслуги.Сумма + КорректировкаПоступленияУслуги.СуммаНДС)) * -1
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ
	|ИЗ
	|	Документ.КорректировкаПоступления.Услуги КАК КорректировкаПоступленияУслуги,
	|	ВТИсходныйДокумент КАК ИсходныйДокумент
	|ГДЕ
	|	КорректировкаПоступленияУслуги.Ссылка = &ДокументОснование
	|	И (КорректировкаПоступленияУслуги.Количество <> КорректировкаПоступленияУслуги.КоличествоДоИзменения
	|			ИЛИ КорректировкаПоступленияУслуги.Сумма <> КорректировкаПоступленияУслуги.СуммаДоИзменения
	|			ИЛИ КорректировкаПоступленияУслуги.СуммаНДС <> КорректировкаПоступленияУслуги.СуммаНДСДоИзменения
	|			ИЛИ КорректировкаПоступленияУслуги.Цена <> КорректировкаПоступленияУслуги.ЦенаДоИзменения)";
		
	Если РеквизитыСФ.Исправление Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ДоИзменения", "ДоКорректировки");
	КонецЕсли;
	
	ТаблицаРезультата = Запрос.Выполнить().Выгрузить();
	Для Каждого Колонка Из ТаблицаРезультата.Колонки Цикл
		Колонка.Имя = СтрЗаменить(Колонка.Имя, "ДоКорректировки", "ДоИзменения");
	КонецЦикла;

	ДанныеДляПечати.Вставить("ТабличнаяЧасть", ТаблицаРезультата); 
	
	Возврат ДанныеДляПечати;

КонецФункции	

// Проверяет правильность заполнения шапки документа.
// проставляет прочерки в незаполненных полях печатной формы счета-фактуры
//
Процедура ПроставитьПрочеркиВПустыеПоля(ОбластьМакета)

	Для т = 0 По ОбластьМакета.Параметры.Количество() - 1 Цикл
		
		ТекПараметр = ОбластьМакета.Параметры.Получить(т);
		
		Если (Найти(ТекПараметр, "Продавец:") <> 0)
		   и (СокрЛП(ТекПараметр) = "Продавец:") Тогда
			ОбластьМакета.Параметры.Установить(т, "Продавец: ----");
			
		ИначеЕсли (Найти(ТекПараметр, "Адрес:") <> 0)
			    и (СокрЛП(ТекПараметр) = "Адрес:") Тогда
			ОбластьМакета.Параметры.Установить(т, "Адрес: ----");
			
		ИначеЕсли (Найти(ТекПараметр, "Идентификационный номер продавца (ИНН):") <> 0)
			    и (СокрЛП(ТекПараметр) = "Идентификационный номер продавца (ИНН):") Тогда
			ОбластьМакета.Параметры.Установить(т, "Идентификационный номер продавца (ИНН): ----");
			
		ИначеЕсли (Найти(ТекПараметр, "Грузоотправитель и его адрес:") <> 0)
			    и (СокрЛП(ТекПараметр) = "Грузоотправитель и его адрес:") Тогда
			ОбластьМакета.Параметры.Установить(т, "Грузоотправитель и его адрес: ----");
			
		ИначеЕсли (Найти(ТекПараметр, "Грузополучатель и его адрес:") <> 0)
		   		и (СокрЛП(ТекПараметр) = "Грузополучатель и его адрес:") Тогда
			ОбластьМакета.Параметры.Установить(т, "Грузополучатель и его адрес: ----");
			
		ИначеЕсли (Найти(ТекПараметр, "К платежно-расчетному документу №") <> 0)
		   		и (СокрЛП(ТекПараметр) = "К платежно-расчетному документу №  от") Тогда
			ОбластьМакета.Параметры.Установить(т, "К платежно-расчетному документу № -- от --");
			
		ИначеЕсли (Найти(ТекПараметр, "Покупатель:") <> 0)
		   		и (СокрЛП(ТекПараметр) = "Покупатель:") Тогда
			ОбластьМакета.Параметры.Установить(т, "Покупатель: ----");
			
		ИначеЕсли (Найти(ТекПараметр, "Идентификационный номер покупателя (ИНН):") <> 0)
			    и (СокрЛП(ТекПараметр) = "Идентификационный номер покупателя (ИНН):") Тогда
			ОбластьМакета.Параметры.Установить(т, "Идентификационный номер покупателя (ИНН): ----");
			
		ИначеЕсли НЕ ЗначениеЗаполнено(ТекПараметр) Тогда
			ОбластьМакета.Параметры.Установить(т, "--");
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры // ПроставитьПрочеркиВПустыеПоля()

Процедура ВывестиРеквизитыКомиссионера(Продавец, Поставщик, Дата, Макет, ТабДокумент)
	
	Если ЗначениеЗаполнено(Продавец) 
		И ЗначениеЗаполнено(Поставщик) 
		И Продавец <> Поставщик Тогда
		
		ОбластьМакета = Макет.ПолучитьОбласть("РеквизитыКомиссионера");
		
		СведенияОКомиссионере = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(Поставщик, Дата);
		
		НаименованиеКомиссионера = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОКомиссионере, "ПолноеНаименование,");
		ЮридическийАдресКомиссионера = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОКомиссионере, "ЮридическийАдрес,");
		ИННКомиссионера = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОКомиссионере, "ИНН,", Ложь);
		КППКомиссионера = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОКомиссионере, "КПП,", Ложь);
		
		ОбластьМакета.Параметры.ПредставлениеКомиссионера = "Составлен комиссионером (агентом): " + НаименованиеКомиссионера 
			+ ", " + ЮридическийАдресКомиссионера 
			+ ", ИНН/КПП: " + ИННКомиссионера 
			+ ?(ЗначениеЗаполнено(КППКомиссионера), "/" + КППКомиссионера, "");
		
		ТабДокумент.Вывести(ОбластьМакета);
		
	КонецЕсли;		
		
КонецПроцедуры

// Финкция возвращает пустую таблицу значений под
// табличную часть документа основания.
// 
Функция ИнициализацияТаблицыСтрок()

	Товары = Новый ТаблицаЗначений();

	Товары.Колонки.Добавить("Товар");
	Товары.Колонки.Добавить("ТоварНаименование");
	Товары.Колонки.Добавить("СтранаПроисхождения");
	Товары.Колонки.Добавить("ПредставлениеСтраны");
	Товары.Колонки.Добавить("СтранаПроисхожденияКод");
	Товары.Колонки.Добавить("НомерГТД");
	Товары.Колонки.Добавить("ПредставлениеГТД");
	Товары.Колонки.Добавить("Количество");
	Товары.Колонки.Добавить("ЕдиницаИзмерения");
	Товары.Колонки.Добавить("ЕдиницаИзмеренияКод");
	Товары.Колонки.Добавить("СуммаВключаетНДС", Новый ОписаниеТипов("Булево"));
	Товары.Колонки.Добавить("Цена");
	Товары.Колонки.Добавить("СтавкаНДС");
	Товары.Колонки.Добавить("СуммаНДС");
	Товары.Колонки.Добавить("Сумма");

	Возврат Товары;

КонецФункции

Процедура ПечатьСчетаФактуры1137(Ссылка, ТабДокумент)
    	
	ДанныеДляПечати = Неопределено;
	
	СобратьДанныеДляПечати(Ссылка, ДанныеДляПечати);
	
	Если ТипЗнч(ДанныеДляПечати) = Тип("Соответствие") Тогда
		Возврат;
	ИначеЕсли ДанныеДляПечати = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитыСФ = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(Ссылка, 
		"Дата,ВидСчетаФактуры,Исправление,НомерИсправления,ДоговорКонтрагента,СчетФактураБезНДС, КППКонтрагента, Продавец");
	Дата               = РеквизитыСФ.Дата;
	ВидСчетаФактуры    = РеквизитыСФ.ВидСчетаФактуры;
	Исправление        = РеквизитыСФ.Исправление;
	НомерИсправления   = РеквизитыСФ.НомерИсправления;
	ДоговорКонтрагента = РеквизитыСФ.ДоговорКонтрагента;
	СчетФактураБезНДС  = РеквизитыСФ.СчетФактураБезНДС;
	КППКонтрагента     = РеквизитыСФ.КППКонтрагента;
	Продавец     	   = РеквизитыСФ.Продавец;

	СведенияОПокупателе = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(ДанныеДляПечати.Покупатель, Дата);
	КонтрагентПоставщик = ?(ЗначениеЗаполнено(Продавец), Продавец, ДанныеДляПечати.Поставщик);
	СведенияОПоставщике = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(КонтрагентПоставщик, Дата);
	
	СведенияОПодразделенииПоставщика = Неопределено;
	Если ВидСчетаФактуры = Перечисления.ВидСчетаФактурыПолученного.НаПоступление Тогда
		
		Если ТипЗнч(КонтрагентПоставщик) = Тип("СправочникСсылка.Контрагенты") И ЗначениеЗаполнено(КонтрагентПоставщик) Тогда
			РеквизитыПоставщика = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(КонтрагентПоставщик, "ГоловнойКонтрагент, ОбособленноеПодразделение");
			Если РеквизитыПоставщика.ОбособленноеПодразделение И ЗначениеЗаполнено(РеквизитыПоставщика.ГоловнойКонтрагент) Тогда
				СведенияОПоставщике = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(РеквизитыПоставщика.ГоловнойКонтрагент, Дата);
				СведенияОПодразделенииПоставщика 	= УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(КонтрагентПоставщик, Дата);	
				Если ДанныеДляПечати.Грузоотправитель = "он же" Тогда
					ДанныеДляПечати.Грузоотправитель = КонтрагентПоставщик;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	// Определяем макет
	Макет = ПолучитьОбщийМакет("СчетФактура1137");
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СчетФактураПолученный_СчетФактура1137";
	
	// Выводим шапку
	
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Заполнить(ДанныеДляПечати);
	
	ОбластьКонтракт = Макет.ПолучитьОбласть("Контракт"); // Шевченков #66046
	
	Если Исправление Тогда
		ОбластьМакета.Параметры.НомерИсправления = "Исправление №" + Строка(НомерИсправления) 
			+ " от " + Формат(Дата, "ДФ='дд ММММ гггг'") + " г.";
 	Иначе 	
		ОбластьМакета.Параметры.НомерИсправления = "Исправление № -- от --";
	КонецЕсли;
	
	ОбластьМакета.Параметры.ПредставлениеПоставщика = "Продавец: " 
		+ ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОПоставщике, "ПолноеНаименование,");
		
	ОбластьМакета.Параметры.АдресПоставщика = "Адрес: " 
		+ ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОПоставщике, "ЮридическийАдрес,");
		
	Если ЗначениеЗаполнено(КППКонтрагента) Тогда
		КПП = КППКонтрагента;
	ИначеЕсли СведенияОПодразделенииПоставщика <> Неопределено Тогда
		КПП = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОПодразделенииПоставщика, "КПП,", Ложь);
	Иначе
		КПП = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОПоставщике, "КПП,", Ложь);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КПП) Тогда
		КПП = "/" + КПП;
	КонецЕсли;
	ОбластьМакета.Параметры.ИННпоставщика = "ИНН/КПП продавца: "
		+ ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОПоставщике, "ИНН,", Ложь) + КПП;
	
	ЗаголовокДляПечати = ОбщегоНазначения.СформироватьЗаголовокДокумента(ДанныеДляПечати, "Счет-фактура") + " г.";
	ОбластьМакета.Параметры.Номер = ЗаголовокДляПечати;
		
	ТолькоУслуги = Истина;
	Для каждого СтрокаТовар Из ДанныеДляПечати.ТабличнаяЧасть Цикл
		Если (ТипЗнч(СтрокаТовар.Товар) = Тип("СправочникСсылка.Номенклатура") И НЕ СтрокаТовар.Товар.Услуга)
			ИЛИ ТипЗнч(СтрокаТовар.Товар) = Тип("СправочникСсылка.ОсновныеСредства") Тогда
			ТолькоУслуги = Ложь;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ТолькоУслуги Тогда
		ОбластьМакета.Параметры.ПредставлениеГрузоотправителя = "Грузоотправитель и его адрес: --";
		ОбластьМакета.Параметры.ПредставлениеГрузополучателя  = "Грузополучатель и его адрес: --";
	Иначе
		СведенияОГрузоотправителе = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(
			?(ДанныеДляПечати.Грузоотправитель = "он же", Неопределено, ДанныеДляПечати.Грузоотправитель), Дата);
		СведенияОГрузополучателе  = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(ДанныеДляПечати.Грузополучатель, Дата);
		
		ОбластьМакета.Параметры.ПредставлениеГрузоотправителя = "Грузоотправитель и его адрес: "
			+ ?(НЕ ЗначениеЗаполнено(ДанныеДляПечати.Грузоотправитель), 
			"--", 
			?(ДанныеДляПечати.Грузоотправитель = "он же", 
				ДанныеДляПечати.Грузоотправитель, 
				ФормированиеПечатныхФормСервер.ОписаниеОрганизации(
					СведенияОГрузоотправителе, "ПолноеНаименование,ФактическийАдрес,")));
		
		Если ДанныеДляПечати.Свойство("АдресДоставки") 
			И НЕ ПустаяСтрока(ДанныеДляПечати.АдресДоставки) Тогда
			ПредставлениеГрузополучателя = "Грузополучатель и его адрес: " 
				+ ?(НЕ ЗначениеЗаполнено(ДанныеДляПечати.Грузополучатель), 
				ДанныеДляПечати.АдресДоставки,
				ФормированиеПечатныхФормСервер.ОписаниеОрганизации(
					СведенияОГрузополучателе, "ПолноеНаименование") + ", " + ДанныеДляПечати.АдресДоставки);
		Иначе
			ПредставлениеГрузополучателя = "Грузополучатель и его адрес: " 
				+ ?(НЕ ЗначениеЗаполнено(ДанныеДляПечати.Грузополучатель), 
				"--",
				ФормированиеПечатныхФормСервер.ОписаниеОрганизации(
					СведенияОГрузополучателе, "ПолноеНаименование, ФактическийАдрес,"));
		КонецЕсли;
		ОбластьМакета.Параметры.ПредставлениеГрузополучателя = ПредставлениеГрузополучателя;
	
	КонецЕсли;

	СтрокаПоДокументу = "К платежно-расчетному документу №    от";
	ОбластьМакета.Параметры.ПоДокументу = СтрокаПоДокументу;
	
	ОбластьМакета.Параметры.ПредставлениеПокупателя = "Покупатель: " 
		+ ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОПокупателе, "ПолноеНаименование,");
	ОбластьМакета.Параметры.АдресПокупателя = "Адрес: "
		+ ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОПокупателе, "ЮридическийАдрес,");
	КПП = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОПокупателе, "КПП,", Ложь);
	Если ЗначениеЗаполнено(КПП) Тогда
		КПП = "/" + КПП;
	КонецЕсли;
	ОбластьМакета.Параметры.ИННПокупателя = "ИНН/КПП покупателя: "
		+ ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОПокупателе, "ИНН,", Ложь) + КПП;
	
	ОбластьМакета.Параметры.Валюта = "Валюта: наименование, код "
		+ ?(ЗначениеЗаполнено(ДанныеДляПечати.Валюта), 
		ДанныеДляПечати.Валюта.НаименованиеПолное + ", " + ДанныеДляПечати.Валюта.Код, 
		"");		
			
	ТабДокумент.Вывести(ОбластьМакета);
	
	// <- Шевченков #66046
	Если ДанныеДляПечати.Дата >= Дата('20170701') Тогда
		ТабДокумент.Вывести(ОбластьКонтракт);
	КонецЕсли;	
	// ->
	
	ВывестиРеквизитыКомиссионера(Продавец, ДанныеДляПечати.Поставщик, Дата, Макет, ТабДокумент);
	
	// Выводим заголовок таблицы
	
	ОбластьМакетаЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ОбластьМакетаЗаголовокТаблицы.Параметры.Заполнить(ДанныеДляПечати);
	ТабДокумент.Вывести(ОбластьМакетаЗаголовокТаблицы);
	
	// Дополнительная подготовка данных для вывода в табличную часть
	
	ВыборкаСтрокТовары = ДанныеДляПечати.ТабличнаяЧасть;
 	ВыборкаСтрокТовары.Колонки.Добавить("СуммаБезНДС");
	ВыборкаСтрокТовары.Колонки.Добавить("СуммаСНДС");

	Для Каждого Строчка Из ВыборкаСтрокТовары Цикл
		
		Строчка.СуммаСНДС = Строчка.Сумма + ?(Строчка.СуммаВключаетНДС, 0, Строчка.СуммаНДС);

		Если Строчка.СтавкаНДС = Перечисления.СтавкиНДС.НДС20_120
		 	ИЛИ Строчка.СтавкаНДС = Перечисления.СтавкиНДС.НДС18_118
		 	ИЛИ Строчка.СтавкаНДС = Перечисления.СтавкиНДС.НДС10_110 Тогда
			Строчка.СуммаБезНДС = Строчка.СуммаСНДС;
			Если НЕ Строчка.СуммаВключаетНДС Тогда
				Строчка.Цена = 0;
			КонецЕсли;
		Иначе
			Строчка.СуммаБезНДС = Строчка.СуммаСНДС - Строчка.СуммаНДС;
			Если Строчка.СуммаВключаетНДС Тогда
				Строчка.Цена = 0;
			КонецЕсли;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Строчка.ЕдиницаИзмерения) Тогда
			Строчка.Количество = 0;
			Строчка.Цена = 0;
		ИначеЕсли Строчка.Количество = 0 Тогда
			Строчка.Цена = 0;
		ИначеЕсли Строчка.Цена = 0 Тогда
			Строчка.Цена = Окр(Строчка.СуммаБезНДС / Строчка.Количество, 2);
		КонецЕсли;
		
	КонецЦикла;
	    
	ВыборкаСтрокТовары.Свернуть("Товар, ТоварНаименование, ЕдиницаИзмерения, ЕдиницаИзмеренияКод, Цена, СтавкаНДС,
		|НомерГТД, ПредставлениеГТД, СтранаПроисхождения, СтранаПроисхожденияКод, ПредставлениеСтраны", 
		"Количество, Сумма, СуммаНДС, СуммаСНДС, СуммаБезНДС");
	
	// Выводим строки таблицы
	
	ОбластьМакета       = Макет.ПолучитьОбласть("Строка");
	ОбластьМакетаПодвал = Макет.ПолучитьОбласть("Подвал");
	ОбластьМакетаИтого  = Макет.ПолучитьОбласть("Итого");
	
	НомерСтроки     = 0;
	КоличествоСтрок = ВыборкаСтрокТовары.Количество();

	Для Каждого Строчка Из ВыборкаСтрокТовары Цикл
		
		НомерСтроки = НомерСтроки + 1;
		
		ОбластьМакета.Параметры.Заполнить(Строчка);

		Если НЕ ЗначениеЗаполнено(Строчка.ТоварНаименование) Тогда
			ОбластьМакета.Параметры.ТоварНаименование = Строчка.Товар;
		КонецЕсли;
		ОбластьМакета.Параметры.ТоварНаименование = СокрЛП(ОбластьМакета.Параметры.ТоварНаименование);
		
		ОбластьМакета.Параметры.Стоимость = Строчка.СуммаБезНДС;
		ОбластьМакета.Параметры.Цена   	  = Строчка.Цена;
		
		ОбластьМакета.Параметры.Всего     = Строчка.СуммаСНДС;
		ОбластьМакета.Параметры.СтавкаНДС = Строчка.СтавкаНДС;
		
		Если Строчка.СтавкаНДС = Перечисления.СтавкиНДС.БезНДС Тогда
			ОбластьМакета.Параметры.СтавкаНДС = "без НДС";
			ОбластьМакета.Параметры.СуммаНДС  = "без НДС";
		КонецЕсли;
		
		ОбластьМакета.Параметры.Акциз = "без акциза";
		
		Если Строка(Строчка.ПредставлениеСтраны) = "Россия" 
			ИЛИ Строка(Строчка.ПредставлениеСтраны) = "Российская Федерация"
			ИЛИ Строчка.СтранаПроисхождения = Справочники.КлассификаторСтранМира.РОССИЯ Тогда			
			ОбластьМакета.Параметры.ПредставлениеСтраны    = "--";
			ОбластьМакета.Параметры.СтранаПроисхожденияКод = "--";
			ОбластьМакета.Параметры.ПредставлениеГТД       = "--";
		Иначе
			Если ЗначениеЗаполнено(Строчка.СтранаПроисхождения) Тогда
				ОбластьМакета.Параметры.ПредставлениеСтраны  = СокрЛП(Строчка.СтранаПроисхождения.Наименование);
			Иначе
				ОбластьМакета.Параметры.СтранаПроисхожденияКод = "--";
				ОбластьМакета.Параметры.ПредставлениеСтраны    = "--";
				ОбластьМакета.Параметры.ПредставлениеГТД       = "--";
            КонецЕсли;
		КонецЕсли; 
	
		// Проставляем прочерки в незаполненные графы в соответствии с Постановлением
		Если НЕ ЗначениеЗаполнено(ОбластьМакета.Параметры.ЕдиницаИзмерения) Тогда
			ОбластьМакета.Параметры.ЕдиницаИзмерения    = "--";
			ОбластьМакета.Параметры.ЕдиницаИзмеренияКод = "--";
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(ОбластьМакета.Параметры.ЕдиницаИзмеренияКод) Тогда
			ОбластьМакета.Параметры.ЕдиницаИзмеренияКод = "--";
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(ОбластьМакета.Параметры.Количество) Тогда
			ОбластьМакета.Параметры.Количество = "--";
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(ОбластьМакета.Параметры.Цена) Тогда
			ОбластьМакета.Параметры.Цена = "--";
		КонецЕсли;
		
		// Проверим возможность вывода табличного документа
		СтрокаСПодвалом = Новый Массив;
		СтрокаСПодвалом.Добавить(ОбластьМакета);
		Если НомерСтроки = КоличествоСтрок Тогда
			// Если последняя строка, то должен поместится итог и подвал
			СтрокаСПодвалом.Добавить(ОбластьМакетаИтого);
			СтрокаСПодвалом.Добавить(ОбластьМакетаПодвал);
		КонецЕсли;
						
		Если НЕ ФормированиеПечатныхФормСервер.ПроверитьВыводТабличногоДокумента(ТабДокумент, СтрокаСПодвалом) Тогда
			
			Если КоличествоСтрок > 0 Тогда
				// Вывод разделителя и заголовка таблицы на новой странице
				ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ТабДокумент.Вывести(ОбластьМакетаЗаголовокТаблицы);
			КонецЕсли;
			
		КонецЕсли;
		
		ТабДокумент.Вывести(ОбластьМакета);

	КонецЦикла;

	Если КоличествоСтрок > 0 Тогда
		ТабДокумент.Область(ТабДокумент.ВысотаТаблицы,,ТабДокумент.ВысотаТаблицы,).ВместеСоСледующим = Истина;
	КонецЕсли; 
		
	// Выводим строку "Всего к оплате"
	
	ОбластьМакетаИтого.Параметры.ИтогоСтоимость = ВыборкаСтрокТовары.Итог("СуммаБезНДС");
	Если СчетФактураБезНДС Тогда
		ОбластьМакетаИтого.Параметры.ИтогоСуммаНДС = "без НДС";
	Иначе
		ОбластьМакетаИтого.Параметры.ИтогоСуммаНДС = ВыборкаСтрокТовары.Итог("СуммаНДС");
	КонецЕсли;
	ОбластьМакетаИтого.Параметры.ИтогоВсего = ВыборкаСтрокТовары.Итог("СуммаСНДС");
	
	ТабДокумент.Вывести(ОбластьМакетаИтого);
	
	// Выводим подвал
    	
	ОбластьМакетаПодвал.Параметры.Заполнить(ДанныеДляПечати);
	ОбластьМакетаПодвал.Параметры.Свидетельство = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(
		СведенияОПоставщике, "Свидетельство,");
	ТабДокумент.Вывести(ОбластьМакетаПодвал);

КонецПроцедуры

Процедура ПечатьКорректировочногоСчетаФактуры1137(Ссылка, ТабДокумент)

	ДанныеДляПечати = Неопределено;
	
	СобратьДанныеДляПечати(Ссылка, ДанныеДляПечати);
	
	Если ТипЗнч(ДанныеДляПечати) = Тип("Соответствие") Тогда
		Возврат;
	ИначеЕсли ДанныеДляПечати = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитыСФ = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(Ссылка, 
		"Дата,НомерВходящегоДокумента,ДатаВходящегоДокумента,
		|Исправление,НомерИсправления,ДатаИсправления,
		|НомерИсходногоДокумента,ДатаИсходногоДокумента,
		|СчетФактураБезНДС, КППКонтрагента, Продавец");
	
	СведенияОПокупателе = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(ДанныеДляПечати.Покупатель, РеквизитыСФ.Дата);
	КонтрагентПоставщик = ?(ЗначениеЗаполнено(РеквизитыСФ.Продавец), РеквизитыСФ.Продавец, ДанныеДляПечати.Поставщик);
	СведенияОПоставщике = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(КонтрагентПоставщик, РеквизитыСФ.Дата);
		
	СведенияОПодразделенииПоставщика = Неопределено;
	Если ТипЗнч(КонтрагентПоставщик) = Тип("СправочникСсылка.Контрагенты") И ЗначениеЗаполнено(КонтрагентПоставщик) Тогда
		РеквизитыПоставщика = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(КонтрагентПоставщик, "ГоловнойКонтрагент, ОбособленноеПодразделение");
		Если РеквизитыПоставщика.ОбособленноеПодразделение И ЗначениеЗаполнено(РеквизитыПоставщика.ГоловнойКонтрагент) Тогда
			СведенияОПоставщике = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(РеквизитыПоставщика.ГоловнойКонтрагент, РеквизитыСФ.Дата);
			СведенияОПодразделенииПоставщика 	= УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(КонтрагентПоставщик, РеквизитыСФ.Дата);	
		КонецЕсли;
	КонецЕсли;
	
	// Определяем макет
	Если УчетНДС.ПрименяетсяПостановление952(РеквизитыСФ.Дата) Тогда
		Макет = ПолучитьОбщийМакет("КорректировочныйСчетФактура952");
	Иначе
		Макет = ПолучитьОбщийМакет("КорректировочныйСчетФактура1137");
	КонецЕсли;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СчетФактураПолученный_КорректировочныйСчетФактура1137";
	
	// Выводим шапку
	
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	
	ОбластьМакета.Параметры.Заполнить(ДанныеДляПечати);
	
	ОбластьМакета.Параметры.Номер = РеквизитыСФ.НомерВходящегоДокумента;
	ОбластьМакета.Параметры.Дата  = Формат(РеквизитыСФ.ДатаВходящегоДокумента, "ДФ='дд ММММ гггг'") + " г.";
	
	Если РеквизитыСФ.Исправление Тогда
		ОбластьМакета.Параметры.НомерИсправленияКорректировочного = РеквизитыСФ.НомерИсправления;
		ОбластьМакета.Параметры.ДатаИсправленияКорректировочного  = Формат(РеквизитыСФ.Дата, "ДФ='дд ММММ гггг'") + " г.";
	Иначе
		ОбластьМакета.Параметры.НомерИсправленияКорректировочного = "--";
		ОбластьМакета.Параметры.ДатаИсправленияКорректировочного  = "--";
	КонецЕсли;
	
	РеквизитыОснований = "";
	Для каждого Основание Из ДанныеДляПечати.КорректируемыеСчетаФактуры Цикл
		РеквизитыОснований = ?(РеквизитыОснований = "", РеквизитыОснований, РеквизитыОснований + ", ") 
		+ "№ "+ Основание.НомерСчетаФактуры + " от " + Основание.ДатаСчетаФактуры 
		+ ", с учетом исправления № " + Основание.НомерИсправления + " от " + Основание.ДатаИсправления;	
	КонецЦикла;
	ОбластьМакета.Параметры.РеквизитыОснований = РеквизитыОснований;

	ОбластьМакета.Параметры.НаименованиеПродавца = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(
		СведенияОПоставщике,  "ПолноеНаименование,");
	ОбластьМакета.Параметры.АдресПродавца = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(
		СведенияОПоставщике,  "ЮридическийАдрес,");

	ОбластьМакета.Параметры.НаименованиеПокупателя = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(
		СведенияОПокупателе, "ПолноеНаименование,");
	ОбластьМакета.Параметры.АдресПокупателя = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(
		СведенияОПокупателе, "ЮридическийАдрес,");
		
	Если ЗначениеЗаполнено(РеквизитыСФ.КППКонтрагента) Тогда
		КПП = РеквизитыСФ.КППКонтрагента;
	ИначеЕсли СведенияОПодразделенииПоставщика <> Неопределено Тогда
		КПП = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОПодразделенииПоставщика, "КПП,", Ложь);
	Иначе
		КПП = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОПоставщике, "КПП,", Ложь);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КПП) Тогда
		КПП = "/" + КПП;
	КонецЕсли;
	ОбластьМакета.Параметры.ИННКПППродавца = ""
		+ ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОПоставщике, "ИНН,", Ложь) + КПП;
	
	КПП = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОПокупателе, "КПП,", Ложь);
	Если ЗначениеЗаполнено(КПП) Тогда
		КПП = "/" + КПП;
	КонецЕсли;
	ОбластьМакета.Параметры.ИННКПППокупателя = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(
		СведенияОПокупателе, "ИНН,", Ложь) + КПП;
	
	ОбластьМакета.Параметры.Валюта = ?(ЗначениеЗаполнено(ДанныеДляПечати.Валюта), 
		"Валюта: наименование, код " + ДанныеДляПечати.Валюта.НаименованиеПолное + ", " + ДанныеДляПечати.Валюта.Код, 
		"Валюта: наименование, код");		
		
	ТабДокумент.Вывести(ОбластьМакета);
	
	ВывестиРеквизитыКомиссионера(РеквизитыСФ.Продавец, ДанныеДляПечати.Поставщик, РеквизитыСФ.Дата, Макет, ТабДокумент);
	
	// Выводим заголовок таблицы
	ОбластьМакетаЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ТабДокумент.Вывести(ОбластьМакетаЗаголовокТаблицы);

	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтого = Макет.ПолучитьОбласть("Итого");
	ОбластьМакетаПодвал = Макет.ПолучитьОбласть("Подвал");
	
	КоличествоСтрок = ДанныеДляПечати.ТабличнаяЧасть.Количество();
	НомерСтроки = 0;
	
	Для Каждого СтрокаТабличнойЧасти Из ДанныеДляПечати.ТабличнаяЧасть Цикл 
		
		НомерСтроки = НомерСтроки + 1;
		
		ОбластьМакета.Параметры.Заполнить(СтрокаТабличнойЧасти);
		Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.НаименованиеТовара) Тогда
			ОбластьМакета.Параметры.НаименованиеНоменклатуры = СтрокаТабличнойЧасти.НаименованиеТовара;               
		Иначе
			ОбластьМакета.Параметры.НаименованиеНоменклатуры = СтрокаТабличнойЧасти.Номенклатура;               
		КонецЕсли;	
		
		Если НЕ ЗначениеЗаполнено(ОбластьМакета.Параметры.ЕдиницаИзмеренияКод) Тогда
			ОбластьМакета.Параметры.ЕдиницаИзмеренияКод = "--";
			ОбластьМакета.Параметры.НаименованиеЕдиницыИзмерения = "--";
		КонецЕсли;	
		
		Если НЕ ЗначениеЗаполнено(ОбластьМакета.Параметры.КоличествоДоИзменения) Тогда
			ОбластьМакета.Параметры.КоличествоДоИзменения = "--";
		КонецЕсли;	
		
		Если НЕ ЗначениеЗаполнено(ОбластьМакета.Параметры.КоличествоПослеИзменения) Тогда
			ОбластьМакета.Параметры.КоличествоПослеИзменения = "--";
		КонецЕсли;	
		       		
		Если НЕ ЗначениеЗаполнено(ОбластьМакета.Параметры.ЦенаДоИзменения) Тогда
			ОбластьМакета.Параметры.ЦенаДоИзменения = "--";
		КонецЕсли;	
		
		Если НЕ ЗначениеЗаполнено(ОбластьМакета.Параметры.ЦенаПослеИзменения) Тогда
			ОбластьМакета.Параметры.ЦенаПослеИзменения = "--";
		КонецЕсли;	
		          		
		Если СтрокаТабличнойЧасти.СтавкаНДС = Перечисления.СтавкиНДС.БезНДС Тогда
			ОбластьМакета.Параметры.СтавкаНДС = "без НДС";
			ОбластьМакета.Параметры.СуммаНДСДоИзменения = "без НДС";
			ОбластьМакета.Параметры.СуммаНДСПослеИзменения = "без НДС";
			ОбластьМакета.Параметры.РазницаНДСУвеличение = "--";
			ОбластьМакета.Параметры.РазницаНДСУменьшение = "--";
		КонецЕсли;
		
		// Проверим возможность вывода табличного документа
		СтрокаСПодвалом = Новый Массив;
		СтрокаСПодвалом.Добавить(ОбластьМакета);
		
		Если НомерСтроки = КоличествоСтрок Тогда
			// Если последняя строка, то должен поместится итог и подвал
			СтрокаСПодвалом.Добавить(ОбластьМакетаИтого);
			СтрокаСПодвалом.Добавить(ОбластьМакетаПодвал);
		КонецЕсли;
						
		Если НЕ ФормированиеПечатныхФормСервер.ПроверитьВыводТабличногоДокумента(ТабДокумент, СтрокаСПодвалом) Тогда
			
			Если КоличествоСтрок > 0 Тогда
				// Вывод разделителя и заголовка таблицы на новой странице
				ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ТабДокумент.Вывести(ОбластьМакетаЗаголовокТаблицы);
			КонецЕсли;
			
		КонецЕсли;
		
		ТабДокумент.Вывести(ОбластьМакета);
		
	КонецЦикла;	
		
	ОбластьМакетаИтого.Параметры.РазницаБезНДСУменьшение = ДанныеДляПечати.ТабличнаяЧасть.Итог("РазницаБезНДСУменьшение");
	ОбластьМакетаИтого.Параметры.РазницаБезНДСУвеличение = ДанныеДляПечати.ТабличнаяЧасть.Итог("РазницаБезНДСУвеличение");
	ОбластьМакетаИтого.Параметры.РазницаСНДСУменьшение   = ДанныеДляПечати.ТабличнаяЧасть.Итог("РазницаСНДСУменьшение");
	ОбластьМакетаИтого.Параметры.РазницаСНДСУвеличение   = ДанныеДляПечати.ТабличнаяЧасть.Итог("РазницаСНДСУвеличение");
	
	Если РеквизитыСФ.СчетФактураБезНДС Тогда
		ОбластьМакетаИтого.Параметры.РазницаНДСУменьшение = "без НДС";
		ОбластьМакетаИтого.Параметры.РазницаНДСУвеличение = "без НДС";
	Иначе
		ОбластьМакетаИтого.Параметры.РазницаНДСУменьшение = ДанныеДляПечати.ТабличнаяЧасть.Итог("РазницаНДСУменьшение");
		ОбластьМакетаИтого.Параметры.РазницаНДСУвеличение = ДанныеДляПечати.ТабличнаяЧасть.Итог("РазницаНДСУвеличение");
	КонецЕсли;
    	
	ТабДокумент.Вывести(ОбластьМакетаИтого);
	
	ОбластьМакетаПодвал.Параметры.Заполнить(ДанныеДляПечати);
	ТабДокумент.Вывести(ОбластьМакетаПодвал);

КонецПроцедуры

// Сформировать печатные формы объектов
//
// ВХОДЯЩИЕ:
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы
//   ОшибкиПечати          - Список значений  - Ошибки печати  (значение - ссылка на объект, представление - текст ошибки)
//   ОбъектыПечати         - Список значений  - Объекты печати (значение - ссылка на объект, представление - имя области в которой был выведен объект)
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СчетФактура") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "СчетФактура", "Счет фактура", ПечатьСчетаФактуры(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СчетФактура283") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "СчетФактура283", "Счет фактура", ПечатьСчетаФактуры(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СчетФактура84")  Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "СчетФактура84", "Счет фактура", ПечатьСчетаФактуры(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СчетФактура575") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "СчетФактура575", "Счет фактура", ПечатьСчетаФактуры(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;
КонецПроцедуры

Функция ПечатьСчетаФактуры(МассивОбъектов, ОбъектыПечати)

	ТабДокумент = Новый ТабличныйДокумент;
	
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ТабДокумент.ПолеСверху = 13;
	ТабДокумент.АвтоМасштаб = Истина;
	
	ПервыйДокумент = Истина;
	ВыводитьКолонтитул = МассивОбъектов.Количество() = 1;
	
	Для каждого Ссылка Из МассивОбъектов Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		НомерСтрокиНачало = ТабДокумент.ВысотаТаблицы + 1;
		
		Корректировочный    = Ссылка.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыПолученного.Корректировочный;
		ВерсияПостановления = УчетНДС.ПолучитьВерсиюПостановления(Ссылка.Дата);
		Если ВерсияПостановления = 1 Тогда
			Если Корректировочный Тогда
				ПечатьКорректировочногоСчетаФактуры(Ссылка, ТабДокумент);
			Иначе
				ПечатьСчетаФактуры914(Ссылка, ТабДокумент);
			КонецЕсли;
		Иначе
			Если Корректировочный Тогда
				ПечатьКорректировочногоСчетаФактуры1137(Ссылка, ТабДокумент);
			Иначе
				ПечатьСчетаФактуры1137(Ссылка, ТабДокумент);
			КонецЕсли;
		КонецЕсли;
		
		ТабДокумент.ВерхнийКолонтитул.Выводить = ВыводитьКолонтитул;
		Если ВыводитьКолонтитул Тогда
			ТабДокумент.ВерхнийКолонтитул.НачальнаяСтраница     = 2;
			ТабДокумент.ВерхнийКолонтитул.ВертикальноеПоложение = ВертикальноеПоложение.Низ;
			ЗаголовокДляПечати = ОбщегоНазначения.СформироватьЗаголовокДокумента(Ссылка, "Счет-фактура") + " г.";
			ТабДокумент.ВерхнийКолонтитул.ТекстСлева  = ЗаголовокДляПечати;
			ТабДокумент.ВерхнийКолонтитул.ТекстСправа = "Лист [&НомерСтраницы]";
		КонецЕсли;
	
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДокумент, НомерСтрокиНачало, ОбъектыПечати, Ссылка);
		
	КонецЦикла;
	
	Возврат ТабДокумент;

КонецФункции

Процедура ПечатьСчетаФактуры914(Ссылка, ТабДокумент)
	
	Если Ссылка.Дата < '20040216' Тогда
		Макет = ПолучитьОбщийМакет("СчетФактура575");
		ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СчетФактураПолученный_СчетФактура575";
	ИначеЕсли Ссылка.Дата < '20060530' Тогда
		Макет = ПолучитьОбщийМакет("СчетФактура84");
		ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СчетФактураПолученный_СчетФактура84";
	ИначеЕсли Ссылка.Дата < '20090609' Тогда
		Макет = ПолучитьОбщийМакет("СчетФактура283");
		ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СчетФактураПолученный_СчетФактура283";
	Иначе
		Макет = ПолучитьОбщийМакет("СчетФактура451");
		ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СчетФактураПолученный_СчетФактура451";
	КонецЕсли;
	
	ДанныеДляПечати = Неопределено;
	
	СобратьДанныеДляПечати(Ссылка, ДанныеДляПечати);
	
	Если ТипЗнч(ДанныеДляПечати) = Тип("Соответствие") Тогда
		Возврат;
	ИначеЕсли ДанныеДляПечати = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если ДанныеДляПечати.Покупатель = Null Тогда
		ТекстСообщения = НСтр("ru = 'В документе основании не указано юр./физ. лицо у организации'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Ссылка);
		Возврат;
	КонецЕсли;
	Если ДанныеДляПечати.Поставщик = Неопределено Тогда
		ТекстСообщения = НСтр("ru = 'В документе основании не указано юр./физ. лицо контрагента'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Ссылка);
		Возврат;
	КонецЕсли;
	
	СведенияОПокупателе = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(
		ДанныеДляПечати.Покупатель, Ссылка.Дата);
	СведенияОПоставщике = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(
		ДанныеДляПечати.Поставщик, Ссылка.Дата);
	СведенияОГрузоотправителе = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(
		?(ДанныеДляПечати.Грузоотправитель = "он же", Неопределено, ДанныеДляПечати.Грузоотправитель), Ссылка.Дата);
	СведенияОГрузополучателе  = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(
		ДанныеДляПечати.Грузополучатель, Ссылка.Дата);
	
	// Выводим шапку накладной
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Заполнить(ДанныеДляПечати);
	ОбластьМакета.Параметры.Номер = ОбщегоНазначения.СформироватьЗаголовокДокумента(ДанныеДляПечати, "Счет-фактура") + " г.";
	Если Ссылка.Дата < '20040216' Тогда
		ОбластьМакета.Параметры.ПредставлениеПоставщика       = "Продавец: "
			+ ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОПоставщике,  "ПолноеНаименование,");
		ОбластьМакета.Параметры.АдресПоставщика               = "Адрес: "
			+ ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОПоставщике,  "ЮридическийАдрес,");
		ОбластьМакета.Параметры.ИННпоставщика                 = "Идентификационный номер продавца (ИНН): "
			+ ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОПоставщике,  "ИНН,", Ложь); 
		ОбластьМакета.Параметры.ПредставлениеГрузоотправителя = "Грузоотправитель и его адрес: " 
			+ ?(НЕ ЗначениеЗаполнено(ДанныеДляПечати.Грузоотправитель), 
			"", 
			?(ДанныеДляПечати.Грузоотправитель = "он же", 
			ДанныеДляПечати.Грузоотправитель, 
			ФормированиеПечатныхФормСервер.ОписаниеОрганизации(
				СведенияОГрузоотправителе,  "ПолноеНаименование,ФактическийАдрес,")));
		ОбластьМакета.Параметры.ПредставлениеГрузополучателя  = "Грузополучатель и его адрес: "
			+ ?(НЕ ЗначениеЗаполнено(ДанныеДляПечати.Грузополучатель), 
			"", 
			ФормированиеПечатныхФормСервер.ОписаниеОрганизации(
				СведенияОГрузополучателе, "ПолноеНаименование,ФактическийАдрес,"));
		ОбластьМакета.Параметры.ПоДокументу                   = "К платежно-расчетному документу №  от: ";
		ОбластьМакета.Параметры.ПредставлениеПокупателя       = "Покупатель: "
			+ ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОПокупателе, "ПолноеНаименование,");
		ОбластьМакета.Параметры.АдресПокупателя               = "Адрес: "
			+ ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОПокупателе, "ЮридическийАдрес,");
		ОбластьМакета.Параметры.ИННПокупателя                 = "Идентификационный номер покупателя (ИНН): "
			+ ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОПокупателе, "ИНН,", Ложь);
	Иначе
		ОбластьМакета.Параметры.ПредставлениеПоставщика       = "Продавец: "
			+ ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОПоставщике,  "ПолноеНаименование,");
		ОбластьМакета.Параметры.АдресПоставщика               = "Адрес: "
			+ ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОПоставщике,  "ЮридическийАдрес,");
		КПП = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОПоставщике, "КПП,", Ложь);
		Если ЗначениеЗаполнено(КПП) Тогда
			КПП = "/" + КПП;
		КонецЕсли;
		ОбластьМакета.Параметры.ИННпоставщика                 = "ИНН/КПП продавца: "
			+ ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОПоставщике,  "ИНН,", Ложь) + КПП;
		ОбластьМакета.Параметры.ПредставлениеГрузоотправителя = "Грузоотправитель и его адрес: "
			+ ?(НЕ ЗначениеЗаполнено(ДанныеДляПечати.Грузоотправитель), 
			"", 
			?(ДанныеДляПечати.Грузоотправитель = "он же", 
			ДанныеДляПечати.Грузоотправитель, 
			ФормированиеПечатныхФормСервер.ОписаниеОрганизации(
				СведенияОГрузоотправителе,  "ПолноеНаименование,ФактическийАдрес,")));
		ОбластьМакета.Параметры.ПредставлениеГрузополучателя  = "Грузополучатель и его адрес: "
			+ ?(НЕ ЗначениеЗаполнено(ДанныеДляПечати.Грузополучатель), 
			"", 
			ФормированиеПечатныхФормСервер.ОписаниеОрганизации(
				СведенияОГрузополучателе, "ПолноеНаименование,ФактическийАдрес,"));
		ОбластьМакета.Параметры.ПоДокументу                   = "К платежно-расчетному документу №  от: ";
		ОбластьМакета.Параметры.ПредставлениеПокупателя       = "Покупатель: "
			+ ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОПокупателе, "ПолноеНаименование,");
		ОбластьМакета.Параметры.АдресПокупателя               = "Адрес: "
			+ ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОПокупателе, "ЮридическийАдрес,");
		КПП = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОПокупателе, "КПП,", Ложь);
		Если ЗначениеЗаполнено(КПП) Тогда
			КПП = "/" + КПП;
		КонецЕсли;
		ОбластьМакета.Параметры.ИННПокупателя                 = "ИНН/КПП покупателя: "
			+ ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОПокупателе, "ИНН,", Ложь) + КПП;
	КонецЕсли;
	ПроставитьПрочеркиВПустыеПоля(ОбластьМакета);
	
	ТабДокумент.Вывести(ОбластьМакета);
	
	// Вывод заголовка таблицы
	ОбластьМакетаЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ОбластьМакетаЗаголовокТаблицы.Параметры.Заполнить(ДанныеДляПечати);
	ТабДокумент.Вывести(ОбластьМакетаЗаголовокТаблицы);
	
	// Вывод строк таблицы
	ОбластьМакета 		= Макет.ПолучитьОбласть("Строка");
	ОбластьМакетаИтого 	= Макет.ПолучитьОбласть("Итого");
	ОбластьМакетаПодвал = Макет.ПолучитьОбласть("Подвал");
	
	ИтогоСуммаНДС = 0;
	ИтогоВсего    = 0;
	
	ВыборкаСтрокТовары = ДанныеДляПечати.ТабличнаяЧасть;
	ВыборкаСтрокТовары.Колонки.Добавить("СуммаБезНДС");
	ВыборкаСтрокТовары.Колонки.Добавить("СуммаСНДС");
	Для Каждого Строчка Из ВыборкаСтрокТовары Цикл
		Строчка.СуммаСНДС = Строчка.Сумма + ?(Строчка.СуммаВключаетНДС, 0, Строчка.СуммаНДС);
		Если (Строчка.СтавкаНДС = Перечисления.СтавкиНДС.НДС20_120)
			Или (Строчка.СтавкаНДС = Перечисления.СтавкиНДС.НДС18_118)
			Или (Строчка.СтавкаНДС = Перечисления.СтавкиНДС.НДС10_110) Тогда
			Строчка.СуммаБезНДС = Строчка.СуммаСНДС;
		Иначе
			Строчка.СуммаБезНДС = Строчка.СуммаСНДС - Строчка.СуммаНДС;
		КонецЕсли;
	КонецЦикла;
	
	ВыборкаСтрокТовары.Свернуть("Товар, ТоварНаименование, СтранаПроисхождения, ПредставлениеСтраны,
		|НомерГТД, ПредставлениеГТД, ЕдиницаИзмерения, Цена, СтавкаНДС", 
		"Количество, Сумма, СуммаНДС, СуммаСНДС, СуммаБезНДС");
		
	НомерСтроки     = 0;
	КоличествоСтрок = ВыборкаСтрокТовары.Количество();
		
	Для Каждого Строчка Из ВыборкаСтрокТовары Цикл
		ОбластьМакета.Параметры.Заполнить(Строчка);
		
		НомерСтроки = НомерСтроки + 1;
		
		Если Строка(Строчка.ПредставлениеСтраны) = "Россия" Тогда
			ОбластьМакета.Параметры.ПредставлениеСтраны  = " ---- ";
		КонецЕсли; 
		
		
		Количество  = Строчка.Количество;
		СуммаНДС    = Строчка.СуммаНДС;
		СуммаСНДС 	= Строчка.СуммаСНДС;
		СуммаБезНДС = Строчка.СуммаБезНДС;
		
		ОбластьМакета.Параметры.Количество = Количество;
		ОбластьМакета.Параметры.Цена       = ?(Количество = 0, 0, СуммаБезНДС / Количество);
		ОбластьМакета.Параметры.Стоимость  = СуммаБезНДС;
		ОбластьМакета.Параметры.СуммаНДС   = СуммаНДС;
		ОбластьМакета.Параметры.Всего      = СуммаСНДС;
		ОбластьМакета.Параметры.СтавкаНДС  = Строчка.СтавкаНДС;
		
		ИтогоСуммаНДС = ИтогоСуммаНДС + СуммаНДС;
		ИтогоВсего    = ИтогоВсего    + СуммаСНДС;
		
		ПроставитьПрочеркиВПустыеПоля(ОбластьМакета);
		
		// Проверим возможность вывода табличного документа
		СтрокаСПодвалом = Новый Массив;
		СтрокаСПодвалом.Добавить(ОбластьМакета);
		Если НомерСтроки = КоличествоСтрок Тогда
			// Если последняя строка, то должен поместится итог и подвал
			СтрокаСПодвалом.Добавить(ОбластьМакетаИтого);
			СтрокаСПодвалом.Добавить(ОбластьМакетаПодвал);
		КонецЕсли;
						
		Если НЕ ФормированиеПечатныхФормСервер.ПроверитьВыводТабличногоДокумента(ТабДокумент, СтрокаСПодвалом) Тогда
			
			Если КоличествоСтрок > 0 Тогда
				// Вывод разделителя и заголовка таблицы на новой странице
				ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ТабДокумент.Вывести(ОбластьМакетаЗаголовокТаблицы);
			КонецЕсли;
			
		КонецЕсли;
		
		ТабДокумент.Вывести(ОбластьМакета);
		
	КонецЦикла;
	
	// Вывод строки Итого
	ОбластьМакетаИтого.Параметры.ИтогоСуммаНДС = ИтогоСуммаНДС;
	ОбластьМакетаИтого.Параметры.ИтогоВсего    = ИтогоВСего;
	ТабДокумент.Вывести(ОбластьМакетаИтого);
	
	// Вывод подвала
	ОбластьМакетаПодвал.Параметры.Заполнить(ДанныеДляПечати);
	ТабДокумент.Вывести(ОбластьМакетаПодвал);
	
КонецПроцедуры

Процедура ПечатьКорректировочногоСчетаФактуры(Ссылка, ТабДокумент)

	Макет = ПолучитьОбщийМакет("КорректировочныйСчетФактура");
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СчетФактураПолученный_КорректировочныйСчетФактура";

	ДанныеДляПечати = Неопределено;
	
	СобратьДанныеДляПечати(Ссылка, ДанныеДляПечати);
	
	Если ТипЗнч(ДанныеДляПечати) = Тип("Соответствие") Тогда
		Возврат;
	ИначеЕсли ДанныеДляПечати = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитыСФ = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(Ссылка, 
		"Номер,Дата,НомерИсходногоДокумента,ДатаИсходногоДокумента");
	
	СведенияОПокупателе = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(ДанныеДляПечати.Покупатель, РеквизитыСФ.Дата);
	СведенияОПоставщике = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(ДанныеДляПечати.Поставщик, РеквизитыСФ.Дата);
	
	// Выводим шапку
	
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	
	ОбластьМакета.Параметры.Заполнить(ДанныеДляПечати);
	
	ОбластьМакета.Параметры.Номер = ОбщегоНазначения.ПолучитьНомерНаПечать(Ссылка);
	ОбластьМакета.Параметры.Дата  = Формат(РеквизитыСФ.Дата, "ДФ='дд ММММ гггг'") + " г.";
	
	ОбластьМакета.Параметры.НомерСчетаФактуры = РеквизитыСФ.НомерИсходногоДокумента;
	ОбластьМакета.Параметры.ДатаСчетаФактуры  = Формат(РеквизитыСФ.ДатаИсходногоДокумента, "ДФ='дд ММММ гггг'") + " г.";
	
	ОбластьМакета.Параметры.НаименованиеПродавца = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(
		СведенияОПоставщике,  "ПолноеНаименование,");
	ОбластьМакета.Параметры.АдресПродавца = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(
		СведенияОПоставщике,  "ЮридическийАдрес,");

	ОбластьМакета.Параметры.НаименованиеПокупателя = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(
		СведенияОПокупателе, "ПолноеНаименование,");
	ОбластьМакета.Параметры.АдресПокупателя = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(
		СведенияОПокупателе, "ЮридическийАдрес,");
	
	КПП = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОПоставщике, "КПП,", Ложь);
	Если ЗначениеЗаполнено(КПП) Тогда
		КПП = "/" + КПП;
	КонецЕсли;
	ОбластьМакета.Параметры.ИННКПППродавца = ""
		+ ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОПоставщике, "ИНН,", Ложь) + КПП;
	
	КПП = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОПокупателе, "КПП,", Ложь);
	Если ЗначениеЗаполнено(КПП) Тогда
		КПП = "/" + КПП;
	КонецЕсли;
	ОбластьМакета.Параметры.ИННКПППокупателя = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(
		СведенияОПокупателе, "ИНН,", Ложь) + КПП;
	
	ТабДокумент.Вывести(ОбластьМакета);
	
	// Вывод заголовка таблицы
	ОбластьМакетаЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ТабДокумент.Вывести(ОбластьМакетаЗаголовокТаблицы);

	// Вывод строк таблицы
	ОбластьМакета 		= Макет.ПолучитьОбласть("Строка");
	ОбластьМакетаИтого 	= Макет.ПолучитьОбласть("Итого");
	ОбластьМакетаПодвал = Макет.ПолучитьОбласть("Подвал");
	
	НомерСтроки     = 0;
	КоличествоСтрок = ДанныеДляПечати.ТабличнаяЧасть.Количество();
	
	Для Каждого СтрокаТабличнойЧасти Из ДанныеДляПечати.ТабличнаяЧасть Цикл 
		
		НомерСтроки = НомерСтроки + 1;
		
		ОбластьМакета.Параметры.Заполнить(СтрокаТабличнойЧасти);
		Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.НаименованиеТовара) Тогда
			ОбластьМакета.Параметры.НаименованиеНоменклатуры = СтрокаТабличнойЧасти.НаименованиеТовара;               
		Иначе
			ОбластьМакета.Параметры.НаименованиеНоменклатуры = СтрокаТабличнойЧасти.Номенклатура;               
		КонецЕсли;	
		
		Если СтрокаТабличнойЧасти.СтавкаНДС = Перечисления.СтавкиНДС.БезНДС Тогда
			ОбластьМакета.Параметры.СтавкаНДС = "Без налога (НДС)";
		КонецЕсли;
		
		// Проверим возможность вывода табличного документа
		СтрокаСПодвалом = Новый Массив;
		СтрокаСПодвалом.Добавить(ОбластьМакета);
		Если НомерСтроки = КоличествоСтрок Тогда
			// Если последняя строка, то должен поместится итог и подвал
			СтрокаСПодвалом.Добавить(ОбластьМакетаИтого);
			СтрокаСПодвалом.Добавить(ОбластьМакетаПодвал);
		КонецЕсли;
						
		Если НЕ ФормированиеПечатныхФормСервер.ПроверитьВыводТабличногоДокумента(ТабДокумент, СтрокаСПодвалом) Тогда
			
			Если КоличествоСтрок > 0 Тогда
				// Вывод разделителя и заголовка таблицы на новой странице
				ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ТабДокумент.Вывести(ОбластьМакетаЗаголовокТаблицы);
			КонецЕсли;
			
		КонецЕсли;
		
		ТабДокумент.Вывести(ОбластьМакета);
		
	КонецЦикла;	
	
	// Вывод строки Итого
	ОбластьМакетаИтого.Параметры.РазницаБезНДСУменьшение = ДанныеДляПечати.ТабличнаяЧасть.Итог("РазницаБезНДСУменьшение");
	ОбластьМакетаИтого.Параметры.РазницаБезНДСУвеличение = ДанныеДляПечати.ТабличнаяЧасть.Итог("РазницаБезНДСУвеличение");
	ОбластьМакетаИтого.Параметры.РазницаСНДСУменьшение   = ДанныеДляПечати.ТабличнаяЧасть.Итог("РазницаСНДСУменьшение");
	ОбластьМакетаИтого.Параметры.РазницаСНДСУвеличение   = ДанныеДляПечати.ТабличнаяЧасть.Итог("РазницаСНДСУвеличение");
	ОбластьМакетаИтого.Параметры.РазницаНДСУменьшение    = ДанныеДляПечати.ТабличнаяЧасть.Итог("РазницаНДСУменьшение");
	ОбластьМакетаИтого.Параметры.РазницаНДСУвеличение    = ДанныеДляПечати.ТабличнаяЧасть.Итог("РазницаНДСУвеличение");
    	
	ТабДокумент.Вывести(ОбластьМакетаИтого);
	
	// Вывод строки Подвал
	ОбластьМакетаПодвал.Параметры.Заполнить(ДанныеДляПечати);
	ТабДокумент.Вывести(ОбластьМакетаПодвал);

КонецПроцедуры

// Функция собирает данные по документу основанию ОтчетКомиссионераОПродажах и 
// возвращает типизированную структуру с данными
// 
Функция СобратьДанныеПоОтчетОПродажахКомиссионера(ДокОснование, Ссылка)

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Документ",  ДокОснование);
	Запрос.УстановитьПараметр("Курс",      ДокОснование.КурсВзаиморасчетов);
	Запрос.УстановитьПараметр("Кратность", ДокОснование.КратностьВзаиморасчетов);

	Запрос.Текст =
	"ВЫБРАТЬ
	|	Организация,
	|	Организация   КАК Покупатель,
	|	Подразделение КАК Подразделение,
	|	Контрагент    КАК Поставщик,
	|	СуммаДокумента          КАК Сумма,
	|	СтавкаНДСВознаграждения КАК СтавкаНДС,
	|	ВалютаДокумента         КАК Валюта,
	|	СуммаВключаетНДС,
	|	УчитыватьНДС,
	|	Товары.(
	|		СУММА(СуммаВознаграждения) КАК Сумма
	|	)
	|ИЗ
	|	Документ.ОтчетКомиссионераОПродажах КАК ОтчетКомиссионераОПродажах
	|
	|ГДЕ
	|	ОтчетКомиссионераОПродажах.Ссылка = &Документ";

	Шапка = Запрос.Выполнить().Выбрать();
	Шапка.Следующий();
	ВыборкаСтрокТовары = Шапка.Товары.Выбрать();
	
	ДанныеДляПечати = Новый Структура();
	ДанныеДляПечати.Вставить("Организация",      Шапка.Организация);
	ДанныеДляПечати.Вставить("Номер",            Ссылка.НомерВходящегоДокумента);
	ДанныеДляПечати.Вставить("Дата",             Ссылка.ДатаВходящегоДокумента);
	ДанныеДляПечати.Вставить("Поставщик",        Шапка.Поставщик);
	ДанныеДляПечати.Вставить("Грузоотправитель", );
	ДанныеДляПечати.Вставить("Подразделение",    Шапка.Подразделение);
	ДанныеДляПечати.Вставить("Покупатель",       Шапка.Покупатель);
	ДанныеДляПечати.Вставить("Грузополучатель",  );
	ДанныеДляПечати.Вставить("Сумма",            Шапка.Сумма);
	ДанныеДляПечати.Вставить("Валюта",           Шапка.Валюта);
	ДанныеДляПечати.Вставить("УчитыватьНДС",     Истина);
	ДанныеДляПечати.Вставить("ФИОРуководителя",       );
	ДанныеДляПечати.Вставить("ФИОГлавногоБухгалтера", );

	Товары = ИнициализацияТаблицыСтрок();

	Пока ВыборкаСтрокТовары.Следующий() Цикл
		Строчка = Товары.Добавить();
		Строчка.Товар               = "Комиссионное вознаграждение";
		Строчка.ТоварНаименование   = "Комиссионное вознаграждение";
		Строчка.СтранаПроисхождения = "";
		Строчка.ПредставлениеСтраны = "";
		Строчка.СтранаПроисхожденияКод = "";
		Строчка.НомерГТД            = "";
		Строчка.ПредставлениеГТД    = "";
		Строчка.Количество          = 1;
		Строчка.ЕдиницаИзмерения    = "";
		Строчка.ЕдиницаИзмеренияКод = "";
		Строчка.СтавкаНДС           = Шапка.СтавкаНДС;
		Строчка.СуммаВключаетНДС	= Истина;
		СуммаНДС                    = УчетНДС.РассчитатьСуммуНДС(ВыборкаСтрокТовары.Сумма, Шапка.УчитыватьНДС, Шапка.СуммаВключаетНДС, УчетНДС.ПолучитьСтавкуНДС(Шапка.СтавкаНДС));
		Строчка.СуммаНДС            = СуммаНДС;
		Строчка.Сумма               = ВыборкаСтрокТовары.Сумма + ?(Шапка.СуммаВключаетНДС, 0, СуммаНДС);
		Строчка.Цена                = ВыборкаСтрокТовары.Сумма - СуммаНДС;
	КонецЦикла;

	ДанныеДляПечати.Вставить("ТабличнаяЧасть", Товары);

	Возврат ДанныеДляПечати;

КонецФункции // СобратьДанныеПоОтчетОПродажахКомиссионера()

// Функция собирает данные по документу основанию Поступление и 
// структуру с данными
// 
Функция СобратьДанныеПоПоступлениюТоваров(ДокОснование, Ссылка)

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументОснование", ДокОснование);
	Запрос.УстановитьПараметр("ПустойКонтрагент", Справочники.Контрагенты.ПустаяСсылка());
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Организация,
	|	Организация           КАК Покупатель,
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(Грузополучатель, &ПустойКонтрагент) = &ПустойКонтрагент
	|			ТОГДА Организация 
	|		ИНАЧЕ Грузополучатель 
	|	КОНЕЦ КАК Грузополучатель,
	|	Подразделение,
	|	Контрагент            КАК Поставщик,
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(Грузоотправитель, &ПустойКонтрагент) = &ПустойКонтрагент
	|			ТОГДА ""он же"" 
	|		ИНАЧЕ Грузоотправитель
	|	КОНЕЦ КАК Грузоотправитель,
	|	СуммаДокумента        КАК Сумма,
	|	ВалютаДокумента       КАК Валюта,
	|	УчитыватьНДС          КАК УчитыватьНДС,
	|	СуммаВключаетНДС      КАК СуммаВключаетНДС
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
	|
	|ГДЕ
	|	ПоступлениеТоваровУслуг.Ссылка = &ДокументОснование";

	ЗапросПоТоварам = Новый Запрос();
	ЗапросПоТоварам.УстановитьПараметр("Курс", ДокОснование.КурсВзаиморасчетов);
	ЗапросПоТоварам.УстановитьПараметр("Кратность", ДокОснование.КратностьВзаиморасчетов);
	ЗапросПоТоварам.УстановитьПараметр("ТекущийДокумент", ДокОснование.Ссылка);
	ЗапросПоТоварам.Текст = 
	"ВЫБРАТЬ
	|	ВложенныйЗапрос.НомерСтроки,
	|	ВложенныйЗапрос.Товар,
	|	ВложенныйЗапрос.Товар.НаименованиеПолное КАК ТоварНаименование,
	|	NULL КАК ОбъектСтроительства,
	|	NULL КАК ОбъектСтроительстваНаименование,
	|	ВложенныйЗапрос.СтранаПроисхождения,
	|	ВложенныйЗапрос.СтранаПроисхождения.Наименование КАК ПредставлениеСтраны,
	|	ВложенныйЗапрос.СтранаПроисхождения.Код КАК СтранаПроисхожденияКод,
	|	ВложенныйЗапрос.НомерГТД,
	|	ВложенныйЗапрос.НомерГТД.Представление КАК ПредставлениеГТД,
	|	ВложенныйЗапрос.ЕдиницаИзмерения.Представление КАК ЕдиницаИзмерения,
	|	ВложенныйЗапрос.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код КАК ЕдиницаИзмеренияКод,
	|	ВложенныйЗапрос.Количество,
	|	ВложенныйЗапрос.Цена,
	|	ВложенныйЗапрос.Сумма,
	|	ВложенныйЗапрос.СуммаНДС,
	|	ВложенныйЗапрос.СтавкаНДС,
	|	ВложенныйЗапрос.Характеристика,
	|	ВложенныйЗапрос.Серия,
	|	1 КАК ID
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаПоТоварам.НомерСтроки КАК НомерСтроки,
	|		ТаблицаПоТоварам.Номенклатура КАК Товар,
	|		ТаблицаПоТоварам.СерияНоменклатуры.СтранаПроисхождения КАК СтранаПроисхождения,
	|		ТаблицаПоТоварам.СерияНоменклатуры.НомерГТД КАК НомерГТД,
	|		ТаблицаПоТоварам.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		СУММА(ТаблицаПоТоварам.Количество) КАК Количество,
	|		ТаблицаПоТоварам.Цена КАК Цена,
	|		ТаблицаПоТоварам.Сумма КАК Сумма,
	|		ТаблицаПоТоварам.СуммаНДС КАК СуммаНДС,
	|		ТаблицаПоТоварам.СтавкаНДС КАК СтавкаНДС,
	|		ТаблицаПоТоварам.ХарактеристикаНоменклатуры КАК Характеристика,
	|		ТаблицаПоТоварам.СерияНоменклатуры КАК Серия
	|	ИЗ
	|		Документ.ПоступлениеТоваровУслуг.Товары КАК ТаблицаПоТоварам
	|	ГДЕ
	|		ТаблицаПоТоварам.Ссылка = &ТекущийДокумент
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТаблицаПоТоварам.НомерСтроки,
	|		ТаблицаПоТоварам.Номенклатура,
	|		ТаблицаПоТоварам.ЕдиницаИзмерения,
	|		ТаблицаПоТоварам.СтавкаНДС,
	|		ТаблицаПоТоварам.Цена,
	|		ТаблицаПоТоварам.СерияНоменклатуры.СтранаПроисхождения,
	|		ТаблицаПоТоварам.СерияНоменклатуры.НомерГТД,
	|		ТаблицаПоТоварам.ХарактеристикаНоменклатуры,
	|		ТаблицаПоТоварам.СерияНоменклатуры,
	|		ТаблицаПоТоварам.Сумма,
	|		ТаблицаПоТоварам.СуммаНДС) КАК ВложенныйЗапрос
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаПоУслугам.НомерСтроки,
	|	ТаблицаПоУслугам.Номенклатура,
	|	ТаблицаПоУслугам.Содержание,
	|	NULL,
	|	NULL,
	|	""Россия"",
	|	""Россия"",
	|	"""",
	|	""--"",
	|	""--"",
	|	ТаблицаПоУслугам.Номенклатура.ЕдиницаХраненияОстатков.Представление,
	|	ТаблицаПоУслугам.Номенклатура.ЕдиницаХраненияОстатков.ЕдиницаПоКлассификатору.Код,
	|	ТаблицаПоУслугам.Количество,
	|	ТаблицаПоУслугам.Цена,
	|	ТаблицаПоУслугам.Сумма,
	|	ТаблицаПоУслугам.СуммаНДС,
	|	ТаблицаПоУслугам.СтавкаНДС,
	|	NULL,
	|	NULL,
	|	2
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Услуги КАК ТаблицаПоУслугам
	|ГДЕ
	|	ТаблицаПоУслугам.Ссылка = &ТекущийДокумент
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаПоОборудованию.НомерСтроки,
	|	ТаблицаПоОборудованию.Номенклатура,
	|	ТаблицаПоОборудованию.Номенклатура.НаименованиеПолное,
	|	NULL,
	|	NULL,
	|	ТаблицаПоОборудованию.СерияНоменклатуры.СтранаПроисхождения,
	|	ТаблицаПоОборудованию.СерияНоменклатуры.СтранаПроисхождения.Наименование,
	|	ТаблицаПоОборудованию.СерияНоменклатуры.СтранаПроисхождения.Код,
	|	ТаблицаПоОборудованию.СерияНоменклатуры.НомерГТД,
	|	ТаблицаПоОборудованию.СерияНоменклатуры.НомерГТД.Представление,
	|	ТаблицаПоОборудованию.ЕдиницаИзмерения.Представление,
	|	ТаблицаПоОборудованию.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код,
	|	ТаблицаПоОборудованию.Количество,
	|	ТаблицаПоОборудованию.Цена,
	|	ТаблицаПоОборудованию.Сумма,
	|	ТаблицаПоОборудованию.СуммаНДС,
	|	ТаблицаПоОборудованию.СтавкаНДС,
	|	ТаблицаПоОборудованию.ХарактеристикаНоменклатуры,
	|	ТаблицаПоОборудованию.СерияНоменклатуры,
	|	3
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Оборудование КАК ТаблицаПоОборудованию
	|ГДЕ
	|	ТаблицаПоОборудованию.Ссылка = &ТекущийДокумент
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаПоОбъектамСтроительства.НомерСтроки,
	|	NULL,
	|	NULL,
	|	ТаблицаПоОбъектамСтроительства.ОбъектСтроительства,
	|	ТаблицаПоОбъектамСтроительства.ОбъектСтроительства,
	|	""Россия"",
	|	""Россия"",
	|	"""",
	|	""--"",
	|	""--"",
	|	""--"",
	|	"""",
	|	0,
	|	ТаблицаПоОбъектамСтроительства.Сумма,
	|	ТаблицаПоОбъектамСтроительства.Сумма,
	|	ТаблицаПоОбъектамСтроительства.СуммаНДС,
	|	ТаблицаПоОбъектамСтроительства.СтавкаНДС,
	|	NULL,
	|	NULL,
	|	4
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.ОбъектыСтроительства КАК ТаблицаПоОбъектамСтроительства
	|ГДЕ
	|	ТаблицаПоОбъектамСтроительства.Ссылка = &ТекущийДокумент";

	Шапка              = Запрос.Выполнить().Выбрать();
	ВыборкаСтрокТовары = ЗапросПоТоварам.Выполнить().Выбрать();

	Шапка.Следующий();
	ДанныеДляПечати = Новый Структура();
	ДанныеДляПечати.Вставить("Организация",      Шапка.Организация);
	ДанныеДляПечати.Вставить("Номер",            Ссылка.НомерВходящегоДокумента);
	ДанныеДляПечати.Вставить("Дата",             Ссылка.ДатаВходящегоДокумента);
	ДанныеДляПечати.Вставить("Поставщик",        Шапка.Поставщик);
	ДанныеДляПечати.Вставить("Грузоотправитель", Шапка.Грузоотправитель);
	ДанныеДляПечати.Вставить("Подразделение",    Шапка.Подразделение);
	ДанныеДляПечати.Вставить("Покупатель",       Шапка.Покупатель);
	ДанныеДляПечати.Вставить("Грузополучатель",  Шапка.Грузополучатель);
	ДанныеДляПечати.Вставить("Сумма",            Шапка.Сумма);
	ДанныеДляПечати.Вставить("Валюта",           Шапка.Валюта);
	ДанныеДляПечати.Вставить("УчитыватьНДС",     Шапка.УчитыватьНДС);
	ДанныеДляПечати.Вставить("ФИОРуководителя",       );
	ДанныеДляПечати.Вставить("ФИОГлавногоБухгалтера", );

	Товары = ИнициализацияТаблицыСтрок();

	ЕстьТовары = Ложь;
	
	// <- Шевченков 20170523 №65872
	МаксСумма  = 0;
	МаксСтрока = 0;
	Инк        = 0;
	КорректироватьПоТаре = ДокОснование.Ссылка.ВозвратнаяТара.Количество()>0;
	// ->
	
	Пока ВыборкаСтрокТовары.Следующий() Цикл

		Если ВыборкаСтрокТовары.ID = 1 
			ИЛИ ВыборкаСтрокТовары.ID = 3 
			ИЛИ ВыборкаСтрокТовары.ID = 4 Тогда
			ЕстьТовары = Истина;                            
		КонецЕсли;
		
		Строчка = Товары.Добавить();
		ЗаполнитьЗначенияСвойств(Строчка, ВыборкаСтрокТовары);
		Если ЗначениеЗаполнено(ВыборкаСтрокТовары.Товар) Тогда
			Строчка.ТоварНаименование   = ВыборкаСтрокТовары.ТоварНаименование  + ФормированиеПечатныхФормСервер.ПредставлениеСерий(ВыборкаСтрокТовары);
		ИначеЕсли ЗначениеЗаполнено(ВыборкаСтрокТовары.ОбъектСтроительства) Тогда
			Строчка.Товар               = ВыборкаСтрокТовары.ОбъектСтроительства;
			Строчка.ТоварНаименование   = ВыборкаСтрокТовары.ОбъектСтроительстваНаименование;
		КонецЕсли;
		Строчка.СуммаВключаетНДС	= Шапка.СуммаВключаетНДС;
		
		// <- Шевченков 20170523 №65872
		Если КорректироватьПоТаре Тогда
			Если ВыборкаСтрокТовары.Сумма > МаксСумма Тогда
				МаксСумма  = ВыборкаСтрокТовары.Сумма;
				МаксСтрока = Инк;
			КонецЕсли;
			Инк = Инк + 1;
		КонецЕсли;		
		// ->

	КонецЦикла;
	
	// <- Шевченков 20170523 №65872
	Если ЕстьТовары И КорректироватьПоТаре Тогда
		Товары[МаксСтрока].Сумма = Товары[МаксСтрока].Сумма + 0.01;
	КонецЕсли;		
	// ->
	
	Если Не ЕстьТовары Тогда
		ДанныеДляПечати.Грузоотправитель = "";
		ДанныеДляПечати.Грузополучатель = "";		
	КонецЕсли;

	ДанныеДляПечати.Вставить("ТабличнаяЧасть", Товары);

	Возврат ДанныеДляПечати;

КонецФункции // СобратьДанныеПоПоступлениюТоваров()

// Функция собирает данные по документу основанию ВозвратТоваровОтПокупателя и 
// структуру с данными
// 
Функция СобратьДанныеПоВозвратуТоваровОтПокупателя(ДокОснование, Ссылка)

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументОснование", ДокОснование);
	Запрос.УстановитьПараметр("ПустойКонтрагент", Справочники.Контрагенты.ПустаяСсылка());
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Организация,
	|	Организация           КАК Покупатель,
	|	Организация КАК Грузополучатель,
	|	Подразделение,
	|	Контрагент            КАК Поставщик,
	|	""он же"" КАК Грузоотправитель,
	|	СуммаДокумента        КАК Сумма,
	|	ВалютаДокумента       КАК Валюта,
	|	УчитыватьНДС          КАК УчитыватьНДС,
	|	СуммаВключаетНДС      КАК СуммаВключаетНДС
	|ИЗ
	|	Документ.ВозвратТоваровОтПокупателя КАК ВозвратТоваровОтПокупателя
	|
	|ГДЕ
	|	ВозвратТоваровОтПокупателя.Ссылка = &ДокументОснование";

	ЗапросПоТоварам = Новый Запрос();
	ЗапросПоТоварам.УстановитьПараметр("Курс", ДокОснование.КурсВзаиморасчетов);
	ЗапросПоТоварам.УстановитьПараметр("Кратность", ДокОснование.КратностьВзаиморасчетов);
	ЗапросПоТоварам.УстановитьПараметр("ТекущийДокумент", ДокОснование.Ссылка);
	ЗапросПоТоварам.Текст = 
	"ВЫБРАТЬ
	|	ВозвратТоваровОтПокупателяТовары.НомерСтроки КАК НомерСтроки,
	|	ВозвратТоваровОтПокупателяТовары.Номенклатура КАК Товар,
	|	ВЫРАЗИТЬ(ВозвратТоваровОтПокупателяТовары.Номенклатура.НаименованиеПолное КАК СТРОКА(1000)) КАК ТоварНаименование,
	|	ВозвратТоваровОтПокупателяТовары.СерияНоменклатуры.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ВозвратТоваровОтПокупателяТовары.СерияНоменклатуры.СтранаПроисхождения.Наименование КАК ПредставлениеСтраны,
	|	ВозвратТоваровОтПокупателяТовары.СерияНоменклатуры.СтранаПроисхождения.Код КАК СтранаПроисхожденияКод,
	|	ВозвратТоваровОтПокупателяТовары.СерияНоменклатуры.НомерГТД КАК НомерГТД,
	|	ВозвратТоваровОтПокупателяТовары.СерияНоменклатуры.НомерГТД.Представление КАК ПредставлениеГТД,
	|	ВозвратТоваровОтПокупателяТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВозвратТоваровОтПокупателяТовары.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код КАК ЕдиницаИзмеренияКод,
	|	СУММА(ВозвратТоваровОтПокупателяТовары.Количество) КАК Количество,
	|	ВозвратТоваровОтПокупателяТовары.Цена КАК Цена,
	|	ВозвратТоваровОтПокупателяТовары.Сумма КАК Сумма,
	|	ВозвратТоваровОтПокупателяТовары.СуммаНДС КАК СуммаНДС,
	|	ВозвратТоваровОтПокупателяТовары.СтавкаНДС КАК СтавкаНДС,
	|	ВозвратТоваровОтПокупателяТовары.ХарактеристикаНоменклатуры КАК Характеристика,
	|	ВозвратТоваровОтПокупателяТовары.СерияНоменклатуры КАК Серия
	|ИЗ
	|	Документ.ВозвратТоваровОтПокупателя.Товары КАК ВозвратТоваровОтПокупателяТовары
	|ГДЕ
	|	ВозвратТоваровОтПокупателяТовары.Ссылка = &ТекущийДокумент
	|
	|СГРУППИРОВАТЬ ПО
	|	ВозвратТоваровОтПокупателяТовары.НомерСтроки,
	|	ВозвратТоваровОтПокупателяТовары.Номенклатура,
	|	ВозвратТоваровОтПокупателяТовары.ЕдиницаИзмерения,
	|	ВозвратТоваровОтПокупателяТовары.СтавкаНДС,
	|	ВозвратТоваровОтПокупателяТовары.Цена,
	|	ВозвратТоваровОтПокупателяТовары.СерияНоменклатуры.СтранаПроисхождения,
	|	ВозвратТоваровОтПокупателяТовары.СерияНоменклатуры.НомерГТД,
	|	ВозвратТоваровОтПокупателяТовары.ХарактеристикаНоменклатуры,
	|	ВозвратТоваровОтПокупателяТовары.СерияНоменклатуры,
	|	ВозвратТоваровОтПокупателяТовары.Сумма,
	|	ВозвратТоваровОтПокупателяТовары.СуммаНДС,
	|	ВозвратТоваровОтПокупателяТовары.СерияНоменклатуры.НомерГТД.Представление,
	|	ВЫРАЗИТЬ(ВозвратТоваровОтПокупателяТовары.Номенклатура.НаименованиеПолное КАК СТРОКА(1000)),
	|	ВозвратТоваровОтПокупателяТовары.СерияНоменклатуры.СтранаПроисхождения.Наименование,
	|	ВозвратТоваровОтПокупателяТовары.СерияНоменклатуры.СтранаПроисхождения.Код,
	|	ВозвратТоваровОтПокупателяТовары.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код";

	Шапка              = Запрос.Выполнить().Выбрать();
	ВыборкаСтрокТовары = ЗапросПоТоварам.Выполнить().Выбрать();

	Шапка.Следующий();
	ДанныеДляПечати = Новый Структура();
	ДанныеДляПечати.Вставить("Организация",      Шапка.Организация);
	ДанныеДляПечати.Вставить("Номер",            Ссылка.НомерВходящегоДокумента);
	ДанныеДляПечати.Вставить("Дата",             Ссылка.ДатаВходящегоДокумента);
	ДанныеДляПечати.Вставить("Поставщик",        Шапка.Поставщик);
	ДанныеДляПечати.Вставить("Грузоотправитель", Шапка.Грузоотправитель);
	ДанныеДляПечати.Вставить("Подразделение",    Шапка.Подразделение);
	ДанныеДляПечати.Вставить("Покупатель",       Шапка.Покупатель);
	ДанныеДляПечати.Вставить("Грузополучатель",  Шапка.Грузополучатель);
	ДанныеДляПечати.Вставить("Сумма",            Шапка.Сумма);
	ДанныеДляПечати.Вставить("Валюта",           Шапка.Валюта);
	ДанныеДляПечати.Вставить("УчитыватьНДС",     Шапка.УчитыватьНДС);
	ДанныеДляПечати.Вставить("ФИОРуководителя",       );
	ДанныеДляПечати.Вставить("ФИОГлавногоБухгалтера", );

	Товары = ИнициализацияТаблицыСтрок();

	Пока ВыборкаСтрокТовары.Следующий() Цикл
		
		Строчка = Товары.Добавить();
		ЗаполнитьЗначенияСвойств(Строчка, ВыборкаСтрокТовары);
		Строчка.ТоварНаименование   = СокрЛП(ВыборкаСтрокТовары.Товар.НаименованиеПолное) + ФормированиеПечатныхФормСервер.ПредставлениеСерий(ВыборкаСтрокТовары);
		Строчка.СуммаВключаетНДС	= Шапка.СуммаВключаетНДС;

	КонецЦикла;
	
	ДанныеДляПечати.Вставить("ТабличнаяЧасть", Товары);

	Возврат ДанныеДляПечати;

КонецФункции // СобратьДанныеПоВозвратуТоваровОтПокупателя()

// Функция собирает данные по документу основанию ВозвратТоваровПоставщику и 
// структуру с данными
// 
Функция СобратьДанныеПоВозвратуТоваров(ДокОснование, Ссылка)

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументОснование", ДокОснование);
	Запрос.УстановитьПараметр("ПустойКонтрагент", Справочники.Контрагенты.ПустаяСсылка());
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Организация,
	|	Организация           КАК Покупатель,
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(Грузополучатель, &ПустойКонтрагент) = &ПустойКонтрагент
	|			ТОГДА Организация 
	|		ИНАЧЕ Грузополучатель 
	|	КОНЕЦ КАК Грузополучатель,
	|	Подразделение,
	|	Контрагент            КАК Поставщик,
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(Грузоотправитель, &ПустойКонтрагент) = &ПустойКонтрагент
	|			ТОГДА ""он же"" 
	|		ИНАЧЕ Грузоотправитель
	|	КОНЕЦ КАК Грузоотправитель,
	|	СуммаДокумента        КАК Сумма,
	|	ВалютаДокумента       КАК Валюта,
	|	УчитыватьНДС          КАК УчитыватьНДС,
	|	СуммаВключаетНДС      КАК СуммаВключаетНДС
	|ИЗ
	|	Документ.ВозвратТоваровПоставщику КАК ВозвратТоваровПоставщику
	|
	|ГДЕ
	|	ВозвратТоваровПоставщику.Ссылка = &ДокументОснование";

	ЗапросПоТоварам = Новый Запрос();
	ЗапросПоТоварам.УстановитьПараметр("Курс", ДокОснование.КурсВзаиморасчетов);
	ЗапросПоТоварам.УстановитьПараметр("Кратность", ДокОснование.КратностьВзаиморасчетов);
	ЗапросПоТоварам.УстановитьПараметр("ТекущийДокумент", ДокОснование.Ссылка);
	ЗапросПоТоварам.Текст = 
	"ВЫБРАТЬ
	|	ВложенныйЗапрос.НомерСтроки,
	|	ВложенныйЗапрос.Товар,
	|	ВложенныйЗапрос.Товар.НаименованиеПолное КАК ТоварНаименование,
	|	ВложенныйЗапрос.СтранаПроисхождения,
	|	ВложенныйЗапрос.СтранаПроисхождения.Наименование КАК ПредставлениеСтраны,
	|	ВложенныйЗапрос.СтранаПроисхождения.Код КАК СтранаПроисхожденияКод,
	|	ВложенныйЗапрос.НомерГТД,
	|	ВложенныйЗапрос.НомерГТД.Представление КАК ПредставлениеГТД,
	|	ВложенныйЗапрос.ЕдиницаИзмерения.Представление КАК ЕдиницаИзмерения,
	|	ВложенныйЗапрос.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код КАК ЕдиницаИзмеренияКод,
	|	ВложенныйЗапрос.Количество,
	|	ВложенныйЗапрос.Цена,
	|	ВложенныйЗапрос.Сумма,
	|	ВложенныйЗапрос.СуммаНДС,
	|	ВложенныйЗапрос.СтавкаНДС,
	|	ВложенныйЗапрос.Характеристика,
	|	ВложенныйЗапрос.Серия,
	|	ВложенныйЗапрос.ДокументПоступления
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаПоТоварам.НомерСтроки КАК НомерСтроки,
	|		ТаблицаПоТоварам.Номенклатура КАК Товар,
	|		ТаблицаПоТоварам.СерияНоменклатуры.СтранаПроисхождения КАК СтранаПроисхождения,
	|		ТаблицаПоТоварам.СерияНоменклатуры.НомерГТД КАК НомерГТД,
	|		ТаблицаПоТоварам.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		СУММА(ТаблицаПоТоварам.Количество) КАК Количество,
	|		-ТаблицаПоТоварам.Цена КАК Цена,
	|		-ТаблицаПоТоварам.Сумма КАК Сумма,
	|		-ТаблицаПоТоварам.СуммаНДС КАК СуммаНДС,
	|		ТаблицаПоТоварам.СтавкаНДС КАК СтавкаНДС,
	|		ТаблицаПоТоварам.ХарактеристикаНоменклатуры КАК Характеристика,
	|		ТаблицаПоТоварам.СерияНоменклатуры КАК Серия,
	|		ТаблицаПоТоварам.ДокументПоступления КАК ДокументПоступления
	|	ИЗ
	|		Документ.ВозвратТоваровПоставщику.Товары КАК ТаблицаПоТоварам
	|	ГДЕ
	|		ТаблицаПоТоварам.Ссылка = &ТекущийДокумент
	|		И ТаблицаПоТоварам.ДокументПоступления <> НЕОПРЕДЕЛЕНО
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТаблицаПоТоварам.НомерСтроки,
	|		ТаблицаПоТоварам.Номенклатура,
	|		ТаблицаПоТоварам.ЕдиницаИзмерения,
	|		ТаблицаПоТоварам.СтавкаНДС,
	|		ТаблицаПоТоварам.Цена,
	|		ТаблицаПоТоварам.СерияНоменклатуры.СтранаПроисхождения,
	|		ТаблицаПоТоварам.СерияНоменклатуры.НомерГТД,
	|		ТаблицаПоТоварам.ХарактеристикаНоменклатуры,
	|		ТаблицаПоТоварам.СерияНоменклатуры,
	|		ТаблицаПоТоварам.Сумма,
	|		ТаблицаПоТоварам.СуммаНДС,
	|		ТаблицаПоТоварам.ДокументПоступления) КАК ВложенныйЗапрос
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаПоОборудованию.НомерСтроки,
	|	ТаблицаПоОборудованию.Номенклатура,
	|	ТаблицаПоОборудованию.Номенклатура.НаименованиеПолное,
	|	ТаблицаПоОборудованию.СерияНоменклатуры.СтранаПроисхождения,
	|	ТаблицаПоОборудованию.СерияНоменклатуры.СтранаПроисхождения.Наименование,
	|	ТаблицаПоОборудованию.СерияНоменклатуры.СтранаПроисхождения.Код,
	|	ТаблицаПоОборудованию.СерияНоменклатуры.НомерГТД,
	|	ТаблицаПоОборудованию.СерияНоменклатуры.НомерГТД.Представление,
	|	ТаблицаПоОборудованию.ЕдиницаИзмерения.Представление,
	|	ТаблицаПоОборудованию.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код,
	|	ТаблицаПоОборудованию.Количество,
	|	-ТаблицаПоОборудованию.Цена,
	|	-ТаблицаПоОборудованию.Сумма,
	|	-ТаблицаПоОборудованию.СуммаНДС,
	|	ТаблицаПоОборудованию.СтавкаНДС,
	|	ТаблицаПоОборудованию.ХарактеристикаНоменклатуры,
	|	ТаблицаПоОборудованию.СерияНоменклатуры,
	|	ТаблицаПоОборудованию.ДокументПоступления
	|ИЗ
	|	Документ.ВозвратТоваровПоставщику.Оборудование КАК ТаблицаПоОборудованию
	|ГДЕ
	|	ТаблицаПоОборудованию.Ссылка = &ТекущийДокумент
	|	И ТаблицаПоОборудованию.ДокументПоступления <> НЕОПРЕДЕЛЕНО";

	Шапка              = Запрос.Выполнить().Выбрать();
	ВыборкаСтрокТовары = ЗапросПоТоварам.Выполнить().Выбрать();

	Шапка.Следующий();
	ДанныеДляПечати = Новый Структура();
	ДанныеДляПечати.Вставить("Организация",      Шапка.Организация);
	ДанныеДляПечати.Вставить("Номер",            Ссылка.НомерВходящегоДокумента);
	ДанныеДляПечати.Вставить("Дата",             Ссылка.ДатаВходящегоДокумента);
	ДанныеДляПечати.Вставить("Поставщик",        Шапка.Поставщик);
	ДанныеДляПечати.Вставить("Грузоотправитель", Шапка.Грузоотправитель);
	ДанныеДляПечати.Вставить("Подразделение",    Шапка.Подразделение);
	ДанныеДляПечати.Вставить("Покупатель",       Шапка.Покупатель);
	ДанныеДляПечати.Вставить("Грузополучатель",  Шапка.Грузополучатель);
	ДанныеДляПечати.Вставить("Сумма",            Шапка.Сумма);
	ДанныеДляПечати.Вставить("Валюта",           Шапка.Валюта);
	ДанныеДляПечати.Вставить("УчитыватьНДС",     Шапка.УчитыватьНДС);
	ДанныеДляПечати.Вставить("ФИОРуководителя",       );
	ДанныеДляПечати.Вставить("ФИОГлавногоБухгалтера", );

	Товары = ИнициализацияТаблицыСтрок();

	Пока ВыборкаСтрокТовары.Следующий() Цикл
		
		Если НЕ ЗначениеЗаполнено(ВыборкаСтрокТовары.ДокументПоступления) Тогда

			Строчка = Товары.Добавить();
			ЗаполнитьЗначенияСвойств(Строчка, ВыборкаСтрокТовары);
			Строчка.ТоварНаименование   = ВыборкаСтрокТовары.ТоварНаименование  + ФормированиеПечатныхФормСервер.ПредставлениеСерий(ВыборкаСтрокТовары);
			Строчка.СуммаВключаетНДС	= Шапка.СуммаВключаетНДС;
			
		КонецЕсли;

	КонецЦикла;
	
	ДанныеДляПечати.Вставить("ТабличнаяЧасть", Товары);

	Возврат ДанныеДляПечати;

КонецФункции // СобратьДанныеПоВозвратуТоваров()

// Функция собирает данные по документу основанию Поступление товаров и услуг в НТТ 
// и возвращает структуру с данными
// 
Функция СобратьДанныеПоПоступлениюТоваровУслугВНТТ(ДокОснование, Ссылка)

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументОснование", ДокОснование);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Организация,
	|	Организация           КАК Покупатель,
	|	Организация           КАК Грузополучатель,
	|	Контрагент            КАК Поставщик,
	|	Контрагент            КАК Грузоотправитель,
	|	СуммаДокумента        КАК Сумма,
	|	ВалютаДокумента       КАК Валюта,
	|	УчитыватьНДС          КАК УчитыватьНДС,
	|	СуммаВключаетНДС      КАК СуммаВключаетНДС
	|ИЗ
	|	Документ.ПоступлениеТоваровУслугВНТТ КАК ПоступлениеТоваровУслугВНТТ
	|
	|ГДЕ
	|	ПоступлениеТоваровУслугВНТТ.Ссылка = &ДокументОснование";

	ЗапросПоТоварам = Новый Запрос();
	ЗапросПоТоварам.УстановитьПараметр("Курс",            ДокОснование.КурсВзаиморасчетов);
	ЗапросПоТоварам.УстановитьПараметр("Кратность",       ДокОснование.КратностьВзаиморасчетов);
	ЗапросПоТоварам.УстановитьПараметр("ТекущийДокумент", ДокОснование.Ссылка);
	ЗапросПоТоварам.Текст = 
	"ВЫБРАТЬ
	|	ВложенныйЗапрос.НомерСтроки,
	|	ВложенныйЗапрос.Товар,
	|	ВложенныйЗапрос.Товар.НаименованиеПолное КАК ТоварНаименование,
	|	ВложенныйЗапрос.Характеристика,
	|	ВложенныйЗапрос.Серия,
	|	ВложенныйЗапрос.СтранаПроисхождения,
	|	ВложенныйЗапрос.СтранаПроисхождения.Наименование КАК ПредставлениеСтраны,
	|	ВложенныйЗапрос.СтранаПроисхождения.Код КАК СтранаПроисхожденияКод,
	|	ВложенныйЗапрос.НомерГТД,
	|	ВложенныйЗапрос.НомерГТД.Представление КАК ПредставлениеГТД,
	|	ВложенныйЗапрос.ЕдиницаИзмерения.Представление КАК ЕдиницаИзмерения,
	|	ВложенныйЗапрос.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код КАК ЕдиницаИзмеренияКод,
	|	ВложенныйЗапрос.Количество,
	|	ВложенныйЗапрос.Цена,
	|	ВложенныйЗапрос.Сумма,
	|	ВложенныйЗапрос.СуммаНДС,
	|	ВложенныйЗапрос.СтавкаНДС
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаПоТоварам.НомерСтроки КАК НомерСтроки,
	|		ТаблицаПоТоварам.Номенклатура КАК Товар,
	|		ТаблицаПоТоварам.ХарактеристикаНоменклатуры КАК Характеристика,
	|		ТаблицаПоТоварам.СерияНоменклатуры КАК Серия,
	|		ТаблицаПоТоварам.СерияНоменклатуры.СтранаПроисхождения КАК СтранаПроисхождения,
	|		ТаблицаПоТоварам.СерияНоменклатуры.НомерГТД КАК НомерГТД,
	|		ТаблицаПоТоварам.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		СУММА(ТаблицаПоТоварам.Количество) КАК Количество,
	|		ТаблицаПоТоварам.Цена КАК Цена,
	|		ТаблицаПоТоварам.Сумма КАК Сумма,
	|		ТаблицаПоТоварам.СуммаНДС КАК СуммаНДС,
	|		ТаблицаПоТоварам.СтавкаНДС КАК СтавкаНДС
	|	ИЗ
	|		Документ.ПоступлениеТоваровУслугВНТТ.Товары КАК ТаблицаПоТоварам
	|	ГДЕ
	|		ТаблицаПоТоварам.Ссылка = &ТекущийДокумент
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТаблицаПоТоварам.НомерСтроки,
	|		ТаблицаПоТоварам.Номенклатура,
	|		ТаблицаПоТоварам.ХарактеристикаНоменклатуры,
	|		ТаблицаПоТоварам.СерияНоменклатуры,
	|		ТаблицаПоТоварам.ЕдиницаИзмерения,
	|		ТаблицаПоТоварам.СтавкаНДС,
	|		ТаблицаПоТоварам.Цена,
	|		ТаблицаПоТоварам.СерияНоменклатуры.СтранаПроисхождения,
	|		ТаблицаПоТоварам.СерияНоменклатуры.НомерГТД,
	|		ТаблицаПоТоварам.Сумма,
	|		ТаблицаПоТоварам.СуммаНДС) КАК ВложенныйЗапрос
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаПоУслугам.НомерСтроки,
	|	ТаблицаПоУслугам.Номенклатура,
	|	ТаблицаПоУслугам.Содержание,
	|	"""",
	|	"""",
	|	""Россия"",
	|	""Россия"",
	|	"""",
	|	""--"",
	|	""--"",
	|	ТаблицаПоУслугам.Номенклатура.ЕдиницаХраненияОстатков.Представление,
	|	ТаблицаПоУслугам.Номенклатура.ЕдиницаХраненияОстатков.ЕдиницаПоКлассификатору.Код,
	|	ТаблицаПоУслугам.Количество,
	|	ТаблицаПоУслугам.Цена,
	|	ТаблицаПоУслугам.Сумма,
	|	ТаблицаПоУслугам.СуммаНДС,
	|	ТаблицаПоУслугам.СтавкаНДС
	|ИЗ
	|	Документ.ПоступлениеТоваровУслугВНТТ.Услуги КАК ТаблицаПоУслугам
	|ГДЕ
	|	ТаблицаПоУслугам.Ссылка = &ТекущийДокумент";

	Шапка              = Запрос.Выполнить().Выбрать();
	ВыборкаСтрокТовары = ЗапросПоТоварам.Выполнить().Выбрать();

	Шапка.Следующий();
	ДанныеДляПечати = Новый Структура();
	ДанныеДляПечати.Вставить("Организация",      Шапка.Организация);
	ДанныеДляПечати.Вставить("Номер",            Ссылка.НомерВходящегоДокумента);
	ДанныеДляПечати.Вставить("Дата",             Ссылка.ДатаВходящегоДокумента);
	ДанныеДляПечати.Вставить("Поставщик",        Шапка.Поставщик);
	ДанныеДляПечати.Вставить("Грузоотправитель", Шапка.Грузоотправитель);
	ДанныеДляПечати.Вставить("Покупатель",       Шапка.Покупатель);
	ДанныеДляПечати.Вставить("Грузополучатель",  Шапка.Грузополучатель);
	ДанныеДляПечати.Вставить("Сумма",            Шапка.Сумма);
	ДанныеДляПечати.Вставить("Валюта",           Шапка.Валюта);
	ДанныеДляПечати.Вставить("УчитыватьНДС",     Шапка.УчитыватьНДС);
	ДанныеДляПечати.Вставить("ФИОРуководителя",       );
	ДанныеДляПечати.Вставить("ФИОГлавногоБухгалтера", );

	Товары = ИнициализацияТаблицыСтрок();

	Пока ВыборкаСтрокТовары.Следующий() Цикл

		Строчка = Товары.Добавить();
		ЗаполнитьЗначенияСвойств(Строчка, ВыборкаСтрокТовары);

		Строчка.ТоварНаименование   = СокрЛП(ВыборкаСтрокТовары.ТоварНаименование) + ФормированиеПечатныхФормСервер.ПредставлениеСерий(ВыборкаСтрокТовары);
		Строчка.СуммаВключаетНДС	= Шапка.СуммаВключаетНДС;

	КонецЦикла;

	ДанныеДляПечати.Вставить("ТабличнаяЧасть", Товары);

	Возврат ДанныеДляПечати;

КонецФункции // СобратьДанныеПоПоступлениюТоваровУслугВНТТ()

// Функция собирает данные по документу основанию Поступление Доп. расходов и
// структуру с данными
// 
Функция СобратьДанныеДопРасходам(ДокОснование, Ссылка)

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументОснование", ДокОснование);

	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВложенныйЗапрос.Ссылка,
	|	СУММА(ВложенныйЗапрос.СуммаДенег) КАК СуммаДенег,
	|	СУММА(ВложенныйЗапрос.СуммаНДС) КАК СуммаНДС
	|ПОМЕСТИТЬ ВременнаяТаблицаСумм
	|ИЗ
	|	(ВЫБРАТЬ
	|		ПоступлениеДопРасходовТовары.Ссылка КАК Ссылка,
	|		ЕСТЬNULL(СУММА(ПоступлениеДопРасходовТовары.Сумма), 0) КАК СуммаДенег,
	|		ЕСТЬNULL(СУММА(ПоступлениеДопРасходовТовары.СуммаНДС), 0) КАК СуммаНДС
	|	ИЗ
	|		Документ.ПоступлениеДопРасходов.Товары КАК ПоступлениеДопРасходовТовары
	|	ГДЕ
	|		ПоступлениеДопРасходовТовары.Ссылка = &ДокументОснование
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ПоступлениеДопРасходовТовары.Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ПоступлениеДопРасходовОборудование.Ссылка,
	|		ЕСТЬNULL(СУММА(ПоступлениеДопРасходовОборудование.Сумма), 0),
	|		ЕСТЬNULL(СУММА(ПоступлениеДопРасходовОборудование.СуммаНДС), 0)
	|	ИЗ
	|		Документ.ПоступлениеДопРасходов.Оборудование КАК ПоступлениеДопРасходовОборудование
	|	ГДЕ
	|		ПоступлениеДопРасходовОборудование.Ссылка = &ДокументОснование
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ПоступлениеДопРасходовОборудование.Ссылка) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоступлениеДопРасходов.Организация,
	|	ПоступлениеДопРасходов.Организация КАК Покупатель,
	|	ПоступлениеДопРасходов.Подразделение,
	|	ПоступлениеДопРасходов.Контрагент КАК Поставщик,
	|	ПоступлениеДопРасходов.Содержание КАК СтатьяЗатрат,
	|	ПоступлениеДопРасходов.ВалютаДокумента КАК Валюта,
	|	ПоступлениеДопРасходов.СуммаДокумента КАК СуммаДокумента,
	|	ВременнаяТаблицаСумм.СуммаНДС + ПоступлениеДопРасходов.СуммаНДС КАК СуммаНДС,
	|	ПоступлениеДопРасходов.СтавкаНДС,
	|	ПоступлениеДопРасходов.УчитыватьНДС КАК УчитыватьНДС,
	|	ПоступлениеДопРасходов.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	ВременнаяТаблицаСумм.СуммаДенег
	|ИЗ
	|	Документ.ПоступлениеДопРасходов КАК ПоступлениеДопРасходов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаСумм КАК ВременнаяТаблицаСумм
	|		ПО ПоступлениеДопРасходов.Ссылка = ВременнаяТаблицаСумм.Ссылка
	|ГДЕ
	|	ПоступлениеДопРасходов.Ссылка = &ДокументОснование";

	Шапка = Запрос.Выполнить().Выбрать();

	Шапка.Следующий();
	ДанныеДляПечати = Новый Структура();
	ДанныеДляПечати.Вставить("Организация",      Шапка.Организация);
	ДанныеДляПечати.Вставить("Номер",            Ссылка.НомерВходящегоДокумента);
	ДанныеДляПечати.Вставить("Дата",             Ссылка.ДатаВходящегоДокумента);
	ДанныеДляПечати.Вставить("Поставщик",        Шапка.Поставщик);
	ДанныеДляПечати.Вставить("Грузоотправитель", );
	ДанныеДляПечати.Вставить("Подразделение",    Шапка.Подразделение);
	ДанныеДляПечати.Вставить("Покупатель",       Шапка.Покупатель);
	ДанныеДляПечати.Вставить("Грузополучатель",  );
	ДанныеДляПечати.Вставить("Сумма",            0);
	ДанныеДляПечати.Вставить("Валюта",           Шапка.Валюта);
	ДанныеДляПечати.Вставить("УчитыватьНДС",     Шапка.УчитыватьНДС);
	ДанныеДляПечати.Вставить("ФИОРуководителя",       );
	ДанныеДляПечати.Вставить("ФИОГлавногоБухгалтера", );

	Товары = ИнициализацияТаблицыСтрок();

	Строчка = Товары.Добавить();
	Строчка.Товар               = Шапка.СтатьяЗатрат;
	Строчка.ТоварНаименование   = Шапка.СтатьяЗатрат;
	Строчка.СтранаПроисхождения = "";
	Строчка.ПредставлениеСтраны = "";
	Строчка.СтранаПроисхожденияКод = "";
	Строчка.НомерГТД            = "";
	Строчка.ПредставлениеГТД    = "";
	Строчка.Количество          = 1;
	Строчка.ЕдиницаИзмерения    = "";
	Строчка.ЕдиницаИзмеренияКод = "";
	Строчка.СуммаВключаетНДС	= Шапка.СуммаВключаетНДС;
	Строчка.СтавкаНДС 			= Шапка.СтавкаНДС;
	Строчка.СуммаНДС  			= Шапка.СуммаНДС;
	Строчка.Сумма    			= Шапка.СуммаДокумента - ?(Шапка.СуммаВключаетНДС, 0, Шапка.СуммаНДС);
	Строчка.Цена     			= Строчка.Сумма;

	ДанныеДляПечати.Вставить("ТабличнаяЧасть", Товары);

	Возврат ДанныеДляПечати;

КонецФункции // СобратьДанныеДопРасходам()

// Функция собирает данные по документу основанию Поступление и 
// структуру с данными
// 
Функция СобратьДанныеПоПоступлениюНМА(ДокОснование, Ссылка)

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументОснование", ДокОснование);

	Запрос.Текст =
	"ВЫБРАТЬ
	|	Организация,
	|	Организация           КАК Покупатель,
	|	Организация           КАК Грузополучатель,
	|	"""" 				  КАК Подразделение,
	|	Контрагент            КАК Поставщик,
	|	Контрагент            КАК Грузоотправитель,
	|	СуммаДокумента        КАК Сумма,
	|	ВалютаДокумента       КАК Валюта,
	|	УчитыватьНДС          КАК УчитыватьНДС,
	|	СуммаВключаетНДС      КАК СуммаВключаетНДС
	|ИЗ
	|	Документ.ПоступлениеНМА КАК ПоступлениеНМА
	|
	|ГДЕ
	|	ПоступлениеНМА.Ссылка = &ДокументОснование";

	ЗапросПоОбъектамНМА = Новый Запрос();
	ЗапросПоОбъектамНМА.УстановитьПараметр("Курс", ДокОснование.КурсВзаиморасчетов);
	ЗапросПоОбъектамНМА.УстановитьПараметр("Кратность", ДокОснование.КратностьВзаиморасчетов);
	ЗапросПоОбъектамНМА.УстановитьПараметр("ТекущийДокумент", ДокОснование);
	ЗапросПоОбъектамНМА.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаПоНМА.НомерСтроки КАК НомерСтроки,
	|	ТаблицаПоНМА.НематериальныйАктив КАК НМА,
	|	0 КАК Количество,
	|	ТаблицаПоНМА.Сумма КАК Цена,
	|	ТаблицаПоНМА.Сумма КАК Сумма,
	|	ТаблицаПоНМА.СуммаНДС КАК СуммаНДС,
	|	ТаблицаПоНМА.СтавкаНДС КАК СтавкаНДС
	|ИЗ
	|	Документ.ПоступлениеНМА.НематериальныеАктивы КАК ТаблицаПоНМА
	|ГДЕ
	|	ТаблицаПоНМА.Ссылка = &ТекущийДокумент";

	Шапка              = Запрос.Выполнить().Выбрать();
	ВыборкаСтрокТовары = ЗапросПоОбъектамНМА.Выполнить().Выбрать();

	Шапка.Следующий();
	ДанныеДляПечати = Новый Структура();
	ДанныеДляПечати.Вставить("Организация",      Шапка.Организация);
	ДанныеДляПечати.Вставить("Номер",            Ссылка.НомерВходящегоДокумента);
	ДанныеДляПечати.Вставить("Дата",             Ссылка.ДатаВходящегоДокумента);
	ДанныеДляПечати.Вставить("Поставщик",        Шапка.Поставщик);
	ДанныеДляПечати.Вставить("Грузоотправитель", Шапка.Грузоотправитель);
	ДанныеДляПечати.Вставить("Подразделение",    Шапка.Подразделение);
	ДанныеДляПечати.Вставить("Покупатель",       Шапка.Покупатель);
	ДанныеДляПечати.Вставить("Грузополучатель",  Шапка.Грузополучатель);
	ДанныеДляПечати.Вставить("Сумма",            Шапка.Сумма);
	ДанныеДляПечати.Вставить("Валюта",           Шапка.Валюта);
	ДанныеДляПечати.Вставить("УчитыватьНДС",     Шапка.УчитыватьНДС);
	ДанныеДляПечати.Вставить("ФИОРуководителя",       );
	ДанныеДляПечати.Вставить("ФИОГлавногоБухгалтера", );

	Товары = ИнициализацияТаблицыСтрок();

	Пока ВыборкаСтрокТовары.Следующий() Цикл

		Строчка = Товары.Добавить();
		Строчка.Товар               = ВыборкаСтрокТовары.НМА;
		Строчка.ТоварНаименование   = ВыборкаСтрокТовары.НМА;
		Строчка.СтранаПроисхождения = "Россия";
		Строчка.ПредставлениеСтраны = "Россия";
		Строчка.СтранаПроисхожденияКод = "";
		Строчка.НомерГТД            = "--";
		Строчка.ПредставлениеГТД    = "--";
		Строчка.Количество          = ВыборкаСтрокТовары.Количество;
		Строчка.ЕдиницаИзмерения    = "--";
		Строчка.ЕдиницаИзмеренияКод = "";
		Строчка.СуммаВключаетНДС	= Шапка.СуммаВключаетНДС;
		Строчка.Цена      = ВыборкаСтрокТовары.Цена;
		Строчка.Сумма     = ВыборкаСтрокТовары.Сумма;
		Строчка.СуммаНДС  = ВыборкаСтрокТовары.СуммаНДС;
		Строчка.СтавкаНДС = ВыборкаСтрокТовары.СтавкаНДС;

	КонецЦикла;

	ДанныеДляПечати.Вставить("ТабличнаяЧасть", Товары);

	Возврат ДанныеДляПечати;

КонецФункции // СобратьДанныеПоПоступлениюНМА()

// Функция собирает данные по документу основанию ПолучениеУслугПоПереработке
// структуру с данными
// 
Функция СобратьДанныеПолучениеУслугПоПереработке(ДокОснование, Ссылка)

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументОснование", ДокОснование);

	Запрос.Текст = 	
	"ВЫБРАТЬ
	|	ПолучениеУслугПоПереработке.Организация,
	|	ПолучениеУслугПоПереработке.Организация КАК Покупатель,
	|	ПолучениеУслугПоПереработке.Подразделение,
	|	ПолучениеУслугПоПереработке.Контрагент КАК Поставщик,
	|	ПолучениеУслугПоПереработке.СуммаДокумента КАК Сумма,
	|	ПолучениеУслугПоПереработке.ВалютаДокумента КАК Валюта,
	|	ПолучениеУслугПоПереработке.УчитыватьНДС КАК УчитыватьНДС,
	|	ПолучениеУслугПоПереработке.СуммаВключаетНДС КАК СуммаВключаетНДС
	|ИЗ
	|	Документ.ПолучениеУслугПоПереработке КАК ПолучениеУслугПоПереработке
	|ГДЕ
	|	ПолучениеУслугПоПереработке.Ссылка = &ДокументОснование";

	Шапка = Запрос.Выполнить().Выбрать();
	Шапка.Следующий();
	
	ЗапросПоТоварам = Новый Запрос;
	ЗапросПоТоварам.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ЗапросПоТоварам.УстановитьПараметр("ДокументОснование", ДокОснование);
	ЗапросПоТоварам.Текст =
	"ВЫБРАТЬ
	|	ПолучениеУслугПоПереработке.СтавкаНДС КАК СтавкаНДС,
	|	ПолучениеУслугПоПереработке.СуммаНДС КАК СуммаНДС,
	|	ПолучениеУслугПоПереработке.Сумма КАК Сумма,
	|	ПолучениеУслугПоПереработке.Цена КАК Цена,
	|	ПолучениеУслугПоПереработке.Номенклатура КАК Номенклатура,
	|	ПолучениеУслугПоПереработке.Номенклатура.Представление КАК НоменклатураПредставление,
	|	ПолучениеУслугПоПереработке.Количество КАК Количество,
	|	ПолучениеУслугПоПереработке.Номенклатура.ЕдиницаХраненияОстатков.Представление КАК ЕдиницаИзмерения,
	|	ПолучениеУслугПоПереработке.Номенклатура.ЕдиницаХраненияОстатков.ЕдиницаПоКлассификатору.Код КАК ЕдиницаИзмеренияКод
	|ПОМЕСТИТЬ ВТ_ПолучениеУслугПоПереработке
	|ИЗ
	|	Документ.ПолучениеУслугПоПереработке.Услуги КАК ПолучениеУслугПоПереработке
	|ГДЕ
	|	ПолучениеУслугПоПереработке.Ссылка = &ДокументОснование";
	
	Если ОбщегоНазначения.ЕстьРеквизитТабЧастиДокумента("СтавкаНДС", ДокОснование.Метаданные(), "Товары") Тогда
		ЗапросПоТоварам.Текст = ЗапросПоТоварам.Текст + "
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|" +
		"	
		|ВЫБРАТЬ
		|	ПолучениеУслугПоПереработке.СтавкаНДС,
		|	ПолучениеУслугПоПереработке.СуммаНДС,
		|	ПолучениеУслугПоПереработке.Сумма,
		|	ПолучениеУслугПоПереработке.Цена,
		|	ПолучениеУслугПоПереработке.Номенклатура,
		|	ПолучениеУслугПоПереработке.Номенклатура.Представление,
		|	ПолучениеУслугПоПереработке.Количество,
		|	ПолучениеУслугПоПереработке.ЕдиницаИзмерения,
		|	ПолучениеУслугПоПереработке.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код
		|ИЗ
		|	Документ.ПолучениеУслугПоПереработке.Товары КАК ПолучениеУслугПоПереработке
		|ГДЕ
		|	ПолучениеУслугПоПереработке.Ссылка = &ДокументОснование";
	КонецЕсли;
	
	ЗапросПоТоварам.Выполнить();
	ЗапросПоТоварам.Текст = 
	"ВЫБРАТЬ
	|	ПолучениеУслугПоПереработке.Номенклатура КАК Товар,
	|	ПолучениеУслугПоПереработке.НоменклатураПредставление КАК ТоварНаименование,
	|	ПолучениеУслугПоПереработке.ЕдиницаИзмерения,
	|	ПолучениеУслугПоПереработке.ЕдиницаИзмеренияКод,
	|	ПолучениеУслугПоПереработке.СтавкаНДС КАК СтавкаНДС,
	|	СУММА(ПолучениеУслугПоПереработке.СуммаНДС) КАК СуммаНДС,
	|	СУММА(ПолучениеУслугПоПереработке.Сумма) КАК Сумма,
	|	СУММА(ПолучениеУслугПоПереработке.Количество) КАК Количество,
	|	СУММА(ПолучениеУслугПоПереработке.Цена) КАК Цена
	|ИЗ
	|	ВТ_ПолучениеУслугПоПереработке КАК ПолучениеУслугПоПереработке
	|
	|СГРУППИРОВАТЬ ПО
	|	ПолучениеУслугПоПереработке.СтавкаНДС,
	|	ПолучениеУслугПоПереработке.Номенклатура,
	|	ПолучениеУслугПоПереработке.НоменклатураПредставление,
	|	ПолучениеУслугПоПереработке.ЕдиницаИзмерения,
	|	ПолучениеУслугПоПереработке.ЕдиницаИзмеренияКод";
	
	ВыборкаТовары = ЗапросПоТоварам.Выполнить().Выбрать();

	ДанныеДляПечати = Новый Структура();
	ДанныеДляПечати.Вставить("Организация",      Шапка.Организация);
	ДанныеДляПечати.Вставить("Номер",            Ссылка.НомерВходящегоДокумента);
	ДанныеДляПечати.Вставить("Дата",             Ссылка.ДатаВходящегоДокумента);
	ДанныеДляПечати.Вставить("Поставщик",        Шапка.Поставщик);
	ДанныеДляПечати.Вставить("Грузоотправитель", );
	ДанныеДляПечати.Вставить("Подразделение",    Шапка.Подразделение);
	ДанныеДляПечати.Вставить("Покупатель",       Шапка.Покупатель);
	ДанныеДляПечати.Вставить("Грузополучатель",  );
	ДанныеДляПечати.Вставить("Сумма",            0);
	ДанныеДляПечати.Вставить("Валюта",           Шапка.Валюта);
	ДанныеДляПечати.Вставить("УчитыватьНДС",     Шапка.УчитыватьНДС);
	ДанныеДляПечати.Вставить("ФИОРуководителя",       );
	ДанныеДляПечати.Вставить("ФИОГлавногоБухгалтера", );
	
	Товары = ИнициализацияТаблицыСтрок();

	Пока ВыборкаТовары.Следующий() Цикл
		
		Строчка = Товары.Добавить();
		ЗаполнитьЗначенияСвойств(Строчка, ВыборкаТовары);
		Строчка.СуммаВключаетНДС = Шапка.СуммаВключаетНДС;
		
	КонецЦикла;

	ДанныеДляПечати.Вставить("ТабличнаяЧасть", Товары);

	Возврат ДанныеДляПечати;

КонецФункции // СобратьДанныеПолучениеУслугПоПереработке()

// Функция собирает данные по документу основанию ОтражениеПоступленияТоваровИУслугНДС и 
// структуру с данными
// 
Функция СобратьДанныеПоОтражениеПоступленияТоваровУслугНДС(ДокОснование, Ссылка)
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументОснование", ДокОснование);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Организация,
	|	Организация           КАК Покупатель,
	|	Организация           КАК Грузополучатель,
	|	Контрагент            КАК Поставщик,
	|	Контрагент            КАК Грузоотправитель,
	|	СуммаДокумента        КАК Сумма,
	|	ВалютаДокумента       КАК Валюта,
	|	ИСТИНА		          КАК УчитыватьНДС,
	|	ЛОЖЬ			      КАК СуммаВключаетНДС
	|ИЗ
	|	Документ.ОтражениеПоступленияТоваровИУслугНДС КАК ОтражениеПоступленияТоваровИУслугНДС
	|
	|ГДЕ
	|	ОтражениеПоступленияТоваровИУслугНДС.Ссылка = &ДокументОснование";
	
	ЗапросПоТоварам = Новый Запрос();
	ЗапросПоТоварам.УстановитьПараметр("ТекущийДокумент", Ссылка.ДокументОснование);
	ЗапросПоТоварам.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаПоТоварамИУслугам.НомерСтроки КАК НомерСтроки,
	|	ТаблицаПоТоварамИУслугам.Номенклатура КАК Товар,
	|	ВЫРАЗИТЬ(ТаблицаПоТоварамИУслугам.Номенклатура.НаименованиеПолное КАК СТРОКА(1000)) КАК ТоварНаименование,
	|	ТаблицаПоТоварамИУслугам.Номенклатура.ЕдиницаХраненияОстатков КАК ЕдиницаИзмерения,
	|	ТаблицаПоТоварамИУслугам.Номенклатура.ЕдиницаХраненияОстатков.ЕдиницаПоКлассификатору.Код КАК ЕдиницаИзмеренияКод,
	|	0 КАК Количество,
	|	ТаблицаПоТоварамИУслугам.Сумма КАК Цена,
	|	ТаблицаПоТоварамИУслугам.Сумма КАК Сумма,
	|	ТаблицаПоТоварамИУслугам.СуммаНДС КАК СуммаНДС,
	|	ТаблицаПоТоварамИУслугам.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаПоТоварамИУслугам.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ТаблицаПоТоварамИУслугам.СтранаПроисхождения.Код КАК СтранаПроисхожденияКод,
	|	ТаблицаПоТоварамИУслугам.НомерГТД
	|ИЗ
	|	Документ.ОтражениеПоступленияТоваровИУслугНДС.ТоварыИУслуги КАК ТаблицаПоТоварамИУслугам
	|ГДЕ
	|	ТаблицаПоТоварамИУслугам.Ссылка = &ТекущийДокумент";
	
	Шапка              = Запрос.Выполнить().Выбрать();
	ВыборкаСтрокТовары = ЗапросПоТоварам.Выполнить().Выбрать();

	Шапка.Следующий();
	ДанныеДляПечати = Новый Структура();
	ДанныеДляПечати.Вставить("Организация",      Шапка.Организация);
	ДанныеДляПечати.Вставить("Номер",            Ссылка.НомерВходящегоДокумента);
	ДанныеДляПечати.Вставить("Дата",             Ссылка.ДатаВходящегоДокумента);
	ДанныеДляПечати.Вставить("Поставщик",        Шапка.Поставщик);
	ДанныеДляПечати.Вставить("Грузоотправитель", Шапка.Грузоотправитель);
	ДанныеДляПечати.Вставить("Покупатель",       Шапка.Покупатель);
	ДанныеДляПечати.Вставить("Грузополучатель",  Шапка.Грузополучатель);
	ДанныеДляПечати.Вставить("Сумма",            Шапка.Сумма);
	ДанныеДляПечати.Вставить("Валюта",           Шапка.Валюта);
	ДанныеДляПечати.Вставить("УчитыватьНДС",     Шапка.УчитыватьНДС);
	ДанныеДляПечати.Вставить("ФИОРуководителя",       );
	ДанныеДляПечати.Вставить("ФИОГлавногоБухгалтера", );

	Товары = ИнициализацияТаблицыСтрок();

	Пока ВыборкаСтрокТовары.Следующий() Цикл

		Строчка = Товары.Добавить();

		ЗаполнитьЗначенияСвойств(Строчка, ВыборкаСтрокТовары);
		Строчка.СуммаВключаетНДС	= Шапка.СуммаВключаетНДС;
		
	КонецЦикла;

	ДанныеДляПечати.Вставить("ТабличнаяЧасть", Товары);

	Возврат ДанныеДляПечати;
	
КонецФункции

// Функция собирает данные по документу основанию АвансовыйОтчет и 
// структуру с данными
// 
Функция СобратьДанныеПоАвансовыйОтчет(ДокОснование, Ссылка)
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументОснование", ДокОснование);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	АвансовыйОтчет.Организация,
	|	АвансовыйОтчет.Организация КАК Покупатель,
	|	АвансовыйОтчет.Организация КАК Грузополучатель,
	|	ВложенныйЗапрос.Контрагент КАК Поставщик,
	|	ВложенныйЗапрос.Контрагент КАК Грузоотправитель,
	|	АвансовыйОтчет.ВалютаДокумента КАК Валюта,
	|	АвансовыйОтчет.УчитыватьНДС КАК УчитыватьНДС,
	|	АвансовыйОтчет.СуммаВключаетНДС КАК СуммаВключаетНДС
	|ИЗ
	|	Документ.АвансовыйОтчет КАК АвансовыйОтчет,
	|	(ВЫБРАТЬ
	|		Товары.Поставщик КАК Контрагент
	|	ИЗ
	|		Документ.АвансовыйОтчет.Товары КАК Товары
	|	ГДЕ
	|		Товары.СчетФактура = &Ссылка
	|		И Товары.Ссылка = &ДокументОснование
	|		И Товары.ПредъявленСФ
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Товары.Поставщик
	|	ИЗ
	|		Документ.АвансовыйОтчет.Прочее КАК Товары
	|	ГДЕ
	|		Товары.СчетФактура = &Ссылка
	|		И Товары.Ссылка = &ДокументОснование) КАК ВложенныйЗапрос
	|ГДЕ
	|	АвансовыйОтчет.Ссылка = &ДокументОснование";
	
	ЗапросПоТоварам = Новый Запрос();
	ЗапросПоТоварам.УстановитьПараметр("ДокументОснование", ДокОснование);
	ЗапросПоТоварам.УстановитьПараметр("Ссылка", Ссылка);
	ЗапросПоТоварам.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаПоТоварам.НомерСтроки,
	|	ТаблицаПоТоварам.Номенклатура КАК Товар,
	|	ТаблицаПоТоварам.Номенклатура.НаименованиеПолное КАК ТоварНаименование,
	|	ТаблицаПоТоварам.СерияНоменклатуры.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ТаблицаПоТоварам.СерияНоменклатуры.СтранаПроисхождения.Наименование КАК ПредставлениеСтраны,
	|	ТаблицаПоТоварам.СерияНоменклатуры.СтранаПроисхождения.Код КАК СтранаПроисхожденияКод,
	|	ТаблицаПоТоварам.СерияНоменклатуры.НомерГТД КАК НомерГТД,
	|	ТаблицаПоТоварам.СерияНоменклатуры.НомерГТД.Представление КАК ПредставлениеГТД,
	|	ТаблицаПоТоварам.ЕдиницаИзмерения.Представление КАК ЕдиницаИзмерения,
	|	ТаблицаПоТоварам.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код КАК ЕдиницаИзмеренияКод,
	|	ТаблицаПоТоварам.Количество,
	|	ТаблицаПоТоварам.Цена,
	|	ТаблицаПоТоварам.Сумма,
	|	ТаблицаПоТоварам.СуммаНДС,
	|	ТаблицаПоТоварам.СтавкаНДС,
	|	1 КАК ID
	|ИЗ
	|	Документ.АвансовыйОтчет.Товары КАК ТаблицаПоТоварам
	|ГДЕ
	|	ТаблицаПоТоварам.Ссылка = &ДокументОснование
	|	И ТаблицаПоТоварам.СчетФактура = &Ссылка
	|	И ТаблицаПоТоварам.ПредъявленСФ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаПоПрочее.НомерСтроки,
	|	ТаблицаПоПрочее.Номенклатура,
	|	ТаблицаПоПрочее.Содержание,
	|	""Россия"",
	|	""Россия"",
	|	"""",
	|	""--"",
	|	""--"",
	|	""--"",
	|	"""",
	|	0,
	|	0,
	|	ТаблицаПоПрочее.Сумма,
	|	ТаблицаПоПрочее.СуммаНДС,
	|	ТаблицаПоПрочее.СтавкаНДС,
	|	2
	|ИЗ
	|	Документ.АвансовыйОтчет.Прочее КАК ТаблицаПоПрочее
	|ГДЕ
	|	ТаблицаПоПрочее.Ссылка = &ДокументОснование
	|	И ТаблицаПоПрочее.СчетФактура = &Ссылка
	|	И ТаблицаПоПрочее.ПредъявленСФ";
	
	Шапка              = Запрос.Выполнить().Выбрать();
	ВыборкаСтрокТовары = ЗапросПоТоварам.Выполнить().Выбрать();

	Шапка.Следующий();
	ДанныеДляПечати = Новый Структура();
	ДанныеДляПечати.Вставить("Организация",      Шапка.Организация);
	ДанныеДляПечати.Вставить("Номер",            Ссылка.НомерВходящегоДокумента);
	ДанныеДляПечати.Вставить("Дата",             Ссылка.ДатаВходящегоДокумента);
	ДанныеДляПечати.Вставить("Поставщик",        Шапка.Поставщик);
	ДанныеДляПечати.Вставить("Грузоотправитель", Шапка.Грузоотправитель);
	ДанныеДляПечати.Вставить("Покупатель",       Шапка.Покупатель);
	ДанныеДляПечати.Вставить("Грузополучатель",  Шапка.Грузополучатель);
	ДанныеДляПечати.Вставить("Валюта",           Шапка.Валюта);
	ДанныеДляПечати.Вставить("УчитыватьНДС",     Шапка.УчитыватьНДС);
	ДанныеДляПечати.Вставить("ФИОРуководителя",       );
	ДанныеДляПечати.Вставить("ФИОГлавногоБухгалтера", );
	
	Товары = ИнициализацияТаблицыСтрок();
	
	Пока ВыборкаСтрокТовары.Следующий() Цикл

		Строчка = Товары.Добавить();

		ЗаполнитьЗначенияСвойств(Строчка, ВыборкаСтрокТовары);
		Строчка.СуммаВключаетНДС = Шапка.СуммаВключаетНДС;

	КонецЦикла;
	
	ДанныеДляПечати.Вставить("ТабличнаяЧасть", Товары);

	Возврат ДанныеДляПечати;
	
КонецФункции

// Обработчик обновления
//
// Устанавливает новый код вида операции для сводных счетов-фактур по комиссии
Процедура УстановитьКодВидаОперацииСводныйКомиссионный() Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СчетФактураПолученный.Ссылка КАК Ссылка,
	|	""27"" КАК КодВидаОперации,
	|	ВЫБОР
	|		КОГДА СчетФактураПолученный.КодВидаОперации = ""27""
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КодУстановлен
	|ИЗ
	|	Документ.СчетФактураПолученный КАК СчетФактураПолученный
	|ГДЕ
	|	(СчетФактураПолученный.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыПолученного.НаПоступление)
	|			ИЛИ СчетФактураПолученный.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыПолученного.Корректировочный))
	|	И СчетФактураПолученный.ПометкаУдаления = ЛОЖЬ
	|	И СчетФактураПолученный.СводныйКомиссионный = ИСТИНА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СчетФактураПолученный.Ссылка,
	|	""28"",
	|	ВЫБОР
	|		КОГДА СчетФактураПолученный.КодВидаОперации = ""28""
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ
	|ИЗ
	|	Документ.СчетФактураПолученный КАК СчетФактураПолученный
	|ГДЕ
	|	СчетФактураПолученный.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыПолученного.НаАванс)
	|	И СчетФактураПолученный.ПометкаУдаления = ЛОЖЬ
	|	И СчетФактураПолученный.СводныйКомиссионный = ИСТИНА
	|ИТОГИ
	|	СУММА(КодУстановлен)
	|ПО
	|	ОБЩИЕ";
	
	ВыборкаИтоги = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Если ВыборкаИтоги.Следующий()
		И ВыборкаИтоги.КодУстановлен = 0 Тогда
		ВыборкаДокументы = ВыборкаИтоги.Выбрать();
		Пока ВыборкаДокументы.Следующий() Цикл
			СчетФактураДокумент = ВыборкаДокументы.Ссылка.ПолучитьОбъект();
			СчетФактураДокумент.КодВидаОПерации = ВыборкаДокументы.КодВидаОперации;
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(СчетФактураДокумент);
		КонецЦикла;
	Иначе
		Возврат;
	КонецЕсли;
		
КонецПроцедуры

// Обработчик обновления. Заполняет новый реквизит "КодВидаОперацииНаУменьшение" 
// для корректировочных счетов-фактур дата которых больше или равна 1 января 2015 года
Процедура УстановитьКодВидаОперацииНаУменьшение() Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СчетФактураПолученный.Ссылка
	|ИЗ
	|	Документ.СчетФактураПолученный КАК СчетФактураПолученный
	|ГДЕ
	|	НЕ СчетФактураПолученный.ПометкаУдаления
	|	И СчетФактураПолученный.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыПолученного.Корректировочный)
	|	И СчетФактураПолученный.Дата >= ДАТАВРЕМЯ(2015, 1, 1)
	|	И СчетФактураПолученный.КодВидаОперацииНаУменьшение = """"";
	
	Результат = Запрос.Выполнить();
	
	Если НЕ Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			ОбрабатываемыйСчетФактура = Выборка.Ссылка;
			СчетФактураДокумент = ОбрабатываемыйСчетФактура.ПолучитьОбъект();
			
			СчетФактураДокумент.КодВидаОперацииНаУменьшение = "18";
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(СчетФактураДокумент);
			
		КонецЦикла;
		
	КонецЕсли;
		
КонецПроцедуры
Перем мУдалятьДвижения;

Перем мВалютаРегламентированногоУчета Экспорт;

Перем мОбновлятьРеквизитыПриЗаписи Экспорт;

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОКУМЕНТА

// Процедура для СФ определяет его сумму, валюту и контрагента
// на основе анализа данных документов оснований
//
Процедура ОпределениеПараметровСчетаФактуры(ЗаполнятьРеквизитыСчетаФактуры = Ложь) Экспорт
	
	Если ДокументыОснования.Количество()=0 
		ИЛИ НЕ ЗначениеЗаполнено(ДокументыОснования[0].ДокументОснование) тогда
		// Основание не выбрано, параметры не определяем
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ОтчетКомитентуОПродажах") Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыСФ = Неопределено;
	
	УчетНДС.ПолучитьПараметрыСчетаФактуры(ЭтотОбъект, мВалютаРегламентированногоУчета, ПараметрыСФ);
	
	ВерсияПостановления = УчетНДС.ПолучитьВерсиюПостановления(Дата);
	РеквизитыОбновлены  = Ложь;
	
	Если ПараметрыСФ.Организация <> Неопределено 
		И ПараметрыСФ.Организация <> Организация Тогда
		Номер = "";
		Организация = ПараметрыСФ.Организация;
		РеквизитыОбновлены  = Истина;
	КонецЕсли; 			
	Если ПараметрыСФ.Контрагент <> Неопределено 
		И ПараметрыСФ.Контрагент <> Контрагент Тогда
		Контрагент = ПараметрыСФ.Контрагент;
		КППКонтрагента = "";
		РеквизитыОбновлены  = Истина;
	КонецЕсли; 			
	Если ПараметрыСФ.Договор <> Неопределено 
		И ПараметрыСФ.Договор <> ДоговорКонтрагента Тогда
		/////Вадим 25.02.2014 16:18:39  бп 
		Если не ЭтоДокОснованиеНаМножественнуюСФ(ДокументОснование) тогда
			ДоговорКонтрагента = ПараметрыСФ.Договор;
			РеквизитыОбновлены  = Истина;
		Конецесли;
		////ВадимКонец
		
	КонецЕсли; 			
	Если СуммаДокумента <> ПараметрыСФ.СуммаДокумента Тогда
		/////Вадим 25.02.2014 16:18:39  бп 
		Если не ЭтоДокОснованиеНаМножественнуюСФ(ДокументОснование) тогда
			СуммаДокумента = ПараметрыСФ.СуммаДокумента;
			РеквизитыОбновлены  = Истина;

		Конецесли;	

	КонецЕсли;
	Если ВерсияПостановления = 2 Тогда
		Если СуммаНДСДокумента <> ПараметрыСФ.СуммаНДСДокумента Тогда
			/////Вадим 25.02.2014 16:18:39  бп 
			Если не ЭтоДокОснованиеНаМножественнуюСФ(ДокументОснование) тогда
				СуммаНДСДокумента = ПараметрыСФ.СуммаНДСДокумента;
				РеквизитыОбновлены  = Истина;

			Конецесли;	

		КонецЕсли; 
	КонецЕсли; 
	Если СуммаУвеличение <> ПараметрыСФ.СуммаУвеличение Тогда
		СуммаУвеличение = ПараметрыСФ.СуммаУвеличение;
		РеквизитыОбновлены  = Истина;
	КонецЕсли;
	Если СуммаУменьшение <> ПараметрыСФ.СуммаУменьшение Тогда
		СуммаУменьшение = ПараметрыСФ.СуммаУменьшение;
		РеквизитыОбновлены  = Истина;
	КонецЕсли;
	Если СуммаНДСУвеличение <> ПараметрыСФ.СуммаНДСУвеличение Тогда
		СуммаНДСУвеличение = ПараметрыСФ.СуммаНДСУвеличение;
		РеквизитыОбновлены  = Истина;
	КонецЕсли;
	Если СуммаНДСУменьшение <> ПараметрыСФ.СуммаНДСУменьшение Тогда
		СуммаНДСУменьшение = ПараметрыСФ.СуммаНДСУменьшение;
		РеквизитыОбновлены  = Истина;
	КонецЕсли; 			
	
	Если Дата >= '20150101'
		И НЕ ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.КорректировкаПоступления") Тогда
		Если СуммаДокументаКомиссия <> ПараметрыСФ.СуммаДокументаКомиссия Тогда
			СуммаДокументаКомиссия = ПараметрыСФ.СуммаДокументаКомиссия;
			РеквизитыОбновлены = Истина;
		КонецЕсли;
		Если СуммаНДСДокументаКомиссия <> ПараметрыСФ.СуммаНДСДокументаКомиссия Тогда
			СуммаНДСДокументаКомиссия = ПараметрыСФ.СуммаНДСДокументаКомиссия;
			РеквизитыОбновлены = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если ПараметрыСФ.ВалютаДокумента <> Неопределено 
		И ВалютаДокумента <> ПараметрыСФ.ВалютаДокумента Тогда
		ВалютаДокумента = ПараметрыСФ.ВалютаДокумента;
		РеквизитыОбновлены  = Истина;
	КонецЕсли;
	
	Если ПараметрыСФ.БланкСтрогойОтчетности <> Неопределено 
		И БланкСтрогойОтчетности <> ПараметрыСФ.БланкСтрогойОтчетности Тогда
		БланкСтрогойОтчетности = ПараметрыСФ.БланкСтрогойОтчетности;
		РеквизитыОбновлены  = Истина;
	КонецЕсли;
	
	Если ВерсияПостановления = 2 Тогда
		Если СчетФактураБезНДС <> ПараметрыСФ.СчетФактураБезНДС Тогда
			СчетФактураБезНДС = ПараметрыСФ.СчетФактураБезНДС;
			РеквизитыОбновлены  = Истина;
		КонецЕсли; 			
	КонецЕсли; 
	
	Если ЭтоНовый() И ВерсияПостановления = 2 Тогда
		УстановитьКодВидаОперации();
	КонецЕсли;
	
	Если ТипЗнч(ДокументыОснования[0].ДокументОснование) = Тип("ДокументСсылка.КорректировкаПоступления") Тогда
		РезультатПоискаИсправляемыйСчетФактура = УчетНДС.НайтиПодчиненныйСчетФактуру(ДокументыОснования[0].ДокументОснование.ИсправляемыйДокументПоступления, "СчетФактураПолученный");
		Если РезультатПоискаИсправляемыйСчетФактура = Неопределено Тогда
			РезультатПоискаИсправляемыйСчетФактура = Документы.СчетФактураПолученный.ПустаяСсылка();
		КонецЕсли;
	Иначе
		РезультатПоискаИсправляемыйСчетФактура = Документы.СчетФактураПолученный.ПустаяСсылка();
	КонецЕсли;
	Если ТипЗнч(РезультатПоискаИсправляемыйСчетФактура) = Тип("ДокументСсылка.СчетФактураПолученный")
		И ИсправляемыйСчетФактура <> РезультатПоискаИсправляемыйСчетФактура Тогда
		ИсправляемыйСчетФактура = РезультатПоискаИсправляемыйСчетФактура;
		Если Исправление Тогда 
			НомерВходящегоДокумента = ИсправляемыйСчетФактура.НомерВходящегоДокумента;
			ДатаВходящегоДокумента	= ИсправляемыйСчетФактура.ДатаВходящегоДокумента;
		КонецЕсли;
		РеквизитыОбновлены  = Истина;
	КонецЕсли;
	
	Если (НЕ ЗначениеЗаполнено(ДокументОснование) И ДокументыОснования.Количество() > 0)
		ИЛИ (ДокументыОснования.Количество() = 1 И ДокументыОснования[0].ДокументОснование <> ДокументОснование) Тогда
		ДокументОснование = ДокументыОснования[0].ДокументОснование;
		РеквизитыОбновлены  = Истина;
	КонецЕсли; 
	
	Если ВидСчетаФактуры = Перечисления.ВидСчетаФактурыПолученного.Корректировочный 
		И ПараметрыСФ.РеквизитыОснований.Количество() > 0 Тогда
		
		Для Каждого Основание Из ДокументыОснования Цикл
			
			СтрокаСРеквизитами = ПараметрыСФ.РеквизитыОснований.Найти(Основание.ДокументОснование, "ДокументОснование");	
			Если Основание.СуммаУвеличение <> СтрокаСРеквизитами.СуммаУвеличение Тогда
				Основание.СуммаУвеличение = СтрокаСРеквизитами.СуммаУвеличение;
				РеквизитыОбновлены = Истина;
			КонецЕсли;
			Если Основание.СуммаУменьшение <> СтрокаСРеквизитами.СуммаУменьшение Тогда
				Основание.СуммаУменьшение = СтрокаСРеквизитами.СуммаУменьшение;
				РеквизитыОбновлены = Истина;
			КонецЕсли;
			Если Основание.СуммаНДСУвеличение <> СтрокаСРеквизитами.СуммаНДСУвеличение Тогда
				Основание.СуммаНДСУвеличение = СтрокаСРеквизитами.СуммаНДСУвеличение;
				РеквизитыОбновлены = Истина;
			КонецЕсли;
			Если Основание.СуммаНДСУменьшение <> СтрокаСРеквизитами.СуммаНДСУменьшение Тогда
				Основание.СуммаНДСУменьшение = СтрокаСРеквизитами.СуммаНДСУменьшение;
				РеквизитыОбновлены = Истина;
			КонецЕсли;
			Если ЗаполнятьРеквизитыСчетаФактуры Тогда
				Если Основание.НомерИсходногоДокумента <> СтрокаСРеквизитами.НомерИсходногоДокумента Тогда
					Основание.НомерИсходногоДокумента = СтрокаСРеквизитами.НомерИсходногоДокумента;
					РеквизитыОбновлены = Истина;
				КонецЕсли;
				Если Основание.ДатаИсходногоДокумента <> СтрокаСРеквизитами.ДатаИсходногоДокумента Тогда
					Основание.ДатаИсходногоДокумента = СтрокаСРеквизитами.ДатаИсходногоДокумента;
					РеквизитыОбновлены = Истина;
				КонецЕсли;
				Если Основание.УчитыватьИсправлениеИсходногоДокумента <> СтрокаСРеквизитами.УчитыватьИсправлениеИсходногоДокумента Тогда
					Основание.УчитыватьИсправлениеИсходногоДокумента = СтрокаСРеквизитами.УчитыватьИсправлениеИсходногоДокумента;
					РеквизитыОбновлены = Истина;
				КонецЕсли;
				Если Основание.НомерИсправленияИсходногоДокумента <> СтрокаСРеквизитами.НомерИсправленияИсходногоДокумента Тогда
					Основание.НомерИсправленияИсходногоДокумента = СтрокаСРеквизитами.НомерИсправленияИсходногоДокумента;
					РеквизитыОбновлены = Истина;
				КонецЕсли;
				Если Основание.ДатаИсправленияИсходногоДокумента <> СтрокаСРеквизитами.ДатаИсправленияИсходногоДокумента Тогда
					Основание.ДатаИсправленияИсходногоДокумента = СтрокаСРеквизитами.ДатаИсправленияИсходногоДокумента;
					РеквизитыОбновлены = Истина;
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
		Если ПараметрыСФ.РеквизитыОснований.Количество() > 1 Тогда
			СводныйКорректировочный = Истина;
		Иначе
			СводныйКорректировочный = Ложь;
		КонецЕсли; 
	Иначе
		СводныйКорректировочный = Ложь;
	КонецЕсли;	
	
	Если ПустаяСтрока(КППКонтрагента) И ДокументыОснования.Количество() > 0 Тогда
		ОснованиеДокумент = ДокументыОснования[0].ДокументОснование;
		Если НЕ ТипЗнч(ОснованиеДокумент) = Тип("ДокументСсылка.ОтчетКомиссионераОПродажах") Тогда
			КППКонтрагентаНовый = УчетНДС.ПолучитьКПППодразделенияКонтрагента(ОснованиеДокумент, "Грузоотправитель");
			Если НЕ ПустаяСтрока(КППКонтрагентаНовый) Тогда
				КППКонтрагента = КППКонтрагентаНовый;
				РеквизитыОбновлены	= Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если РеквизитыОбновлены 
		И ДополнительныеСвойства.Свойство("СообщитьОбИзмененииРеквизитов")
		И ДополнительныеСвойства.СообщитьОбИзмененииРеквизитов Тогда
		ОбщегоНазначения.СообщитьИнформациюПользователю(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Реквизиты документа ""%1"" перезаполнены автоматически'"),
			Ссылка));
	КонецЕсли;
	
КонецПроцедуры 

/////Вадим 26.02.2014 13:36:31  бп 
Функция ЭтоДокОснованиеНаМножественнуюСФ(ДокОснование)
	Если (ТипЗнч(ДокОснование)=тип("ДокументСсылка.ПоступлениеТоваровУслуг")) 	 тогда
		Если ДокОснование.УЗ_НесколькоСФ тогда
			возврат Истина;
		Конецесли;				
	Конецесли;
	возврат ложь;
КонецФункции 
// ПроверитьДокОснованиеНаМножественнуюСФ() ВадимКонец


Процедура ЗаполнитьСчетФактуруНаАванс() Экспорт
	
	Если ДокументыОснования.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДокументыОснования[0].ДокументОснование) Тогда
		Возврат;
	КонецЕсли;
	
	Организация = ДокументыОснования[0].ДокументОснование.Организация;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументОснование", ДокументыОснования[0].ДокументОснование);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РасчетыПоПриобретениюВУсловныхЕдиницахОрганизации.Контрагент,
	|	РасчетыПоПриобретениюВУсловныхЕдиницахОрганизации.ДоговорКонтрагента,
	|	РасчетыПоПриобретениюВУсловныхЕдиницахОрганизации.ДоговорКонтрагента.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	СУММА(ВЫБОР
	|			КОГДА РасчетыПоПриобретениюВУсловныхЕдиницахОрганизации.Документ ССЫЛКА Документ.КорректировкаПоступления
	|				ТОГДА -РасчетыПоПриобретениюВУсловныхЕдиницахОрганизации.СуммаРег
	|			ИНАЧЕ РасчетыПоПриобретениюВУсловныхЕдиницахОрганизации.СуммаРег
	|		КОНЕЦ) КАК СуммаДокумента,
	|	СУММА(ВЫБОР
	|			КОГДА РасчетыПоПриобретениюВУсловныхЕдиницахОрганизации.Документ ССЫЛКА Документ.КорректировкаПоступления
	|				ТОГДА -РасчетыПоПриобретениюВУсловныхЕдиницахОрганизации.СуммаВзаиморасчетов
	|			ИНАЧЕ РасчетыПоПриобретениюВУсловныхЕдиницахОрганизации.СуммаВзаиморасчетов
	|		КОНЕЦ) КАК ВалютнаяСумма
	|ИЗ
	|	РегистрНакопления.РасчетыПоПриобретениюВУсловныхЕдиницахОрганизации КАК РасчетыПоПриобретениюВУсловныхЕдиницахОрганизации
	|ГДЕ
	|	РасчетыПоПриобретениюВУсловныхЕдиницахОрганизации.Регистратор = &ДокументОснование
	|	И РасчетыПоПриобретениюВУсловныхЕдиницахОрганизации.Документ = &ДокументОснование
	|	И (РасчетыПоПриобретениюВУсловныхЕдиницахОрганизации.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ИЛИ (РасчетыПоПриобретениюВУсловныхЕдиницахОрганизации.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.РАСХОД)
	|				ИЛИ РасчетыПоПриобретениюВУсловныхЕдиницахОрганизации.Документ ССЫЛКА Документ.КорректировкаПоступления))
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетыПоПриобретениюВУсловныхЕдиницахОрганизации.Контрагент,
	|	РасчетыПоПриобретениюВУсловныхЕдиницахОрганизации.ДоговорКонтрагента,
	|	РасчетыПоПриобретениюВУсловныхЕдиницахОрганизации.ДоговорКонтрагента.ВалютаВзаиморасчетов";
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	СоответствиеСтавок = Новый Соответствие();
	СоответствиеСтавок.Вставить(Перечисления.СтавкиНДС.НДС10, Перечисления.СтавкиНДС.НДС10_110);
	СоответствиеСтавок.Вставить(Перечисления.СтавкиНДС.НДС18, Перечисления.СтавкиНДС.НДС18_118);
	СоответствиеСтавок.Вставить(Перечисления.СтавкиНДС.НДС20, Перечисления.СтавкиНДС.НДС20_120);
	СоответствиеСтавок.Вставить(Перечисления.СтавкиНДС.НДС10_110, Перечисления.СтавкиНДС.НДС10_110);
	СоответствиеСтавок.Вставить(Перечисления.СтавкиНДС.НДС18_118, Перечисления.СтавкиНДС.НДС18_118);
	СоответствиеСтавок.Вставить(Перечисления.СтавкиНДС.НДС20_120, Перечисления.СтавкиНДС.НДС20_120);
	СоответствиеСтавок.Вставить(Перечисления.СтавкиНДС.НДС0, Перечисления.СтавкиНДС.НДС18_118);
	СоответствиеСтавок.Вставить(Перечисления.СтавкиНДС.ПустаяСсылка(), Перечисления.СтавкиНДС.НДС18_118);
	
	ОшибкаПриЗаполнении = Ложь;
	СтрокаСообщения = "";
	СуммаДокумента  = 0;
	Для Каждого СтрокаТаблицы Из Результат Цикл
		
		Если ДокументыОснования[0].ДокументОснование.Метаданные().ТабличныеЧасти.Найти("РасшифровкаПлатежа") <> Неопределено Тогда
			
			ТаблицаПоСтавкам = Новый ТаблицаЗначений();
			ТаблицаПоСтавкам.Колонки.Добавить("СтавкаНДС", Новый ОписаниеТипов("ПеречислениеСсылка.СтавкиНДС"));
			ТаблицаПоСтавкам.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
			
			Для Каждого СтрПлатежа Из ДокументыОснования[0].ДокументОснование.РасшифровкаПлатежа Цикл
				Если СтрПлатежа.ДоговорКонтрагента = СтрокаТаблицы.ДоговорКонтрагента Тогда
					ТекСтавкаНДС = СоответствиеСтавок[СтрПлатежа.СтавкаНДС];
					Если ТекСтавкаНДС = Неопределено Тогда
						ТекСтавкаНДС = СтрПлатежа.СтавкаНДС;
					КонецЕсли; 
					СтрокаПоСтавке = ТаблицаПоСтавкам.Добавить();
					СтрокаПоСтавке.СтавкаНДС = ТекСтавкаНДС;
					СтрокаПоСтавке.Сумма = СтрПлатежа.СуммаПлатежа;
				КонецЕсли;
			КонецЦикла;
			
			ТаблицаПоСтавкам.Свернуть("СтавкаНДС", "Сумма");
			
			//Распределение суммы рег учета
			МассивСумм = ОбщегоНазначения.РаспределитьПропорционально(СтрокаТаблицы.СуммаДокумента, ТаблицаПоСтавкам.ВыгрузитьКолонку("Сумма"));
			Если Не МассивСумм = Неопределено Тогда
				ТаблицаПоСтавкам.ЗагрузитьКолонку(МассивСумм,"Сумма");
			Иначе
				МассивСумм = ТаблицаПоСтавкам.ВыгрузитьКолонку("Сумма");
			КонецЕсли; 
			
			Для Каждого СтрокаПоСтавке Из ТаблицаПоСтавкам Цикл
				
				Если СтрокаПоСтавке.СтавкаНДС = Перечисления.СтавкиНДС.НДС0 
					Или СтрокаПоСтавке.СтавкаНДС = Перечисления.СтавкиНДС.БезНДС Тогда
					// НДС с авансов под экспортные поставки не начисляется
					Продолжить;
				КонецЕсли;
				
				ДоговорКонтрагента = СтрокаТаблицы.ДоговорКонтрагента;
				СуммаДокумента		= СуммаДокумента + СтрокаПоСтавке.Сумма;
				НовыйАванс = Авансы.Добавить();
				НовыйАванс.Сумма = СтрокаПоСтавке.Сумма;
				НовыйАванс.СтавкаНДС	  = ?(ЗначениеЗаполнено(СоответствиеСтавок[СтрокаПоСтавке.СтавкаНДС]), СоответствиеСтавок[СтрокаПоСтавке.СтавкаНДС], Перечисления.СтавкиНДС.НДС18_118);;
				НовыйАванс.СуммаНДС		  = УчетНДС.РассчитатьСуммуНДС(НовыйАванс.Сумма, Истина, Истина, УчетНДС.ПолучитьСтавкуНДС(НовыйАванс.СтавкаНДС));
				ОшибкаПриЗаполнении = Ложь;
			КонецЦикла;
			
			СтрокаСообщенияТекущая = ПроверитьВозможностьЗаписиСФ(ОшибкаПриЗаполнении, Истина);
			Если ОшибкаПриЗаполнении Тогда
				СтрокаСообщения = СтрокаСообщения + ?(ПустаяСтрока(СтрокаСообщения), "", Символы.ПС) + СтрокаСообщенияТекущая;
				Авансы.Очистить();
				СуммаДокумента = 0;
			Иначе
				Прервать;
			КонецЕсли;

		Иначе
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, СтрокаТаблицы);
			СтрокаАванса = Авансы.Добавить();
			СтрокаАванса.Сумма = СтрокаТаблицы.СуммаДокумента;
			СтрокаАванса.СтавкаНДС = Перечисления.СтавкиНДС.НДС18_118;
			СтрокаАванса.СуммаНДС = УчетНДС.РассчитатьСуммуНДС(СтрокаАванса.Сумма, Истина, Истина, УчетНДС.ПолучитьСтавкуНДС(СтрокаАванса.СтавкаНДС));
			СтрокаСообщения = ПроверитьВозможностьЗаписиСФ(ОшибкаПриЗаполнении, Истина);
		КонецЕсли;
		
	КонецЦикла;
	
	Если Результат.Количество() = 0 Тогда 
		ДокументОснование = Неопределено;
		ДоговорКонтрагента = Неопределено;
		СтавкаНДС = Перечисления.СтавкиНДС.ПустаяСсылка();
		СуммаДокумента = 0;
		ВалютнаяСумма = 0;
		СуммаНДС = 0;
		СуммаНДСДокумента = 0;
		ОбщегоНазначения.СообщитьОбОшибке("Не обнаружены данные для регистрации счета-фактуры на предварительную оплату.");
		Возврат;
	КонецЕсли;		
	
	Если ОшибкаПриЗаполнении Тогда
		ДокументыОснования.Очистить();
		ДокументОснование = Неопределено;
		ДоговорКонтрагента = Неопределено;
		СтавкаНДС = Перечисления.СтавкиНДС.ПустаяСсылка();
		СуммаДокумента = 0;
		ВалютнаяСумма = 0;
		СуммаНДС = 0;
		СуммаНДСДокумента = 0;
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаСообщения);
		Возврат;
	КонецЕсли;
	
	СуммаНДСДокумента = Авансы.Итог("СуммаНДС");
	УстановитьКодВидаОперации();
	
КонецПроцедуры

Функция ПроверитьВозможностьЗаписиСФ(Отказ = Ложь, УдалятьЛишниеСтроки = Ложь) Экспорт
	
	Если Исправление Тогда
		
		СтрокаСообщения = "";
		СтруктураОтбора = Новый Структура("ПометкаУдаления, ВидСчетаФактуры, ДоговорКонтрагента, НомерИсправления, ИсправляемыйСчетФактура", 
			Ложь, ВидСчетаФактуры, ДоговорКонтрагента, НомерИсправления, ИсправляемыйСчетФактура);
		
		СуществующийСФ = УчетНДС.НайтиПодчиненныйСчетФактуру(ДокументыОснования[0].ДокументОснование, "СчетФактураПолученный", СтруктураОтбора, Ссылка);
		
		Если НЕ СуществующийСФ = Неопределено Тогда
			Реквизиты = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(СуществующийСФ, "НомерИсправления, ИсправляемыйСчетФактура"); 
			Если Реквизиты.НомерИсправления = НомерИсправления
				И Реквизиты.ИсправляемыйСчетФактура = ИсправляемыйСчетФактура Тогда
							
				СтрокаСообщения = "На основании документа «" + ИсправляемыйСчетФактура + "» уже введен исправленный счет-фактура "
					+ Символы.ПС + "«" +Строка(СуществующийСФ) + "» с номером исправления «" + НомерИсправления + "»";
				
				Отказ = Истина;
				Возврат СтрокаСообщения;
				
			КонецЕсли;	
		КонецЕсли;	
		
	КонецЕсли;
	
	СтрокаСообщения = "";
	СтруктураОтбора = Новый Структура("ПометкаУдаления, ВидСчетаФактуры, ДоговорКонтрагента, НомерИсправления, ИсправляемыйСчетФактура", 
		Ложь, ВидСчетаФактуры, ДоговорКонтрагента, НомерИсправления, ИсправляемыйСчетФактура);
    			
	Для Каждого ТекущееОснование Из ДокументыОснования Цикл
		Если ТипЗнч(ТекущееОснование.ДокументОснование) = Тип("ДокументСсылка.АвансовыйОтчет")
			ИЛИ ТипЗнч(ТекущееОснование.ДокументОснование) = Тип("ДокументСсылка.ОтчетКомитентуОПродажах") Тогда 
			// Может быть много СФ, проверка затруднительна. СФ отрабатываются при записи документа АО.
			Продолжить;
		КонецЕсли;

		СуществующийСФ = УчетНДС.НайтиПодчиненныйСчетФактуру(ТекущееОснование.ДокументОснование, "СчетФактураПолученный", СтруктураОтбора, Ссылка);
		
		Если НЕ СуществующийСФ = неопределено Тогда
			Если ПустаяСтрока(СтрокаСообщения) Тогда
				СтрокаСообщения = "Для указанного документа-основания уже зарегистрирован счет-фактура:";
			КонецЕсли;
			СтрокаСообщения = СтрокаСообщения + Символы.ПС + " - " + ?(ДокументыОснования.Количество() > 1, " строка № " + ТекущееОснование.НомерСтроки 
				+ ": для «" + ТекущееОснование.ДокументОснование + "» зарегистрирован счет-фактура ","");
			#Если Клиент Тогда
				СтрокаСообщения = СтрокаСообщения + " «" + РаботаСДиалогами.ПолучитьТекстСчетаФактуры(СуществующийСФ) + "»";
			#Иначе
				СтрокаСообщения = СтрокаСообщения + " «" + Строка(СуществующийСФ) + "»";
			#КонецЕсли
			Отказ = Истина;
		КонецЕсли;
	КонецЦикла; 
	
	Если НЕ ПустаяСтрока(СтрокаСообщения) Тогда
		СтрокаСообщения = СтрокаСообщения + Символы.ПС + "Регистрация еще одного счета-фактуры не допускается."
	КонецЕсли;
	
	Возврат СтрокаСообщения;

КонецФункции 

Функция ПолучитьСписокТиповПоВидуСчетаФактуры(ВидСчетаФактуры, Исправление = Ложь) Экспорт
	
	СписокТипов = Новый Массив;
	
	Если ВидСчетаФактуры = Перечисления.ВидСчетаФактурыПолученного.Корректировочный ИЛИ 
		(Исправление И ВидСчетаФактуры = Перечисления.ВидСчетаФактурыПолученного.НаПоступление) Тогда
		СписокТипов.Добавить(Тип("ДокументСсылка.КорректировкаПоступления"));
	Иначе
		
		СписокТипов.Добавить(Тип("ДокументСсылка.ДокументРасчетовСКонтрагентом"));
		СписокТипов.Добавить(Тип("ДокументСсылка.АвансовыйОтчет"));
		СписокТипов.Добавить(Тип("ДокументСсылка.ПлатежноеПоручениеИсходящее"));
		СписокТипов.Добавить(Тип("ДокументСсылка.КорректировкаДолга"));
		СписокТипов.Добавить(Тип("ДокументСсылка.РасходныйКассовыйОрдер"));
		СписокТипов.Добавить(Тип("ДокументСсылка.ПлатежныйОрдерСписаниеДенежныхСредств"));
		СписокТипов.Добавить(Тип("ДокументСсылка.АккредитивПереданный"));
		СписокТипов.Добавить(Тип("ДокументСсылка.ИнкассовоеПоручениеПолученное"));
		СписокТипов.Добавить(Тип("ДокументСсылка.КорректировкаПоступления"));
		
		Если ВидСчетаФактуры <> Перечисления.ВидСчетаФактурыПолученного.НаАванс Тогда
			СписокТипов = Новый ОписаниеТипов(
				ЭтотОбъект.Метаданные().ТабличныеЧасти.ДокументыОснования.Реквизиты.ДокументОснование.Тип, ,СписокТипов);
			СписокТипов = СписокТипов.Типы();
		КонецЕсли;
		
		Если НЕ Исправление И ВидСчетаФактуры <> Перечисления.ВидСчетаФактурыПолученного.НаАванс Тогда
			ИндексУдаляемогоЭлемента = СписокТипов.Найти(Тип("ДокументСсылка.КорректировкаПоступления"));
			Если ИндексУдаляемогоЭлемента <> Неопределено Тогда
				СписокТипов.Удалить(ИндексУдаляемогоЭлемента);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	СписокТипов.Добавить(Тип("ДокументСсылка.ОтчетКомитентуОПродажах"));
	
	Возврат СписокТипов;
	
КонецФункции

Процедура УстановитьКодВидаОперации(КодВидаОперацииОснования = Неопределено) Экспорт
	
	ВерсияПостановления = УчетНДС.ПолучитьВерсиюПостановления(Дата);
	
	Если ВерсияПостановления <> 2 Тогда
		Возврат;
	КонецЕсли;
	
	ВерсияКодовВидовОпераций = УчетНДС.ВерсияКодовВидовОпераций(Дата);
	
	Если Исправление ИЛИ ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.Корректировочный Тогда
		Если КодВидаОперацииОснования <> Неопределено Тогда
			КодВидаОперации = УчетНДС.АктуальныйКодВидаОперации(КодВидаОперацииОснования, ВерсияКодовВидовОпераций);
			Возврат;
		КонецЕсли;
	КонецЕсли;

	Если ВидСчетаФактуры = Перечисления.ВидСчетаФактурыПолученного.Корректировочный Тогда
		
		Если ВерсияКодовВидовОпераций = 1 Тогда
			КодВидаОперацииНаУменьшение = "";
		ИначеЕсли ВерсияКодовВидовОпераций = 2 Тогда
			КодВидаОперацииНаУменьшение = "18";
		Иначе
			КодВидаОперацииНаУменьшение = ?(СводныйКорректировочный, "01", "18");
		КонецЕсли;
		
	ИначеЕсли ВидСчетаФактуры = Перечисления.ВидСчетаФактурыПолученного.НаПоступление Тогда
		
		Для Каждого СтрокаТабличнойЧасти ИЗ ДокументыОснования Цикл
			
			ТипОснования = ТипЗнч(СтрокаТабличнойЧасти.ДокументОснование);
			Если ТипОснования = Тип("ДокументСсылка.ОтражениеПоступленияТоваровИУслугНДС")
				И ВерсияКодовВидовОпераций > 1 Тогда
				// Код операции указывается в документе
				КодВидаОперацииИзДокумента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
				СтрокаТабличнойЧасти.ДокументОснование, "КодВидаОперации");
				КодВидаОперации = УчетНДС.АктуальныйКодВидаОперации(КодВидаОперацииИзДокумента, ВерсияКодовВидовОпераций);
				Прервать;
			ИначеЕсли ВерсияКодовВидовОпераций = 3 Тогда
				КодВидаОперации = "01";
				// Коды операций до 1 июля 2016 года.
			ИначеЕсли ТипОснования = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя") Тогда
				КодВидаОперации = "03";
			ИначеЕсли ТипОснования = Тип("ДокументСсылка.ОтчетКомитентуОПродажах") Тогда
				КодВидаОперации = "04";
			КонецЕсли;
			
		КонецЦикла;
		
	ИначеЕсли ВерсияКодовВидовОпераций = 3 Тогда 
		Если ВидСчетаФактуры = Перечисления.ВидСчетаФактурыПолученного.НаАванс
			ИЛИ ВидСчетаФактуры = Перечисления.ВидСчетаФактурыПолученного.НаАвансКомитента Тогда
			КодВидаОперации = "02";
		КонецЕсли;
		// Коды операций до 1 июля 2016 года
	ИначеЕсли ВидСчетаФактуры = Перечисления.ВидСчетаФактурыПолученного.НаАванс Тогда
		Если ДокументыОснования.Количество() > 0
			И ТипЗнч(ДокументыОснования[0].ДокументОснование) = Тип("ДокументСсылка.ОтчетКомитентуОПродажах") Тогда
			КодВидаОперации = "05";
		Иначе
			КодВидаОперации = "02";
		КонецЕсли;
	ИначеЕсли ВидСчетаФактуры = Перечисления.ВидСчетаФактурыПолученного.НаАвансКомитента Тогда
		КодВидаОперации = "05";
	Иначе
		Для Каждого СтрокаТабличнойЧасти ИЗ ДокументыОснования Цикл
			Если ТипЗнч(СтрокаТабличнойЧасти.ДокументОснование) = Тип("ДокументСсылка.ОтчетКомитентуОПродажах") Тогда
				КодВидаОперации = "04";
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если КодВидаОперации = "" Тогда
		КодВидаОперации = "01";
	КонецЕсли;
	
КонецПроцедуры
 
Процедура ЗаполнитьНаОснованииСчетаФактуры(Основание)
	
	Если Основание.ВидСчетаФактуры <> Перечисления.ВидСчетаФактурыПолученного.НаАванс Тогда
		Возврат;
	КонецЕсли;	
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Основание, , "Номер,Дата,Проведен,ПометкаУдаления");
	Дата = ОбщегоНазначения.ПолучитьРабочуюДату();
	
	Для Каждого СтрокаАванса Из Основание.Авансы Цикл 
		НоваяСтрока = Авансы.Добавить();		
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаАванса);
	КонецЦикла;	
	
	Исправление      = Истина;
	НомерИсправления = НомерИсправления + 1;
	
	Если Основание.Исправление Тогда
		ИсправляемыйСчетФактура = Основание.ИсправляемыйСчетФактура;
	Иначе
		ИсправляемыйСчетФактура = Основание;
	КонецЕсли;
	
	УстановитьКодВидаОперации(КодВидаОперации);
			
КонецПроцедуры	

Процедура ЗаполнитьНаОснованииИсправления(Основание, СтрокаОснования)
	
	ИсходныйДокумент = УчетНДС.ПолучитьИсправляемыйДокументПоступления(Основание.ДокументПоступления);
	ДанныеИсходногоДокумента = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(ИсходныйДокумент, 
		Новый Структура("ВалютаДокумента, РасчетыВУсловныхЕдиницах", 
			"ВалютаДокумента", "ДоговорКонтрагента.РасчетыВУсловныхЕдиницах"));
	
	Если ЗначениеЗаполнено(ДанныеИсходногоДокумента.ВалютаДокумента) 
		И ДанныеИсходногоДокумента.ВалютаДокумента <> мВалютаРегламентированногоУчета 
		И ДанныеИсходногоДокумента.РасчетыВУсловныхЕдиницах Тогда
		
		Если Основание.ВидОперации = Перечисления.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение Тогда
			УточнениеСообщения = НСтр("ru='корректировочных'"); 
		Иначе
			УточнениеСообщения = НСтр("ru='исправленных'");
		КонецЕсли;
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Для договоров в условных единицах регистрация %1 счетов-фактур, оформленных в валюте, не поддерживается.
			|Документ-основание должен быть оформлен в рублях.'"),
			УточнениеСообщения);
		ВызватьИсключение ТекстСообщения;
		
	КонецЕсли;
	
	Если НЕ Основание.КорректироватьНДС Тогда
		ВызватьИсключение "Для корректировки поступления с отражением корректировки ""Только в печатной форме"" счет-фактура не выставляется";
	КонецЕсли;	
	
	Если Основание.ВидОперации = Перечисления.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение 
		ИЛИ ТипЗнч(Основание.ИсправляемыйДокументПоступления) = Тип("ДокументСсылка.КорректировкаПоступления") Тогда
		ВидСчетаФактуры = Перечисления.ВидСчетаФактурыПолученного.Корректировочный;
	Иначе
		ВидСчетаФактуры = Перечисления.ВидСчетаФактурыПолученного.НаПоступление;
	КонецЕсли;
	
	ВерсияКодовВидовОпераций = УчетНДС.ВерсияКодовВидовОпераций(Дата);
	НомерИсправленияСФ = 0;
	
	Исправление = Основание.ВидОперации = Перечисления.ВидыОперацийИсправленияПоступленияРеализации.ИсправлениеОшибки;
	
	Если ТипЗнч(ИсходныйДокумент) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда 
		ДанныеИсходногоДокумента = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(
			ИсходныйДокумент,
			"НомерВходящегоДокумента,ДатаВходящегоДокумента,Исправление,НомерИсправления,ДатаИсправления,
			|КодВидаОперации,КодВидаОперацииНаУменьшение");
		
		Если Исправление Тогда
			НомерВходящегоДокумента = ДанныеИсходногоДокумента.НомерВходящегоДокумента;
			ДатаВходящегоДокумента  = ДанныеИсходногоДокумента.ДатаВходящегоДокумента;
		Иначе
			СтрокаОснования.НомерИсходногоДокумента = ДанныеИсходногоДокумента.НомерВходящегоДокумента;
			СтрокаОснования.ДатаИсходногоДокумента  = ДанныеИсходногоДокумента.ДатаВходящегоДокумента;
			
			Если ДанныеИсходногоДокумента.Исправление Тогда
				СтрокаОснования.НомерИсправленияИсходногоДокумента     = ДанныеИсходногоДокумента.НомерИсправления;
				СтрокаОснования.ДатаИсправленияИсходногоДокумента      = ДанныеИсходногоДокумента.ДатаИсправления;
				СтрокаОснования.УчитыватьИсправлениеИсходногоДокумента = ДанныеИсходногоДокумента.Исправление;
			КонецЕсли;
		КонецЕсли;
		
		КодВидаОперацииОснования = ДанныеИсходногоДокумента.КодВидаОперации;
		
		Если ВидСчетаФактуры = Перечисления.ВидСчетаФактурыПолученного.Корректировочный
			И ВерсияКодовВидовОпераций > 1 Тогда
			
			КодВидаОперацииНаУменьшение = 
			?(ДанныеИсходногоДокумента.КодВидаОперацииНаУменьшение = "",
			ДанныеИсходногоДокумента.КодВидаОперацииНаУменьшение,
			"18");
			
		КонецЕсли;
	КонецЕсли;
 	
	ИсправляемыйСчетФактура = УчетНДС.НайтиПодчиненныйСчетФактуру(Основание.ДокументПоступления, "СчетФактураПолученный");
	
	Если ЗначениеЗаполнено(ИсправляемыйСчетФактура) Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ИсходныйДокумент",        Основание.ДокументПоступления);
		Запрос.УстановитьПараметр("ИсправляемыйСчетФактура", ИсправляемыйСчетФактура);
		
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВЫБОР
		|		КОГДА СчетФактураПолученныйДокументыОснования.Ссылка.Дата >= ДАТАВРЕМЯ(2015, 1, 1)
		|			ТОГДА ВЫБОР
		|					КОГДА СчетФактураПолученныйДокументыОснования.Ссылка.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыПолученного.Корректировочный)
		|							И СчетФактураПолученныйДокументыОснования.Ссылка.КодВидаОперацииНаУменьшение <> """"
		|						ТОГДА СчетФактураПолученныйДокументыОснования.Ссылка.КодВидаОперацииНаУменьшение
		|					ИНАЧЕ ""18""
		|				КОНЕЦ
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК КодВидаОперацииНаУменьшение,
		|	СчетФактураПолученныйДокументыОснования.Ссылка КАК Ссылка,
		|	СчетФактураПолученныйДокументыОснования.Ссылка.КодВидаОперации КАК КодВидаОперации,
		|	СчетФактураПолученныйДокументыОснования.Ссылка.НомерВходящегоДокумента КАК НомерВходящегоДокумента,
		|	СчетФактураПолученныйДокументыОснования.Ссылка.ДатаВходящегоДокумента КАК ДатаВходящегоДокумента,
		|	СчетФактураПолученныйДокументыОснования.Ссылка.КППКонтрагента КАК КППКонтрагента,
		|	СчетФактураПолученныйДокументыОснования.Ссылка.Продавец КАК Продавец,
		|	СчетФактураПолученныйДокументыОснования.Ссылка.Исправление КАК Исправление,
		|	СчетФактураПолученныйДокументыОснования.Ссылка.НомерИсправления КАК НомерИсправления,
		|	СчетФактураПолученныйДокументыОснования.Ссылка.ДатаИсправления КАК ДатаИсправления,
		|	СчетФактураПолученныйДокументыОснования.УчитыватьИсправлениеИсходногоДокумента КАК УчитыватьИсправлениеИсходногоДокумента,
		|	СчетФактураПолученныйДокументыОснования.НомерИсправленияИсходногоДокумента КАК НомерИсправленияИсходногоДокумента,
		|	СчетФактураПолученныйДокументыОснования.ДатаИсправленияИсходногоДокумента КАК ДатаИсправленияИсходногоДокумента,
		|	СчетФактураПолученныйДокументыОснования.НомерИсходногоДокумента КАК НомерИсходногоДокумента,
		|	СчетФактураПолученныйДокументыОснования.ДатаИсходногоДокумента КАК ДатаИсходногоДокумента
		|ИЗ
		|	Документ.СчетФактураПолученный.ДокументыОснования КАК СчетФактураПолученныйДокументыОснования
		|ГДЕ
		|	СчетФактураПолученныйДокументыОснования.ДокументОснование = &ИсходныйДокумент
		|	И СчетФактураПолученныйДокументыОснования.Ссылка = &ИсправляемыйСчетФактура
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			РеквизитыСФ = Выборка;
		Иначе
			РеквизитыСФ = Новый Структура("НомерВходящегоДокумента,ДатаВходящегоДокумента,
				|Исправление,НомерИсправления,ДатаИсправления,
				|НомерИсходногоДокумента,ДатаИсходногоДокумента,
				|УчитыватьИсправлениеИсходногоДокумента,НомерИсправленияИсходногоДокумента,ДатаИсправленияИсходногоДокумента,
				|КодВидаОперации, КППКонтрагента, Продавец");
		КонецЕсли;
		
		Если ВидСчетаФактуры = Перечисления.ВидСчетаФактурыПолученного.Корректировочный Тогда
			
			Если ВерсияКодовВидовОпераций > 1 Тогда
				КодВидаОперацииНаУменьшение = РеквизитыСФ.КодВидаОперацииНаУменьшение;
			КонецЕсли;
			
			Если Исправление Тогда
				
				НомерИсправленияСФ  = РеквизитыСФ.НомерИсправления;
				
				НомерВходящегоДокумента = РеквизитыСФ.НомерВходящегоДокумента;
				ДатаВходящегоДокумента  = РеквизитыСФ.ДатаВходящегоДокумента;
				
				СтрокаОснования.НомерИсходногоДокумента = РеквизитыСФ.НомерИсходногоДокумента;
				СтрокаОснования.ДатаИсходногоДокумента  = РеквизитыСФ.ДатаИсходногоДокумента;
				
				СтрокаОснования.УчитыватьИсправлениеИсходногоДокумента = РеквизитыСФ.УчитыватьИсправлениеИсходногоДокумента;
				Если СтрокаОснования.УчитыватьИсправлениеИсходногоДокумента Тогда
					СтрокаОснования.НомерИсправленияИсходногоДокумента = РеквизитыСФ.НомерИсправленияИсходногоДокумента;
					СтрокаОснования.ДатаИсправленияИсходногоДокумента  = РеквизитыСФ.ДатаИсправленияИсходногоДокумента;
				КонецЕсли;
				
			Иначе
				
				СтрокаОснования.НомерИсходногоДокумента = РеквизитыСФ.НомерВходящегоДокумента;
				СтрокаОснования.ДатаИсходногоДокумента  = РеквизитыСФ.ДатаВходящегоДокумента;
				
				СтрокаОснования.УчитыватьИсправлениеИсходногоДокумента = РеквизитыСФ.Исправление;
				Если СтрокаОснования.УчитыватьИсправлениеИсходногоДокумента Тогда
					СтрокаОснования.НомерИсправленияИсходногоДокумента = РеквизитыСФ.НомерИсправления;
					СтрокаОснования.ДатаИсправленияИсходногоДокумента  = РеквизитыСФ.ДатаИсправления;
				КонецЕсли;
				
			КонецЕсли;
			
			
		Иначе
		
			НомерВходящегоДокумента = РеквизитыСФ.НомерВходящегоДокумента;
			ДатаВходящегоДокумента  = РеквизитыСФ.ДатаВходящегоДокумента;
			
			Если Исправление Тогда
				НомерИсправленияСФ  = РеквизитыСФ.НомерИсправления;
			КонецЕсли;
			
		КонецЕсли;
		
		КППКонтрагента = РеквизитыСФ.КППКонтрагента;
		Продавец       = РеквизитыСФ.Продавец;
	
		УстановитьКодВидаОперации(РеквизитыСФ.КодВидаОперации);
		
	Иначе
	
		УстановитьКодВидаОперации();
		
	КонецЕсли;
	
	Если Исправление Тогда
		НомерИсправления = НомерИсправленияСФ + 1;
		ДатаИсправления  = Дата;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(КППКонтрагента) И ДокументыОснования.Количество() > 0 
		И НЕ ТипЗнч(ДокументыОснования[0].ДокументОснование) = Тип("ДокументСсылка.ОтчетКомиссионераОПродажах") Тогда
		КППКонтрагента = УчетНДС.ПолучитьКПППодразделенияКонтрагента(ДокументыОснования[0].ДокументОснование, "Грузоотправитель")
	КонецЕсли;
		
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДОКУМЕНТА

#Если Клиент Тогда

// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходимое количество копий.
//
//  Название макета печати передается в качестве параметра,
// по переданному названию находим имя макета в соответствии.
//
// Параметры:
//  НазваниеМакета - строка, название макета.
//
Процедура Печать(ИмяМакета, КоличествоЭкземпляров = 1, НаПринтер = Ложь) Экспорт

	Если ЭтоНовый() Тогда
		Предупреждение(НСтр("ru = 'Документ можно распечатать только после его записи'"));
		Возврат;
	ИначеЕсли Не УправлениеДопПравамиПользователей.РазрешитьПечатьНепроведенныхДокументов(Проведен) Тогда
		Предупреждение(НСтр("ru = Недостаточно полномочий для печати непроведенного документа!'"));
		Возврат;
	КонецЕсли;

	Если Не РаботаСДиалогами.ПроверитьМодифицированность(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;

	Если ТипЗнч(ИмяМакета) = Тип("ДвоичныеДанные") Тогда

		ТабДокумент = УниверсальныеМеханизмы.НапечататьВнешнююФорму(Ссылка, ИмяМакета);

		Если ТабДокумент = Неопределено Тогда
			Возврат
		КонецЕсли; 
	Иначе
		ПараметрКоманды = Новый Массив;
		ПараметрКоманды.Добавить(Ссылка);
		
		Если НаПринтер Тогда
			УправлениеПечатьюКлиент.ВыполнитьКомандуПечатиНаПринтер("Документ.СчетФактураПолученный", ИмяМакета, 
										ПараметрКоманды, Неопределено);
		Иначе
			УправлениеПечатьюКлиент.ВыполнитьКомандуПечати("Документ.СчетФактураПолученный", ИмяМакета, 
										ПараметрКоманды, Неопределено, Неопределено);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	УниверсальныеМеханизмы.НапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер, ОбщегоНазначения.СформироватьЗаголовокДокумента(ЭтотОбъект, ЭтотОбъект.Метаданные().Представление()), Ссылка);

КонецПроцедуры // Печать

// Возвращает доступные варианты печати документа
//
// Возвращаемое значение:
//  Структура, каждая строка которой соответствует одному из вариантов печати
//  
Функция ПолучитьСтруктуруПечатныхФорм() Экспорт
	
	СтруктураМакетов = Новый Структура;
	
	Если Дата < '20040216' Тогда
		СтруктураМакетов.Вставить("СчетФактура575", "Счет-фактура");
	ИначеЕсли Дата < '20060530' Тогда
		СтруктураМакетов.Вставить("СчетФактура84", "Счет-фактура");
	Иначе
		СтруктураМакетов.Вставить("СчетФактура283", "Счет-фактура");
	КонецЕсли;
	
	Возврат СтруктураМакетов;

КонецФункции // ПолучитьСтруктуруПечатныхФорм()

#КонецЕсли

// Проверяет правильность заполнения шапки документа.
// Если какой-то из реквизитов шапки, влияющий на проведение не заполнен или
// заполнен не корректно, то выставляется флаг отказа в проведении.
// Проверяется также правильность заполнения реквизитов ссылочных полей документа.
// Проверка выполняется по объекту и по выборке из результата запроса по шапке.
//
// Параметры: 
//  СтруктураШапкиДокумента - структура, содержащая реквизиты шапки документа и результаты запроса по шапке,
//  Отказ                   - флаг отказа в проведении,
//  Заголовок               - строка, заголовок сообщения об ошибке проведения.
//
Процедура ПроверитьЗаполнениеШапки(СтруктураШапкиДокумента, Отказ, Заголовок)
	
	// Укажем, что надо проверить:
	СтруктураОбязательныхПолей = Новый Структура("Организация,ВидСчетаФактуры,Контрагент");
	
	Если НЕ СформированПриВводеНачальныхОстатковНДС Тогда
		СтруктураОбязательныхПолей.Вставить("ДатаВходящегоДокумента");
		СтруктураОбязательныхПолей.Вставить("НомерВходящегоДокумента");
		
		Если Дата >= '20111001' Тогда
			
			ВерсияПостановления = УчетНДС.ПолучитьВерсиюПостановления(Дата);
			Если ВерсияПостановления = 2 Тогда
				Если НЕ БланкСтрогойОтчетности Тогда
					СтруктураОбязательныхПолей.Вставить("КодВидаОперации");
				КонецЕсли;
				Если Исправление Тогда
					СтруктураОбязательныхПолей.Вставить("НомерИсправления");
					СтруктураОбязательныхПолей.Вставить("ДатаИсправления");
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// Теперь вызовем общую процедуру проверки.
	ЗаполнениеДокументов.ПроверитьЗаполнениеШапкиДокумента(ЭтотОбъект, СтруктураОбязательныхПолей, Отказ, Заголовок);

КонецПроцедуры 

// Проверяет правильность заполнения строк табличной части "ДокументыОснования".
//
// Параметры:
// Параметры: 
//  ТаблицаПоТоварам        - таблица значений, содержащая данные для проведения и проверки ТЧ Товары
//  СтруктураШапкиДокумента - структура, содержащая реквизиты шапки документа и результаты запроса по шапке
//  Отказ                   - флаг отказа в проведении.
//  Заголовок               - строка, заголовок сообщения об ошибке проведения.
//
Процедура ПроверитьЗаполнениеТабличнойЧастиПоДокументамОснованиям(ТаблицаПоДокументамОснованиям, Отказ, Заголовок)
	
	Для Каждого СтрокаТЧ Из ТаблицаПоДокументамОснованиям Цикл
		
		Если ЗначениеЗаполнено(СтрокаТЧ.ДокументОснование) 
			И Не ТипЗнч(СтрокаТЧ.ДокументОснование) = Тип("ДокументСсылка.ДокументРасчетовСКонтрагентом") И Не СтрокаТЧ.ДокументОснование.Проведен Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Счет-фактуру можно вводить только на основании проведенного документа (строка № " + СтрокаТЧ.НомерСтроки + ")." , Отказ, Заголовок);
		КонецЕсли;	
		
		Если НЕ СформированПриВводеНачальныхОстатковНДС Тогда
			Если ВидСчетаФактуры = Перечисления.ВидСчетаФактурыПолученного.Корректировочный Тогда
				ОшибкиСтроки = "";
				Если НЕ ЗначениеЗаполнено(СтрокаТЧ.НомерИсходногоДокумента) Тогда
					ОшибкиСтроки = ОшибкиСтроки + ?(ОшибкиСтроки = "", "", ", ") + "Номер счета-фактуры";
				КонецЕсли;
				Если НЕ ЗначениеЗаполнено(СтрокаТЧ.ДатаИсходногоДокумента) Тогда
					ОшибкиСтроки = ОшибкиСтроки + ?(ОшибкиСтроки = "", "", ", ") + "Дата счета-фактуры";
				КонецЕсли;
				Если СтрокаТЧ.УчитыватьИсправлениеИсходногоДокумента Тогда
					Если НЕ ЗначениеЗаполнено(СтрокаТЧ.НомерИсправленияИсходногоДокумента) Тогда
						ОшибкиСтроки = ОшибкиСтроки + ?(ОшибкиСтроки = "", "", ", ") + "Номер исправления счета-фактуры";
					КонецЕсли;
					Если НЕ ЗначениеЗаполнено(СтрокаТЧ.ДатаИсправленияИсходногоДокумента) Тогда
						ОшибкиСтроки = ОшибкиСтроки + ?(ОшибкиСтроки = "", "", ", ") + "Дата исправления счета-фактуры";
					КонецЕсли;
				КонецЕсли;
				Если Не ПустаяСтрока(ОшибкиСтроки) Тогда
					СтрокаСообщения = "В строке № " + СтрокаТЧ.НомерСтроки + " табличной части ""Документы основания"" не заполнены поля: " + ОшибкиСтроки;
					ОбщегоНазначения.СообщитьОбОшибке(СтрокаСообщения);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры // ПроверитьЗаполнениеТабличнойЧастиТовары()

// Процедура формирует структуру шапки документа и дополнительных полей.
//
Процедура ПодготовитьСтруктуруШапкиДокумента(Заголовок, СтруктураШапкиДокумента, Отказ) Экспорт
	
	// Заголовок для сообщений об ошибках проведения.
	Заголовок = ОбщегоНазначения.ПредставлениеДокументаПриПроведении(Ссылка);
	
	// Сформируем структуру реквизитов шапки документа
	СтруктураШапкиДокумента = ОбщегоНазначения.СформироватьСтруктуруШапкиДокумента(ЭтотОбъект);
	
	Если ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
		Если НЕ СтруктураШапкиДокумента.Свойство("РасчетыВУсловныхЕдиницах") Тогда
			СтруктураШапкиДокумента.Вставить("РасчетыВУсловныхЕдиницах"	, 
						ОбщегоНазначения.ПолучитьЗначениеРеквизита(ДоговорКонтрагента, "РасчетыВУсловныхЕдиницах"));
		КонецЕсли;	
		Если НЕ СтруктураШапкиДокумента.Свойство("ВалютаРегламентированногоУчета") Тогда	
			СтруктураШапкиДокумента.Вставить("ВалютаРегламентированногоУчета", Константы.ВалютаРегламентированногоУчета.Получить());
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ СтруктураШапкиДокумента.Свойство("ВидДоговора") Тогда
		СтруктураШапкиДокумента.Вставить("ВидДоговора", ОбщегоНазначения.ПолучитьЗначениеРеквизита(ДоговорКонтрагента, "ВидДоговора"));
	КонецЕсли;	
	
	Если НЕ ЗначениеЗаполнено(СтруктураШапкиДокумента.КППКонтрагента) Тогда
		СтруктураШапкиДокумента.Вставить("КППКонтрагента", ОбщегоНазначения.ПолучитьЗначениеРеквизита(СтруктураШапкиДокумента.Контрагент, "КПП"));
	КонецЕсли;
	
	Если Дата >= '20150101'
		И БланкСтрогойОтчетности Тогда
		СтруктураШапкиДокумента.Вставить("КодВидаОперации", "23");
	КонецЕсли;
	
КонецПроцедуры // ПодготовитьСтруктуруШапкиДокумента()

// Процедура формирует таблицы документа.
//
Процедура ПодготовитьТаблицыДокумента(СтруктураШапкиДокумента, ТаблицаПоДокументамОснованиям, ПодготовитьТаблицыДокументовОснования = Ложь) Экспорт
	
	// Получим необходимые данные для проведения и проверки заполнения по табличной части "ДокументыОснования".
	СтруктураПолей = Новый Структура;
	СтруктураПолей.Вставить("ДокументОснование"      	, "ДокументОснование");
	СтруктураПолей.Вставить("ДокументОснованиеПроведен" , "ДокументОснование.Проведен");
	СтруктураПолей.Вставить("УчитыватьНДС"				, "ДокументОснование.УчитыватьНДС");
	СтруктураПолей.Вставить("НДСВключенВСтоимость"		, "ДокументОснование.НДСВключенВСтоимость");
	СтруктураПолей.Вставить("СуммаВключаетНДС"			, "ДокументОснование.СуммаВключаетНДС");
	СтруктураПолей.Вставить("СуммаВключаетНДС"			, "ДокументОснование.СуммаВключаетНДС");
	
	Если СтруктураШапкиДокумента.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыПолученного.Корректировочный Тогда
		
		СтруктураПолей.Вставить("НомерИсходногоДокумента"			 	 , "НомерИсходногоДокумента");
		СтруктураПолей.Вставить("ДатаИсходногоДокумента"			 	 , "ДатаИсходногоДокумента");
		СтруктураПолей.Вставить("НомерИсправленияИсходногоДокумента" 	 , "НомерИсправленияИсходногоДокумента");
		СтруктураПолей.Вставить("ДатаИсправленияИсходногоДокумента"	 	 , "ДатаИсправленияИсходногоДокумента");
		СтруктураПолей.Вставить("СуммаУвеличение"			 		 	 , "СуммаУвеличение");
		СтруктураПолей.Вставить("СуммаУменьшение"					 	 , "СуммаУменьшение");
		СтруктураПолей.Вставить("СуммаНДСУвеличение"				 	 , "СуммаНДСУвеличение");
		СтруктураПолей.Вставить("СуммаНДСУменьшение"					 , "СуммаНДСУменьшение");
		СтруктураПолей.Вставить("УчитыватьИсправлениеИсходногоДокумента" , "УчитыватьИсправлениеИсходногоДокумента");
		
	КонецЕсли;
	
	РезультатЗапросаПоДокументамОснованиям = УправлениеЗапасами.СформироватьЗапросПоТабличнойЧасти(ЭтотОбъект, "ДокументыОснования", СтруктураПолей);
	
	ТаблицаПоДокументамОснованиям = РезультатЗапросаПоДокументамОснованиям.Выгрузить();
	
	Если СтруктураШапкиДокумента.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыПолученного.НаАванс Тогда
		
		ТаблицаПоДокументамОснованиям.Колонки.Добавить("ТаблицаДанных");
		
		Для Каждого СтрокаОснования Из ТаблицаПоДокументамОснованиям Цикл
			
			СтрокаОснования.УчитыватьНДС = Истина;
			СтруктураПолей = Новый Структура;
			СтруктураПолей.Вставить("Сумма",		"Сумма");
			СтруктураПолей.Вставить("НДС",			"СуммаНДС");
			СтруктураПолей.Вставить("СтавкаНДС",	"СтавкаНДС");
			
			РезультатЗапросаПоАвансам = УправлениеЗапасами.СформироватьЗапросПоТабличнойЧасти(ЭтотОбъект, "Авансы", СтруктураПолей);
			
			СтрокаОснования.ТаблицаДанных = РезультатЗапросаПоАвансам.Выгрузить();
			ТаблицаПоАвансам = СтрокаОснования.ТаблицаДанных; 
			
			ТаблицаПоАвансам.Колонки.Добавить("СчетФактура", ТаблицаПоДокументамОснованиям.Колонки.ДокументОснование.ТипЗначения);
			
			ТаблицаПоАвансам.Колонки.Добавить("ВидЦенности", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыЦенностей"));
			ТаблицаПоАвансам.ЗаполнитьЗначения(Перечисления.ВидыЦенностей.АвансыВыданные, "ВидЦенности");
			ТаблицаПоАвансам.Колонки.Добавить("Поставщик", Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
			ТаблицаПоАвансам.ЗаполнитьЗначения(СтруктураШапкиДокумента.Контрагент, "Поставщик");
			ТаблицаПоАвансам.Колонки.Добавить("Контрагент", Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
			ТаблицаПоАвансам.ЗаполнитьЗначения(СтруктураШапкиДокумента.Контрагент, "Контрагент");
			ТаблицаПоАвансам.Колонки.Добавить("ДоговорКонтрагента", Новый ОписаниеТипов("СправочникСсылка.ДоговорыКонтрагентов"));
			ТаблицаПоАвансам.ЗаполнитьЗначения(СтруктураШапкиДокумента.ДоговорКонтрагента, "ДоговорКонтрагента");
			ТаблицаПоАвансам.Колонки.Добавить("СуммаБезНДС", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
			ТаблицаПоАвансам.ЗаполнитьЗначения(СтруктураШапкиДокумента.Контрагент, "СуммаБезНДС");
			ТаблицаПоАвансам.Колонки.Добавить("СчетУчетаНДС", Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
			ТаблицаПоАвансам.ЗаполнитьЗначения(ПланыСчетов.Хозрасчетный.НДСпоАвансамИПредоплатамВыданным, "СчетУчетаНДС");
			ТаблицаПоАвансам.Колонки.Добавить("СчетУчетаНДСПоРеализации", Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
			ТаблицаПоАвансам.ЗаполнитьЗначения(ПланыСчетов.Хозрасчетный.НДСпоАвансамИПредоплатамВыданным, "СчетУчетаНДСПоРеализации");
			ТаблицаПоАвансам.Колонки.Добавить("Событие", Новый ОписаниеТипов("ПеречислениеСсылка.СобытияПоНДСПокупки"));
			ТаблицаПоАвансам.ЗаполнитьЗначения(Перечисления.СобытияПоНДСПокупки.ПредъявленНДСПоставщиком, "Событие");
			ТаблицаПоАвансам.Колонки.Добавить("КодВидаОперации", ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(10));
			Если СтруктураШапкиДокумента.Дата >= '20150101' Тогда
				ТаблицаПоАвансам.ЗаполнитьЗначения(СтруктураШапкиДокумента.КодВидаОперации, "КодВидаОперации");
			КонецЕсли;
			
			Для Каждого СтрокаТаблицы Из ТаблицаПоАвансам Цикл
				
				СтрокаТаблицы.СчетФактура = СтрокаОснования.ДокументОснование;
				СтрокаТаблицы.СуммаБезНДС = СтрокаТаблицы.Сумма - СтрокаТаблицы.НДС;
			
			КонецЦикла;
			
		КонецЦикла; 
	ИначеЕсли ПодготовитьТаблицыДокументовОснования Тогда
		
		ТаблицаПоДокументамОснованиям.Колонки.Добавить("ТаблицаДанных");
		
		Для Каждого СтрокаОснования Из ТаблицаПоДокументамОснованиям Цикл
			
			Если Не ЗначениеЗаполнено(СтрокаОснования.ДокументОснование) Тогда
				Продолжить;
			КонецЕсли;
			
			Если ТипЗнч(СтрокаОснования.ДокументОснование) = Тип("ДокументСсылка.ДокументРасчетовСКонтрагентом") 
				ИЛИ СтруктураШапкиДокумента.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыПолученного.НаАванс Тогда
				СтрокаОснования.УчитыватьНДС = Истина;
				СтрокаОснования.НДСВключенВСтоимость = Ложь;
				СтрокаОснования.СуммаВключаетНДС = Ложь;
			КонецЕсли;
			
			Если СтрокаОснования.НДСВключенВСтоимость Тогда
				Продолжить;
			КонецЕсли;
			
			СтрокаОснования.ТаблицаДанных = УчетНДС.ПолучитьТаблицуДокументаНДС(СтрокаОснования.ДокументОснование);
			
			Если СтрокаОснования.ТаблицаДанных = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Если ТипЗнч(СтрокаОснования.ДокументОснование) = Тип("ДокументСсылка.АвансовыйОтчет") Тогда
				СтрокиКУдалению = Новый Массив;
				Для Каждого СтрокаТаблицы Из СтрокаОснования.ТаблицаДанных Цикл
					Если СтрокаТаблицы.СчетФактура <> Ссылка Тогда
						СтрокиКУдалению.Добавить(СтрокаТаблицы);
					КонецЕсли;
				КонецЦикла;
				
				Для Каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
					СтрокаОснования.ТаблицаДанных.Удалить(СтрокаКУдалению);
				КонецЦикла;
			КонецЕсли;
			
			Если СтрокаОснования.ТаблицаДанных.Колонки.Найти("СчетФактура") = Неопределено Тогда
				
				СтрокаОснования.ТаблицаДанных.Колонки.Добавить("СчетФактура", Новый ОписаниеТипов(ТаблицаПоДокументамОснованиям.Колонки.ДокументОснование.ТипЗначения));
				СтрокаОснования.ТаблицаДанных.ЗаполнитьЗначения(СтрокаОснования.ДокументОснование, "СчетФактура");
				
			КонецЕсли;			
			
		КонецЦикла; 
	КонецЕсли;
		
КонецПроцедуры // СформироватьТаблицыДокумента()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ФОРМИРОВАНИЯ ДВИЖЕНИЙ

Процедура ДвиженияПоРегистрам(СтруктураШапкиДокумента, ТаблицаДокумента, Отказ, Заголовок)
	
	УчетНДСФормированиеДвижений.СформироватьДвиженияСчетФактураПолученныйНаАванс(СтруктураШапкиДокумента, ТаблицаДокумента);
	
	
	Если НЕ СтруктураШапкиДокумента.БланкСтрогойОтчетности Тогда
		
		ТаблицаЗаписейЖурнала = Новый ТаблицаЗначений;
		Для Каждого Элемент ИЗ СтруктураШапкиДокумента Цикл
			МассивТипов = Новый Массив;
			МассивТипов.Добавить(ТипЗнч(Элемент.Значение));
			ТаблицаЗаписейЖурнала.Колонки.Добавить(Элемент.Ключ, Новый ОписаниеТипов(МассивТипов));
		КонецЦикла;
		ТаблицаЗаписейЖурнала.Колонки.Добавить("ИндексСтроки", Новый ОписаниеТипов("Число"));
		ТаблицаЗаписейЖурнала.Колонки.Добавить("НомерИсправленияКорректировочногоСчетаФактуры", Новый ОписаниеТипов("Строка"));
		ТаблицаЗаписейЖурнала.Колонки.Добавить("ДатаИсправленияКорректировочногоСчетаФактуры", Новый ОписаниеТипов("Дата"));
		
		Если СтруктураШапкиДокумента.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыПолученного.Корректировочный Тогда
			
			СтруктураСтроки = Новый Структура;
			Для Каждого Элемент ИЗ СтруктураШапкиДокумента Цикл
				СтруктураСтроки.Вставить(Элемент.Ключ, Элемент.Значение);
			КонецЦикла;
			
			Для Каждого СтрокаОснования ИЗ ТаблицаДокумента Цикл
				
				СтруктураСтроки.Вставить("ДокументОснование", 		СтрокаОснования.ДокументОснование);
				СтруктураСтроки.Вставить("НомерИсходногоДокумента", СтрокаОснования.НомерИсходногоДокумента);
				СтруктураСтроки.Вставить("ДатаИсходногоДокумента", 	СтрокаОснования.ДатаИсходногоДокумента);
				СтруктураСтроки.Вставить("СуммаУвеличение", 		СтрокаОснования.СуммаУвеличение);
				СтруктураСтроки.Вставить("СуммаУменьшение", 		СтрокаОснования.СуммаУменьшение);
				СтруктураСтроки.Вставить("СуммаНДСУвеличение", 		СтрокаОснования.СуммаНДСУвеличение);
				СтруктураСтроки.Вставить("СуммаНДСУменьшение", 		СтрокаОснования.СуммаНДСУменьшение);
				СтруктураСтроки.Вставить("ИндексСтроки", 			СтрокаОснования.НомерСтроки - 1);
				
				Если СтруктураШапкиДокумента.Исправление Тогда 
					СтруктураСтроки.Вставить("НомерИсправленияКорректировочногоСчетаФактуры", СтруктураШапкиДокумента.НомерИсправления);
					СтруктураСтроки.Вставить("ДатаИсправленияКорректировочногоСчетаФактуры",  СтруктураШапкиДокумента.ДатаИсправления);
				Иначе
					СтруктураСтроки.Вставить("НомерИсправленияКорректировочногоСчетаФактуры", Неопределено);
					СтруктураСтроки.Вставить("ДатаИсправленияКорректировочногоСчетаФактуры",  Неопределено);
				КонецЕсли;
				
				СтруктураСтроки.Вставить("Исправление", СтрокаОснования.УчитыватьИсправлениеИсходногоДокумента);
				Если СтрокаОснования.УчитыватьИсправлениеИсходногоДокумента Тогда
					СтруктураСтроки.Вставить("НомерИсправления", СтрокаОснования.НомерИсправленияИсходногоДокумента);
					СтруктураСтроки.Вставить("ДатаИсправления",  СтрокаОснования.ДатаИсправленияИсходногоДокумента);
				Иначе
					СтруктураСтроки.Вставить("НомерИсправления", Неопределено);
					СтруктураСтроки.Вставить("ДатаИсправления",  Неопределено);
				КонецЕсли;
				
				ЗаписьЖурнала = ТаблицаЗаписейЖурнала.Добавить();
				ЗаполнитьЗначенияСвойств(ЗаписьЖурнала, СтруктураСтроки);
				
			КонецЦикла;
			
		ИначеЕсли СтруктураШапкиДокумента.СводныйКомиссионный Тогда
			
			СтруктураСтроки = Новый Структура();
			Для Каждого Элемент Из СтруктураШапкиДокумента Цикл
				СтруктураСтроки.Вставить(Элемент.Ключ, Элемент.Значение);
			КонецЦикла;
			
			СтруктураСтроки.Вставить("ИндексСтроки");
			
			ТаблицаСводныхСчетовФактур = ПолучитьДанныеСводногоСчетаФактуры(СтруктураШапкиДокумента);
			
			Если ТаблицаСводныхСчетовФактур = Неопределено Тогда
				Возврат;
			КонецЕсли;
			
			ТаблицаЗаписейЖурнала.Колонки.Добавить("СуммаДокументаДляРегистрацииВЖурнале");
			ТаблицаЗаписейЖурнала.Колонки.Добавить("НДСДокументаДляРегистрацииВЖурнале");
			
			Для Каждого СтрокаСводныхСчетовФактур Из ТаблицаСводныхСчетовФактур Цикл
				
				ЗаполнитьЗначенияСвойств(СтруктураСтроки, СтруктураШапкиДокумента);
				ЗаполнитьЗначенияСвойств(СтруктураСтроки, СтрокаСводныхСчетовФактур);
				
				ЗаписьЖурнала = ТаблицаЗаписейЖурнала.Добавить();
				ЗаполнитьЗначенияСвойств(ЗаписьЖурнала, СтруктураСтроки);
				
				Если СтрокаСводныхСчетовФактур.СуммаПоСчетуФактуре <> 0 Или СтрокаСводныхСчетовФактур.СуммаНДС <> 0 Тогда
					ЗаписьЖурнала.СуммаДокументаДляРегистрацииВЖурнале = СтрокаСводныхСчетовФактур.СуммаПоСчетуФактуре;
					ЗаписьЖурнала.НДСДокументаДляРегистрацииВЖурнале = СтрокаСводныхСчетовФактур.СуммаНДС;
				ИначеЕсли СтрокаСводныхСчетовФактур.СуммаПоСчетуФактуреКомиссия <> 0 Или СтрокаСводныхСчетовФактур.СуммаНДСКомиссия <> 0 Тогда
					ЗаписьЖурнала.СуммаДокументаДляРегистрацииВЖурнале = СтрокаСводныхСчетовФактур.СуммаПоСчетуФактуреКомиссия;
					ЗаписьЖурнала.НДСДокументаДляРегистрацииВЖурнале = СтрокаСводныхСчетовФактур.СуммаНДСКомиссия;
				КонецЕсли;
				
			КонецЦикла;
			
		Иначе
			
			ЗаписьЖурнала = ТаблицаЗаписейЖурнала.Добавить();
			ЗаполнитьЗначенияСвойств(ЗаписьЖурнала, СтруктураШапкиДокумента);
			
		КонецЕсли;
		
		УчетНДСФормированиеДвижений.ЗарегистрироватьВЖурналеУчетаСчетовФактур(ТаблицаЗаписейЖурнала, СуммаДокумента, СуммаНДСДокумента, Отказ, Заголовок);
		
	КонецЕсли;	
		
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаЗаполнения(Основание)
	
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
		ЗаполнитьНаОснованииСчетаФактуры(Основание);
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ОтчетКомитентуОПродажах") Тогда
					
		// Заполним реквизиты из стандартного набора по документу основанию.
		
		ВидСчетаФактуры = Перечисления.ВидСчетаФактурыПолученного.НаПоступление;
		ДокументОснование = Основание.Ссылка;	
		
		СтрокаОснования = ДокументыОснования.Добавить();
		СтрокаОснования.ДокументОснование = ДокументОснование;
			
		Контрагент = ДокументОснование.Контрагент;
		ДоговорКонтрагента = ДокументОснование.ДоговорКонтрагента;
		
		СтрокаАвансов = Авансы.Добавить();
		УстановитьКодВидаОперации();
			
	ИначеЕсли ЗначениеЗаполнено(Основание) 
		И ЭтотОбъект.Метаданные().ТабличныеЧасти.ДокументыОснования.Реквизиты.ДокументОснование.Тип.СодержитТип(ТипЗнч(Основание)) Тогда
		
		Дата = Основание.Дата;
		
		СтрДокОснования = ДокументыОснования.Добавить();
		СтрДокОснования.ДокументОснование = Основание.Ссылка;

		// Заполним реквизиты из стандартного набора по документу основанию.
		ЗаполнениеДокументов.ЗаполнитьШапкуДокументаПоОснованию(ЭтотОбъект, Основание);
		
		Если ТипЗнч(Основание) = Тип("ДокументСсылка.КорректировкаПоступления") Тогда
			
			// Для документа "Корректировка поступления" возможен ввод двух видов счетов-фактур
			// на аванс (на суммы переплат) или на поступившие ценности (исправленный или корректировочный)
			
			// Сначала ищем счет-фактуру на приобретенные ценности
			СписокВидовНаПриобретенныеЦенности = Новый СписокЗначений;
			СписокВидовНаПриобретенныеЦенности.Добавить(Перечисления.ВидСчетаФактурыПолученного.НаПоступление);
			СписокВидовНаПриобретенныеЦенности.Добавить(Перечисления.ВидСчетаФактурыПолученного.Корректировочный);
	
			СтруктураОтбора = Новый Структура;
			СтруктураОтбора.Вставить("ПометкаУдаления", Ложь);
			СтруктураОтбора.Вставить("ВидСчетаФактуры", СписокВидовНаПриобретенныеЦенности);
			
			СуществующийСчетФактура = УчетНДС.НайтиПодчиненныйСчетФактуру(Основание, "СчетФактураПолученный", СтруктураОтбора);
			Если СуществующийСчетФактура = Неопределено Тогда
				
				// Нет СФ на приобретенные ценности - создаем его
				ЗаполнитьНаОснованииИсправления(Основание, СтрДокОснования);
				ОпределениеПараметровСчетаФактуры();
				
			Иначе
				
				// Если нашли СФ на приобретенные ценности - дополнительно ищем СФ на аванс
				СтруктураОтбора.Вставить("ВидСчетаФактуры", Перечисления.ВидСчетаФактурыПолученного.НаАванс);
				СуществующийСчетФактураНаАванс = УчетНДС.НайтиПодчиненныйСчетФактуру(Основание, "СчетФактураПолученный", СтруктураОтбора);
				
				// Не нашли СФ на аванс - будем пытаться его создать
				Если СуществующийСчетФактураНаАванс = Неопределено Тогда
					ВалютаДокумента = мВалютаРегламентированногоУчета;
					ВидСчетаФактуры = Перечисления.ВидСчетаФактурыПолученного.НаАванс;
					ЗаполнитьСчетФактуруНаАванс();
					УстановитьКодВидаОперации();
				Иначе
					ЗаполнитьНаОснованииИсправления(Основание, СтрДокОснования);
					ОпределениеПараметровСчетаФактуры();
				КонецЕсли;
				
			КонецЕсли;
			
		Иначе
			
			СписокТиповНаАванс = ПолучитьСписокТиповПоВидуСчетаФактуры(Перечисления.ВидСчетаФактурыПолученного.НаАванс);
			
			Если СписокТиповНаАванс.Найти(ТипЗнч(Основание)) <> Неопределено Тогда
				ВидСчетаФактуры = Перечисления.ВидСчетаФактурыПолученного.НаАванс;
				ЗаполнитьСчетФактуруНаАванс();
			Иначе
				ВидСчетаФактуры = Перечисления.ВидСчетаФактурыПолученного.НаПоступление;
				ОпределениеПараметровСчетаФактуры();
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Заполнить реквизиты значениями по умолчанию.
	ЗаполнениеДокументов.ЗаполнитьШапкуДокумента(ЭтотОбъект);

КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	
	ДокументОснование = Неопределено;
	ДокументыОснования.Очистить();
	ЗаполнениеДокументов.ЗаполнитьШапкуДокумента(ЭтотОбъект, , ОбъектКопирования.Ссылка);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	мУдалятьДвижения = НЕ ЭтоНовый();
	//начало изменений Ожиганов 28.05.2015 немножко оптимизируем 
	ПРГ_ДопФункцииКлиентСервер.ДобавитьВОбъектСвойстваДляУдаленияДвижения(ЭтотОбъект,РежимЗаписи);
	//конец изменений 	
	
	Если НЕ мОбновлятьРеквизитыПриЗаписи 
		ИЛИ ПометкаУдаления Тогда
		Возврат;
	КонецЕсли;

	Если ЗначениеЗаполнено(ДокументОснование) 
		И ДокументыОснования.Найти(ДокументОснование) = Неопределено Тогда
		ДокументОснование = Неопределено;
	КонецЕсли;
	
	Если ДокументОснование = Неопределено 
		И ДокументыОснования.Количество() > 0 Тогда
		ДокументОснование = ДокументыОснования[0].ДокументОснование;
	КонецЕсли;
	
	Если НЕ СформированПриВводеНачальныхОстатковНДС Тогда
		Если (ВидСчетаФактуры = Перечисления.ВидСчетаФактурыПолученного.НаПоступление
			ИЛИ ВидСчетаФактуры = Перечисления.ВидСчетаФактурыПолученного.Корректировочный)
			И НЕ СформированПриВводеНачальныхОстатковНДС Тогда
			ОпределениеПараметровСчетаФактуры();
		Иначе
			СводныйКорректировочный = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если ВидСчетаФактуры = Перечисления.ВидСчетаФактурыПолученного.НаАванс 
		И Авансы.Количество() = 0 Тогда
		Авансы.Добавить();
	КонецЕсли;
	
	ВидДоговораКонтрагента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДоговорКонтрагента, "ВидДоговора");

	Если ВидДоговораКонтрагента = Перечисления.ВидыДоговоровКонтрагентов.СПоставщиком Тогда
		
		// Счет-фактура полученный покупателем-комитентом от комиссионера
		
		Если ЗначениеЗаполнено(Продавец) И Продавцы.Количество() = 0 Тогда
			НоваяСтрока = Продавцы.Добавить();
			НоваяСтрока.Продавец = Продавец;
		КонецЕсли;
		
		Если Продавцы.Количество() > 0 И НЕ ЗначениеЗаполнено(Продавец) Тогда
			Продавец = Продавцы[0].Продавец;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Продавец)
			И Продавцы.Найти(Продавец) = Неопределено
			И Продавцы.Количество() > 0 Тогда
				Продавец = Продавцы[0].Продавец;
		КонецЕсли;
		
		Если Продавцы.Количество() > 1 И Дата < '20150101' Тогда
			Продавцы.Очистить();
			НоваяСтрока = Продавцы.Добавить();
			НоваяСтрока.Продавец = Продавец;
		КонецЕсли;
		
		Если СчетаФактурыВыданныеПокупателям.Количество() > 0 Тогда
			СчетаФактурыВыданныеПокупателям.Очистить();
		КонецЕсли;
		
		СчетФактураВыданныйПокупателю = Неопределено;
		Субкомиссионер = Неопределено;
		
	ИначеЕсли ТипЗнч(ДокументыОснования[0].ДокументОснование) = Тип("ДокументСсылка.ОтчетКомитентуОПродажах") Тогда
		
		// Счет-фактура полученный комиссионером от комитента-продавца
		
		Если ЗначениеЗаполнено(СчетФактураВыданныйПокупателю) И СчетаФактурыВыданныеПокупателям.Количество() = 0 Тогда
			НоваяСтрока = СчетаФактурыВыданныеПокупателям.Добавить();
			НоваяСтрока.СчетФактура = СчетФактураВыданныйПокупателю;
			НоваяСтрока.Субкомиссионер = Субкомиссионер;
			НоваяСтрока.НДС = СуммаНДСДокумента;
			НоваяСтрока.Сумма = СуммаДокумента;
		КонецЕсли;
		
		Если СчетаФактурыВыданныеПокупателям.Количество() > 0 И НЕ ЗначениеЗаполнено(СчетФактураВыданныйПокупателю) Тогда
			СчетФактураВыданныйПокупателю = СчетаФактурыВыданныеПокупателям[0].СчетФактура;
			Субкомиссионер = СчетаФактурыВыданныеПокупателям[0].Субкомиссионер;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СчетФактураВыданныйПокупателю)
			И СчетаФактурыВыданныеПокупателям.Найти(СчетФактураВыданныйПокупателю,"СчетФактура") = Неопределено
			И СчетаФактурыВыданныеПокупателям.Количество() > 0 Тогда
				СчетФактураВыданныйПокупателю = СчетаФактурыВыданныеПокупателям[0].СчетФактура;
				Субкомиссионер = СчетаФактурыВыданныеПокупателям[0].Субкомиссионер;
		КонецЕсли;
		
		Если СчетаФактурыВыданныеПокупателям.Количество() > 1 И Дата < '20150101' Тогда
			СчетаФактурыВыданныеПокупателям.Очистить();
			НоваяСтрока = СчетаФактурыВыданныеПокупателям.Добавить();
			НоваяСтрока.СчетФактура = СчетФактураВыданныйПокупателю;
			НоваяСтрока.Субкомиссионер = Субкомиссионер;
			НоваяСтрока.НДС = СуммаНДСДокумента;
			НоваяСтрока.Сумма = СуммаДокумента;
		КонецЕсли;
		
		Если Продавцы.Количество() > 0 Тогда
			Продавцы.Очистить();
		КонецЕсли;
		
		Продавец = Неопределено;
		
		// Реквизиты такого счета-фактуры вносятся вручную
		СчетФактураБезНДС = СуммаНДСДокумента = 0;
		
	КонецЕсли;
	
	СводныйКомиссионный = СводныйКомиссионный();
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ)
	
	Перем Заголовок, СтруктураШапкиДокумента, ТаблицаПоДокументамОснованиям;
	
	Если мУдалятьДвижения Тогда
		ОбщегоНазначения.УдалитьДвиженияРегистратора(ЭтотОбъект, Отказ);
	КонецЕсли;
	
	Если СформированПриВводеНачальныхОстатковНДС Тогда
		Возврат;
	КонецЕсли;
	
	ПодготовитьСтруктуруШапкиДокумента(Заголовок, СтруктураШапкиДокумента, Отказ);
	
	ПроверитьЗаполнениеШапки(СтруктураШапкиДокумента, Отказ, Заголовок);
	
	ПодготовитьТаблицыДокумента(СтруктураШапкиДокумента, ТаблицаПоДокументамОснованиям);
	
	ПроверитьЗаполнениеТабличнойЧастиПоДокументамОснованиям(ТаблицаПоДокументамОснованиям, Отказ, Заголовок);
	
	Если НЕ Отказ Тогда
		ДвиженияПоРегистрам(СтруктураШапкиДокумента, ТаблицаПоДокументамОснованиям, Отказ, Заголовок);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	НепроверяемыеРеквизиты = Новый Массив;
		
	Если СформированПриВводеНачальныхОстатковНДС Тогда
		НепроверяемыеРеквизиты.Добавить("ДокументыОснования.ДокументОснование");
	КонецЕсли;
	
	Если СформированПриВводеНачальныхОстатковНДС
		ИЛИ ВидСчетаФактуры <> Перечисления.ВидСчетаФактурыПолученного.НаАванс
		ИЛИ СчетаФактурыВыданныеПокупателям.Количество() > 0 Тогда
		НепроверяемыеРеквизиты.Добавить("Авансы.Сумма");
		НепроверяемыеРеквизиты.Добавить("Авансы.СтавкаНДС");
	КонецЕсли;
	
	Если СформированПриВводеНачальныхОстатковНДС Тогда
		НепроверяемыеРеквизиты.Добавить("НомерВходящегоДокумента");
		НепроверяемыеРеквизиты.Добавить("ДатаВходящегоДокумента");
	КонецЕсли;
	
	Если ВидСчетаФактуры <> Перечисления.ВидСчетаФактурыПолученного.Корректировочный Тогда
		НепроверяемыеРеквизиты.Добавить("НомерИсходногоДокумента");
		НепроверяемыеРеквизиты.Добавить("ДатаИсходногоДокумента");				
	КонецЕсли;
	
	ВерсияПостановления = УчетНДС.ПолучитьВерсиюПостановления(Дата);
	Если ВерсияПостановления = 1 Тогда
		НепроверяемыеРеквизиты.Добавить("КодСпособаПолучения");
		НепроверяемыеРеквизиты.Добавить("НомерИсправленияИсходногоДокумента");
		НепроверяемыеРеквизиты.Добавить("ДатаИсправленияИсходногоДокумента");
		НепроверяемыеРеквизиты.Добавить("НомерИсправления");
		НепроверяемыеРеквизиты.Добавить("ДатаИсправления");
		
	Иначе
		
		Если СформированПриВводеНачальныхОстатковНДС Тогда
			НепроверяемыеРеквизиты.Добавить("КодВидаОперации");
			НепроверяемыеРеквизиты.Добавить("КодСпособаПолучения");
		КонецЕсли;
		
		Если СформированПриВводеНачальныхОстатковНДС ИЛИ НЕ Исправление Тогда
			НепроверяемыеРеквизиты.Добавить("НомерИсправления");
			НепроверяемыеРеквизиты.Добавить("ДатаИсправления");			
		КонецЕсли;
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);	
	
КонецПроцедуры

Функция ПолучитьДанныеСводногоСчетаФактуры(СтруктураШапкиДокумента)

	СводныйКомиссияПоПродаже = СчетаФактурыВыданныеПокупателям.Количество() > 1;
	СводныйКомиссияПоЗакупке = Продавцы.Количество() > 1;
	
	ТекстЗапросаРеквизиты =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка,
	|	Реквизиты.Дата,
	|	Реквизиты.Организация,
	|	Реквизиты.ДокументОснование,
	|	Реквизиты.ВалютаДокумента,
	|	Реквизиты.Контрагент,
	|	ВЫБОР
	|		КОГДА Реквизиты.КППКонтрагента ПОДОБНО """"
	|			ТОГДА Реквизиты.Контрагент.КПП
	|		ИНАЧЕ Реквизиты.КППКонтрагента
	|	КОНЕЦ КАК КППКонтрагента,
	|	Реквизиты.Продавец,
	|	Реквизиты.ДоговорКонтрагента,
	|	Реквизиты.ДоговорКонтрагента.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДокументОснованиеДата,
	|	Реквизиты.ДоговорКонтрагента.ВидДоговора КАК ВидДоговора,
	|	Реквизиты.ДоговорКонтрагента.ВидАгентскогоДоговора КАК ВидАгентскогоДоговора,
	|	ЕСТЬNULL(Реквизиты.ДоговорКонтрагента.УчетАгентскогоНДС, ЛОЖЬ) КАК УчетАгентскогоНДС,
	|	ЕСТЬNULL(Реквизиты.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах, ЛОЖЬ) КАК РасчетыВУсловныхЕдиницах,
	|	Реквизиты.ВидСчетаФактуры,
	|	Реквизиты.ДатаВходящегоДокумента,
	|	Реквизиты.НомерВходящегоДокумента,
	|	Реквизиты.Исправление,
	|	Реквизиты.НомерИсправления,
	|	Реквизиты.ДатаИсправления,
	|	Реквизиты.СформированПриВводеНачальныхОстатковНДС,
	|	Реквизиты.БланкСтрогойОтчетности,
	|	Реквизиты.КодСпособаПолучения,
	|	Реквизиты.КодВидаОперации,
	|	Реквизиты.СуммаДокумента,
	|	Реквизиты.СуммаНДСДокумента,
	|	Реквизиты.СуммаУвеличение,
	|	Реквизиты.СуммаУменьшение,
	|	Реквизиты.СуммаНДСУвеличение,
	|	Реквизиты.СуммаНДСУменьшение,
	|	Реквизиты.СуммаДокументаКомиссия,
	|	Реквизиты.СуммаНДСДокументаКомиссия,
	|	Реквизиты.СуммаУвеличениеКомиссия,
	|	Реквизиты.СуммаУменьшениеКомиссия,
	|	Реквизиты.СуммаНДСУвеличениеКомиссия,
	|	Реквизиты.СуммаНДСУменьшениеКомиссия,
	|	Реквизиты.СчетФактураБезНДС,
	|	Реквизиты.Субкомиссионер,
	|	Реквизиты.СчетФактураВыданныйПокупателю,
	|	ВЫБОР
	|		КОГДА Реквизиты.ДоговорКонтрагента.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.СКомитентом)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК РеализацияТоваровКомитента,
	|	ВЫБОР
	|		КОГДА Реквизиты.ДоговорКонтрагента.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.СКомитентом)
	|				И Реквизиты.Дата >= ДАТАВРЕМЯ(2015, 1, 1)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СводныйСчетФактураКомитентаПо1279,
	|	ВЫБОР
	|		КОГДА Реквизиты.ДоговорКонтрагента.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.СПоставщиком)
	|				И Реквизиты.Дата >= ДАТАВРЕМЯ(2015, 1, 1)
	|				И Реквизиты.СводныйКомиссионный
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СводныйСчетФактураКомиссионераПо1279
	|ПОМЕСТИТЬ Реквизиты
	|ИЗ
	|	Документ.СчетФактураПолученный КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|;
	|////////////////////////////////////////////////////////////////////////////////";
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", СтруктураШапкиДокумента.Ссылка);
	
	Если СводныйКомиссияПоПродаже Тогда
		
		Запрос.Текст = ТекстЗапросаРеквизиты + "
		|ВЫБРАТЬ
		|	СУММА(СчетаФактурыВыданныеПокупателям.Сумма) КАК Сумма,
		|	СУММА(СчетаФактурыВыданныеПокупателям.НДС) КАК НДС,
		|	МИНИМУМ(СчетаФактурыВыданныеПокупателям.НомерСтроки) КАК НомерСтроки,
		|	СчетаФактурыВыданныеПокупателям.Субкомиссионер,
		|	СчетаФактурыВыданныеПокупателям.СчетФактура
		|ПОМЕСТИТЬ СчетаФактурыВыданныеПокупателям
		|ИЗ
		|	Документ.СчетФактураПолученный.СчетаФактурыВыданныеПокупателям КАК СчетаФактурыВыданныеПокупателям
		|ГДЕ
		|	СчетаФактурыВыданныеПокупателям.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	СчетаФактурыВыданныеПокупателям.Субкомиссионер,
		|	СчетаФактурыВыданныеПокупателям.СчетФактура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Реквизиты.Дата КАК Период,
		|	Реквизиты.Ссылка КАК Регистратор,
		|	Реквизиты.Организация,
		|	Реквизиты.Контрагент,
		|	Реквизиты.КППКонтрагента,
		|	ВЫБОР
		|		КОГДА Реквизиты.Продавец = Реквизиты.Контрагент
		|				ИЛИ Реквизиты.Продавец = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ Реквизиты.Продавец
		|	КОНЕЦ КАК Продавец,
		|	Реквизиты.Ссылка КАК СчетФактура,
		|	ЗНАЧЕНИЕ(Перечисление.ЧастиЖурналаУчетаСчетовФактур.ПолученныеСчетаФактуры) КАК ЧастьЖурнала,
		|	Реквизиты.Дата КАК ДатаВыставленияПолучения,
		|	Реквизиты.КодСпособаПолучения КАК КодСпособаВыставленияПолучения,
		|	Реквизиты.КодВидаОперации КАК КодВидаОперации,
		|	Реквизиты.НомерВходящегоДокумента КАК НомерСчетаФактуры,
		|	Реквизиты.ДатаВходящегоДокумента КАК ДатаСчетаФактуры,
		|	НЕОПРЕДЕЛЕНО КАК НомерКорректировочногоСчетаФактуры,
		|	НЕОПРЕДЕЛЕНО КАК ДатаКорректировочногоСчетаФактуры,
		|	ВЫБОР
		|		КОГДА Реквизиты.Исправление
		|			ТОГДА Реквизиты.НомерИсправления
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК НомерИсправления,
		|	ВЫБОР
		|		КОГДА Реквизиты.Исправление
		|			ТОГДА Реквизиты.ДатаИсправления
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК ДатаИсправления,
		|	Реквизиты.ВалютаДокумента КАК Валюта,
		|	СчетаФактурыВыданныеПокупателям.Сумма КАК СуммаПоСчетуФактуре,
		|	СчетаФактурыВыданныеПокупателям.Сумма КАК СуммаПоСчетуФактуреКомиссия,
		|	СчетаФактурыВыданныеПокупателям.НДС КАК СуммаНДС,
		|	СчетаФактурыВыданныеПокупателям.НДС КАК СуммаНДСКомиссия,
		|	0 КАК СуммаПоСчетуФактуреРазницаУвеличение,
		|	0 КАК СуммаПоСчетуФактуреРазницаУменьшение,
		|	0 КАК СуммаНДСРазницаУвеличение,
		|	0 КАК СуммаНДСРазницаУменьшение,
		|	0 КАК СуммаПоСчетуФактуреРазницаУвеличениеКомиссия,
		|	0 КАК СуммаПоСчетуФактуреРазницаУменьшениеКомиссия,
		|	0 КАК СуммаНДСРазницаУвеличениеКомиссия,
		|	0 КАК СуммаНДСРазницаУменьшениеКомиссия,
		|	Реквизиты.СчетФактураБезНДС КАК ПоСтавкеБезНДС,
		|	НЕОПРЕДЕЛЕНО КАК СчетФактураНеВыставляется,
		|	Реквизиты.РасчетыВУсловныхЕдиницах,
		|	НЕОПРЕДЕЛЕНО КАК НомерИсправленияКорректировочногоСчетаФактуры,
		|	НЕОПРЕДЕЛЕНО КАК ДатаИсправленияКорректировочногоСчетаФактуры,
		|	СчетаФактурыВыданныеПокупателям.НомерСтроки - 1 КАК ИндексСтроки,
		|	НЕОПРЕДЕЛЕНО КАК ДокументОснование,
		|	СчетаФактурыВыданныеПокупателям.Субкомиссионер КАК Субкомиссионер,
		|	СчетаФактурыВыданныеПокупателям.СчетФактура КАК СчетФактураВыданныйПокупателю,
		|	2 КАК КодВидаСделки,
		|	НЕОПРЕДЕЛЕНО КАК Посредник,
		|	НЕОПРЕДЕЛЕНО КАК НомерСчетаФактурыПродавца,
		|	НЕОПРЕДЕЛЕНО КАК КодВидаОперацииКомиссия
		|ИЗ
		|	Реквизиты КАК Реквизиты
		|		ЛЕВОЕ СОЕДИНЕНИЕ СчетаФактурыВыданныеПокупателям КАК СчетаФактурыВыданныеПокупателям
		|		ПО (ИСТИНА)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ИндексСтроки";
		
	ИначеЕсли СводныйКомиссияПоЗакупке Тогда
		
		Запрос.Текст = ТекстЗапросаРеквизиты + "
		|ВЫБРАТЬ
		|	СчетФактураПолученныйПродавцы.Продавец,
		|	МИНИМУМ(СчетФактураПолученныйПродавцы.НомерСтроки) КАК НомерСтроки
		|ПОМЕСТИТЬ ВременнаяТаблицаПродавцы
		|ИЗ
		|	Документ.СчетФактураПолученный.Продавцы КАК СчетФактураПолученныйПродавцы
		|ГДЕ
		|	СчетФактураПолученныйПродавцы.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	СчетФактураПолученныйПродавцы.Продавец
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Реквизиты.Дата КАК Период,
		|	Реквизиты.Ссылка КАК Регистратор,
		|	Реквизиты.Организация,
		|	Реквизиты.Контрагент,
		|	Реквизиты.КППКонтрагента,
		|	ВЫБОР
		|		КОГДА Реквизиты.Продавец = Реквизиты.Контрагент
		|				ИЛИ Реквизиты.Продавец = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|				ИЛИ Реквизиты.СводныйСчетФактураКомиссионераПо1279
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ Реквизиты.Продавец
		|	КОНЕЦ КАК Продавец,
		|	Реквизиты.Ссылка КАК СчетФактура,
		|	ЗНАЧЕНИЕ(Перечисление.ЧастиЖурналаУчетаСчетовФактур.ПолученныеСчетаФактуры) КАК ЧастьЖурнала,
		|	Реквизиты.Дата КАК ДатаВыставленияПолучения,
		|	Реквизиты.КодСпособаПолучения КАК КодСпособаВыставленияПолучения,
		|	Реквизиты.КодВидаОперации КАК КодВидаОперации,
		|	Реквизиты.НомерВходящегоДокумента КАК НомерСчетаФактуры,
		|	Реквизиты.ДатаВходящегоДокумента КАК ДатаСчетаФактуры,
		|	НЕОПРЕДЕЛЕНО КАК НомерКорректировочногоСчетаФактуры,
		|	НЕОПРЕДЕЛЕНО КАК ДатаКорректировочногоСчетаФактуры,
		|	ВЫБОР
		|		КОГДА Реквизиты.Исправление
		|			ТОГДА Реквизиты.НомерИсправления
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК НомерИсправления,
		|	ВЫБОР
		|		КОГДА Реквизиты.Исправление
		|			ТОГДА Реквизиты.ДатаИсправления
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК ДатаИсправления,
		|	Реквизиты.ВалютаДокумента КАК Валюта,
		|	Реквизиты.СуммаДокумента КАК СуммаПоСчетуФактуре,
		|	Реквизиты.СуммаНДСДокумента КАК СуммаНДС,
		|	ВЫБОР
		|		КОГДА Реквизиты.СуммаНДСДокумента = 0
		|			ИЛИ Реквизиты.СуммаНДСДокументаКомиссия = 0
		|		ТОГДА 0
		|	ИНАЧЕ 
		|		Реквизиты.СуммаНДСДокументаКомиссия
		|	КОНЕЦ КАК СуммаНДСКомиссия,
		|	Реквизиты.СуммаДокументаКомиссия КАК СуммаПоСчетуФактуреКомиссия,
		|	0 КАК СуммаПоСчетуФактуреРазницаУвеличение,
		|	0 КАК СуммаПоСчетуФактуреРазницаУменьшение,
		|	0 КАК СуммаНДСРазницаУвеличение,
		|	0 КАК СуммаНДСРазницаУменьшение,
		|	0 КАК СуммаПоСчетуФактуреРазницаУвеличениеКомиссия,
		|	0 КАК СуммаПоСчетуФактуреРазницаУменьшениеКомиссия,
		|	0 КАК СуммаНДСРазницаУвеличениеКомиссия,
		|	0 КАК СуммаНДСРазницаУменьшениеКомиссия,
		|	Реквизиты.СчетФактураБезНДС КАК ПоСтавкеБезНДС,
		|	НЕОПРЕДЕЛЕНО КАК СчетФактураНеВыставляется,
		|	Реквизиты.РасчетыВУсловныхЕдиницах,
		|	НЕОПРЕДЕЛЕНО КАК НомерИсправленияКорректировочногоСчетаФактуры,
		|	НЕОПРЕДЕЛЕНО КАК ДатаИсправленияКорректировочногоСчетаФактуры,
		|	0 КАК ИндексСтроки,
		|	НЕОПРЕДЕЛЕНО КАК ДокументОснование,
		|	НЕОПРЕДЕЛЕНО КАК Субкомиссионер,
		|	ВЫБОР
		|		КОГДА Реквизиты.РеализацияТоваровКомитента
		|			ТОГДА Реквизиты.СчетФактураВыданныйПокупателю
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК СчетФактураВыданныйПокупателю,
		|	ВЫБОР
		|		КОГДА Реквизиты.РеализацияТоваровКомитента
		|			ТОГДА 2
		|		КОГДА Реквизиты.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыПолученного.НаАванс)
		|			ТОГДА 1
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК КодВидаСделки,
		|	НЕОПРЕДЕЛЕНО КАК Посредник,
		|	НЕОПРЕДЕЛЕНО КАК НомерСчетаФактурыПродавца,
		|	НЕОПРЕДЕЛЕНО КАК КодВидаОперацииКомиссия
		|ИЗ
		|	Реквизиты КАК Реквизиты
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Реквизиты.Дата,
		|	Реквизиты.Ссылка,
		|	Реквизиты.Организация,
		|	Реквизиты.Контрагент,
		|	Реквизиты.КППКонтрагента,
		|	ТаблицаПродавцы.Продавец,
		|	Реквизиты.Ссылка,
		|	ЗНАЧЕНИЕ(Перечисление.ЧастиЖурналаУчетаСчетовФактур.ПолученныеСчетаФактуры),
		|	Реквизиты.Дата,
		|	Реквизиты.КодСпособаПолучения,
		|	Реквизиты.КодВидаОперации,
		|	Реквизиты.НомерВходящегоДокумента,
		|	Реквизиты.ДатаВходящегоДокумента,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ВЫБОР
		|		КОГДА Реквизиты.Исправление
		|			ТОГДА Реквизиты.НомерИсправления
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА Реквизиты.Исправление
		|			ТОГДА Реквизиты.ДатаИсправления
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ,
		|	Реквизиты.ВалютаДокумента,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	Реквизиты.СчетФактураБезНДС,
		|	НЕОПРЕДЕЛЕНО,
		|	Реквизиты.РасчетыВУсловныхЕдиницах,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаПродавцы.НомерСтроки,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО
		|ИЗ
		|	Реквизиты КАК Реквизиты
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаПродавцы КАК ТаблицаПродавцы
		|		ПО (ИСТИНА)
		|ГДЕ
		|	Реквизиты.СводныйСчетФактураКомиссионераПо1279
		|
		|УПОРЯДОЧИТЬ ПО
		|	ИндексСтроки";
		
	Иначе
		
		Возврат Неопределено;
		
	КонецЕсли;
	
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции

Функция СводныйКомиссионный()

	Возврат	Дата >= '20150101'
			И ВидСчетаФактуры <> Перечисления.ВидСчетаФактурыПолученного.Корректировочный
			И (СчетаФактурыВыданныеПокупателям.Количество() > 1 ИЛИ Продавцы.Количество() > 1);

КонецФункции

мВалютаРегламентированногоУчета = глЗначениеПеременной("ВалютаРегламентированногоУчета");

мОбновлятьРеквизитыПриЗаписи = Истина;

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураСертификата = Параметры.СтруктураСертификата;
	Отпечаток = Параметры.Отпечаток;
	
	Если Параметры.Свойство("АдресСертификата") Тогда
		АдресСертификата = Параметры.АдресСертификата;
	КонецЕсли;	
	
	КомуВыдан = СтруктураСертификата.КомуВыдан;
	КемВыдан = СтруктураСертификата.КемВыдан;
	ДействителенДо = СтруктураСертификата.ДействителенДо;
	Назначение = СтруктураСертификата.Назначение;
	
	НовоеНазначение = "";
	ДобавлятьКодНазначения = Истина;
	ЭлектроннаяЦифроваяПодпись.ЗаполнитьНазначениеСертификата(Назначение, НовоеНазначение, ДобавлятьКодНазначения);
	Назначение = НовоеНазначение;
	
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	
	Если НЕ ПодключитьРасширениеРаботыСФайлами() Тогда
		Возврат;
	КонецЕсли;
		
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	Фильтр = НСтр("ru = 'Все файлы(*.cer)|*.cer'");
	ДиалогОткрытияФайла.Фильтр = Фильтр;
	ДиалогОткрытияФайла.МножественныйВыбор = Ложь;
	ДиалогОткрытияФайла.Заголовок = НСтр("ru = 'Выберите файл для сохранения сертификата'");
	Если ДиалогОткрытияФайла.Выбрать() Тогда
		
		ДвоичныеДанныеОтпечатка = Base64Значение(Отпечаток);
		
		МенеджерКриптографии = ЭлектроннаяЦифроваяПодписьКлиент.ПолучитьМенеджерКриптографии();

		Сертификат = Неопределено;
		Если НЕ ПустаяСтрока(АдресСертификата) Тогда
			ДвоичныеДанныеСертификата = ПолучитьИзВременногоХранилища(АдресСертификата);
			Сертификат = Новый СертификатКриптографии(ДвоичныеДанныеСертификата);
		Иначе	
			ХранилищеСертификатов = МенеджерКриптографии.ПолучитьХранилищеСертификатов();
			Сертификат = ХранилищеСертификатов.НайтиПоОтпечатку(ДвоичныеДанныеОтпечатка);
		КонецЕсли;	
		
		Если Сертификат = Неопределено Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Сертификат не найден'"));
			Возврат;
		КонецЕсли;
		
		Сертификат.Выгрузить(ДиалогОткрытияФайла.ПолноеИмяФайла);
		
		Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Сертификат ЭЦП сохранен в файл ""%1""'"),
			ДиалогОткрытияФайла.ПолноеИмяФайла);
		Состояние(Текст);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Проверить(Команда)
	
	ДвоичныеДанныеОтпечатка = Base64Значение(Отпечаток);
	
	МенеджерКриптографии = ЭлектроннаяЦифроваяПодписьКлиент.ПолучитьМенеджерКриптографии();

	Сертификат = Неопределено;
	Если НЕ ПустаяСтрока(АдресСертификата) Тогда
		ДвоичныеДанныеСертификата = ПолучитьИзВременногоХранилища(АдресСертификата);
		Сертификат = Новый СертификатКриптографии(ДвоичныеДанныеСертификата);
	Иначе	
		ХранилищеСертификатов = МенеджерКриптографии.ПолучитьХранилищеСертификатов();
		Сертификат = ХранилищеСертификатов.НайтиПоОтпечатку(ДвоичныеДанныеОтпечатка);
	КонецЕсли;	
	
	Если Сертификат = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю((НСтр("ru = 'Сертификат не найден'")));
		Возврат;
	КонецЕсли;
	
	Попытка
		МенеджерКриптографии.ПроверитьСертификат(Сертификат, РежимПроверкиСертификатаКриптографии.РазрешитьТестовыеСертификаты);
		Предупреждение(НСтр("ru = 'Сертификат действителен'"));
	Исключение
		Текст = НСтр("ru = 'Сертификат не действителен.'") + Символы.ПС + КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		Предупреждение(Текст);
	КонецПопытки;	
	
КонецПроцедуры

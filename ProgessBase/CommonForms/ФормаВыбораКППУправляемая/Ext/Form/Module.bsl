////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Параметры.Контрагент) Тогда
		Отказ	= Истина;
		Возврат;
	КонецЕсли;
	
	СвойстваКонтрагента	= ОбщегоНазначения.ПолучитьЗначенияРеквизитов(Параметры.Контрагент, "КПП, ЮрФизЛицо");
	
	Если СвойстваКонтрагента.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru ='КПП для физических лиц не задается'"), , , , Отказ);
		Отказ	= Истина;
		Возврат;
	КонецЕсли;
	
	КППКонтрагента	= Параметры.КППКонтрагента;
	ЗаполнитьСписокВыбораКППКонтрагента(Элементы.КППКонтрагента.СписокВыбора, Параметры.Контрагент);
	Если ПустаяСтрока(КППКонтрагента) И Элементы.КППКонтрагента.СписокВыбора.Количество() > 0 Тогда
		КППКонтрагента	= Элементы.КППКонтрагента.СписокВыбора[0].Значение;
	КонецЕсли;
	
	Если Параметры.РольКонтрагента = "Поставщик" Тогда
		Заголовок	= НСтр("ru = 'КПП поставщика'");
		Элементы.СтраницыПодсказки.ТекущаяСтраница	= Элементы.СтраницаПодсказкаПоставщика;
	ИначеЕсли Параметры.РольКонтрагента = "Покупатель" Тогда
		Заголовок	= НСтр("ru = 'КПП покупателя'");
		Элементы.СтраницыПодсказки.ТекущаяСтраница	= Элементы.СтраницаПодсказкаПокупателя;
	Иначе
		Заголовок	= НСтр("ru = 'КПП контрагента'");
		Элементы.СтраницыПодсказки.ТекущаяСтраница	= Элементы.СтраницаПодсказкаПустая;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		
		Ответ = Вопрос(НСтр("ru = 'Сохранить изменения?'"), РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Да);
		
		Если Ответ = КодВозвратаДиалога.Да Тогда
			СохранитьИзменения();
		ИначеЕсли Ответ = КодВозвратаДиалога.Отмена Тогда
			Отказ = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ОК(Команда)
	
	СохранитьИзменения();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервереБезКонтекста
Процедура ЗаполнитьСписокВыбораКППКонтрагента(СписокВыбора, ГоловнойКонтрагент)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ГоловнойКонтрагент", ГоловнойКонтрагент);

	// Первый элемент - всегда КПП головного контрагента, даже, если не задан
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Контрагенты.КПП КАК КПП,
		|	Контрагенты.Представление КАК ПредставлениеКонтрагента
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|ГДЕ
		|	Контрагенты.Ссылка = &ГоловнойКонтрагент
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Контрагенты.КПП,
		|	Контрагенты.Представление
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|ГДЕ
		|	НЕ Контрагенты.Ссылка = &ГоловнойКонтрагент
		|	И Контрагенты.ОбособленноеПодразделение
		|	И Контрагенты.ГоловнойКонтрагент = &ГоловнойКонтрагент
		|	И НЕ Контрагенты.ПометкаУдаления
		|	И НЕ Контрагенты.КПП ПОДОБНО """"";
	
	СписокВыбора.Очистить();
	Выборка	= Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ПредставлениеКПП	= СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"%1 (%2)", ?(ПустаяСтрока(Выборка.КПП), "<не задан>", Выборка.КПП), Выборка.ПредставлениеКонтрагента);
		СписокВыбора.Добавить(Выборка.КПП, ПредставлениеКПП);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИзменения()
	
	Если ПроверитьЗаполнение() Тогда
		Модифицированность	= Ложь;
		
		ЗначениеВыбора	= Новый Структура("КППКонтрагента", КППКонтрагента);
			
		ОповеститьОВыборе(ЗначениеВыбора);
	КонецЕсли;
	
КонецПроцедуры

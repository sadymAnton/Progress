
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

&НаСервере
Процедура ДобавитьПарольВСертификат(Пароль)
	
	Сертификат = ВыбранныйСертификат.ПолучитьОбъект();
	Сертификат.ЗапомнитьПарольКСертификату = Истина;
	Сертификат.ПарольПользователя = Пароль;
	Сертификат.Записать();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьВидимость()
	
	Если Не ЗначениеЗаполнено(Элементы.ДекорацияОперация.Заголовок)
		ИЛИ Элементы.ДекорацияОперация.Заголовок = "Установка пароля в сертификате" Тогда
		
		Элементы.ГруппаЗаполнитьПароль.Видимость = Ложь;
	КонецЕсли;
	Если ЗначениеЗаполнено(ВыбранныйСертификат) Тогда
		
		Элементы.ВыбранныйСертификат.ТолькоПросмотр = Истина;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("ВидОперации") Тогда
		Элементы.ДекорацияОперация.Заголовок = Параметры.ВидОперации;
	КонецЕсли;
	ВыбранныйСертификат = Параметры.ПредставлениеСертификата;
	
	Элементы.ГруппаОбъектОперации.Видимость = Ложь;
	Если Параметры.Свойство("ПакетЭД") Тогда
		ПакетЭД = Параметры.ПакетЭД;
		ТекстГиперссылки = НСтр("ru = 'Пакет ЭД: %1'");
		ТекстГиперссылки = СтрЗаменить(ТекстГиперссылки, "%1", Параметры.ПакетЭД);
		Элементы.ПредварительныйПросмотрДокумента.Заголовок = ТекстГиперссылки;
		Элементы.ГруппаОбъектОперации.Видимость = Истина;
	КонецЕсли;
	
	СписокЭД = "";
	Если Параметры.Свойство("Документы", СписокЭД) И СписокЭД <> Неопределено Тогда
		Для каждого СтрокаЭД Из Параметры.Документы Цикл
			НоваяСтрока = ТаблицаЭД.Добавить();
			НоваяСтрока.ЭлектронныйДокумент = СтрокаЭД;
			НоваяСтрока.направлениеЭД = Перечисления.НаправленияЭД.Исходящий;
			НоваяСтрока.ВладелецЭД = СтрокаЭД.ВладелецФайла;
		КонецЦикла;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТаблицаЭД) Тогда
		Если Параметры.Документы.Количество() = 1 Тогда
			
			
			ТекстГиперссылки = НСтр("ru = 'Документ: %1'");
			
			СтруктураРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Параметры.Документы[0].ВладелецФайла, "Номер, Дата");
			ШаблонФайла = НСтр("ru = '%1 № %2 от %3'");
			НаименованиеФайла = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонФайла,
				Строка(ТипЗнч(Параметры.Документы[0].ВладелецФайла)),
			СтруктураРеквизитов.Номер, Формат(СтруктураРеквизитов.Дата, "ДЛФ=Д"));
			НаименованиеФайла = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(НаименованиеФайла);
			
			ТекстГиперссылки = СтрЗаменить(ТекстГиперссылки, "%1", НаименованиеФайла);
		ИначеЕсли Параметры.Документы.Количество() > 1 Тогда
			
			ТекстГиперссылки = НСтр("ru = 'Открыть список электронных документов (%1)'");
			ТекстГиперссылки = СтрЗаменить(ТекстГиперссылки, "%1", Параметры.Документы.Количество());
			
		КонецЕсли;
		
		Элементы.ПредварительныйПросмотрДокумента.Заголовок = ТекстГиперссылки;
		Элементы.ГруппаОбъектОперации.Видимость = Истина;
	КонецЕсли;
	
	УстановитьДоступностьВидимость();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура Выбрать(Команда)
	
	Если ЗапомнитьПароль Тогда
		ДобавитьПарольВСертификат(Пароль);
	КонецЕсли;
	
	ЭтаФорма.Закрыть(Пароль);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредварительныйПросмотрДокументаНажатие(Элемент)
	
	Если ТаблицаЭД.Количество() > 1 Тогда
		МассивСтруктур = Новый Массив;
		Для Каждого СтрокаДанных Из ТаблицаЭД Цикл
			СтруктураПараметров = Новый Структура;
			СтруктураПараметров.Вставить("НаправлениеЭД", СтрокаДанных.НаправлениеЭД);
			СтруктураПараметров.Вставить("ЭлектронныйДокумент", СтрокаДанных.ЭлектронныйДокумент);
			СтруктураПараметров.Вставить("ВладелецЭД", СтрокаДанных.ВладелецЭД);
			МассивСтруктур.Добавить(СтруктураПараметров);
		КонецЦикла;
		ФормаПросмотраЭД = ОткрытьФорму("Обработка.ЭлектронныеДокументы.Форма.ФормаСпискаВыгружаемыхДокументов",
			Новый Структура("СтруктураЭД", МассивСтруктур), ЭтаФорма);
	Иначе
		ЭлектронныеДокументыСлужебныйКлиент.ОткрытьЭДДляПросмотра(ТаблицаЭД[0].ЭлектронныйДокумент);
	КонецЕсли;
	
КонецПроцедуры
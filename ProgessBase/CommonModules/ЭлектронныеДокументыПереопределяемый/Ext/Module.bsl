////////////////////////////////////////////////////////////////////////////////
// ЭлектронныеДокументыПереопределяемый: механизм обмена электронными документами.
//
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Осуществляет разбор файла с реквизитами контрагента
// в ней можно внести изменения в структуру возвращаемых данных
//
// Параметры:
//  СсылкаНаФайл - адрес хранилища файла с реквизитами контрагента;
//  СтруктураВозврата - Структура - перечень параметров;
//  ОшибкаРазбора - текст, описание ошибки.
//
// Возвращаемое значение:
//  РезультатРазбора - Булево - Истина - разбор файла выполнен; Ложь - разбор файла не выполнялся.
//
Процедура РазобратьФайлРеквизитовКонтрагента(СсылкаНаФайл, СтруктураВозврата, РезультатРазбора, ОшибкаРазбора) Экспорт
	
	
КонецПроцедуры

// Определяет текст сообщения о необходимости настройки системы в зависимости от вида операции.
//
// Параметры:
//  ВидОперации    - строка - признак выполняемой операции;
//  ТекстСообщения - строка - текст сообщения.
//
Процедура ТекстСообщенияОНеобходимостиНастройкиСистемы(ВидОперации, ТекстСообщения) Экспорт
	
	Если ВРег(ВидОперации) = "РАБОТАСЭД" Тогда
		ТекстСообщения = НСтр("ru = 'Для работы с электронными документами необходимо
		|в форме ""Настройка программы"" включить использование обмена электронными документами.'");
	ИначеЕсли ВРег(ВидОперации) = "ПОДПИСАНИЕЭД" Тогда
		ТекстСообщения = НСтр("ru = 'Для возможности подписания ЭД необходимо
		|в форме ""Настройка программы"" включить опцию использования электронных цифровых подписей.'");
	ИначеЕсли ВРег(ВидОперации) = "НАСТРОЙКАКРИПТОГРАФИИ" Тогда
		ТекстСообщения = НСтр("ru = 'Для возможности настройки криптографии необходимо 
		|в форме ""Настройка программы"" опцию использования электронных цифровых подписей.'");
	ИначеЕсли ВРег(ВидОперации) = "РАБОТАСБАНКАМИ" Тогда
		ТекстСообщения = НСтр("ru = 'Для возможности обмена ЭД с банками необходимо 
		|в форме ""Настройка программы"" опцию использования прямого взаимодействия с банками.'");
	КонецЕсли;
	
КонецПроцедуры

// Переопределяет сообщение о нехватке прав доступа
//
// Параметры:
//  ТекстСообщения - строка сообщения
//
Процедура ПодготовитьТекстСообщенияОНарушенииПравДоступа(ТекстСообщения) Экспорт
	
	// При необходимости можно переопределить или дополнить текст сообщения
	
КонецПроцедуры

// Производит заполнение реквизитов формы переданными значениями 
//
// Параметры:
//  ДанныеФормы - Данные управляемой формы;
//  ЗначениеЗаполнения - ссылка данные во временном хранилище.
//
Процедура ЗаполнитьИсточник(ДанныеФормы, ЗначениеЗаполнения) Экспорт
	
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Работа с типами объектов

// Определяет параметры электронного документа по типу владельца.
//
// Параметры:
//  Источник - объекта либо ссылка документа/справочника-источника.
//
// Возвращаемое значение:
//  ПараметрыЭД - структура параметров источника, необходимых для определения
//                настроек обмена ЭД. Обязательные параметры: НаправлениеЭД, ВидЭД,
//                Контрагент, СоглашениеЭД или Организация.
//
Процедура ЗаполнитьПараметрыЭДПоИсточнику(Источник, ПараметрыЭД, ФорматCML = Ложь) Экспорт
	
	ТипИсточника = ТипЗнч(Источник);
	
	Если ТипИсточника = Тип("ДокументСсылка.ПроизвольныйЭД")
		 ИЛИ ТипИсточника = Тип("ДокументОбъект.ПроизвольныйЭД") Тогда
		 
		ПараметрыЭД.ВидЭД =  Перечисления.ВидыЭД.ПроизвольныйЭД;
		ПараметрыЭД.НаправлениеЭД = Перечисления.НаправленияЭД.Исходящий;
		
	ИначеЕсли ТипИсточника = Тип("ДокументСсылка.РеализацияТоваровУслуг") 
		 ИЛИ ТипИсточника = Тип("ДокументОбъект.РеализацияТоваровУслуг") Тогда
		 
		Если ФорматCML Тогда
			ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.ТОРГ12;
			ПараметрыЭД.НаправлениеЭД = Перечисления.НаправленияЭД.Исходящий;
			
		ИначеЕсли Источник.ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.АктВыполненныхРабот Тогда
			ПараметрыЭД.ВидЭД =  Перечисления.ВидыЭД.АктИсполнитель;
			ПараметрыЭД.НаправлениеЭД = Перечисления.НаправленияЭД.Исходящий;
			
		Иначе
			ПараметрыЭД.ВидЭД =  Перечисления.ВидыЭД.ТОРГ12Продавец;
			ПараметрыЭД.НаправлениеЭД = Перечисления.НаправленияЭД.Исходящий;
			
		КонецЕсли;
		
	ИначеЕсли ТипИсточника = Тип("ДокументСсылка.ПоступлениеТоваровУслуг")
		 ИЛИ ТипИсточника = Тип("ДокументОбъект.ПоступлениеТоваровУслуг") Тогда
		 
		Если ФорматCML Тогда
			ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.ТОРГ12;
		КонецЕсли;
		
		ПараметрыЭД.НаправлениеЭД = Перечисления.НаправленияЭД.Исходящий;
		
	ИначеЕсли ТипИсточника = Тип("ДокументСсылка.ЗаказПоставщику")
		 ИЛИ ТипИсточника = Тип("ДокументОбъект.ЗаказПоставщику") Тогда 
		 
		ПараметрыЭД.ВидЭД =  Перечисления.ВидыЭД.ЗаказТовара;
		ПараметрыЭД.НаправлениеЭД = Перечисления.НаправленияЭД.Исходящий;
		
	ИначеЕсли ТипИсточника = Тип("ДокументСсылка.ЗаказПокупателя")
		 ИЛИ ТипИсточника = Тип("ДокументОбъект.ЗаказПокупателя") Тогда
		 
		ПараметрыЭД.ВидЭД =  Перечисления.ВидыЭД.ОтветНаЗаказ;
		ПараметрыЭД.НаправлениеЭД = Перечисления.НаправленияЭД.Исходящий;
	
	ИначеЕсли ТипИсточника = Тип("СправочникСсылка.СоглашенияОбИспользованииЭД")
		 ИЛИ ТипИсточника = Тип("СправочникОбъект.СоглашенияОбИспользованииЭД") Тогда
		 
		ПараметрыЭД.ВидЭД =  Перечисления.ВидыЭД.КаталогТоваров;
		ПараметрыЭД.НаправлениеЭД = Перечисления.НаправленияЭД.Исходящий;
		
	ИначеЕсли ТипИсточника = Тип("ДокументСсылка.СчетФактураВыданный") 
		 ИЛИ ТипИсточника = Тип("ДокументОбъект.СчетФактураВыданный") Тогда
		 
		Если ЭтоКорректировочныйДокумент(Источник) Тогда
			ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.КорректировочныйСчетФактура;
		Иначе
			ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.СчетФактура;
		КонецЕсли;
		
		ПараметрыЭД.НаправлениеЭД = Перечисления.НаправленияЭД.Исходящий;
		
	ИначеЕсли ТипИсточника = Тип("ДокументСсылка.СчетФактураПолученный") 
		 ИЛИ ТипИсточника = Тип("ДокументОбъект.СчетФактураПолученный") Тогда
		 
		Если ЭтоКорректировочныйДокумент(Источник) Тогда
			ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.КорректировочныйСчетФактура;
		Иначе
			ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.СчетФактура;
		КонецЕсли;
		
		ПараметрыЭД.ВидЭД =  Перечисления.ВидыЭД.СчетФактура;
		ПараметрыЭД.НаправлениеЭД = Перечисления.НаправленияЭД.Входящий;
		
	ИначеЕсли ТипИсточника = Тип("ДокументСсылка.АктОбОказанииПроизводственныхУслуг") 
		 ИЛИ ТипИсточника = Тип("ДокументОбъект.АктОбОказанииПроизводственныхУслуг") Тогда
		 
		ПараметрыЭД.ВидЭД =  Перечисления.ВидыЭД.АктИсполнитель;
		ПараметрыЭД.НаправлениеЭД = Перечисления.НаправленияЭД.Исходящий;
		
	ИначеЕсли ТипИсточника = Тип("ДокументСсылка.СчетНаОплатуПокупателю") 
		 ИЛИ ТипИсточника = Тип("ДокументОбъект.СчетНаОплатуПокупателю") Тогда
		 
		ПараметрыЭД.ВидЭД =  Перечисления.ВидыЭД.СчетНаОплату;
		ПараметрыЭД.НаправлениеЭД = Перечисления.НаправленияЭД.Исходящий;
		
	ИначеЕсли ТипИсточника = Тип("ДокументСсылка.СчетНаОплатуПоставщика") 
		 ИЛИ ТипИсточника = Тип("ДокументОбъект.СчетНаОплатуПоставщика") Тогда
		 
		ПараметрыЭД.ВидЭД =  Перечисления.ВидыЭД.СчетНаОплату;
		ПараметрыЭД.НаправлениеЭД = Перечисления.НаправленияЭД.Входящий;
		
	ИначеЕсли ТипИсточника = Тип("ДокументСсылка.КорректировкаРеализации") 
		 ИЛИ ТипИсточника = Тип("ДокументОбъект.КорректировкаРеализации") Тогда
		
		ИсходныйИсправляемыйДокумент = УчетНДС.ПолучитьИсправляемыйДокументРеализации(Источник.ДокументРеализации, Истина);
		
		Если Источник.ВидОперации = Перечисления.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение
			 ИЛИ (ТипЗнч(Источник.ИсправляемыйДокументРеализации) = Тип("ДокументСсылка.КорректировкаРеализации") 
			     И Источник.ИсправляемыйДокументРеализации.ВидОперации = Перечисления.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение) Тогда
			 
			// Выполняется согласованное изменение или исправление согласованного изменения
			ПараметрыЭД.ВидЭД         = Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиОтправитель;
			ПараметрыЭД.НаправлениеЭД = Перечисления.НаправленияЭД.Исходящий;
			
		ИначеЕсли ТипЗнч(ИсходныйИсправляемыйДокумент) = Тип("ДокументСсылка.АктОбОказанииПроизводственныхУслуг")
			 ИЛИ (ТипЗнч(ИсходныйИсправляемыйДокумент) = Тип("ДокументСсылка.РеализацияТоваровУслуг")
			     И ИсходныйИсправляемыйДокумент.ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.АктВыполненныхРабот) Тогда
			 
			// Выплняется исправление Акта выполненных работ
			ПараметрыЭД.ВидЭД         = Перечисления.ВидыЭД.АктИсполнитель;
			ПараметрыЭД.НаправлениеЭД = Перечисления.НаправленияЭД.Исходящий;
			
		ИначеЕсли ТипЗнч(ИсходныйИсправляемыйДокумент) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
			
			// Выполняется исправление ТОРГ-12
			ПараметрыЭД.ВидЭД         = Перечисления.ВидыЭД.ТОРГ12Продавец;
			ПараметрыЭД.НаправлениеЭД = Перечисления.НаправленияЭД.Исходящий;
			
		КонецЕсли;
		
	ИначеЕсли ТипИсточника = Тип("ДокументСсылка.КорректировкаПоступления") 
		 ИЛИ ТипИсточника = Тип("ДокументОбъект.КорректировкаПоступления") Тогда
		 
		ПараметрыЭД.НаправлениеЭД = Перечисления.НаправленияЭД.Исходящий;
		
	КонецЕсли;
	
	ПараметрыЭД.Организация = Источник.Организация;
	ПараметрыЭД.Контрагент  = Источник.Контрагент;
	
КонецПроцедуры

// Определяет имя реквизита владельца справочника НоменклатураПоставщика.
//
// Параметры:
//  ИмяРеквизитаВладельца - строка - имя реквизита владельца.
//
Процедура ОпределитьИмяРеквизитаВладельцаНоменклатурыПоставщиков(ИмяРеквизитаВладельца) Экспорт
	
	ИмяРеквизитаВладельца = "Контрагент";
	
КонецПроцедуры

// Определяет соответствие справочников библиотеки и прикладного решения,
// в случае различий в наименовании справочников.
//
// Параметры:
//  СоотвествиеСправочников - соотвествие - список справочников.
//
Процедура ПолучитьСоответствиеСправочников(СоотвествиеСправочников) Экспорт
	
	СоотвествиеСправочников.Вставить("Организации", "Организации");
	СоотвествиеСправочников.Вставить("Контрагенты", "Контрагенты");
	СоотвествиеСправочников.Вставить("Партнеры",    "");
	СоотвествиеСправочников.Вставить("Банки",       "Банки");
	
	СоотвествиеСправочников.Вставить("Номенклатура",                "Номенклатура");
	СоотвествиеСправочников.Вставить("ХарактеристикиНоменклатуры",  "ХарактеристикиНоменклатуры");
	СоотвествиеСправочников.Вставить("ЕдиницыИзмерения",            "ЕдиницыИзмерения");
	СоотвествиеСправочников.Вставить("НоменклатураПоставщиков",     "");
	СоотвествиеСправочников.Вставить("Валюты",                      "Валюты");
	СоотвествиеСправочников.Вставить("УпаковкиНоменклатуры",        "");
	СоотвествиеСправочников.Вставить("БанковскиеСчетаОрганизаций",  "БанковскиеСчета");
	СоотвествиеСправочников.Вставить("БанковскиеСчетаКонтрагентов", "БанковскиеСчета");
	
КонецПроцедуры

// Определяет соответствие функциональных опций библиотеки и прикладного решения,
// в случае различий в наименовании.
//
// Параметры:
//  СоответствиеФО - Соответствие - список функциональных опций.
//
Процедура ПолучитьСоответствиеФункциональныхОпций(СоотвествиеФО) Экспорт
	
	// Электронные документы
	СоотвествиеФО.Вставить("ИспользоватьОбменЭД",                    "ИспользоватьОбменЭД");
	СоотвествиеФО.Вставить("ИспользоватьЭлектронныеЦифровыеПодписи", "ИспользоватьЭлектронныеЦифровыеПодписи");
	СоотвествиеФО.Вставить("ИспользоватьОбменЭДМеждуОрганизациями",  "ИспользоватьОбменЭДМеждуОрганизациями");
	СоотвествиеФО.Вставить("ИспользоватьОбменЭДСБанками",            "ИспользоватьОбменЭДСБанками");
	// Конец электронные документы
	
	СоотвествиеФО.Вставить("ИспользоватьРучныеСкидкиВПродажах",         Неопределено);
	СоотвествиеФО.Вставить("ИспользоватьАвтоматическиеСкидкиВПродажах", Неопределено);
	СоотвествиеФО.Вставить("ИспользоватьРучныеСкидкиВЗакупках",         Неопределено);
	
КонецПроцедуры

// В функции описана структура сопоставления имен переменных библиотеки,
// наименованиям объектов и реквизитов метаданных прикладного решения.
// 
// Параметры:
//  Ключ соответствия - имя переменной, используемой в коде библиотеки;
//  Значение соответствия - наименование объекта метаданных или реквизита объекта
//  в прикладном решении.
//
Процедура ПолучитьСоответствиеНаименованийОбъектовМДиРеквизитов(СоответствиеРеквизитовОбъекта) Экспорт
	
	СоответствиеРеквизитовОбъекта.Вставить("СчетФактураВыданныйВМетаданных",       "СчетФактураВыданный");
	СоответствиеРеквизитовОбъекта.Вставить("СчетФактураПолученныйВМетаданных",     "СчетФактураПолученный");
	СоответствиеРеквизитовОбъекта.Вставить("ДатаВыставленияВСчетеФактуреВыданном", "ДатаВыставления");
	СоответствиеРеквизитовОбъекта.Вставить("ДатаПолученияВСчетеФактуреПолученном", "Дата");
	СоответствиеРеквизитовОбъекта.Вставить("НомерСчета",                           "НомерСчета");
	
КонецПроцедуры

// Получает значение перечисления по имени объектов метаданных.
// 
// Параметры:
//  Наименование - Строка, наименование перечисления.
//  ПредставлениеПеречисления - Строка, наименование значения перечисления.
//
// Возвращаемое значение:
//  НайденноеЗначение - значение искомого перечисления.
//
Процедура ПолучитьСоответствиеПеречислений(СоответствиеПеречислений) Экспорт
	
	СоответствиеПеречислений.Вставить("НДС",                        "СтавкиНДС");
	СоответствиеПеречислений.Вставить("ЮрФизЛицо",                  "ЮрФизЛицо");
	СоответствиеПеречислений.Вставить("ВариантыОплатыКлиентом",     "");
	СоответствиеПеречислений.Вставить("ВариантыОплатыПоставщику",   "");
	СоответствиеПеречислений.Вставить("ФормыОплаты",                "");
	СоответствиеПеречислений.Вставить("СпособРасчета",              "");
	
КонецПроцедуры

// Получает значение перечисления по имени перечисления и представлению в библиотеке.
// 
// Параметры:
//  ИмяПеречисления - Строка, наименование перечисления.
//  ПредставлениеПеречисления - Строка, наименование значения перечисления.
//  НайденноеЗначение - значение искомого перечисления.
//
Процедура ПолучитьЗначениеПеречисления(ИмяПеречисления, ПредставлениеПеречисления, НайденноеЗначение) Экспорт
	
	Если ИмяПеречисления = "СтавкиНДС" Тогда
		
		НайденноеЗначение = ЗначениеПеречисленияСтавкаНДС(ПредставлениеПеречисления);
		
	Иначе
		
		Для Каждого ЭлПеречисления Из Метаданные.Перечисления[ИмяПеречисления].ЗначенияПеречисления Цикл
			
			Если Найти(Врег(ЭлПеречисления.Синоним), Врег(ПредставлениеПеречисления)) > 0
				 ИЛИ Найти(Врег(ЭлПеречисления.Имя), Врег(ПредставлениеПеречисления)) > 0 Тогда
				 
				НайденноеЗначение = Перечисления[ИмяПеречисления][ЭлПеречисления.Имя];
				Прервать;
				
			КонецЕсли;
			
		КонецЦикла;

	КонецЕсли;
	
КонецПроцедуры

// Заполняет массив актуальными видами электПронных документов для прикладного решения.
//
// Параметры:
//  Массив - виды актуальных ЭД.
//
Процедура ПолучитьАктуальныеВидыЭД(Массив) Экспорт
	
	Массив.Добавить(Перечисления.ВидыЭД.ТОРГ12);
	Массив.Добавить(Перечисления.ВидыЭД.ТОРГ12Продавец);
	Массив.Добавить(Перечисления.ВидыЭД.ТОРГ12Покупатель);
	Массив.Добавить(Перечисления.ВидыЭД.ЗаказТовара);
	Массив.Добавить(Перечисления.ВидыЭД.ОтветНаЗаказ);
	Массив.Добавить(Перечисления.ВидыЭД.КаталогТоваров);
	Массив.Добавить(Перечисления.ВидыЭД.СчетФактура);
	Массив.Добавить(Перечисления.ВидыЭД.ПроизвольныйЭД);
	Массив.Добавить(Перечисления.ВидыЭД.Подтверждение);
	Массив.Добавить(Перечисления.ВидыЭД.ИзвещениеОПолучении);
	Массив.Добавить(Перечисления.ВидыЭД.УведомлениеОбУточнении);
	Массив.Добавить(Перечисления.ВидыЭД.АктЗаказчик);
	Массив.Добавить(Перечисления.ВидыЭД.АктИсполнитель);
	Массив.Добавить(Перечисления.ВидыЭД.СчетНаОплату);
	Массив.Добавить(Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиОтправитель);
	Массив.Добавить(Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиПолучатель);
	
КонецПроцедуры

// Проверяет на корректность заполнения параметров электронного документа.
//
// Параметры:
//  ПараметрыЭД - структура - перечень параметров ЭД.
//
// Возвращаемое значение:
//  Булево - Истина если правильно заполнен объект выгрузки
//
Функция ПроверитьПравильностьЗаполненияОбъекта(ПараметрыЭД) Экспорт
	
	Возврат Истина;
	
КонецФункции

// Формирует тест запроса для получения таблицы сопоставления номенклатуры
//
// Параметры:
//  ТекстЗапроса - Строка - текст запроса
//
Процедура ТекстЗапросаСопоставленияНоменклатуры(ТекстЗапроса) Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаИнформацияОТоваре.Ид 				КАК Идентификатор,
	|	ТаблицаИнформацияОТоваре.Артикул 			КАК АртикулНоменклатурыКонтрагента,
	|	ТаблицаИнформацияОТоваре.Наименование 		КАК НаименованиеНоменклатурыКонтрагента,
	|	ТаблицаИнформацияОТоваре.БазоваяЕдиницаКод 	КАК БазоваяЕдиницаКод,
	|	ТаблицаИнформацияОТоваре.Описание 			КАК Описание
	|ПОМЕСТИТЬ ТаблицаИнформацияОТоваре
	|ИЗ
	|	&ТаблицаИнформацияОТоваре КАК ТаблицаИнформацияОТоваре
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаИнформацияОТоваре.Идентификатор,
	|	ТаблицаИнформацияОТоваре.АртикулНоменклатурыКонтрагента,
	|	ТаблицаИнформацияОТоваре.НаименованиеНоменклатурыКонтрагента,
	|	ЕСТЬNULL(КлассификаторЕдиницИзмерения.Ссылка, НЕОПРЕДЕЛЕНО) КАК ЕдиницаНоменклатурыКонтрагента,
	|	ТаблицаИнформацияОТоваре.Описание
	|ИЗ
	|	ТаблицаИнформацияОТоваре КАК ТаблицаИнформацияОТоваре
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		РегистрСведений.НоменклатураКонтрагентов КАК НоменклатураКонтрагентов
	|	ПО 
	|		НоменклатураКонтрагентов.Контрагент = &Контрагент
	|		И НоменклатураКонтрагентов.Идентификатор = ТаблицаИнформацияОТоваре.Идентификатор
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		Справочник.КлассификаторЕдиницИзмерения КАК КлассификаторЕдиницИзмерения
	|	ПО 
	|		ТаблицаИнформацияОТоваре.БазоваяЕдиницаКод = КлассификаторЕдиницИзмерения.Код
	|	
	|ГДЕ
	|	НоменклатураКонтрагентов.Номенклатура ЕСТЬ NULL";
	
КонецПроцедуры

// Возвращает структуру, содержащую информацию о юридическом адресе контрагента.
//
// Параметры:
//  СтруктураПараметров - Структура, содержит ссылки на элементы справочника
//  ВидКонтрагента - строка, имя метаданных справочника
//  ВидАдреса = Строка, "Факт" или "Юр"
//
// Возвращаемое значение:
//  Структура - информация о юридическом адресе
//
Процедура ПолучитьАдресСтруктурой(СтруктураАдреса, СтруктураПараметров, ВидКонтрагента, ВидАдреса, ТекстОшибки = "") Экспорт
	
	Если НЕ ЗначениеЗаполнено(СтруктураПараметров[ВидКонтрагента]) Тогда
		Возврат;
	КонецЕсли; 
	
	СтруктураАдреса.Удалить("КодСтр");
	СтруктураАдреса.Удалить("АдрТекст");
	
	Если ТипЗнч(СтруктураПараметров[ВидКонтрагента]) = Тип("СправочникСсылка.Организации") Тогда
		Если ОбщегоНазначения.ПолучитьЗначениеРеквизита(СтруктураПараметров[ВидКонтрагента], "ЮрФизЛицо") = Перечисления.ЮрФизЛицо.ЮрЛицо Тогда
			Объект = СтруктураПараметров[ВидКонтрагента];
			ВидАдресаОрганизации = ?(ВидАдреса = "Юр", Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации, Справочники.ВидыКонтактнойИнформации.ФактАдресОрганизации);
		Иначе
			Объект = ОбщегоНазначения.ПолучитьЗначениеРеквизита(СтруктураПараметров[ВидКонтрагента], "ИндивидуальныйПредприниматель");
			ВидАдресаОрганизации = ?(ВидАдреса = "Юр", Справочники.ВидыКонтактнойИнформации.ЮрАдресФизЛица, Справочники.ВидыКонтактнойИнформации.ФактАдресФизЛица);
		КонецЕсли;
	Иначе
		Объект = СтруктураПараметров[ВидКонтрагента];
		
		Если ТипЗнч(Объект) = Тип("СправочникСсылка.АдресаПоставки") Тогда
			ВидАдресаОрганизации = Справочники.ВидыКонтактнойИнформации.АП_ФактАдресАдресаПоставки;
		Иначе 
			ВидАдресаОрганизации = ?(ВидАдреса = "Юр", Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента, Справочники.ВидыКонтактнойИнформации.ФактАдресКонтрагента);
		КонецЕсли;
		
	КонецЕсли;
	
	Данные = Новый Структура("Объект, Тип, Вид", Объект, Перечисления.ТипыКонтактнойИнформации.Адрес, ВидАдресаОрганизации);
	Адрес = РегистрыСведений.КонтактнаяИнформация.Получить(Данные);
	
	ПроизвольныйАдрес = (ВРЕГ(УправлениеКонтактнойИнформацией.ПолучитьПредставлениеАдреса(Адрес)) <> ВРЕГ(Адрес.Представление));
	Если ПроизвольныйАдрес Тогда
		СтруктураАдреса.Вставить("АдресРФ", Ложь);
		//Попытаемся найти код страны
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Наименование", Адрес.Поле1);
		Запрос.Текст = "
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	КлассификаторСтранМира.Код КАК КодСтраны
		|ИЗ
		|	Справочник.КлассификаторСтранМира КАК КлассификаторСтранМира
		|
		|ГДЕ
		|	КлассификаторСтранМира.Наименование = &Наименование
		|";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			СтруктураАдреса.Вставить("КодСтраны", Выборка.КодСтраны);
			ТекущееПредставлениеАдреса = Адрес.Представление;
			СтруктураАдреса.Вставить("АдресСтрокой", УправлениеКонтактнойИнформацией.ПолучитьПредставлениеАдресаЗаПределамиРФБезСтраны(Адрес.Поле1, ТекущееПредставлениеАдреса));
		Иначе
			// Если страна в адресе не указана, то адрес не пройдет проверку.
			СтруктураАдреса.Вставить("КодСтраны", "");
			СтруктураАдреса.Вставить("АдресСтрокой", Адрес.Представление);
		КонецЕсли;
	Иначе
		СтруктураАдреса.Вставить("АдресРФ", Истина);
		СтруктураАдреса.Вставить("Индекс", СокрЛП(Адрес.Поле1));
		СтруктураАдреса.Вставить("КодСтраны", Справочники.КлассификаторСтранМира.Россия.Код);
		
		СтруктураКлассификатора = УправлениеКонтактнойИнформацией.ВернутьСтрокуАдресногоКлассификатораПоАдреснымЭлементам(Адрес.Поле2, "", "", "", "");
		СтруктураАдреса.Вставить("КодРегион", Формат(СтруктураКлассификатора.КодРегионаВКоде, "ЧЦ=2; ЧВН=; ЧГ="));
		
		СтруктураАдреса.Вставить("Район", СокрЛП(Адрес.Поле3));
		СтруктураАдреса.Вставить("Город", СокрЛП(Адрес.Поле4));
		СтруктураАдреса.Вставить("НаселПункт", СокрЛП(Адрес.Поле5));
		СтруктураАдреса.Вставить("Улица", СокрЛП(Адрес.Поле6));
		СтруктураАдреса.Вставить("Дом", СокрЛП(Адрес.Поле7));
		СтруктураАдреса.Вставить("Корпус", СокрЛП(Адрес.Поле8));
		СтруктураАдреса.Вставить("Кварт", СокрЛП(Адрес.Поле9));
	КонецЕсли;

КонецПроцедуры

// Возвращает название региона по коду
//
// Параметры:
//  КодРегиона - Строка, содержащая двухсимвольный код региона
//
// Возвращаемое значение:
//  Строка - название региона.
//
Функция НазваниеРегиона(КодРегиона) Экспорт
	
	Если НЕ ЗначениеЗаполнено(КодРегиона) Тогда
		Возврат "";
	КонецЕсли;
	
	КодРегионаЧисло = Число(КодРегиона);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	|	АдресныйКлассификатор.КодРегионаВКоде,
	|	АдресныйКлассификатор.ТипАдресногоЭлемента,
	|	АдресныйКлассификатор.Наименование,
	|	АдресныйКлассификатор.Код
	|ИЗ
	|	РегистрСведений.АдресныйКлассификатор КАК АдресныйКлассификатор
	|ГДЕ
	|	АдресныйКлассификатор.ТипАдресногоЭлемента = &ТипАдресногоЭлемента
	|	И АдресныйКлассификатор.КодРегионаВКоде = &КодРегионаВКоде";
	Запрос.УстановитьПараметр("КодРегионаВКоде", КодРегионаЧисло);
	Запрос.УстановитьПараметр("ТипАдресногоЭлемента", 1);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Возврат УправлениеКонтактнойИнформацией.ПолучитьПолноеНазвание(Выборка.Код);
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции

// Определяет, является ли объект корректировочным документом
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка.СчетФактураВыданный
//
// Возвращаемое значение:
//  Булево
//
Функция ЭтоКорректировочныйДокумент(СсылкаНаОбъект) Экспорт
	
	Результат = Ложь;
	
	Если ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
		Результат = (СсылкаНаОбъект.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.Корректировочный);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает пустую ссылку на объект по переданному строковому представлению объекта
// 
// Параметры:
//  ПредставлениеОбъекта       - Строка, - представление объекта
// 
// Возвращаемое значение:
//  ПустаяСсылка 
// 
Функция ПолучитьПустуюСсылку(ПредставлениеОбъекта) Экспорт
	
	Если ПредставлениеОбъекта = "Справочники.БанковскиеСчета" Тогда
		Возврат Справочники.БанковскиеСчета.ПустаяСсылка();
	КонецЕсли;
	
КонецФункции

// Определяет возможно ли редактировать объект информационной базы
//
// Параметры
//  <СсылкаНаОбъект>  - <любая ссылка> - ссылка на проверяемый объект
/// <РедактированиеРазрешено> - <Булево>   - возвращает разрешено или нет редактирование
//
Функция ПроверитьВозможностьРедактированияОбъекта(СсылкаНаОбъект, РедактированиеРазрешено) Экспорт
	
	
КонецФункции

// Выполняет заполнение списка документов по виду электронного документа.
//
// Параметры:
//  ВидЭД           - Перечисления   - вид электронного документа;
//  СписокВозврата  - СписокЗначений - список ссылок на документы информационной базы.
//
Процедура СписокТиповДокументовПоВидуЭД(ВидЭД, СписокВозврата) Экспорт
	
	Если ВидЭД = Перечисления.ВидыЭД.ТОРГ12 ИЛИ ВидЭД = Перечисления.ВидыЭД.ТОРГ12Продавец Тогда
		СписокВозврата.Добавить(Документы.ПоступлениеТоваровУслуг.ПустаяСсылка(), Метаданные.Документы.ПоступлениеТоваровУслуг.Представление());
	ИначеЕсли ВидЭД = Перечисления.ВидыЭД.СчетФактура ИЛИ ВидЭД = Перечисления.ВидыЭД.КорректировочныйСчетФактура Тогда
		СписокВозврата.Добавить(Документы.СчетФактураПолученный.ПустаяСсылка(),Метаданные.Документы.СчетФактураПолученный.Представление());
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Определение ключевых реквизитов объектов для регистрации изменений

// Получает ключевые реквизиты объекта по текстовому представлению.
//
// Параметры:
//  ИмяОбъекта - Строка, текстовое представление объекта, ключевые реквизиты которого необходимо получить.
//
// Возвращаемое значение:
//  СтруктураКлючевыхРеквизитов - перечень параметров объекта.
//
Процедура ПолучитьСтруктуруКлючевыхРеквизитовОбъекта(ИмяОбъекта, СтруктураКлючевыхРеквизитов) Экспорт
	
	Если ИмяОбъекта = "Документ.РеализацияТоваровУслуг" Тогда
		// шапка
		СтрокаРеквизитовОбъекта = ("Дата, Номер, Организация, Контрагент, ПометкаУдаления");
		СтруктураКлючевыхРеквизитов.Вставить("Шапка", СтрокаРеквизитовОбъекта);
		
		// ТЧ
		СтрокаРеквизитовОбъекта = ("Номенклатура, ХарактеристикаНоменклатуры, Количество, Цена, Сумма, СтавкаНДС");
		СтруктураКлючевыхРеквизитов.Вставить("Товары", СтрокаРеквизитовОбъекта);
		СтрокаРеквизитовОбъекта = ("Номенклатура, Количество, Цена, Сумма, СтавкаНДС");
		СтруктураКлючевыхРеквизитов.Вставить("Услуги", СтрокаРеквизитовОбъекта);
		
	ИначеЕсли ИмяОбъекта = "Документ.ПроизвольныйЭД" Тогда
		// шапка
		СтрокаРеквизитовОбъекта = ("Дата, Номер, Организация, Контрагент, Текст");
		СтруктураКлючевыхРеквизитов.Вставить("Шапка", СтрокаРеквизитовОбъекта);
		
	ИначеЕсли ИмяОбъекта = "Документ.ЗаказПоставщику" Тогда
		// шапка
		СтрокаРеквизитовОбъекта = ("Дата, Номер, Организация, Контрагент, ПометкаУдаления");
		СтруктураКлючевыхРеквизитов.Вставить("Шапка", СтрокаРеквизитовОбъекта);
		
		// ТЧ
		СтрокаРеквизитовОбъекта = ("Номенклатура, ХарактеристикаНоменклатуры, Количество, Цена, Сумма, СтавкаНДС");
		СтруктураКлючевыхРеквизитов.Вставить("Товары", СтрокаРеквизитовОбъекта);
		СтрокаРеквизитовОбъекта = ("Номенклатура, Содержание, Количество, Цена, Сумма, СтавкаНДС");
		СтруктураКлючевыхРеквизитов.Вставить("Услуги", СтрокаРеквизитовОбъекта);
		
	ИначеЕсли ИмяОбъекта = "Документ.ЗаказПокупателя" Тогда  // для сравнения при загрузке
		// шапка
		СтрокаРеквизитовОбъекта = ("Организация, Контрагент");
		СтруктураКлючевыхРеквизитов.Вставить("Шапка", СтрокаРеквизитовОбъекта);
		
		// ТЧ
		СтрокаРеквизитовОбъекта = ("Номенклатура, ХарактеристикаНоменклатуры, Количество, Цена, Сумма, СтавкаНДС");
		СтруктураКлючевыхРеквизитов.Вставить("Товары", СтрокаРеквизитовОбъекта);
		СтрокаРеквизитовОбъекта = ("Номенклатура, Содержание, Количество, Цена, Сумма, СтавкаНДС");
		СтруктураКлючевыхРеквизитов.Вставить("Услуги", СтрокаРеквизитовОбъекта);
		
	ИначеЕсли ИмяОбъекта = "Документ.СчетФактураВыданный" Тогда
		// шапка
		СтрокаРеквизитовОбъекта = ("Дата, Номер, Организация, ПометкаУдаления, КодСпособаВыставления, СуммаДокумента, СуммаНДСДокумента");
		СтруктураКлючевыхРеквизитов.Вставить("Шапка", СтрокаРеквизитовОбъекта);
		
	ИначеЕсли ИмяОбъекта = "Документ.АктОбОказанииПроизводственныхУслуг" Тогда
		// шапка
		СтрокаРеквизитовОбъекта = ("Дата, Номер, Организация, Контрагент, ПометкаУдаления");
		СтруктураКлючевыхРеквизитов.Вставить("Шапка", СтрокаРеквизитовОбъекта);
		
		// ТЧ
		СтрокаРеквизитовОбъекта = ("Номенклатура, Количество, Цена, Сумма, СтавкаНДС");
		СтруктураКлючевыхРеквизитов.Вставить("Услуги", СтрокаРеквизитовОбъекта);
		
	ИначеЕсли ИмяОбъекта = "Документ.СчетНаОплатуПокупателю" Тогда
		// шапка
		СтрокаРеквизитовОбъекта = ("Дата, Номер, Организация, Контрагент, ПометкаУдаления");
		СтруктураКлючевыхРеквизитов.Вставить("Шапка", СтрокаРеквизитовОбъекта);
		
		// ТЧ
		СтрокаРеквизитовОбъекта = ("Номенклатура, ХарактеристикаНоменклатуры, Количество, Цена, Сумма, СтавкаНДС");
		СтруктураКлючевыхРеквизитов.Вставить("Товары", СтрокаРеквизитовОбъекта);
		СтрокаРеквизитовОбъекта = ("Номенклатура, Количество, Цена, Сумма, СтавкаНДС");
		СтруктураКлючевыхРеквизитов.Вставить("Услуги", СтрокаРеквизитовОбъекта);
		
	ИначеЕсли ИмяОбъекта = "Документ.КорректировкаРеализации" Тогда
		// шапка
		СтрокаРеквизитовОбъекта = ("Дата, Номер, Организация, Контрагент, ПометкаУдаления");
		СтруктураКлючевыхРеквизитов.Вставить("Шапка", СтрокаРеквизитовОбъекта);
		
		// ТЧ
		СтрокаРеквизитовОбъекта = ("Номенклатура, ХарактеристикаНоменклатуры, Количество, Цена, Сумма, СтавкаНДС");
		СтруктураКлючевыхРеквизитов.Вставить("Товары", СтрокаРеквизитовОбъекта);
		СтрокаРеквизитовОбъекта = ("Номенклатура, Количество, Цена, Сумма, СтавкаНДС");
		СтруктураКлючевыхРеквизитов.Вставить("Услуги", СтрокаРеквизитовОбъекта);
		
	КонецЕсли;
	
КонецПроцедуры

// Получает таблицу соответствий параметров для типов метаданных их пользовательским представлениям.
//
// Параметры:
//  ТаблицаСоответствия - таблица - соответствие параметров для типов метаданных их пользовательским представлениям
//  содержит следующие колонки: ТипИсточника, Параметр, Представление.
//
Процедура ПолучитьТаблицуСоответствияПараметровПользовательскимПредставлениям(ТаблицаСоответствия) Экспорт
	
	Макет                   = Обработки.ЭлектронныеДокументы.ПолучитьМакет("ПользовательскоеПредставлениеОбязательныхПолей");
	ОбластьДокументов       = Макет.ПолучитьОбласть("ОбязательныеПоля");
	ОбластьДокументовВысота = ОбластьДокументов.ВысотаТаблицы;

	Для НСтр = 1 По ОбластьДокументовВысота Цикл
		
		НоваяСтрока = ТаблицаСоответствия.Добавить();
		НоваяСтрока.ТипИсточника  = Тип(СокрЛП(ОбластьДокументов.Область(НСтр, 1).Текст));
		НоваяСтрока.Параметр      = СокрЛП(ОбластьДокументов.Область(НСтр, 2).Текст);
		НоваяСтрока.Представление = СокрЛП(ОбластьДокументов.Область(НСтр, 3).Текст);
	КонецЦикла;
	
КонецПроцедуры

// Заполняет соответствие кодов реквизитов схем электронных документов их пользовательскому представлению.
//
// Параметры:
//  СоответствиеВозврата - Соответствие, исходное соответствие для заполнения.
//
Процедура СоответствиеКодовРеквизитовИПредставлений(СоответствиеВозврата) Экспорт
	
	Макет = Обработки.ЭлектронныеДокументы.ПолучитьМакет("ПрикладноеПредставлениеРеквизитов");
	ВысотаТаблицы = Макет.ВысотаТаблицы;
	Для НСтр = 1 По ВысотаТаблицы Цикл
		СоответствиеВозврата.Вставить(СокрЛП(Макет.Область(НСтр, 1).Текст), СокрЛП(Макет.Область(НСтр,2).Текст));
	КонецЦикла;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Поиск и создание документов

// Сохраняет данные из электронного документа в объект ИБ.
//
// Параметры:
//  СтрокаДляЗагрузки - строка параметров для загрузки,
//  ДеревоРазбора     - ДеревоЗначений, структура параметров документа ИБ.
//  СсылкаНаВладельца - Ссылка на объект ИБ, владельца электронного документа.
//
// Возвращаемое значение:
//  НайденныйОбъект - ссылка объекта.
//
Функция СохранитьДанныеОбъектаВБД(СтрокаДляЗагрузки, ДеревоРазбора, СсылкаНаВладельца = Неопределено, Записывать = Истина) Экспорт
	
	НайденныйОбъект = Неопределено;
	
	Если СтрокаДляЗагрузки.ВидЭД = Перечисления.ВидыЭД.ТОРГ12 
		 ИЛИ СтрокаДляЗагрузки.ВидЭД = Перечисления.ВидыЭД.ТОРГ12Продавец
		 ИЛИ СтрокаДляЗагрузки.ВидЭД = Перечисления.ВидыЭД.АктИсполнитель Тогда
		 
		ВидОперации = ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "ВидОперации", Истина, ДеревоРазбора);
		
		Если ВидОперации = Перечисления.ВидыОперацийЭД.Исправление Тогда
			НайденныйОбъект = НайтиСоздатьКорректировкуПоступления(СтрокаДляЗагрузки, ДеревоРазбора, СсылкаНаВладельца, Записывать);
		Иначе
			НайденныйОбъект = НайтиСоздатьПоступлениеТоваровУслуг(СтрокаДляЗагрузки, ДеревоРазбора, СсылкаНаВладельца, Записывать);
		КонецЕсли;
		
	ИначеЕсли СтрокаДляЗагрузки.ВидЭД = Перечисления.ВидыЭД.СчетНаОплату Тогда
		НайденныйОбъект = НайтиСоздатьСчетНаОплатуПоставщика(СтрокаДляЗагрузки, ДеревоРазбора, СсылкаНаВладельца, Записывать);
		
	ИначеЕсли СтрокаДляЗагрузки.ВидЭД = Перечисления.ВидыЭД.ЗаказТовара Тогда
		НайденныйОбъект = НайтиСоздатьЗаказПокупателя(СтрокаДляЗагрузки, ДеревоРазбора, СсылкаНаВладельца, Записывать);
		
	ИначеЕсли СтрокаДляЗагрузки.ВидЭД = Перечисления.ВидыЭД.ОтветНаЗаказ Тогда
		НайденныйОбъект = НайтиСоздатьЗаказПоставщику(СтрокаДляЗагрузки, ДеревоРазбора, СсылкаНаВладельца, Записывать);
		
	ИначеЕсли (СтрокаДляЗагрузки.ВидЭД = Перечисления.ВидыЭД.СчетФактура 
		ИЛИ СтрокаДляЗагрузки.ВидЭД = Перечисления.ВидыЭД.КорректировочныйСчетФактура) Тогда
		НайденныйОбъект = НайтиСоздатьСчетФактуру(СтрокаДляЗагрузки, ДеревоРазбора, СсылкаНаВладельца, Записывать);
		
	ИначеЕсли СтрокаДляЗагрузки.ВидЭД = Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиОтправитель Тогда
		НайденныйОбъект = НайтиСоздатьКорректировкуПоступления(СтрокаДляЗагрузки, ДеревоРазбора, СсылкаНаВладельца, Записывать);
	КонецЕсли;
	
	Возврат НайденныйОбъект;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Создание элементов справочников

// Находит ссылку на объект ИБ по типу, ИД и дополнительным реквизитам
// 
// Параметры:
//  ТипОбъекта - Строка, идентификатор типа объекта, который необходимо найти,
//  ИДОбъекта - Строка, идентификатор объекта заданного типа,
//  ДополнительныеРеквизиты - Структура, набор дополнительных полей объекта для поиска.
//
Функция НайтиСсылкуНаОбъект(ТипОбъекта, ИдОбъекта = "", ДополнительныеРеквизиты = Неопределено, ИДЭД = Неопределено) Экспорт
	
	Результат = Неопределено;
	
	Если ТипОбъекта = "Валюты" 
		 ИЛИ ТипОбъекта = "Банки" Тогда
		Результат = НайтиСсылкуНаОбъектПоРеквизиту(ТипОбъекта, "Код", ИдОбъекта);
		
	ИначеЕсли ТипОбъекта = "БанковскиеСчетаКонтрагентов"
		 ИЛИ ТипОбъекта = "БанковскиеСчетаОрганизаций" Тогда
		
		Владелец = ""; 
		НомерСчета = "";
		Если ДополнительныеРеквизиты.Свойство("Владелец", Владелец) 
			 И ДополнительныеРеквизиты.Свойство("НомерСчета", НомерСчета) Тогда
			 
			 Результат = НайтиСсылкуНаОбъектПоРеквизиту("БанковскиеСчета", "НомерСчета", НомерСчета, Владелец);
			
		КонецЕсли;
		
	ИначеЕсли ТипОбъекта = "ЕдиницыИзмерения" Тогда
		Результат = НайтиСсылкуНаОбъектПоРеквизиту("КлассификаторЕдиницИзмерения", "Код", ИдОбъекта);
		
	ИначеЕсли (ТипОбъекта = "Контрагенты" ИЛИ ТипОбъекта = "Организации") И ЗначениеЗаполнено(ДополнительныеРеквизиты) Тогда
		ПараметрПоиска = "";
		ИНН = "";
		КПП = "";
		Если ДополнительныеРеквизиты.Свойство("ИНН") Тогда 
			ИНН = ДополнительныеРеквизиты.ИНН;
		КонецЕсли;
		Если ДополнительныеРеквизиты.Свойство("КПП") Тогда 
			КПП = ДополнительныеРеквизиты.КПП;
		КонецЕсли;
		Если ЗначениеЗаполнено(Строка(ИНН)+Строка(КПП)) Тогда // по ИНН+КПП
			Результат = СсылкаНаОбъектПоИННКПП(ТипОбъекта, ИНН, КПП); 
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Результат) И ДополнительныеРеквизиты.Свойство("Наименование", ПараметрПоиска) Тогда // по Наименованию
			Результат = НайтиСсылкуНаОбъектПоРеквизиту(ТипОбъекта, "Наименование", ПараметрПоиска); 
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Результат) И ДополнительныеРеквизиты.Свойство("ПолноеНаименование", ПараметрПоиска) Тогда // по Полному наименованию
			Результат = НайтиСсылкуНаОбъектПоРеквизиту(ТипОбъекта, "НаименованиеПолное", ПараметрПоиска); 
		КонецЕсли;
		
	ИначеЕсли ТипОбъекта = "НоменклатураПоставщиков" И ЗначениеЗаполнено(ДополнительныеРеквизиты) Тогда
		
		Владелец = "";
		
		Если ДополнительныеРеквизиты.Свойство("Владелец", Владелец) Тогда // есть Владелец
			
			Результат = Новый Структура(
				"Владелец, Идентификатор, Код, Артикул, Наименование", 
				Владелец, "", "", "", "");
			
			Параметр = "";
			Если ДополнительныеРеквизиты.Свойство("Идентификатор", Параметр) Тогда // по Идентификатору
				Результат.Идентификатор = Параметр;
			КонецЕсли;
			
			Если ДополнительныеРеквизиты.Свойство("Код", Параметр) Тогда // по Коду
				Результат.Код = Параметр;
			КонецЕсли;
			
			Если ДополнительныеРеквизиты.Свойство("Артикул", Параметр) Тогда  // по Артикулу
				Результат.Артикул = Параметр;
			КонецЕсли;
			
			Если ДополнительныеРеквизиты.Свойство("Наименование", Параметр) Тогда // по Наименованию
				Результат.Наименование = Параметр;
			КонецЕсли;
			
			
		КонецЕсли;
		
	ИначеЕсли ТипОбъекта = "Номенклатура" Тогда
		
		Код = "";
		
		Если ЗначениеЗаполнено(ИдОбъекта) Тогда
			// Если задан Ид, то будем искать по коду
			Результат = НайтиСсылкуНаОбъектПоРеквизиту(ТипОбъекта, "Код", ИдОбъекта);
			
		ИначеЕсли ЗначениеЗаполнено(ДополнительныеРеквизиты) И ДополнительныеРеквизиты.Свойство("Код", Код) Тогда
			
			Результат = НайтиСсылкуНаОбъектПоРеквизиту(ТипОбъекта, "Код", Код);
			
		ИначеЕсли ЗначениеЗаполнено(ДополнительныеРеквизиты) Тогда
			
			ДанныеТовара = ПолучитьДанныеТовараПоНоменклатуреПоставщика(ДополнительныеРеквизиты);
			Результат = ДанныеТовара.Номенклатура;
			
		КонецЕсли;
		
	ИначеЕсли ТипОбъекта = "ВидыКонтактнойИнформации" Тогда
		Результат = Справочники[ТипОбъекта][ИдОбъекта];
	КонецЕсли;
		
	Возврат Результат;
	
КонецФункции

// Находит ссылку на справочник по переданному реквизиту.
//
// Параметры:
//  ИмяСправочника - Строка, имя справочника, объект которого надо найти,
//  ИмяРеквизита - Строка, имя реквизита, по которому будет проведен поиск,
//  ЗначРеквизита - произвольное значение, значение реквизита, по которому будет проведен поиск,
//  Владелец - Ссылка на владельца для поиска в иерархическом справочнике.
//
Функция НайтиСсылкуНаОбъектПоРеквизиту(ИмяСправочника, ИмяРеквизита, ЗначРеквизита, Владелец = Неопределено) Экспорт
	
	Результат = Неопределено;
	
	Если ИмяСправочника = "НоменклатураПоставщиков" Тогда
		
		// Справочник отсутсвует в системе - вернем структуру содержащую значение реквизита
		Результат = Новый Структура(ИмяРеквизита, ЗначРеквизита);
		
	Иначе
		
		ОбъектМетаданных = Метаданные.Справочники[ИмяСправочника];
		Если НЕ ОбщегоНазначения.ЭтоСтандартныйРеквизит(ОбъектМетаданных.СтандартныеРеквизиты, ИмяРеквизита) Тогда
			// это не стандартный реквизит
			Если НЕ ОбъектМетаданных.Реквизиты.Найти(ИмяРеквизита)<> Неопределено Тогда 
				// это не обычный реквизит
				Возврат Неопределено;
			Иначе
				ОписаниеРеквизита = ОбъектМетаданных.Реквизиты.Найти(ИмяРеквизита);
			КонецЕсли;
		Иначе
			ОписаниеРеквизита = ОбъектМетаданных.СтандартныеРеквизиты[ИмяРеквизита];
		КонецЕсли;
		
		СпособСравнения = " = ";
		Если ОписаниеРеквизита.Тип.Типы().Найти(Тип("Строка")) <> Неопределено И ОписаниеРеквизита.Тип.Типы().Количество() = 1 Тогда
			Если ОписаниеРеквизита.Тип.КвалификаторыСтроки.Длина = 0 Тогда
				СпособСравнения = " ПОДОБНО ";
			КонецЕсли;
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ИскСправочник.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник."+ИмяСправочника+" КАК ИскСправочник
		|ГДЕ
		|	ИскСправочник."+ИмяРеквизита+СпособСравнения+"&ЗначРеквизита";
		
		Если ЗначениеЗаполнено(Владелец) Тогда
			Запрос.Текст = Запрос.Текст + " И ИскСправочник.Владелец = &Владелец";
			Запрос.УстановитьПараметр("Владелец", 	Владелец);
		КонецЕсли;
		Запрос.УстановитьПараметр("ЗначРеквизита", ЗначРеквизита);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Результат = Выборка.Ссылка;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Находит элемент справочника по реквизитам ИНН и КПП
// Если элемент не найден возвращаем Неопределено
// Параметры:
//  ТипОбъекта - Строка, имя справочника в метаданных
//  ИНН - строка
//  КПП - строка
//
// Возвращаемое значение:
//  Результат - ссылка на справочник или неопределено
//
Функция СсылкаНаОбъектПоИННКПП(ТипОбъекта, ИНН, КПП, Организация = Неопределено) Экспорт
	
	Результат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Контрагенты.Ссылка
	|ИЗ
	|	Справочник."+ТипОбъекта+" КАК Контрагенты
	|ГДЕ
	|	Контрагенты.ИНН = &ИНН И
	|	Контрагенты.КПП = &КПП";
	
	Запрос.УстановитьПараметр("ИНН", ИНН);
	Запрос.УстановитьПараметр("КПП", КПП);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Результат = Выборка.Ссылка;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Заполняет реквизиты объекта данными из структуры реквизитов.
//
// Параметры:
//  СтруктураРеквизитов - структура - перечень значений реквизитов
//
// Возвращаемое значение:
//  Контрагент.Ссылка - ссылка на справочник контрагентов
//
Функция ЗаполнитьРеквизитыКонтрагента(СтруктураРеквизитов) Экспорт
	
	Результат = Неопределено;
	
	Если ЗначениеЗаполнено(СтруктураРеквизитов.Контрагент) Тогда
		Контрагент = СтруктураРеквизитов.Контрагент.ПолучитьОбъект();
	Иначе	
		Контрагент = Справочники.Контрагенты.СоздатьЭлемент();
	КонецЕсли;
	
	Контрагент.Наименование = СтруктураРеквизитов.Наименование;
	ИНН_КПП = СтруктураРеквизитов.ИНН_КПП;
	Контрагент.ИНН = Сред(ИНН_КПП,1,Найти(ИНН_КПП,"/")-1);
	Контрагент.КПП = Сред(ИНН_КПП,Найти(ИНН_КПП,"/")+1);
	Контрагент.Записать();
	
	Возврат Результат;
	
КонецФункции

// Заполняет структуру реквизитов товара
//
// Параметры:
//  РеквизитыНоменклатуры - Структура, содержащая параметры поиска 
//  СтруктураВозврата -Структура содержащая ссылки на номенклатуру, характеристику, упаковку
//  ИД - идентификатор обмена ЭД
//
Процедура ПолучитьРеквизитыТовара(РеквизитыНоменклатуры, СтруктураВозврата, ИД = Неопределено) Экспорт
	
	ДанныеТовара = ПолучитьДанныеТовараПоНоменклатуреПоставщика(РеквизитыНоменклатуры);
	
	СтруктураВозврата.Номенклатура   = ДанныеТовара.Номенклатура;
	СтруктураВозврата.Характеристика = ДанныеТовара.ХарактеристикаНоменклатуры;
	
КонецПроцедуры

// Сохраняет результат ручного сопоставления Номенклатуры в БД
//
// Параметры:
//  ТаблицаСопоставления -таблицаЗначений, содержащая данные сопоставления
//  Контрагент - СправочникСсылка.Контрагенты
//  Отказ - Булево, признак ошибки
//
Процедура ЗаписатьСопоставлениеНоменклатуры(ТаблицаСопоставления, Контрагент, Отказ) Экспорт
	
	НаборЗаписей = РегистрыСведений.НоменклатураКонтрагентов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Контрагент.Установить(Контрагент);
	
	Для Каждого Строка Из ТаблицаСопоставления Цикл
		
		Если ЗначениеЗаполнено(Строка.Номенклатура) Тогда
			
			Запись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(Запись, Строка);
			
			Запись.Контрагент = Контрагент;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если НаборЗаписей.Количество() > 0 Тогда
		Попытка
			НаборЗаписей.Записать(Ложь);
		Исключение
			ОбщегоНазначения.СообщитьОбОшибке(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()), Отказ);
		КонецПопытки;
	КонецЕсли;
	
КонецПроцедуры

// Возвращает ИД контрагента.
//
// Параметры
//  Контрагент – ссылка на контрагента (Организация или Контрагент)
//  ВидКонтрагента - строка, определяющая вид контрагента
//
// Возвращаемое значение:
// ИдКонтрагента - строка - значение ИдКонтрагента
//
Функция ПолучитьИДКонтрагента(Контрагент, ВидКонтрагента) Экспорт
	
	ИдКонтрагента = "";
	
	Если ВРег(ВидКонтрагента) = ВРег("Организация") Тогда
		ИдКонтрагента = Контрагент.ИНН+"_"+Контрагент.КПП;
	ИначеЕсли ВРег(ВидКонтрагента) = ВРег("Контрагент") Тогда
		ИдКонтрагента = Контрагент.ИНН+"_"+Контрагент.КПП;
	КонецЕсли;
	
	Возврат ИдКонтрагента;
	
КонецФункции

// Создает объект в ИБ по дереву параметров.
//
// Параметры:
//  СтрокаОбъекта - Структура параметров записываемого объекта,
//  ДеревоРазбора - ДеревоЗначений, результат разбора электронного документа.
//
// Возвращаемое значение:
//  НовыйЭлемент - ссылка на новый элемент в информационной базе.
//
Функция СоздатьОбъектВБД(СтрокаОбъекта, ДеревоРазбора) Экспорт
	
	НовыйЭлемент = Неопределено;
	
	Возврат НовыйЭлемент;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Заполнение таблиц соответствий элементов справочников

// Создает таблицу порядка создания типов при загрузке электронного документа.
//
// Возвращаемое значение:
//  Таблица - таблица значений.
//
Функция ЗаполнитьТаблицуПорядкаСозданияТиповОбъектов() Экспорт
	
	Таблица = Новый ТаблицаЗначений;
	
	Таблица.Колонки.Добавить("ТипОбъекта");
	Таблица.Колонки.Добавить("Порядок");
	
	Возврат Таблица;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Настройка обмена

Процедура ДополнительнаяАналитикаКонтрагентовСправочникПартнеры(ИспользуетсяСправочникПартнеры) Экспорт
	
	ИспользуетсяСправочникПартнеры = Ложь;
	
КонецПроцедуры

// Получает текст запроса по настройкам обмена.
//
// Возвращаемое значение:
//  ТекстЗапроса - текст запроса.
//
Функция ПолучитьТекстНастроекОбменаПоСоглашению() Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТЧ_Соглашения.Организация КАК Организация,
	|	ТЧ_Соглашения.Контрагент КАК Контрагент,
	|	ТЧ_Соглашения.ВидЭД,
	|	ТЧ_Соглашения.НаправлениеЭД,
	|	ТЧ_Соглашения.ИспользоватьЭЦП КАК Подписывать,
	|	ТЧ_Соглашения.Ссылка.СертификатКонтрагентаДляШифрования КАК СертификатКонтрагентаДляШифрования,
	|	ТЧ_Соглашения.Ссылка.СертификатОрганизацииДляРасшифровки КАК СертификатОрганизацииДляРасшифровки,
	|	ТЧ_Соглашения.Ссылка.РесурсВходящихДокументов КАК РесурсВходящихДокументов,
	|	ТЧ_Соглашения.Ссылка.РесурсИсходящихДокументов КАК РесурсИсходящихДокументов,
	|	ТЧ_Соглашения.ИдентификаторКонтрагента КАК ИдентификаторКонтрагента,
	|	ТЧ_Соглашения.ИдентификаторОрганизации КАК ИдентификаторОрганизации,
	|	ТЧ_Соглашения.Ссылка КАК Основание,
	|	ТЧ_Соглашения.СпособОбменаЭД КАК СпособОбменаЭД,
	|	ТЧ_Соглашения.ВерсияРегламентаЭДО,
	|	ТЧ_Соглашения.ВерсияФормата,
	|	ТЧ_Соглашения.ВерсияФорматаПакета,
	|	ТЧ_Соглашения.Ссылка.ПрограммаБанка КАК ПрограммаБанка
	|ПОМЕСТИТЬ ВТ_ТЧ_Соглашения
	|ИЗ
	|	(ВЫБРАТЬ
	|		СоглашенияОбИспользованииЭДВходящиеДокументы.Ссылка КАК Ссылка,
	|		СоглашенияОбИспользованииЭДВходящиеДокументы.ВходящийДокумент КАК ВидЭД,
	|		СоглашенияОбИспользованииЭДВходящиеДокументы.ИспользоватьЭЦП КАК ИспользоватьЭЦП,
	|		СоглашенияОбИспользованииЭДВходящиеДокументы.ОжидатьКвитанциюОДоставке КАК ОжидатьКвитанциюОДоставке,
	|		&НаправлениеЭД КАК НаправлениеЭД,
	|		СоглашенияОбИспользованииЭДВходящиеДокументы.Ссылка.Организация КАК Организация,
	|		СоглашенияОбИспользованииЭДВходящиеДокументы.Ссылка.Контрагент КАК Контрагент,
	|		СоглашенияОбИспользованииЭДВходящиеДокументы.Ссылка.ИдентификаторКонтрагента КАК ИдентификаторКонтрагента,
	|		СоглашенияОбИспользованииЭДВходящиеДокументы.Ссылка.ИдентификаторОрганизации КАК ИдентификаторОрганизации,
	|		СоглашенияОбИспользованииЭДВходящиеДокументы.Ссылка.СпособОбменаЭД КАК СпособОбменаЭД,
	|		NULL КАК ВерсияРегламентаЭДО,
	|		NULL КАК ВерсияФормата,
	|		СоглашенияОбИспользованииЭДВходящиеДокументы.Ссылка.ВерсияФорматаПакета КАК ВерсияФорматаПакета
	|	ИЗ
	|		Справочник.СоглашенияОбИспользованииЭД.ВходящиеДокументы КАК СоглашенияОбИспользованииЭДВходящиеДокументы
	|	ГДЕ
	|		СоглашенияОбИспользованииЭДВходящиеДокументы.Ссылка = &СоглашениеЭД
	|		И СоглашенияОбИспользованииЭДВходящиеДокументы.ВходящийДокумент = &ВидЭД
	|		И &НаправлениеЭД = ЗНАЧЕНИЕ(Перечисление.НаправленияЭД.Входящий)
	|		И НЕ СоглашенияОбИспользованииЭДВходящиеДокументы.Ссылка.ПометкаУдаления
	|		И СоглашенияОбИспользованииЭДВходящиеДокументы.Ссылка.СтатусСоглашения = ЗНАЧЕНИЕ(Перечисление.СтатусыСоглашенийЭД.Действует)
	|		И СоглашенияОбИспользованииЭДВходящиеДокументы.Ссылка.СпособОбменаЭД <> ЗНАЧЕНИЕ(Перечисление.СпособыОбменаЭД.ЧерезОператораЭДОТакском)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка,
	|		СоглашенияОбИспользованииЭДИсходящиеДокументы.ИсходящийДокумент,
	|		СоглашенияОбИспользованииЭДИсходящиеДокументы.ИспользоватьЭЦП,
	|		СоглашенияОбИспользованииЭДИсходящиеДокументы.ОжидатьКвитанциюОДоставке,
	|		&НаправлениеЭД,
	|		СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка.Организация,
	|		СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка.Контрагент,
	|		СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка.ИдентификаторКонтрагента,
	|		СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка.ИдентификаторОрганизации,
	|		СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка.СпособОбменаЭД,
	|		NULL,
	|		СоглашенияОбИспользованииЭДИсходящиеДокументы.ВерсияФормата,
	|		СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка.ВерсияФорматаПакета
	|	ИЗ
	|		Справочник.СоглашенияОбИспользованииЭД.ИсходящиеДокументы КАК СоглашенияОбИспользованииЭДИсходящиеДокументы
	|	ГДЕ
	|		СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка = &СоглашениеЭД
	|		И СоглашенияОбИспользованииЭДИсходящиеДокументы.ИсходящийДокумент = &ВидЭД
	|		И (&НаправлениеЭД = ЗНАЧЕНИЕ(Перечисление.НаправленияЭД.Исходящий)
	|				ИЛИ &НаправлениеЭД = ЗНАЧЕНИЕ(Перечисление.НаправленияЭД.Интеркампани))
	|		И СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка.СтатусСоглашения = ЗНАЧЕНИЕ(Перечисление.СтатусыСоглашенийЭД.Действует)
	|		И НЕ СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка.ПометкаУдаления
	|		И СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка.СпособОбменаЭД <> ЗНАЧЕНИЕ(Перечисление.СпособыОбменаЭД.ЧерезОператораЭДОТакском)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СоглашенияОбИспользованииЭД.Ссылка,
	|		&ВидЭД,
	|		ИСТИНА,
	|		NULL,
	|		&НаправлениеЭД,
	|		СоглашенияОбИспользованииЭД.Организация,
	|		СоглашенияОбИспользованииЭД.Контрагент,
	|		СоглашенияОбИспользованииЭД.ИдентификаторКонтрагента,
	|		СоглашенияОбИспользованииЭД.ИдентификаторОрганизации,
	|		СоглашенияОбИспользованииЭД.СпособОбменаЭД,
	|		NULL,
	|		NULL,
	|		NULL
	|	ИЗ
	|		Справочник.СоглашенияОбИспользованииЭД КАК СоглашенияОбИспользованииЭД
	|	ГДЕ
	|		СоглашенияОбИспользованииЭД.Ссылка = &СоглашениеЭД
	|		И СоглашенияОбИспользованииЭД.СтатусСоглашения = ЗНАЧЕНИЕ(Перечисление.СтатусыСоглашенийЭД.Действует)
	|		И (&ВидЭД = ЗНАЧЕНИЕ(Перечисление.ВидыЭД.АктЗаказчик)
	|				ИЛИ &ВидЭД = ЗНАЧЕНИЕ(Перечисление.ВидыЭД.ТОРГ12Покупатель)
	|				ИЛИ &ВидЭД = ЗНАЧЕНИЕ(Перечисление.ВидыЭД.АктИсполнитель)
	|				ИЛИ &ВидЭД = ЗНАЧЕНИЕ(Перечисление.ВидыЭД.ТОРГ12Продавец)
	|				ИЛИ &ВидЭД = ЗНАЧЕНИЕ(Перечисление.ВидыЭД.СоглашениеОбИзмененииСтоимостиОтправитель)
	|				ИЛИ &ВидЭД = ЗНАЧЕНИЕ(Перечисление.ВидыЭД.СоглашениеОбИзмененииСтоимостиПолучатель))
	|		И НЕ СоглашенияОбИспользованииЭД.ПометкаУдаления
	|		И СоглашенияОбИспользованииЭД.СпособОбменаЭД <> ЗНАЧЕНИЕ(Перечисление.СпособыОбменаЭД.ЧерезОператораЭДОТакском)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СоглашениеЧерезОЭДО.Ссылка,
	|		&ВидЭД,
	|		ИСТИНА,
	|		NULL,
	|		&НаправлениеЭД,
	|		СоглашениеЧерезОЭДО.Организация,
	|		УчастникиОбменовЭДЧерезОператоровЭДО.Участник,
	|		УчастникиОбменовЭДЧерезОператоровЭДО.Идентификатор,
	|		СоглашениеЧерезОЭДО.ИдентификаторОрганизации,
	|		СоглашениеЧерезОЭДО.СпособОбменаЭД,
	|		УчастникиОбменовЭДЧерезОператоровЭДО.ВерсияРегламентаЭДО,
	|		NULL,
	|		NULL
	|	ИЗ
	|		РегистрСведений.УчастникиОбменовЭДЧерезОператоровЭДО КАК УчастникиОбменовЭДЧерезОператоровЭДО
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СоглашенияОбИспользованииЭД КАК СоглашениеЧерезОЭДО
	|			ПО УчастникиОбменовЭДЧерезОператоровЭДО.СоглашениеОбИспользованииЭД = СоглашениеЧерезОЭДО.Ссылка
	|	ГДЕ
	|		СоглашениеЧерезОЭДО.Ссылка = &СоглашениеЭД
	|		И НЕ СоглашениеЧерезОЭДО.ПометкаУдаления
	|		И СоглашениеЧерезОЭДО.СтатусСоглашения = ЗНАЧЕНИЕ(Перечисление.СтатусыСоглашенийЭД.Действует)
	|		И УчастникиОбменовЭДЧерезОператоровЭДО.Участник = &Контрагент
	|		И УчастникиОбменовЭДЧерезОператоровЭДО.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыУчастниковОбменаЭД.Присоединен)
	|		И СоглашениеЧерезОЭДО.СпособОбменаЭД = ЗНАЧЕНИЕ(Перечисление.СпособыОбменаЭД.ЧерезОператораЭДОТакском)) КАК ТЧ_Соглашения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СправочникСертификатов.Ссылка КАК Ссылка,
	|	СправочникСертификатов.ВидДокумента КАК ВидДокумента,
	|	СправочникСертификатов.Ссылка.Организация КАК Организация,
	|	СправочникСертификатов.Ссылка.ЗапомнитьПарольКСертификату,
	|	СправочникСертификатов.Ссылка.ПарольПользователя
	|ПОМЕСТИТЬ ВТ_Сертификаты
	|ИЗ
	|	Справочник.СертификатыЭЦП.ВидыДокументов КАК СправочникСертификатов
	|ГДЕ
	|	НЕ СправочникСертификатов.Ссылка.ПометкаУдаления
	|	И НЕ СправочникСертификатов.Ссылка.Отозван
	|	И СправочникСертификатов.ИспользоватьДляПодписи
	|	И СправочникСертификатов.ВидДокумента = &ВидЭД
	|	И (СправочникСертификатов.Ссылка.Пользователь = &Пользователь
	|			ИЛИ СправочникСертификатов.Ссылка.Пользователь = ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяССылка))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТЧ_Соглашения.Организация,
	|	ВТ_ТЧ_Соглашения.Контрагент,
	|	ВТ_ТЧ_Соглашения.ВидЭД,
	|	ВТ_ТЧ_Соглашения.НаправлениеЭД,
	|	ВТ_ТЧ_Соглашения.Подписывать КАК Подписывать,
	|	ВТ_ТЧ_Соглашения.СертификатКонтрагентаДляШифрования,
	|	ВТ_ТЧ_Соглашения.СертификатОрганизацииДляРасшифровки,
	|	ВТ_ТЧ_Соглашения.РесурсВходящихДокументов КАК РесурсВходящихДокументов,
	|	ВТ_ТЧ_Соглашения.РесурсИсходящихДокументов КАК РесурсИсходящихДокументов,
	|	ВТ_ТЧ_Соглашения.ИдентификаторКонтрагента КАК ИдентификаторКонтрагента,
	|	ВТ_ТЧ_Соглашения.ИдентификаторОрганизации КАК ИдентификаторОрганизации,
	|	ВТ_ТЧ_Соглашения.Основание КАК СоглашениеЭД,
	|	ВТ_ТЧ_Соглашения.СпособОбменаЭД КАК СпособОбменаЭД,
	|	ВЫБОР
	|		КОГДА ВТ_Сертификаты.Ссылка ЕСТЬ NULL 
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СертификатыЭЦП.ПустаяСсылка)
	|		ИНАЧЕ ВТ_Сертификаты.Ссылка
	|	КОНЕЦ КАК СертификатОрганизацииДляПодписи,
	|	ВЫБОР
	|		КОГДА ВТ_Сертификаты.Ссылка ЕСТЬ NULL 
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СертификатыЭЦП.ПустаяСсылка)
	|		ИНАЧЕ ВТ_Сертификаты.Ссылка
	|	КОНЕЦ КАК СертификатОрганизацииДляПодтверждения,
	|	ВЫБОР
	|		КОГДА &НаправлениеЭД <> ЗНАЧЕНИЕ(Перечисление.НаправленияЭД.Интеркампани)
	|				ИЛИ ВТ_СертификатыОрганизацииПолучателя.Ссылка ЕСТЬ NULL 
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СертификатыЭЦП.ПустаяСсылка)
	|		ИНАЧЕ ВТ_СертификатыОрганизацииПолучателя.Ссылка
	|	КОНЕЦ КАК СертификатОрганизацииПолучателяДляПодписи,
	|	ЕСТЬNULL(ВТ_Сертификаты.ЗапомнитьПарольКСертификату, ЛОЖЬ) КАК ЗапомнитьПарольКСертификату,
	|	ВТ_Сертификаты.ПарольПользователя,
	|	ЕСТЬNULL(ВТ_СертификатыОрганизацииПолучателя.ЗапомнитьПарольКСертификату, ЛОЖЬ) КАК ЗапомнитьПарольКСертификатуОргПолуч,
	|	ВТ_СертификатыОрганизацииПолучателя.ПарольПользователя КАК ПарольПользователяОргПолуч,
	|	ВТ_ТЧ_Соглашения.ВерсияРегламентаЭДО,
	|	ВТ_ТЧ_Соглашения.ВерсияФормата,
	|	ВТ_ТЧ_Соглашения.ВерсияФорматаПакета,
	|	ВТ_ТЧ_Соглашения.ПрограммаБанка
	|ИЗ
	|	ВТ_ТЧ_Соглашения КАК ВТ_ТЧ_Соглашения
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Сертификаты КАК ВТ_Сертификаты
	|		ПО ВТ_ТЧ_Соглашения.ВидЭД = ВТ_Сертификаты.ВидДокумента
	|			И (ВТ_Сертификаты.Организация = ВТ_ТЧ_Соглашения.Организация)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Сертификаты КАК ВТ_СертификатыОрганизацииПолучателя
	|		ПО ВТ_ТЧ_Соглашения.ВидЭД = ВТ_СертификатыОрганизацииПолучателя.ВидДокумента
	|			И ВТ_ТЧ_Соглашения.Контрагент = ВТ_СертификатыОрганизацииПолучателя.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СоглашенияОбИспользованииЭДСертификатыПодписейОрганизации.Сертификат.Отпечаток КАК Отпечаток,
	|	СоглашенияОбИспользованииЭДСертификатыПодписейОрганизации.Ссылка КАК Соглашение
	|ИЗ
	|	Справочник.СоглашенияОбИспользованииЭД.СертификатыПодписейОрганизации КАК СоглашенияОбИспользованииЭДСертификатыПодписейОрганизации
	|ГДЕ
	|	СоглашенияОбИспользованииЭДСертификатыПодписейОрганизации.Ссылка = &СоглашениеЭД";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Получает текст запроса по настройкам обмена с приоритетами.
//
// Возвращаемое значение:
//  ТекстЗапроса - текст запроса.
//
Функция ПолучитьТекстЗапросаНастроекОбменаСПриоритетами() Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТЧ_Соглашения.Организация КАК Организация,
	|	ТЧ_Соглашения.Контрагент КАК Контрагент,
	|	ТЧ_Соглашения.ВидЭД,
	|	ТЧ_Соглашения.НаправлениеЭД,
	|	ТЧ_Соглашения.ИспользоватьЭЦП КАК Подписывать,
	|	ТЧ_Соглашения.Ссылка.СертификатКонтрагентаДляШифрования КАК СертификатКонтрагентаДляШифрования,
	|	ТЧ_Соглашения.Ссылка.СертификатОрганизацииДляРасшифровки КАК СертификатОрганизацииДляРасшифровки,
	|	ТЧ_Соглашения.Ссылка.РесурсВходящихДокументов КАК РесурсВходящихДокументов,
	|	ТЧ_Соглашения.Ссылка.РесурсИсходящихДокументов КАК РесурсИсходящихДокументов,
	|	ТЧ_Соглашения.ИдентификаторКонтрагента КАК ИдентификаторКонтрагента,
	|	ТЧ_Соглашения.ИдентификаторОрганизации КАК ИдентификаторОрганизации,
	|	ТЧ_Соглашения.Ссылка КАК Основание,
	|	ТЧ_Соглашения.СпособОбменаЭД КАК СпособОбменаЭД,
	|	ТЧ_Соглашения.ВерсияРегламентаЭДО КАК ВерсияРегламентаЭДО,
	|	ТЧ_Соглашения.Приоритет,
	|	ТЧ_Соглашения.ВерсияФормата,
	|	ТЧ_Соглашения.ВерсияФорматаПакета,
	|	ТЧ_Соглашения.Ссылка.ПрограммаБанка КАК ПрограммаБанка
	|ПОМЕСТИТЬ ВТ_ТЧ_Соглашения
	|ИЗ
	|	(ВЫБРАТЬ
	|		СоглашенияОбИспользованииЭДВходящиеДокументы.Ссылка КАК Ссылка,
	|		СоглашенияОбИспользованииЭДВходящиеДокументы.ВходящийДокумент КАК ВидЭД,
	|		СоглашенияОбИспользованииЭДВходящиеДокументы.ИспользоватьЭЦП КАК ИспользоватьЭЦП,
	|		СоглашенияОбИспользованииЭДВходящиеДокументы.ОжидатьКвитанциюОДоставке КАК ОжидатьКвитанциюОДоставке,
	|		&НаправлениеЭД КАК НаправлениеЭД,
	|		СоглашенияОбИспользованииЭДВходящиеДокументы.Ссылка.Организация КАК Организация,
	|		СоглашенияОбИспользованииЭДВходящиеДокументы.Ссылка.Контрагент КАК Контрагент,
	|		СоглашенияОбИспользованииЭДВходящиеДокументы.Ссылка.ИдентификаторКонтрагента КАК ИдентификаторКонтрагента,
	|		СоглашенияОбИспользованииЭДВходящиеДокументы.Ссылка.ИдентификаторОрганизации КАК ИдентификаторОрганизации,
	|		СоглашенияОбИспользованииЭДВходящиеДокументы.Ссылка.СпособОбменаЭД КАК СпособОбменаЭД,
	|		NULL КАК ВерсияРегламентаЭДО,
	|		NULL КАК ВерсияФормата,
	|		СоглашенияОбИспользованииЭДВходящиеДокументы.Ссылка.ВерсияФорматаПакета КАК ВерсияФорматаПакета,
	|		0 КАК Приоритет
	|	ИЗ
	|		Справочник.СоглашенияОбИспользованииЭД.ВходящиеДокументы КАК СоглашенияОбИспользованииЭДВходящиеДокументы
	|	ГДЕ
	|		НЕ СоглашенияОбИспользованииЭДВходящиеДокументы.Ссылка.ПометкаУдаления
	|		И СоглашенияОбИспользованииЭДВходящиеДокументы.Ссылка.СпособОбменаЭД <> ЗНАЧЕНИЕ(Перечисление.СпособыОбменаЭД.ЧерезОператораЭДОТакском)
	|		И СоглашенияОбИспользованииЭДВходящиеДокументы.Ссылка.СтатусСоглашения = ЗНАЧЕНИЕ(Перечисление.СтатусыСоглашенийЭД.Действует)
	|		И ВЫБОР
	|				КОГДА &ВидЭД = НЕОПРЕДЕЛЕНО
	|						И &НаправлениеЭД = НЕОПРЕДЕЛЕНО
	|					ТОГДА СоглашенияОбИспользованииЭДВходящиеДокументы.Ссылка.Организация = &Организация
	|							И СоглашенияОбИспользованииЭДВходящиеДокументы.Ссылка.Контрагент = &Контрагент
	|				КОГДА СоглашенияОбИспользованииЭДВходящиеДокументы.Ссылка.Организация = &Организация
	|						И СоглашенияОбИспользованииЭДВходящиеДокументы.Ссылка.Контрагент = &Контрагент
	|						И СоглашенияОбИспользованииЭДВходящиеДокументы.ВходящийДокумент = &ВидЭД
	|					ТОГДА СоглашенияОбИспользованииЭДВходящиеДокументы.Формировать
	|				КОГДА СоглашенияОбИспользованииЭДВходящиеДокументы.ВходящийДокумент = ЗНАЧЕНИЕ(Перечисление.ВидыЭД.ПрайсЛист)
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ ЛОЖЬ
	|			КОНЕЦ
	|		И &НаправлениеЭД = ЗНАЧЕНИЕ(Перечисление.НаправленияЭД.Входящий)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка,
	|		СоглашенияОбИспользованииЭДИсходящиеДокументы.ИсходящийДокумент,
	|		СоглашенияОбИспользованииЭДИсходящиеДокументы.ИспользоватьЭЦП,
	|		СоглашенияОбИспользованииЭДИсходящиеДокументы.ОжидатьКвитанциюОДоставке,
	|		&НаправлениеЭД,
	|		СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка.Организация,
	|		СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка.Контрагент,
	|		СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка.ИдентификаторКонтрагента,
	|		СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка.ИдентификаторОрганизации,
	|		СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка.СпособОбменаЭД,
	|		NULL,
	|		СоглашенияОбИспользованииЭДИсходящиеДокументы.ВерсияФормата,
	|		СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка.ВерсияФорматаПакета,
	|		0
	|	ИЗ
	|		Справочник.СоглашенияОбИспользованииЭД.ИсходящиеДокументы КАК СоглашенияОбИспользованииЭДИсходящиеДокументы
	|	ГДЕ
	|		НЕ СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка.ПометкаУдаления
	|		И СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка.СпособОбменаЭД <> ЗНАЧЕНИЕ(Перечисление.СпособыОбменаЭД.ЧерезОператораЭДОТакском)
	|		И СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка.СтатусСоглашения = ЗНАЧЕНИЕ(Перечисление.СтатусыСоглашенийЭД.Действует)
	|		И ВЫБОР
	|				КОГДА &ВидЭД = НЕОПРЕДЕЛЕНО
	|						И &НаправлениеЭД = НЕОПРЕДЕЛЕНО
	|					ТОГДА СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка.Организация = &Организация
	|							И СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка.Контрагент = &Контрагент
	|				КОГДА СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка.Организация = &Организация
	|						И СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка.Контрагент = &Контрагент
	|						И СоглашенияОбИспользованииЭДИсходящиеДокументы.ИсходящийДокумент = &ВидЭД
	|					ТОГДА СоглашенияОбИспользованииЭДИсходящиеДокументы.Формировать
	|				КОГДА СоглашенияОбИспользованииЭДИсходящиеДокументы.ИсходящийДокумент = ЗНАЧЕНИЕ(Перечисление.ВидыЭД.ПрайсЛист)
	|						И СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка.Организация = &Организация
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ ЛОЖЬ
	|			КОНЕЦ
	|		И (&НаправлениеЭД = ЗНАЧЕНИЕ(Перечисление.НаправленияЭД.Исходящий)
	|				ИЛИ &НаправлениеЭД = ЗНАЧЕНИЕ(Перечисление.НаправленияЭД.Интеркампани))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СоглашениеЧерезОЭДО.Ссылка,
	|		&ВидЭД,
	|		ИСТИНА,
	|		NULL,
	|		&НаправлениеЭД,
	|		СоглашениеЧерезОЭДО.Организация,
	|		УчастникиОбменовЭДЧерезОператоровЭДО.Участник,
	|		УчастникиОбменовЭДЧерезОператоровЭДО.Идентификатор,
	|		СоглашениеЧерезОЭДО.ИдентификаторОрганизации,
	|		СоглашениеЧерезОЭДО.СпособОбменаЭД,
	|		УчастникиОбменовЭДЧерезОператоровЭДО.ВерсияРегламентаЭДО,
	|		NULL,
	|		NULL,
	|		1
	|	ИЗ
	|		РегистрСведений.УчастникиОбменовЭДЧерезОператоровЭДО КАК УчастникиОбменовЭДЧерезОператоровЭДО
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СоглашенияОбИспользованииЭД КАК СоглашениеЧерезОЭДО
	|			ПО УчастникиОбменовЭДЧерезОператоровЭДО.СоглашениеОбИспользованииЭД = СоглашениеЧерезОЭДО.Ссылка
	|	ГДЕ
	|		НЕ СоглашениеЧерезОЭДО.ПометкаУдаления
	|		И СоглашениеЧерезОЭДО.СтатусСоглашения = ЗНАЧЕНИЕ(Перечисление.СтатусыСоглашенийЭД.Действует)
	|		И НЕ &НаправлениеЭД = ЗНАЧЕНИЕ(Перечисление.НаправленияЭД.Интеркампани)
	|		И СоглашениеЧерезОЭДО.Организация = &Организация
	|		И УчастникиОбменовЭДЧерезОператоровЭДО.Участник = &Контрагент
	|		И УчастникиОбменовЭДЧерезОператоровЭДО.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыУчастниковОбменаЭД.Присоединен)
	|		И СоглашениеЧерезОЭДО.СпособОбменаЭД = ЗНАЧЕНИЕ(Перечисление.СпособыОбменаЭД.ЧерезОператораЭДОТакском)) КАК ТЧ_Соглашения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СправочникСертификатов.Ссылка КАК Ссылка,
	|	СправочникСертификатов.ВидДокумента КАК ВидДокумента,
	|	СправочникСертификатов.Ссылка.Организация КАК Организация,
	|	СправочникСертификатов.Ссылка.ЗапомнитьПарольКСертификату,
	|	СправочникСертификатов.Ссылка.ПарольПользователя
	|ПОМЕСТИТЬ ВТ_Сертификаты
	|ИЗ
	|	Справочник.СертификатыЭЦП.ВидыДокументов КАК СправочникСертификатов
	|ГДЕ
	|	НЕ СправочникСертификатов.Ссылка.ПометкаУдаления
	|	И (СправочникСертификатов.Ссылка.Организация = &Организация
	|			ИЛИ ВЫБОР
	|				КОГДА &НаправлениеЭД = ЗНАЧЕНИЕ(Перечисление.НаправленияЭД.Интеркампани)
	|					ТОГДА СправочникСертификатов.Ссылка.Организация = &Контрагент
	|				ИНАЧЕ ЛОЖЬ
	|			КОНЕЦ)
	|	И НЕ СправочникСертификатов.Ссылка.Отозван
	|	И ВЫБОР
	|			КОГДА &ВидЭД = НЕОПРЕДЕЛЕНО
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ СправочникСертификатов.ВидДокумента = &ВидЭД
	|		КОНЕЦ
	|	И СправочникСертификатов.ИспользоватьДляПодписи
	|	И (СправочникСертификатов.Ссылка.Пользователь = &Пользователь
	|			ИЛИ СправочникСертификатов.Ссылка.Пользователь = ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяССылка))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТЧ_Соглашения.Приоритет КАК Приоритет,
	|	ВТ_ТЧ_Соглашения.Организация,
	|	ВТ_ТЧ_Соглашения.Контрагент,
	|	ВТ_ТЧ_Соглашения.ВидЭД,
	|	ВТ_ТЧ_Соглашения.НаправлениеЭД,
	|	ВТ_ТЧ_Соглашения.Подписывать КАК Подписывать,
	|	ВТ_ТЧ_Соглашения.СертификатКонтрагентаДляШифрования,
	|	ВТ_ТЧ_Соглашения.СертификатОрганизацииДляРасшифровки,
	|	ВТ_ТЧ_Соглашения.РесурсВходящихДокументов КАК РесурсВходящихДокументов,
	|	ВТ_ТЧ_Соглашения.РесурсИсходящихДокументов КАК РесурсИсходящихДокументов,
	|	ВТ_ТЧ_Соглашения.ИдентификаторКонтрагента КАК ИдентификаторКонтрагента,
	|	ВТ_ТЧ_Соглашения.ИдентификаторОрганизации КАК ИдентификаторОрганизации,
	|	ВТ_ТЧ_Соглашения.Основание КАК СоглашениеЭД,
	|	ВТ_ТЧ_Соглашения.СпособОбменаЭД КАК СпособОбменаЭД,
	|	ВЫБОР
	|		КОГДА ВТ_Сертификаты.Ссылка ЕСТЬ NULL 
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СертификатыЭЦП.ПустаяСсылка)
	|		ИНАЧЕ ВТ_Сертификаты.Ссылка
	|	КОНЕЦ КАК СертификатОрганизацииДляПодписи,
	|	ВЫБОР
	|		КОГДА ВТ_Сертификаты.Ссылка ЕСТЬ NULL 
	|				ИЛИ ВТ_Сертификаты.Организация <> &Организация
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СертификатыЭЦП.ПустаяСсылка)
	|		ИНАЧЕ ВТ_Сертификаты.Ссылка
	|	КОНЕЦ КАК СертификатОрганизацииДляПодтверждения,
	|	ВЫБОР
	|		КОГДА НЕ &НаправлениеЭД = ЗНАЧЕНИЕ(Перечисление.НаправленияЭД.Интеркампани)
	|				ИЛИ ВТ_СертификатыОрганизацииПолучателя.Ссылка ЕСТЬ NULL 
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СертификатыЭЦП.ПустаяСсылка)
	|		ИНАЧЕ ВТ_СертификатыОрганизацииПолучателя.Ссылка
	|	КОНЕЦ КАК СертификатОрганизацииПолучателяДляПодписи,
	|	ЕСТЬNULL(ВТ_Сертификаты.ЗапомнитьПарольКСертификату, ЛОЖЬ) КАК ЗапомнитьПарольКСертификату,
	|	ВТ_Сертификаты.ПарольПользователя,
	|	ЕСТЬNULL(ВТ_СертификатыОрганизацииПолучателя.ЗапомнитьПарольКСертификату, ЛОЖЬ) КАК ЗапомнитьПарольКСертификатуОргПолуч,
	|	ВТ_СертификатыОрганизацииПолучателя.ПарольПользователя КАК ПарольПользователяОргПолуч,
	|	ВТ_ТЧ_Соглашения.ВерсияРегламентаЭДО,
	|	ВТ_ТЧ_Соглашения.ВерсияФормата,
	|	ВТ_ТЧ_Соглашения.ВерсияФорматаПакета,
	|	ВТ_ТЧ_Соглашения.ПрограммаБанка
	|ИЗ
	|	ВТ_ТЧ_Соглашения КАК ВТ_ТЧ_Соглашения
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Сертификаты КАК ВТ_Сертификаты
	|		ПО ВТ_ТЧ_Соглашения.ВидЭД = ВТ_Сертификаты.ВидДокумента
	|			И ВТ_ТЧ_Соглашения.Организация = ВТ_Сертификаты.Организация
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Сертификаты КАК ВТ_СертификатыОрганизацииПолучателя
	|		ПО ВТ_ТЧ_Соглашения.ВидЭД = ВТ_СертификатыОрганизацииПолучателя.ВидДокумента
	|			И ВТ_ТЧ_Соглашения.Контрагент = ВТ_СертификатыОрганизацииПолучателя.Организация
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &ВидЭД = НЕОПРЕДЕЛЕНО
	|					И &НаправлениеЭД = НЕОПРЕДЕЛЕНО
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ВТ_ТЧ_Соглашения.НаправлениеЭД = &НаправлениеЭД
	|					И ВТ_ТЧ_Соглашения.ВидЭД = &ВидЭД
	|		КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Приоритет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СоглашенияОбИспользованииЭДСертификатыПодписейОрганизации.Ссылка КАК Соглашение,
	|	СоглашенияОбИспользованииЭДСертификатыПодписейОрганизации.Сертификат.Отпечаток КАК Отпечаток
	|ИЗ
	|	Справочник.СоглашенияОбИспользованииЭД.СертификатыПодписейОрганизации КАК СоглашенияОбИспользованииЭДСертификатыПодписейОрганизации
	|ГДЕ
	|	СоглашенияОбИспользованииЭДСертификатыПодписейОрганизации.Ссылка.СпособОбменаЭД = ЗНАЧЕНИЕ(Перечисление.СпособыОбменаЭД.ЧерезВебРесурсБанка)
	|	И НЕ СоглашенияОбИспользованииЭДСертификатыПодписейОрганизации.Ссылка.ПометкаУдаления
	|	И СоглашенияОбИспользованииЭДСертификатыПодписейОрганизации.Ссылка.СтатусСоглашения = ЗНАЧЕНИЕ(Перечисление.СтатусыСоглашенийЭД.Действует)";
	
	Возврат ТекстЗапроса;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Получение данных для формирования электронных документов

// Заполняет таблицу реквизитов контрагента для приглашения к обмену.
//
// Параметры:
//  ТаблицаРеквизитов - таблица значений с полями: Участник, Наименование, ИНН, КПП, АдресЭП, ВнешнийКод
//  МассивКонтрагентов - массив ссылок на участников-контрагентов.
//
Процедура ЗаполнитьРеквизитыКонтрагентовДляПриглашенияКОбмену(ТаблицаРеквизитов, МассивКонтрагентов, СоглашениеЭД) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Контрагенты.Ссылка                           КАК Участник,
	|	Контрагенты.НаименованиеПолное               КАК Наименование,
	|	Контрагенты.ИНН                              КАК ИНН,
	|	Контрагенты.КПП                              КАК КПП,
	|	Контрагенты.Код                              КАК ВнешнийКод,
	|	УчастникиОбменовЭДЧерезОператоровЭДО.АдресЭП КАК АдресЭП,
	|	Контрагенты.Наименование                     КАК НаименованиеДляСообщенияПользователю
	|ИЗ
	|	РегистрСведений.УчастникиОбменовЭДЧерезОператоровЭДО КАК УчастникиОбменовЭДЧерезОператоровЭДО
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
	|		ПО УчастникиОбменовЭДЧерезОператоровЭДО.Участник = Контрагенты.Ссылка
	|ГДЕ
	|	УчастникиОбменовЭДЧерезОператоровЭДО.Участник В (&СписокУчастников)
	|	И УчастникиОбменовЭДЧерезОператоровЭДО.СоглашениеОбИспользованииЭД = &СоглашениеЭД";
	Запрос.УстановитьПараметр("СписокУчастников", МассивКонтрагентов);
	Запрос.УстановитьПараметр("СоглашениеЭД",     СоглашениеЭД);
	
	ТЗ = Запрос.Выполнить().Выгрузить();
	
	ТаблицаРеквизитов = ТЗ.Скопировать();
	
КонецПроцедуры

// Получает реквизиты элемнта справочника "Организации".
//
// Параметры:
//  Организация - СправочникСсылка.Организации - элемент справочника организации;
//  СтруктураВозврата - струтура - перечень параметров организации.
//
Процедура ПолучитьРеквизитыОрганизации(Организация, СтруктураВозврата) Экспорт
	
	Запрос = Новый Запрос();
	Запрос.Текст =  
	"ВЫБРАТЬ
	|	Организации.Наименование 		КАК Наименование,
	|	Организации.НаименованиеПолное 	КАК НаименованиеПолное,
	|	Организации.ИНН 				КАК ИНН,
	|	Организации.КПП 				КАК КПП
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.Ссылка = &Организация";
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		СтруктураВозврата.Наименование 			= Выборка.Наименование;
		СтруктураВозврата.НаименованиеПолное 	= Выборка.НаименованиеПолное;
		СтруктураВозврата.ИНН 					= Выборка.ИНН;
		СтруктураВозврата.КПП 					= Выборка.КПП;
	КонецЕсли;
	
КонецПроцедуры

// Получает данные о физическом (юридическом) лице по ссылке.
//
// Параметры:
//  ЮрФизЛицо - Ссылка на элемент справочника, по которому надо получить данные.
//
Функция ПолучитьДанныеЮрФизЛица(ЮрФизЛицо, БанковскийСчет = Неопределено) Экспорт
	
	Сведения = Новый Структура;
	
	Сведения = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(ЮрФизЛицо, ТекущаяДатаСеанса(), , БанковскийСчет);
	 
	Если ТипЗнч(ЮрФизЛицо) = Тип("СправочникСсылка.Организации") Тогда
		
		Если ЮрФизЛицо.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо Тогда
			 
			Сведения.Вставить("ПолноеНаименование", ОбщегоНазначения.ПолучитьЗначениеРеквизита(
														ЮрФизЛицо.ИндивидуальныйПредприниматель, 
														"Наименование"));
			
			Фамилия  = "";
			Имя      = "";
			Отчество = "";
			ОбщегоНазначения.ФамилияИнициалыФизЛица(Сведения.ПолноеНаименование, Фамилия, Имя, Отчество);
			Сведения.Вставить("Фамилия",  Фамилия);
			Сведения.Вставить("Имя",      Имя);
			Сведения.Вставить("Отчество", Отчество);
			
		КонецЕсли;
		
		Сведения.Вставить("КодОКОПФ", ЮрФизЛицо.КодОКОПФ);
	Иначе
		Сведения.Вставить("КодОКОПФ", ЮрФизЛицо.ОКОПФ);
	КонецЕсли;
	
	Сведения.Вставить("Ссылка",    ЮрФизЛицо);
	Сведения.Вставить("ЮрФизЛицо", ЮрФизЛицо.ЮрФизЛицо);
	Сведения.Вставить("ОфициальноеНаименование", Сведения.ПолноеНаименование);
	
	Возврат Сведения;
	
КонецФункции

// Получает данные свидетельства о регистрации ИП по ссылке.
//
// Параметры:
//  ИП - Ссылка на элемент справочника - по которому нужно получить данные;
//  Сведения - Строка - сведения о регистрации индивидуального предпринимателя.
//
Процедура ДанныеСвидетельстваОРегистрацииИП(ИП, Сведения)
	
	Реквизиты = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(ИП, "СвидетельствоДатаВыдачи, СвидетельствоСерияНомер");
	
	Сведения = "Свидетельство " + Реквизиты.СвидетельствоСерияНомер + " от " +  Формат(Реквизиты.СвидетельствоДатаВыдачи,"ДФ=dd.MM.yyyy");
	
КонецПроцедуры

// Получает должность подписанта по ФИО.
//
// Параметры:
//  ФИО - строка - фамилия, имя и отчество подписанта,
//  Организация - ссылка - ссылка на элемент справочника организации,
//  Должность - Строка - наименование должности подписанта.
//
Процедура ДолжностьПодписанта(ФИО, Организация, Должность) Экспорт
	
	Должность = "Директор";
	
КонецПроцедуры

// Получает контактную информацию организации по ссылке
//
// Параметры:
//  Организация - ссылка на элемент справочника Организации, по которой нужно получить данные.
//
Функция ПолучитьКонтактнуюИнформацию(Организация) Экспорт
	
	Возврат Новый ТаблицаЗначений;
	
КонецФункции

// Получает адрес электронной почты контрагента.
//
// Параметры:
//  Контрагент - справочник - ссылка на элемент справочника контрагенты,
//                            адрес которого надо получить.
//
// Возвращаемое значение:
//  АдресЭП - адрес электронной почты.
//
Функция АдресЭлектроннойПочтыКонтрагента(Контрагент) Экспорт
	
	АдресЭП = "";
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КонтактнаяИнформация.Представление 	КАК АдресЭП
	|	
	|ИЗ
	|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	|	
	|ГДЕ
	|	КонтактнаяИнформация.Объект = &Объект
	|	И КонтактнаяИнформация.Тип = &Тип
	|	И КонтактнаяИнформация.ЗначениеПоУмолчанию";
	
	Запрос.УстановитьПараметр("Тип", 	Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);
	Запрос.УстановитьПараметр("Объект", Контрагент);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда 
		АдресЭП = Выборка.АдресЭП;
	КонецЕсли;
	
	Возврат АдресЭП;
	
КонецФункции

// Получает печатный номер документа.
//
// Параметры:
//  СсылкаНаОбъект - документссылка - ссылка на документ информационной базы.
//
// Возвращаемое значение:
//  НомерОбъекта - номер документа.
//
Функция ПолучитьПечатныйНомерДокумента(СсылкаНаОбъект) Экспорт
	
	Возврат ОбщегоНазначения.ПолучитьНомерНаПечать(СсылкаНаОбъект);
	
КонецФункции

// Получает банковские счета.
//
// Параметры:
//  Организация - СправочникСсылка.Организация - ссылка на организацию.
//
// Возвращаемое значение:
//  Таблица - таблица значений с перечнем банковских счетов.
//
Функция ПолучитьБанковскиеСчета(Организация, Банк=Неопределено) Экспорт
	
	Таблица = Новый ТаблицаЗначений;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	БанковскиеСчета.Ссылка КАК БанковскийСчет
	|ИЗ
	|	Справочник.БанковскиеСчета КАК БанковскиеСчета
	|ГДЕ
	|	БанковскиеСчета.Владелец = &Организация
	|	//ОтборПоБанку И БанковскиеСчета.Банк = &Банк";
	
	Если ЗначениеЗаполнено(Банк) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "//ОтборПоБанку", "");
		Запрос.УстановитьПараметр("Банк", Банк);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Таблица = Запрос.Выполнить().Выгрузить();
	
	Возврат Таблица;
	
КонецФункции

// Получает банковские реквизиты.
//
// Параметры:
//  МассивСчетов - массив - список банковских счетов.
//
// Возвращаемое значение:
//  Таблица - перечень банковских реквизитов.
//
Функция ПолучитьБанковскиеРеквизиты(МассивСчетов) Экспорт
	
	Таблица = Новый ТаблицаЗначений;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	БанковскиеСчета.Ссылка							КАК Ссылка,
	|	БанковскиеСчета.НомерСчета 						КАК РасчетныйСчет,
	|	БанковскиеСчета.Банк.КоррСчет 					КАК КорреспондентскийСчет,
	|	БанковскиеСчета.Банк.Код 						КАК БИК,
	|	БанковскиеСчета.Банк.Наименование 				КАК Банк,
	|	БанковскиеСчета.БанкДляРасчетов.Наименование 	КАК БанкДляРасчетов,
	|	БанковскиеСчета.БанкДляРасчетов.Код 			КАК БанкДляРасчетовБИК,
	|	БанковскиеСчета.БанкДляРасчетов.КоррСчет 		КАК БанкДляРасчетовКоррСчет
	|ИЗ
	|	Справочник.БанковскиеСчета КАК БанковскиеСчета
	|ГДЕ
	|	БанковскиеСчета.Ссылка В(&МассивСчетов)";
	Запрос.УстановитьПараметр("МассивСчетов", МассивСчетов);
	
	Таблица = Запрос.Выполнить().Выгрузить();
	
	Возврат Таблица;
	
КонецФункции

// Задает имя файла "по умолчанию", с которым будет предложено пользователю сохранить
// ЭД при выгрузке по сценарию "Однократной сделки".
//
// Параметры:
//  ВладелецЭД - ссылка на документ ИБ, на основании которого формируется и выгружается ЭД,
//  НаименованиеФайла - Строка - имя файла.
//
Процедура ЗадатьИмяСохраняемогоФайлаПриБыстромОбмене(ВладелецЭД, НаименованиеФайла) Экспорт
	
КонецПроцедуры

// Получает реквизиты элемнта справочника "Организации", для выгрузки в xml-файл.
//
// Параметры:
//  Организация - СправочникСсылка.Организации - элемент справочника организации;
//  СтруктураВозврата - структура - перечень параметров организации.
//
Процедура ПолучитьРеквизитыОрганизацииДляВыгрузкиВФайл(Организация, СтруктураВозврата) Экспорт
	
	ЗаполнитьЗначенияСвойств(СтруктураВозврата, 
		ОбщегоНазначения.ПолучитьЗначенияРеквизитов(Организация, "Наименование, НаименованиеПолное, ИНН, КПП, ЮрФизЛицо"));
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// Формирование данных для электронных документов

// Подготавливает данные для электронного документа типа СчетФактураВыданный.
//
// Параметры:
//  СсылкаНаОбъект - документСсылка - ссылка на объект информационной базы, по которому необходимо создать электронный документ;
//  СтруктураЭД - структура - структура данных для формирования электронного документа;
//  СтруктураПараметров - структура - параметры заполнения электронного документа.
//
Процедура ПодготовитьДанныеПоСчетФактуре(СсылкаНаОбъект, СтруктураЭД, СтруктураПараметров) Экспорт
	
	ДанныеДляФормированияЭД = Неопределено;
	УчетнаяПолитика = Неопределено;
	
	Документы.СчетФактураВыданный.СобратьДанныеДляПечати(СсылкаНаОбъект, ДанныеДляФормированияЭД, УчетнаяПолитика);
	
	Если ТипЗнч(ДанныеДляФормированияЭД) = Тип("Соответствие") Тогда
		СтруктураПараметров.Вставить("ДанныеПодготовлены", Ложь);
		Возврат;
	ИначеЕсли ДанныеДляФормированияЭД = Неопределено Тогда
		СтруктураПараметров.Вставить("ДанныеПодготовлены", Ложь);
		Возврат;
	КонецЕсли;
	
	РеквизитыСФ = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(СсылкаНаОбъект, 
		"Номер, Дата, Исправление, НомерИсправления, 
		|НомерИсходногоДокумента, ДатаИсходногоДокумента");
		
	СведенияОПокупателе = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(ДанныеДляФормированияЭД.Покупатель, РеквизитыСФ.Дата);
	СведенияОПоставщике  = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(ДанныеДляФормированияЭД.Поставщик, РеквизитыСФ.Дата);
	
	// Подготовим параметры для заполнения шапки
	
	ИндексПодразделения = 0;
	Если ЗначениеЗаполнено(ДанныеДляФормированияЭД.Организация.ГоловнаяОрганизация) Тогда
		ИндексПодразделения = ДанныеДляФормированияЭД.Организация.ЦифровойИндексОбособленногоПодразделения;
	КонецЕсли;
	Если ИндексПодразделения <> 0 Тогда
		ДанныеДляФормированияЭД.Номер = ДанныеДляФормированияЭД.Номер + "/" + ИндексПодразделения;
	КонецЕсли;
	
	СтруктураПараметров.НомерСчФ = ?(РеквизитыСФ.Исправление, РеквизитыСФ.НомерИсходногоДокумента, ПолучитьПечатныйНомерДокумента(СсылкаНаОбъект));
	СтруктураПараметров.ДатаСчФ  = ?(РеквизитыСФ.Исправление, РеквизитыСФ.ДатаИсходногоДокумента,  ДанныеДляФормированияЭД.Дата);

	Если РеквизитыСФ.Исправление Тогда
		СтруктураПараметров.НомерИсправленияИсходногоСчФ = РеквизитыСФ.НомерИсправления;
		СтруктураПараметров.ДатаИсправленияИсходногоСчФ  = РеквизитыСФ.Дата;
	КонецЕсли;

	// Подготовим таблицу товаров
	
	ТолькоУслуги = Истина;
	
	НомерСтроки = 1;
	Для каждого Строка Из ДанныеДляФормированияЭД.ТабличнаяЧасть Цикл
		
		Если ТолькоУслуги
			 И ((ТипЗнч(Строка.Товар) = Тип("СправочникСсылка.Номенклатура") И НЕ Строка.Товар.Услуга)
				 ИЛИ ТипЗнч(Строка.Товар) = Тип("СправочникСсылка.ОсновныеСредства")) Тогда
			ТолькоУслуги = Ложь;
		КонецЕсли;
		
		НоваяСтрока = СтруктураПараметров.ТаблицаТоваров.Добавить();
		
		НоваяСтрока.НомерСтроки               = НомерСтроки;
		НоваяСтрока.НаименованиеНоменклатуры  = Строка.ТоварНаименование;
		НоваяСтрока.ЕдиницаИзмеренияКод       = Строка.ЕдиницаИзмеренияКод;
		НоваяСтрока.Количество                = Строка.Количество;
		Если Строка.Количество = 0 Тогда
			НоваяСтрока.Цена = 0;
		Иначе
			НоваяСтрока.Цена = Окр(?(Строка.СуммаВключаетНДС, Строка.Сумма - Строка.СуммаНДС, Строка.Сумма) / Строка.Количество, 2);
		КонецЕсли;
		НоваяСтрока.СуммаБезНДС               = ?(Строка.СуммаВключаетНДС, Строка.Сумма - Строка.СуммаНДС, Строка.Сумма);
		НоваяСтрока.Сумма                     = ?(Строка.СуммаВключаетНДС, Строка.Сумма, Строка.Сумма + Строка.СуммаНДС);
		НоваяСтрока.СтавкаНДС                 = ПредставлениеСтавкиНДС(Строка.СтавкаНДС);
		НоваяСтрока.СтавкаНДСТип              = ТипСтавкиНДС(Строка.СтавкаНДС);
		НоваяСтрока.СуммаНДС                  = Строка.СуммаНДС;
		НоваяСтрока.КодСтраныПроисхождения    = ?(ЗначениеЗаполнено(Строка.СтранаПроисхожденияКод), Строка.СтранаПроисхожденияКод, "");
		НоваяСтрока.НомерТаможеннойДекларации = ?(ЗначениеЗаполнено(Строка.НомерГТД), СокрЛП(Строка(Строка.НомерГТД)), "");
		НоваяСтрока.Акциза                    = "без акциза";
		
		НомерСтроки = НомерСтроки + 1;
		
	КонецЦикла;
	
	СтруктураПараметров.СуммаБезНДСВсего = СтруктураПараметров.ТаблицаТоваров.Итог("СуммаБезНДС");
	СтруктураПараметров.СуммаСНДСВсего   = СтруктураПараметров.ТаблицаТоваров.Итог("Сумма");
	СтруктураПараметров.СуммаНДСВсего    = СтруктураПараметров.ТаблицаТоваров.Итог("СуммаНДС");
	СтруктураПараметров.ТолькоУслуги     = ТолькоУслуги;
	
	Если НЕ ТолькоУслуги Тогда
		
		// Сведения по грузоотправителю и грузополучаетелю
		Грузоотправитель = ?(ДанныеДляФормированияЭД.Грузоотправитель = "он же", 
				СтруктураЭД.Организация, 
				ДанныеДляФормированияЭД.Грузоотправитель);
		ЗаполнитьСведенияУчастникаОбмена(СтруктураПараметров.СведенияОГрузоотправителе.Грузоотправитель, ПолучитьДанныеЮрФизЛица(Грузоотправитель));
		
		Грузополучатель = ?(ДанныеДляФормированияЭД.Грузополучатель = "он же", 
			СтруктураЭД.Контрагент, 
			ДанныеДляФормированияЭД.Грузополучатель);
		ЗаполнитьСведенияУчастникаОбмена(СтруктураПараметров.Грузополучатель, ПолучитьДанныеЮрФизЛица(Грузополучатель));
		
	КонецЕсли;
	
	// Таблица оплат заполняется только для не корректировочного счета-фактуры
	Для каждого Строка Из ДанныеДляФормированияЭД.ТаблицаДатОплат Цикл
		
		НоваяСтрока = СтруктураПараметров.ПлатежныеДокументы.Добавить();
		
		НоваяСтрока.ДатаПРД  = Строка.ДатаПлатежноРасчетногоДокумента;
		НоваяСтрока.НомерПРД = Строка.НомерПлатежноРасчетногоДокумента;
		
	КонецЦикла;
		
	ЭтоФизЛицо = ЭтоФизЛицо(СтруктураЭД.Организация);
	
	СтруктураПараметров.КодВалюты      = глЗначениеПеременной("ВалютаРегламентированногоУчета").Код;
	
	СведенияОПоставщике = ПолучитьДанныеЮрФизЛица(СтруктураЭД.Организация);
	СведенияОПокупателе = ПолучитьДанныеЮрФизЛица(СтруктураЭД.Контрагент);
	
	ЗаполнитьСведенияУчастникаОбмена(СтруктураПараметров.Организация, СведенияОПоставщике);
	ЗаполнитьСведенияУчастникаОбмена(СтруктураПараметров.Контрагент, СведенияОПокупателе);
	
	// Заполним документы-основания
	МассивДокументовОснований = Новый Массив;
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СчетФактураВыданныйДокументыОснования.ДокументОснование КАК ДокументОснование
	|ИЗ
	|	Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|ГДЕ
	|	СчетФактураВыданныйДокументыОснования.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаОбъект);
	
	МассивДокументовОснований = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ДокументОснование");
	
	Если ЗначениеЗаполнено(МассивДокументовОснований) Тогда
		СтруктураПараметров.ДокументыОснования = МассивДокументовОснований;
	КонецЕсли;
	
	// Заполним данные по подписанту
	ЭтоФизЛицо = ЭтоФизЛицо(СтруктураЭД.Организация);
	ДанныеЮрФизЛица = ПолучитьДанныеЮрФизЛица(СтруктураЭД.Организация);
	ФИО       = "";
	Должность = Неопределено;
	
	Если ЭтоФизЛицо Тогда
		ФИО = ДанныеЮрФизЛица.ПолноеНаименование;
		ДанныеСвидетельстваОРегистрацииИП(ДанныеДляФормированияЭД.Организация, СтруктураПараметров.Подписант.СвидетельствоОРегистрацииИП);
	Иначе
		Руководитель = ОтветственноеЛицоОрганизации(
			РеквизитыСФ.Дата, 
			ДанныеДляФормированияЭД.Организация, 
			Перечисления.ОтветственныеЛицаОрганизаций.Руководитель);
		
		ФИО       = Руководитель.ФИО;
		Должность = Руководитель.Должность;
	КонецЕсли;
	
	СтруктураПараметров.Подписант.ЭтоФизЛицо = ЭтоФизЛицо;
	ЗаполнитьФИОиДолжность(СтруктураПараметров.Подписант, ФИО, Должность);
	СтруктураПараметров.Подписант.ИНН = ДанныеЮрФизЛица.ИНН;
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа КорректировочныйСчетФактураВыданный.
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
//  СтруктураЭД - Структура, структура данных для формирования электронного документа.
//
Процедура ПодготовитьДанныеПоКорректировочномуСчетуФактуре(СсылкаНаОбъект, СтруктураЭД, СтруктураПараметров) Экспорт
	
	ДанныеДляФормированияЭД = Неопределено;
	УчетнаяПолитика = Неопределено;
	
	Документы.СчетФактураВыданный.СобратьДанныеДляПечати(СсылкаНаОбъект, ДанныеДляФормированияЭД, УчетнаяПолитика);
	
	Если ТипЗнч(ДанныеДляФормированияЭД) = Тип("Соответствие") Тогда
		СтруктураПараметров.Вставить("ДанныеПодготовлены", Ложь);
		Возврат;
	ИначеЕсли ДанныеДляФормированияЭД = Неопределено Тогда
		СтруктураПараметров.Вставить("ДанныеПодготовлены", Ложь);
		Возврат;
	КонецЕсли;
	
	РеквизитыСФ = Новый Структура("Номер, Дата,
								|Исправление, НомерИсправления,
								|НомерИсправляемогоКорректировочногоДокумента, ДатаИсправляемогоКорректировочногоДокумента,
								|НомерИсходногоДокумента, ДатаИсходногоДокумента,
								|УчитыватьИсправлениеИсходногоДокумента, НомерИсправленияИсходногоДокумента, ДатаИсправленияИсходногоДокумента");
	
	ЗапросРеквизитовОснований = Новый Запрос();
	ЗапросРеквизитовОснований.Параметры.Вставить("Ссылка", СсылкаНаОбъект.Ссылка);
	ЗапросРеквизитовОснований.Текст = 
	"ВЫБРАТЬ
	|	СчетФактураВыданныйДокументыОснования.НомерИсходногоДокумента КАК НомерИсходногоДокумента,
	|	СчетФактураВыданныйДокументыОснования.ДатаИсходногоДокумента КАК ДатаИсходногоДокумента,
	|	СчетФактураВыданныйДокументыОснования.УчитыватьИсправлениеИсходногоДокумента КАК УчитыватьИсправлениеИсходногоДокумента,
	|	СчетФактураВыданныйДокументыОснования.НомерИсправленияИсходногоДокумента КАК НомерИсправленияИсходногоДокумента,
	|	СчетФактураВыданныйДокументыОснования.ДатаИсправленияИсходногоДокумента КАК ДатаИсправленияИсходногоДокумента,
	|	СчетФактураВыданный.Номер КАК Номер,
	|	СчетФактураВыданный.Дата КАК Дата,
	|	СчетФактураВыданный.Исправление КАК Исправление,
	|	СчетФактураВыданный.НомерИсправления КАК НомерИсправления,
	|	СчетФактураВыданный.НомерИсправляемогоКорректировочногоДокумента КАК НомерИсправляемогоКорректировочногоДокумента,
	|	СчетФактураВыданный.ДатаИсправляемогоКорректировочногоДокумента КАК ДатаИсправляемогоКорректировочногоДокумента
	|ИЗ
	|	Документ.СчетФактураВыданный КАК СчетФактураВыданный
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|		ПО СчетФактураВыданный.Ссылка = СчетФактураВыданныйДокументыОснования.Ссылка
	|			И (СчетФактураВыданныйДокументыОснования.НомерСтроки = 1)
	|ГДЕ
	|	СчетФактураВыданныйДокументыОснования.Ссылка = &Ссылка";
	Выборка = ЗапросРеквизитовОснований.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(РеквизитыСФ, Выборка);
	КонецЕсли;
	
	СведенияОПокупателе = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(ДанныеДляФормированияЭД.Покупатель, РеквизитыСФ.Дата);
	СведенияОПоставщике  = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(ДанныеДляФормированияЭД.Поставщик, РеквизитыСФ.Дата);
	
	Если РеквизитыСФ.Исправление Тогда
		СтруктураПараметров.НомерКоррСчФ            = РеквизитыСФ.НомерИсправляемогоКорректировочногоДокумента;
		СтруктураПараметров.ДатаКоррСчФ             = РеквизитыСФ.ДатаИсправляемогоКорректировочногоДокумента;
		СтруктураПараметров.НомерИсправленияКоррСчФ = РеквизитыСФ.НомерИсправления;
		СтруктураПараметров.ДатаИсправленияКоррСчФ  = РеквизитыСФ.Дата;
	Иначе
		СтруктураПараметров.НомерКоррСчФ = ПолучитьПечатныйНомерДокумента(СсылкаНаОбъект);
		СтруктураПараметров.ДатаКоррСчФ  = РеквизитыСФ.Дата;
	КонецЕсли;
	
	СтруктураПараметров.НомерСчФ = РеквизитыСФ.НомерИсходногоДокумента;
	СтруктураПараметров.ДатаСчФ  = РеквизитыСФ.ДатаИсходногоДокумента;
	
	Если РеквизитыСФ.УчитыватьИсправлениеИсходногоДокумента Тогда
		СтруктураПараметров.НомерИсправленияИсходногоСчФ = РеквизитыСФ.НомерИсправленияИсходногоДокумента;
		СтруктураПараметров.ДатаИсправленияИсходногоСчФ  = РеквизитыСФ.ДатаИсправленияИсходногоДокумента;
	КонецЕсли;
	
	// Подготовим таблицу товаров
	НомерСтроки = 1;
	Для каждого Строка Из ДанныеДляФормированияЭД.ТабличнаяЧасть Цикл
		
		НоваяСтрока = СтруктураПараметров.ТаблицаТоваров.Добавить();
		НоваяСтрока.НомерСтроки              = НомерСтроки;
		НоваяСтрока.НаименованиеНоменклатуры = Строка.НаименованиеТовара;
		НоваяСтрока.ЕдиницаИзмеренияКодДо    = Строка.ЕдиницаИзмеренияКод;
		НоваяСтрока.ЕдиницаИзмеренияКод      = Строка.ЕдиницаИзмеренияКод;
		НоваяСтрока.КоличествоДо             = Строка.КоличествоДоИзменения;
		НоваяСтрока.Количество               = Строка.КоличествоПослеИзменения;
		НоваяСтрока.ЦенаДо                   = Строка.ЦенаДоИзменения;
		НоваяСтрока.Цена                     = Строка.ЦенаПослеИзменения;
		НоваяСтрока.СуммаБезНДСДо            = Строка.СтоимостьБезНДСДоИзменения;
		НоваяСтрока.СуммаБезНДС              = Строка.СтоимостьБезНДСПослеИзменения;
		НоваяСтрока.СуммаБезНДСУвеличение    = Строка.РазницаБезНДСУвеличение;
		НоваяСтрока.СуммаБезНДСУменьшение    = Строка.РазницаБезНДСУменьшение;
		НоваяСтрока.СуммаДо                  = Строка.СтоимостьСНДСДоИзменения;
		НоваяСтрока.Сумма                    = Строка.СтоимостьСНДСПослеИзменения;
		НоваяСтрока.СуммаУвеличение          = Строка.РазницаСНДСУвеличение;
		НоваяСтрока.СуммаУменьшение          = Строка.РазницаСНДСУменьшение;
		НоваяСтрока.СтавкаНДСДо              = ПредставлениеСтавкиНДС(Строка.СтавкаНДС);
		НоваяСтрока.СтавкаНДС                = ПредставлениеСтавкиНДС(Строка.СтавкаНДС);
		НоваяСтрока.СтавкаНДСТипДо           = ТипСтавкиНДС(Строка.СтавкаНДС);
		НоваяСтрока.СтавкаНДСТип             = ТипСтавкиНДС(Строка.СтавкаНДС);
		НоваяСтрока.СуммаНДСДо               = Строка.СуммаНДСДоИзменения;
		НоваяСтрока.СуммаНДС                 = Строка.СуммаНДСПослеИзменения;
		НоваяСтрока.СуммаНДСУвеличение       = Строка.РазницаНДСУвеличение;
		НоваяСтрока.СуммаНДСУменьшение       = Строка.РазницаНДСУменьшение;
		НоваяСтрока.АкцизаДо                 = "без акциза";
		НоваяСтрока.Акциза                   = "без акциза";
		
		НомерСтроки = НомерСтроки + 1;
		
	КонецЦикла;
	
	СтруктураПараметров.СуммаБезНДСВсегоУвеличение = СтруктураПараметров.ТаблицаТоваров.Итог("СуммаБезНДСУвеличение");
	СтруктураПараметров.СуммаСНДСВсегоУвеличение   = СтруктураПараметров.ТаблицаТоваров.Итог("СуммаУвеличение");
	СтруктураПараметров.СуммаНДСУвеличение         = СтруктураПараметров.ТаблицаТоваров.Итог("СуммаНДСУвеличение");
	
	СтруктураПараметров.СуммаБезНДСВсегоУменьшение = СтруктураПараметров.ТаблицаТоваров.Итог("СуммаБезНДСУменьшение");
	СтруктураПараметров.СуммаСНДСВсегоУменьшение   = СтруктураПараметров.ТаблицаТоваров.Итог("СуммаУменьшение");
	СтруктураПараметров.СуммаНДСУменьшение         = СтруктураПараметров.ТаблицаТоваров.Итог("СуммаНДСУменьшение");
	

	ЭтоФизЛицо = ЭтоФизЛицо(СтруктураЭД.Организация);
	
	СтруктураПараметров.КодВалюты      = глЗначениеПеременной("ВалютаРегламентированногоУчета").Код;
	
	СведенияОПоставщике = ПолучитьДанныеЮрФизЛица(СтруктураЭД.Организация);
	СведенияОПокупателе = ПолучитьДанныеЮрФизЛица(СтруктураЭД.Контрагент);
	
	ЗаполнитьСведенияУчастникаОбмена(СтруктураПараметров.Организация, СведенияОПоставщике);
	ЗаполнитьСведенияУчастникаОбмена(СтруктураПараметров.Контрагент, СведенияОПокупателе);
	
	// Заполним документы-основания
	МассивДокументовОснований = Новый Массив;
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СчетФактураВыданныйДокументыОснования.ДокументОснование КАК ДокументОснование
	|ИЗ
	|	Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|ГДЕ
	|	СчетФактураВыданныйДокументыОснования.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаОбъект);
	
	МассивДокументовОснований = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ДокументОснование");
	
	Если ЗначениеЗаполнено(МассивДокументовОснований) Тогда
		СтруктураПараметров.ДокументыОснования = МассивДокументовОснований;
	КонецЕсли;
	
	// Заполним данные по подписанту
	ЭтоФизЛицо = ЭтоФизЛицо(СтруктураЭД.Организация);
	ДанныеЮрФизЛица = ПолучитьДанныеЮрФизЛица(СтруктураЭД.Организация);
	ФИО       = "";
	Должность = Неопределено;
	
	Если ЭтоФизЛицо Тогда
		ФИО = ДанныеЮрФизЛица.ПолноеНаименование;
		ДанныеСвидетельстваОРегистрацииИП(ДанныеДляФормированияЭД.Организация, СтруктураПараметров.Подписант.СвидетельствоОРегистрацииИП);
	Иначе
		Руководитель = ОтветственноеЛицоОрганизации(
			РеквизитыСФ.Дата, 
			ДанныеДляФормированияЭД.Организация, 
			Перечисления.ОтветственныеЛицаОрганизаций.Руководитель);
		
		ФИО       = Руководитель.ФИО;
		Должность = Руководитель.Должность;
	КонецЕсли;
	
	СтруктураПараметров.Подписант.ЭтоФизЛицо = ЭтоФизЛицо;
	ЗаполнитьФИОиДолжность(СтруктураПараметров.Подписант, ФИО, Должность);
	СтруктураПараметров.Подписант.ИНН = ДанныеЮрФизЛица.ИНН;
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа Торг12 титул продавца.
//
// Параметры:
//  СсылкаНаОбъект - документСсылка - ссылка на объект информационной базы,
//  по которому необходимо создать электронный документ.
//  СтруктураЭД - структура - структура данных для формирования электронного документа.
//  СтруктураПараметров - структура - параметры заполнения электронного документа.
//
Процедура ПодготовитьДанныеПоТорг12(СсылкаНаОбъект, СтруктураЭД, СтруктураПараметров) Экспорт
	
	ДанныеДокумента = ПолучитьДанныеРеализации(СсылкаНаОбъект);
	РеквизитыШапки  = ДанныеДокумента.РеквизитыШапки;
	
	Если ДанныеДокумента.ТаблицаТоваров.Количество() = 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Документ не содержит данных для формирования ЭД ""%1""'"),
			СтруктураЭД.ВидЭД);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		СтруктураПараметров.Вставить("ДанныеПодготовлены", Ложь);
		Возврат;
	КонецЕсли;
	
	// Сформируем таблицу товаров
	НомерСтроки = 1;
	Для каждого ДанныеСтрокиТоваров Из ДанныеДокумента.ТаблицаТоваров Цикл
		ДобавитьСтрокуТаблицуДанных(СтруктураПараметров.ТаблицаТоваров, ДанныеСтрокиТоваров, СтруктураПараметров, "Товары", НомерСтроки);
		НомерСтроки = НомерСтроки + 1;
	КонецЦикла;
		
	// Рассчитаем итоговые показатели
	ИтоговыеСуммы = СтруктураИтоговыеСуммы(СтруктураПараметров.ТаблицаТоваров, СтруктураЭД.ВидЭД);
	
	ЗаполнитьЗначенияСвойств(СтруктураПараметров.ВсегоПоНакладной, ИтоговыеСуммы);
	
	СтруктураПараметров.НомерТоварнойНакладной   = ПолучитьПечатныйНомерДокумента(СсылкаНаОбъект);
	СтруктураПараметров.ДатаТоварнойНакладной    = РеквизитыШапки.ДатаДокумента;
	СтруктураПараметров.ВидОперации              = РеквизитыШапки.ВидОперации;
	
	// Передадим документы основания
	Если ЗначениеЗаполнено(РеквизитыШапки.ДокументОснование) Тогда
		МассивДокументовОснований = Новый Массив;
		МассивДокументовОснований.Добавить(РеквизитыШапки.ДокументОснование);
		СтруктураПараметров.ДокументыОснования = МассивДокументовОснований;
	КонецЕсли;
	
	// Заполним данные по договору
	СтруктураПараметров.ДокОснованиеНаименование = РеквизитыШапки.ДоговорНаименование;
	СтруктураПараметров.ДокОснованиеНомер        = РеквизитыШапки.ДоговорНомер;
	СтруктураПараметров.ДокОснованиеДата         = РеквизитыШапки.ДоговорДата;
	
	ЗаполнитьРеквизитыУчастниковТОРГ12(РеквизитыШапки, СтруктураПараметров);
	
	Если ЗначениеЗаполнено(РеквизитыШапки.АдресДоставки) Тогда
		СтруктураДопДанных = Новый Структура;
		СтруктураДопДанных.Вставить("АдресДоставки", УправлениеКонтактнойИнформацией.ПолучитьПредставлениеАдресаПоСтрока(РеквизитыШапки.АдресДоставки));
		ЭлектронныеДокументы.ДобавитьДанныеВДеревоДопДанных(СтруктураПараметров, СтруктураДопДанных, "Шапка", Истина);
	КонецЕсли;

	// Получим данные по ответственным лица
	Руководитель = ОтветственноеЛицоОрганизации(
		РеквизитыШапки.ДатаДокумента, 
		РеквизитыШапки.Организация, 
		Перечисления.ОтветственныеЛицаОрганизаций.Руководитель);
		
	ГлавныйБухгалтер = ОтветственноеЛицоОрганизации(
		РеквизитыШапки.ДатаДокумента, 
		РеквизитыШапки.Организация, 
		Перечисления.ОтветственныеЛицаОрганизаций.ГлавныйБухгалтер);
	
	// Заполним данные по подписанту
	ЭтоФизЛицо = ЭтоФизЛицо(СтруктураЭД.Организация);
	ДанныеЮрФизЛица = ПолучитьДанныеЮрФизЛица(СтруктураЭД.Организация);
	ФИО       = "";
	Должность = Неопределено;
	
	Если ЭтоФизЛицо Тогда
		ФИО = ДанныеЮрФизЛица.ПолноеНаименование;
		ДанныеСвидетельстваОРегистрацииИП(СтруктураЭД.Организация, СтруктураПараметров.Подписант.СвидетельствоОРегистрацииИП);
	Иначе
		ФИО       = Руководитель.ФИО;
		Должность = Руководитель.Должность;
	КонецЕсли;
	
	СтруктураПараметров.Подписант.ЭтоФизЛицо = ЭтоФизЛицо;
	ЗаполнитьФИОиДолжность(СтруктураПараметров.Подписант, ФИО, Должность);
	СтруктураПараметров.Подписант.ИНН = ДанныеЮрФизЛица.ИНН;
	
	// Заполним сведения по отпуску товара
	СведенияПоОтпускуГруза = СтруктураПараметров.СведенияПоОтпускуГруза;
	СведенияПоОтпускуГруза.ДатаОтпуска             = РеквизитыШапки.ДатаДокумента;
	СведенияПоОтпускуГруза.ОтпущеноНаСумму         = ИтоговыеСуммы.СуммаСНДС;
	СведенияПоОтпускуГруза.ОтпущеноНаСуммуПрописью = СуммаПрописью(ИтоговыеСуммы.СуммаСНДС);
	
	// Заполним общие сведения по товароной накладной
	СтруктураПараметров.ОбщиеСведенияОТоварнойНакладной.Вставить("КоличествоПорядковыхНомеровЗаписей", 
		ИтоговыеСуммы.КоличествоПорядковыхНомеровЗаписей);
	СтруктураПараметров.ОбщиеСведенияОТоварнойНакладной.Вставить("КоличествоПорядковыхНомеровЗаписейПрописью", 
		ЧислоПрописью(ИтоговыеСуммы.КоличествоПорядковыхНомеровЗаписей, ,",,,,,,,,0"));
	СтруктураПараметров.ОбщиеСведенияОТоварнойНакладной.Вставить("ВсегоМест", ИтоговыеСуммы.КоличествоМест);
	СтруктураПараметров.ОбщиеСведенияОТоварнойНакладной.Вставить("ВсегоМестПрописью", ЧислоПрописью(ИтоговыеСуммы.КоличествоМест, ,",,,,,,,,0"));
	СтруктураПараметров.ОбщиеСведенияОТоварнойНакладной.Вставить("МассаГрузаНетто", ИтоговыеСуммы.МассаНетто);
	СтруктураПараметров.ОбщиеСведенияОТоварнойНакладной.Вставить("МассаГрузаБрутто", ИтоговыеСуммы.МассаБрутто);
	
	// Бухгалтер
	Бухгалтер = СведенияПоОтпускуГруза.Бухгалтер;
	ЗаполнитьФИОиДолжность(Бухгалтер, ГлавныйБухгалтер.ФИО, ГлавныйБухгалтер.Должность);
	
	// Отпуск разрешил
	ОтпускРазрешил = СведенияПоОтпускуГруза.ОтпускРазрешил;
	Если НЕ ЗначениеЗаполнено(РеквизитыШапки.ОтпускРазрешил) Тогда
		ФИО       = Руководитель.ФИО;
		Должность = Руководитель.Должность;
	Иначе
		ФИО       = ФормированиеПечатныхФормСервер.ФамилияИмяОтчество(РеквизитыШапки.ОтпускРазрешил, РеквизитыШапки.ДатаДокумента);
		Должность = ПолныеПрава.СведенияОСотруднике(РеквизитыШапки.ОтпускРазрешил, РеквизитыШапки.ДатаДокумента, РеквизитыШапки.Организация).Должность;
	КонецЕсли;
	ЗаполнитьФИОиДолжность(ОтпускРазрешил, ФИО, Должность);
	
	// Отпуск произвел
	ОтпускПроизвел = СведенияПоОтпускуГруза.ОтпускПроизвел;
	Если НЕ ЗначениеЗаполнено(РеквизитыШапки.ОтпускПроизвел) Тогда
		ФИО       = ФормированиеПечатныхФормСервер.ФамилияИмяОтчество(РеквизитыШапки.ОтветственноеЛицо, РеквизитыШапки.ДатаДокумента);
		Должность = ПолныеПрава.СведенияОСотруднике(РеквизитыШапки.ОтветственноеЛицо, РеквизитыШапки.ДатаДокумента, РеквизитыШапки.Организация).Должность;
	Иначе
		ФИО       = ФормированиеПечатныхФормСервер.ФамилияИмяОтчество(РеквизитыШапки.ОтпускПроизвел, РеквизитыШапки.ДатаДокумента);
		Должность = ПолныеПрава.СведенияОСотруднике(РеквизитыШапки.ОтпускПроизвел, РеквизитыШапки.ДатаДокумента, РеквизитыШапки.Организация).Должность;
	КонецЕсли;
	ЗаполнитьФИОиДолжность(ОтпускПроизвел, ФИО, Должность);
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа Торг12 титул покупателя.
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
//  СтруктураЭД - Структура, структура данных для формирования электронного документа.
//  СтруктураПараметров - структура - параметры заполнения электронного документа.
//
Процедура ПодготовитьДанныеПоТорг12Покупатель(СсылкаНаОбъект, СтруктураЭД, СтруктураПараметров) Экспорт
	
	// Заполним данные по подписанту
	ЭтоФизЛицо = ЭтоФизЛицо(СтруктураЭД.Организация);
	
	ДанныеЮрФизЛица = ПолучитьДанныеЮрФизЛица(СтруктураЭД.Организация);
	ФИО       = "";
	Должность = Неопределено;
	
	Если ЭтоФизЛицо Тогда
		ФИО = ДанныеЮрФизЛица.ПолноеНаименование;
		ДанныеСвидетельстваОРегистрацииИП(СтруктураЭД.Организация, СтруктураПараметров.Подписант.СвидетельствоОРегистрацииИП);
	Иначе
		Руководитель = ОтветственноеЛицоОрганизации(
			СтруктураЭД.ДатаЭД, 
			СтруктураЭД.Организация, 
			Перечисления.ОтветственныеЛицаОрганизаций.Руководитель);
		
		ФИО       = Руководитель.ФИО;
		Должность = Руководитель.Должность;
		
	КонецЕсли;
	
	СтруктураПараметров.Подписант.ЭтоФизЛицо = ЭтоФизЛицо;
	ЗаполнитьФИОиДолжность(СтруктураПараметров.Подписант, ФИО, Должность);
	СтруктураПараметров.Подписант.ИНН = ДанныеЮрФизЛица.ИНН;
	
КонецПроцедуры

// Подготавливает данные титула исполнителя для электронного документа типа Акт выполненных работ
// формата 5.01.
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
//  СтруктураЭД - Структура, структура данных для формирования электронного документа.
//  СтруктураПараметров - структура - параметры заполнения электронного документа.
//
Процедура ПодготовитьДанныеПоАкт501(СсылкаНаОбъект, СтруктураЭД, СтруктураПараметров) Экспорт
	
	ДанныеДокумента = ПодготовитьДанныеАктаОбОказанииУслуг(СсылкаНаОбъект);
	РеквизитыШапки = ДанныеДокумента.РеквизитыШапки;
	
	Если ДанныеДокумента.ТаблицаУслуг.Количество() = 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Документ не содержит данных для формирования ЭД ""%1""'"),
			СтруктураЭД.ВидЭД);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	СтруктураПараметров.НомерАкта    = ПолучитьПечатныйНомерДокумента(СсылкаНаОбъект);
	СтруктураПараметров.ДатаАкта     = РеквизитыШапки.Дата;
	СтруктураПараметров.ВидОперации  = РеквизитыШапки.ВидОперации;
	ЗаполнитьРеквизитыУчастниковАкт501(РеквизитыШапки, СтруктураПараметров);
	
	// Передадим документы основания
	Если ЗначениеЗаполнено(РеквизитыШапки.ДокументОснование) Тогда
		МассивДокументовОснований = Новый Массив;
		МассивДокументовОснований.Добавить(РеквизитыШапки.ДокументОснование);
		СтруктураПараметров.ДокументыОснования = МассивДокументовОснований;
	КонецЕсли;
	
	// Сформируем таблицу услуг
	НомерСтроки = 1;
	Для каждого ДанныеСтрокиУслуг Из ДанныеДокумента.ТаблицаУслуг Цикл
		ДобавитьСтрокуТаблицуДанных(СтруктураПараметров.ТаблицаУслуг, ДанныеСтрокиУслуг, СтруктураПараметров, "Услуги", НомерСтроки);
		НомерСтроки = НомерСтроки + 1;
	КонецЦикла;
	
	// Заполним таблицу описаний услуг, вкладывая туда таблицу с услугами.
	НоваяСтрока = СтруктураПараметров.ТаблицаОписанийУслуг.Добавить();
	НоваяСтрока.НачалоРабот      = РеквизитыШапки.Дата;
	НоваяСтрока.КонецРабот       = РеквизитыШапки.Дата;
	НоваяСтрока.СуммаБезНДСИтого = СтруктураПараметров.ТаблицаУслуг.Итог("СуммаБезНДС");
	НоваяСтрока.СуммаНДСИтого    = СтруктураПараметров.ТаблицаУслуг.Итог("СуммаНДС");
	НоваяСтрока.СуммаСНДСИтого   = СтруктураПараметров.ТаблицаУслуг.Итог("СуммаСНДС");
	НоваяСтрока.Услуги           = СтруктураПараметров.ТаблицаУслуг;
	
	Руководитель = ОтветственноеЛицоОрганизации(
		РеквизитыШапки.Дата, 
		РеквизитыШапки.Исполнитель, 
		Перечисления.ОтветственныеЛицаОрганизаций.Руководитель);
		
	СтруктураПараметров.СведенияПоВыполнениюУслуг.ДатаИсполнения = РеквизитыШапки.Дата;
	ЗаполнитьФИОиДолжность(СтруктураПараметров.СведенияПоВыполнениюУслуг.ПодписьИсполнителя, Руководитель.ФИО, Руководитель.Должность);
	
	// Заполним данные по подписанту
	ЭтоФизЛицо = ЭтоФизЛицо(РеквизитыШапки.Исполнитель);
	ДанныеЮрФизЛица = ПолучитьДанныеЮрФизЛица(РеквизитыШапки.Исполнитель);
	ФИО       = "";
	Должность = Неопределено;
	
	Если ЭтоФизЛицо Тогда
		ФИО = ДанныеЮрФизЛица.ПолноеНаименование;
		ДанныеСвидетельстваОРегистрацииИП(РеквизитыШапки.Исполнитель, СтруктураПараметров.Подписант.СвидетельствоОРегистрацииИП);
	Иначе
		ФИО       = Руководитель.ФИО;
		Должность = Руководитель.Должность;
	КонецЕсли;
	
	СтруктураПараметров.Подписант.ЭтоФизЛицо = ЭтоФизЛицо;
	ЗаполнитьФИОиДолжность(СтруктураПараметров.Подписант, ФИО, Должность);
	СтруктураПараметров.Подписант.ИНН = ДанныеЮрФизЛица.ИНН;
	
КонецПроцедуры

// Подготавливает данные титула заказчика для электронного документа типа Акт выполненных работ
// формата 5.01.
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
//  СтруктураЭД - Структура, структура данных для формирования электронного документа.
//  СтруктураПараметров - структура - параметры заполнения электронного документа.
//
Процедура ПодготовитьДанныеПоАкт501Заказчик(СсылкаНаОбъект, СтруктураЭД, СтруктураПараметров) Экспорт
	
	// Заполним данные по подписанту
	ЭтоФизЛицо = ЭтоФизЛицо(СтруктураЭД.Организация);
	ДанныеЮрФизЛица = ПолучитьДанныеЮрФизЛица(СтруктураЭД.Организация);
	ФИО       = "";
	Должность = Неопределено;
	
	Если ЭтоФизЛицо Тогда
		ФИО = ДанныеЮрФизЛица.ПолноеНаименование;
		ДанныеСвидетельстваОРегистрацииИП(СтруктураЭД.Организация, СтруктураПараметров.Подписант.СвидетельствоОРегистрацииИП);
	Иначе
		Руководитель = ОтветственноеЛицоОрганизации(
			СтруктураЭД.ДатаЭД, 
			СтруктураЭД.Организация, 
			Перечисления.ОтветственныеЛицаОрганизаций.Руководитель);
		
		ФИО       = Руководитель.ФИО;
		Должность = Руководитель.Должность;
		
	КонецЕсли;
	
	СтруктураПараметров.Подписант.ЭтоФизЛицо = ЭтоФизЛицо;
	ЗаполнитьФИОиДолжность(СтруктураПараметров.Подписант, ФИО, Должность);
	СтруктураПараметров.Подписант.ИНН = ДанныеЮрФизЛица.ИНН;
	
	СтруктураПараметров.ДатаЗаказа = ТекущаяДатаСеанса();
	СтруктураПараметров.Претензия = "Претензий нет";
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа Накладная.
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
//  СтруктураЭД - Структура, структура данных для формирования электронного документа.
//  СтруктураПараметров - структура - параметры заполнения электронного документа.
//
Процедура ПодготовитьДанныеПоНакладной(СсылкаНаОбъект, СтруктураЭД, СтруктураПараметров) Экспорт
	
	ОписаниеТиповКоличество = Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(0, 4));
	ОписаниеТиповСумма      = Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(18, 2));
	
	ТаблицаТоваров = Новый ТаблицаЗначений;
	ТаблицаТоваров.Колонки.Добавить("Ид");
	ТаблицаТоваров.Колонки.Добавить("Артикул");
	ТаблицаТоваров.Колонки.Добавить("Наименование");
	ТаблицаТоваров.Колонки.Добавить("Номенклатура");
	ТаблицаТоваров.Колонки.Добавить("Характеристика");
	ТаблицаТоваров.Колонки.Добавить("БазоваяЕдиница");
	ТаблицаТоваров.Колонки.Добавить("БазоваяЕдиницаКод");
	ТаблицаТоваров.Колонки.Добавить("БазоваяЕдиницаНаименование");
	ТаблицаТоваров.Колонки.Добавить("БазоваяЕдиницаНаименованиеПолное");
	ТаблицаТоваров.Колонки.Добавить("БазоваяЕдиницаМеждународноеСокращение");
	ТаблицаТоваров.Колонки.Добавить("Количество",    ОписаниеТиповКоличество);
	ТаблицаТоваров.Колонки.Добавить("УпаковкаКод");
	ТаблицаТоваров.Колонки.Добавить("УпаковкаНаименование");
	ТаблицаТоваров.Колонки.Добавить("Коэффициент");
	ТаблицаТоваров.Колонки.Добавить("СуммаСНДС",     ОписаниеТиповСумма);
	ТаблицаТоваров.Колонки.Добавить("СтавкаНДС");
	ТаблицаТоваров.Колонки.Добавить("СуммаНДС",      ОписаниеТиповСумма);
	ТаблицаТоваров.Колонки.Добавить("Цена",          ОписаниеТиповСумма);
	ТаблицаТоваров.Колонки.Добавить("СуммаСкидки",   ОписаниеТиповСумма);
	ТаблицаТоваров.Колонки.Добавить("СуммаБезНДС",   ОписаниеТиповСумма);
	ТаблицаТоваров.Колонки.Добавить("КодТовара");
	
	ДанныеДокумента = ПолучитьДанныеРеализации(СсылкаНаОбъект);
	РеквизитыШапки  = ДанныеДокумента.РеквизитыШапки;
	
	// Сформируем таблицу товаров
	ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(ДанныеДокумента.ТаблицаТоваров, ТаблицаТоваров);
	ОбработатьТаблицуТоваров(ТаблицаТоваров);
	
	СтруктураПараметров.Вставить("Исполнитель", 		СтруктураЭД.Отправитель);
	СтруктураПараметров.Вставить("ВерсияСхемы", 		"4.02");
	СтруктураПараметров.Вставить("ТаблицаТоваров", 		ТаблицаТоваров);
	СтруктураПараметров.Вставить("Организация", 		СтруктураЭД.Организация);
	СтруктураПараметров.Вставить("Контрагент", 			СтруктураЭД.Контрагент);
	СтруктураПараметров.Вставить("Ид", 					СтруктураЭД.НомерЭД);
	СтруктураПараметров.Вставить("ДатаФормирования", 	ТекущаяДатаСеанса());
	СтруктураПараметров.Вставить("Номер", 				СтруктураЭД.НомерДокументаОтправителя);
	СтруктураПараметров.Вставить("Дата", 				СтруктураЭД.ДатаДокументаОтправителя);
	СтруктураПараметров.Вставить("СуммаСНДСИтого",		ТаблицаТоваров.Итог("СуммаСНДС"));
	СтруктураПараметров.Вставить("ВидЭД", 				СтруктураЭД.ВидЭД);
	СтруктураПараметров.Вставить("НаправлениеЭД", 		СтруктураЭД.НаправлениеЭД);
	СтруктураПараметров.Вставить("Грузоотправитель", 	РеквизитыШапки.Грузоотправитель);
	СтруктураПараметров.Вставить("ТипГрузоотправителя", "Организация");
	СтруктураПараметров.Вставить("Грузополучатель",		РеквизитыШапки.Грузополучатель);
	СтруктураПараметров.Вставить("ЦенаВключаетНДС",		РеквизитыШапки.СуммаВключаетНДС);
	СтруктураПараметров.Вставить("Валюта",				РеквизитыШапки.ВалютаДокумента);
	СтруктураПараметров.Вставить("Курс",				РеквизитыШапки.Курс);
	СтруктураПараметров.Вставить("Кратность",			РеквизитыШапки.Кратность);
	
	// заполнение спец.реквизитов для связки заказ-поступление
	Если ТипЗнч(РеквизитыШапки.Сделка) = Тип("ДокументСсылка.ЗаказПокупателя")
		И ЗначениеЗаполнено(РеквизитыШапки.Сделка) Тогда
		
		ЗаказПокупателя = РеквизитыШапки.Сделка;
		
		СтруктураПараметров.Вставить("НомерПоДаннымПоставщика",	ЗаказПокупателя.Номер);
		СтруктураПараметров.Вставить("ДатаПоДаннымПоставщика", 	ЗаказПокупателя.Дата);
		
		Если ЗначениеЗаполнено(ЗаказПокупателя.НомерПоДаннымПокупателя) 
			И ЗначениеЗаполнено(ЗаказПокупателя.ДатаПоДаннымПокупателя) Тогда 
			СтруктураПараметров.Вставить("НомерПоДаннымКлиента",	ЗаказПокупателя.НомерПоДаннымПокупателя);
			СтруктураПараметров.Вставить("ДатаПоДаннымКлиента",		ЗаказПокупателя.ДатаПоДаннымПокупателя);
		КонецЕсли;
		
	КонецЕсли;
	
	СтруктураПараметров.Вставить("ОбязательныеПоля", "Организация, Контрагент, Ид, ДатаФормирования, 
		|Номер, Дата, ВидЭД, НаправлениеЭД, ТаблицаТоваров");
	СтруктураПараметров.Вставить("ОбязательныеПоляТаблицыЗначений", "Ид, Наименование, Номенклатура, БазоваяЕдиницаКод, УпаковкаКод");
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа Счет.
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
//  СтруктураЭД - Структура, структура данных для формирования электронного документа.
//  СтруктураПараметров - структура - параметры заполнения электронного документа.
//
Процедура ПодготовитьДанныеПоСчету(СсылкаНаОбъект, СтруктураЭД, СтруктураПараметров) Экспорт
	
	ОписаниеТиповКоличество = Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(0, 4));
	ОписаниеТиповСумма      = Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(18, 2));
	
	ТаблицаТоваров = Новый ТаблицаЗначений;
	ТаблицаТоваров.Колонки.Добавить("Ид");
	ТаблицаТоваров.Колонки.Добавить("Артикул");
	ТаблицаТоваров.Колонки.Добавить("Наименование");
	ТаблицаТоваров.Колонки.Добавить("Номенклатура");
	ТаблицаТоваров.Колонки.Добавить("Характеристика");
	ТаблицаТоваров.Колонки.Добавить("БазоваяЕдиница");
	ТаблицаТоваров.Колонки.Добавить("БазоваяЕдиницаКод");
	ТаблицаТоваров.Колонки.Добавить("БазоваяЕдиницаНаименование");
	ТаблицаТоваров.Колонки.Добавить("БазоваяЕдиницаНаименованиеПолное");
	ТаблицаТоваров.Колонки.Добавить("БазоваяЕдиницаМеждународноеСокращение");
	ТаблицаТоваров.Колонки.Добавить("Количество",    ОписаниеТиповКоличество);
	ТаблицаТоваров.Колонки.Добавить("Упаковка");
	ТаблицаТоваров.Колонки.Добавить("УпаковкаКод");
	ТаблицаТоваров.Колонки.Добавить("УпаковкаНаименование");
	ТаблицаТоваров.Колонки.Добавить("Коэффициент");
	ТаблицаТоваров.Колонки.Добавить("СуммаБезНДС",   ОписаниеТиповСумма);
	ТаблицаТоваров.Колонки.Добавить("СуммаСНДС",     ОписаниеТиповСумма);
	ТаблицаТоваров.Колонки.Добавить("СтавкаНДС");
	ТаблицаТоваров.Колонки.Добавить("СуммаНДС",      ОписаниеТиповСумма);
	ТаблицаТоваров.Колонки.Добавить("Цена",          ОписаниеТиповСумма);
	ТаблицаТоваров.Колонки.Добавить("СуммаСкидки",   ОписаниеТиповСумма);
	ТаблицаТоваров.Колонки.Добавить("Сумма",         ОписаниеТиповСумма);
	ТаблицаТоваров.Колонки.Добавить("КодТовара");
	
	РеквизитыДокумента = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(СсылкаНаОбъект, 
		"Дата, Номер, Организация, СуммаВключаетНДС, ВалютаДокумента, КурсВзаиморасчетов, СтруктурнаяЕдиница, ДатаОплаты");
	
	СтрокаВыборкиПоляСодержания    = ОбработкаТабличныхЧастей.ПолучитьЧастьЗапросаДляВыбораСодержания("СчетНаОплатуПокупателю");
	ДопКолонка = Константы.ДополнительнаяКолонкаПечатныхФормДокументов.Получить();
	Если ДопКолонка = Перечисления.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул Тогда
		ТоварКод = "Артикул";
	Иначе
		ТоварКод = "Код";
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СчетНаОплатуПокупателю.Номенклатура.Код                                        КАК КодТовара,
	|	СчетНаОплатуПокупателю.Номенклатура." + ТоварКод + "                           КАК Артикул,
	|	СчетНаОплатуПокупателю.Номенклатура.НаименованиеПолное                         КАК Наименование,
	|	СчетНаОплатуПокупателю.Номенклатура                                            КАК Номенклатура,
	|	СчетНаОплатуПокупателю.ХарактеристикаНоменклатуры                              КАК Характеристика,
	|	СчетНаОплатуПокупателю.Номенклатура.БазоваяЕдиницаИзмерения                    КАК БазоваяЕдиница,
	|	СчетНаОплатуПокупателю.Номенклатура.БазоваяЕдиницаИзмерения.Код                КАК БазоваяЕдиницаКод,
	|	СчетНаОплатуПокупателю.Номенклатура.БазоваяЕдиницаИзмерения.Наименование       КАК БазоваяЕдиницаНаименование,
	|	СчетНаОплатуПокупателю.Номенклатура.БазоваяЕдиницаИзмерения.НаименованиеПолное КАК БазоваяЕдиницаНаименованиеПолное,
	|	СчетНаОплатуПокупателю.Номенклатура.БазоваяЕдиницаИзмерения.МеждународноеСокращение КАК БазоваяЕдиницаМеждународноеСокращение,
	|	СчетНаОплатуПокупателю.ЕдиницаИзмерения.ЕдиницаПоКлассификатору                КАК ЕдиницаИзмерения,
	|	СчетНаОплатуПокупателю.ЕдиницаИзмерения.ЕдиницаПоКлассификатору                КАК Упаковка,
	|	СчетНаОплатуПокупателю.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код            КАК УпаковкаКод,
	|	СчетНаОплатуПокупателю.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Наименование   КАК УпаковкаНаименование,
	|	СчетНаОплатуПокупателю.Цена,
	|	СчетНаОплатуПокупателю.Сумма,
	|	СчетНаОплатуПокупателю.СтавкаНДС,
	|	СчетНаОплатуПокупателю.СуммаНДС,
	|	СчетНаОплатуПокупателю.Количество
	|ИЗ
	|	Документ.СчетНаОплатуПокупателю.Товары КАК СчетНаОплатуПокупателю
	|ГДЕ
	|	СчетНаОплатуПокупателю.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СчетНаОплатуПокупателю.Номенклатура.Код,
	|	СчетНаОплатуПокупателю.Номенклатура." + ТоварКод + ",
	|	" + СтрокаВыборкиПоляСодержания + ",
	|	СчетНаОплатуПокупателю.Номенклатура,
	|	"""",
	|	СчетНаОплатуПокупателю.Номенклатура.БазоваяЕдиницаИзмерения,
	|	СчетНаОплатуПокупателю.Номенклатура.БазоваяЕдиницаИзмерения.Код,
	|	СчетНаОплатуПокупателю.Номенклатура.БазоваяЕдиницаИзмерения.Наименование,
	|	СчетНаОплатуПокупателю.Номенклатура.БазоваяЕдиницаИзмерения.НаименованиеПолное,
	|	СчетНаОплатуПокупателю.Номенклатура.БазоваяЕдиницаИзмерения.МеждународноеСокращение,
	|	СчетНаОплатуПокупателю.Номенклатура.БазоваяЕдиницаИзмерения,
	|	СчетНаОплатуПокупателю.Номенклатура.БазоваяЕдиницаИзмерения,
	|	СчетНаОплатуПокупателю.Номенклатура.БазоваяЕдиницаИзмерения.Код,
	|	СчетНаОплатуПокупателю.Номенклатура.БазоваяЕдиницаИзмерения.Наименование,
	|	СчетНаОплатуПокупателю.Цена,
	|	СчетНаОплатуПокупателю.Сумма,
	|	СчетНаОплатуПокупателю.СтавкаНДС,
	|	СчетНаОплатуПокупателю.СуммаНДС,
	|	СчетНаОплатуПокупателю.Количество
	|ИЗ
	|	Документ.СчетНаОплатуПокупателю.Услуги КАК СчетНаОплатуПокупателю
	|ГДЕ
	|	СчетНаОплатуПокупателю.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаОбъект);
	
	ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(Запрос.Выполнить().Выгрузить(), ТаблицаТоваров);
	ОбработатьТаблицуТоваров(ТаблицаТоваров);
	
	Для каждого СтрокаТаблицы из ТаблицаТоваров Цикл
		
		СуммаСНДС   = Окр((СтрокаТаблицы.Сумма + ?(РеквизитыДокумента.СуммаВключаетНДС, 0, СтрокаТаблицы.СуммаНДС)), 2);
		СуммаБезНДС = СуммаСНДС - СтрокаТаблицы.СуммаНДС;
		
		СтрокаТаблицы.СуммаБезНДС = СуммаБезНДС;
		СтрокаТаблицы.СуммаСНДС   = СуммаСНДС;
		
	КонецЦикла;

	// получение данных по таблицам товаров
	СтруктураПараметров.Вставить("Исполнитель",      СтруктураЭД.Отправитель);
	СтруктураПараметров.Вставить("ВерсияСхемы",      "4.02");
	СтруктураПараметров.Вставить("ТаблицаТоваров",   ТаблицаТоваров);
	СтруктураПараметров.Вставить("Организация",      СтруктураЭД.Организация);
	СтруктураПараметров.Вставить("Контрагент",       СтруктураЭД.Контрагент);
	СтруктураПараметров.Вставить("Ид",               СтруктураЭД.НомерЭД);
	СтруктураПараметров.Вставить("ДатаФормирования", ТекущаяДатаСеанса());
	СтруктураПараметров.Вставить("Номер",            СтруктураЭД.НомерДокументаОтправителя);
	СтруктураПараметров.Вставить("Дата",             СтруктураЭД.ДатаДокументаОтправителя);
	СтруктураПараметров.Вставить("Валюта",           РеквизитыДокумента.ВалютаДокумента);
	СтруктураПараметров.Вставить("Курс",             РеквизитыДокумента.КурсВзаиморасчетов);
	СтруктураПараметров.Вставить("Сумма",            ТаблицаТоваров.Итог("Сумма"));
	СтруктураПараметров.Вставить("ЦенаВключаетНДС",  РеквизитыДокумента.СуммаВключаетНДС);
	СтруктураПараметров.Вставить("СуммаНДС",         ТаблицаТоваров.Итог("СуммаНДС"));
	СтруктураПараметров.Вставить("ВидЭД",            СтруктураЭД.ВидЭД);
	СтруктураПараметров.Вставить("НаправлениеЭД",    СтруктураЭД.НаправлениеЭД);
	
	СтруктураПараметров.Вставить("СрокПлатежа", РеквизитыДокумента.ДатаОплаты);
	СтруктураПараметров.Вставить("НазначениеПлатежа", "");
	
	Если ЗначениеЗаполнено(РеквизитыДокумента.СтруктурнаяЕдиница)
		И ТипЗнч(РеквизитыДокумента.СтруктурнаяЕдиница) = Тип("СправочникСсылка.БанковскиеСчета") Тогда
		
		РасчетныйСчет  = РеквизитыДокумента.СтруктурнаяЕдиница;
		РеквизитыБанка = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(РасчетныйСчет.Банк, 
			"Наименование, КоррСчет, Код");
		
		РасчетныйСчетСтруктура = Новый Структура;
		РасчетныйСчетСтруктура.Вставить("НомерСчета", РасчетныйСчет.НомерСчета);
		РасчетныйСчетСтруктура.Вставить("Банк",       РеквизитыБанка.Наименование);
		РасчетныйСчетСтруктура.Вставить("КоррСчет",   РеквизитыБанка.КоррСчет);
		РасчетныйСчетСтруктура.Вставить("БИК",        РеквизитыБанка.Код);
		СтруктураПараметров.Вставить("РасчетныйСчет", РасчетныйСчетСтруктура);
		
		Если ЗначениеЗаполнено(РасчетныйСчет.БанкДляРасчетов) Тогда
			
			РеквизитыБанкаДляРасчетов = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(РасчетныйСчет.БанкДляРасчетов, 
				"Наименование, КоррСчет, Код");
			
			БанкКорреспондентСтруктура = Новый Структура;
			БанкКорреспондентСтруктура.Вставить("Банк",     РеквизитыБанкаДляРасчетов.Наименование);
			БанкКорреспондентСтруктура.Вставить("КоррСчет", РеквизитыБанкаДляРасчетов.КоррСчет);
			БанкКорреспондентСтруктура.Вставить("БИК",      РеквизитыБанкаДляРасчетов.Код);
			СтруктураПараметров.Вставить("БанкКорреспондент", БанкКорреспондентСтруктура);
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Получим данные по ответственным лица
	Руководитель = ОтветственноеЛицоОрганизации(
		РеквизитыДокумента.Дата, 
		РеквизитыДокумента.Организация, 
		Перечисления.ОтветственныеЛицаОрганизаций.Руководитель);
		
	ГлавныйБухгалтер = ОтветственноеЛицоОрганизации(
		РеквизитыДокумента.Дата, 
		РеквизитыДокумента.Организация, 
		Перечисления.ОтветственныеЛицаОрганизаций.ГлавныйБухгалтер);
	
	СтруктураПараметров.Вставить("Руководитель", Руководитель.ФИО);
	СтруктураПараметров.Вставить("Бухгалтер",    ГлавныйБухгалтер.ФИО);
	
	ИтоговаяСтрока = НСтр("ru='Всего наименований %Количество%, на сумму %Сумма%'");
	ИтоговаяСтрока = СтрЗаменить(ИтоговаяСтрока, "%Количество%", ТаблицаТоваров.Количество());
	ИтоговаяСтрока = СтрЗаменить(ИтоговаяСтрока, "%Сумма%",		 ОбщегоНазначения.ФорматСумм(СсылкаНаОбъект.СуммаДокумента, СсылкаНаОбъект.ВалютаДокумента));
	СуммаПрописью  = ОбщегоНазначения.СформироватьСуммуПрописью(СсылкаНаОбъект.СуммаДокумента, СсылкаНаОбъект.ВалютаДокумента);
	ИтоговаяСтрока = ИтоговаяСтрока + Символы.ПС + СуммаПрописью;
	СтруктураПараметров.Вставить("ИтогиПрописью", ИтоговаяСтрока);
	
	СтруктураПараметров.Вставить("ОбязательныеПоля", "Организация, Контрагент, Ид, ДатаФормирования,
		|Номер, Дата, ВидЭД, НаправлениеЭД, РасчетныйСчет, ТаблицаТоваров");
	СтруктураПараметров.Вставить("ОбязательныеПоляТаблицыЗначений", "Ид, Наименование, БазоваяЕдиницаКод, УпаковкаКод");
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа ЗаказТоваров.
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
//  СтруктураЭД - Структура, структура данных для формирования электронного документа.
//  СтруктураПараметров - структура - параметры заполнения электронного документа.
//
Процедура ПодготовитьДанныеПоЗаказуТоваров(СсылкаНаОбъект, СтруктураЭД, СтруктураПараметров) Экспорт
	
	ОписаниеТиповКоличество = Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(0, 4));
	ОписаниеТиповСумма = Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(18, 2));
	
	ТаблицаТоваров = Новый ТаблицаЗначений;
	ТаблицаТоваров.Колонки.Добавить("Ид");
	ТаблицаТоваров.Колонки.Добавить("Артикул");
	ТаблицаТоваров.Колонки.Добавить("Наименование");
	ТаблицаТоваров.Колонки.Добавить("БазоваяЕдиница");
	ТаблицаТоваров.Колонки.Добавить("БазоваяЕдиницаКод");
	ТаблицаТоваров.Колонки.Добавить("БазоваяЕдиницаНаименование");
	ТаблицаТоваров.Колонки.Добавить("БазоваяЕдиницаНаименованиеПолное");
	ТаблицаТоваров.Колонки.Добавить("БазоваяЕдиницаМеждународноеСокращение");
	ТаблицаТоваров.Колонки.Добавить("Количество", ОписаниеТиповКоличество);
	ТаблицаТоваров.Колонки.Добавить("УпаковкаКод");
	ТаблицаТоваров.Колонки.Добавить("УпаковкаНаименование");
	ТаблицаТоваров.Колонки.Добавить("Коэффициент");
	ТаблицаТоваров.Колонки.Добавить("СуммаСНДС", ОписаниеТиповСумма);
	ТаблицаТоваров.Колонки.Добавить("СтавкаНДС");
	ТаблицаТоваров.Колонки.Добавить("СуммаНДС", ОписаниеТиповСумма);
	ТаблицаТоваров.Колонки.Добавить("Цена", ОписаниеТиповСумма);
	ТаблицаТоваров.Колонки.Добавить("СуммаСкидки");
	ТаблицаТоваров.Колонки.Добавить("Сумма", ОписаниеТиповСумма);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЕСТЬNULL(НоменклатураПоставщика.Идентификатор, Неопределено)						КАК Ид,
	|	ЕСТЬNULL(НоменклатураПоставщика.АртикулНоменклатурыКонтрагента, """")				КАК Артикул,
	|	ЕСТЬNULL(НоменклатураПоставщика.НаименованиеНоменклатурыКонтрагента, """")			КАК Наименование,
	|	ЗаказПоставщику.Номенклатура														КАК Номенклатура,
	|	ЗаказПоставщику.Количество 															КАК Количество,
	|	ЗаказПоставщику.Сумма																КАК Сумма,
	|	ЗаказПоставщику.Цена																КАК Цена,
	|	ЗаказПоставщику.ХарактеристикаНоменклатуры 											КАК Характеристика,
	|	ЗаказПоставщику.Номенклатура.БазоваяЕдиницаИзмерения 								КАК БазоваяЕдиница,
	|	ЗаказПоставщику.Номенклатура.БазоваяЕдиницаИзмерения.Код 							КАК БазоваяЕдиницаКод,
	|	ЗаказПоставщику.Номенклатура.БазоваяЕдиницаИзмерения.Наименование 					КАК БазоваяЕдиницаНаименование,
	|	ЗаказПоставщику.Номенклатура.БазоваяЕдиницаИзмерения.НаименованиеПолное 			КАК БазоваяЕдиницаНаименованиеПолное,
	|	ЗаказПоставщику.Номенклатура.БазоваяЕдиницаИзмерения.МеждународноеСокращение 		КАК БазоваяЕдиницаМеждународноеСокращение,
	|	ЗаказПоставщику.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код 						КАК УпаковкаКод,
	|	ЗаказПоставщику.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Наименование 				КАК УпаковкаНаименование,
	|	ЗаказПоставщику.Коэффициент															КАК Коэффициент,
	|	ЗаказПоставщику.СтавкаНДС 															КАК СтавкаНДС,
	|	ЗаказПоставщику.СуммаНДС															КАК СуммаНДС,
	|	0 																					КАК СуммаСкидки,
	|	ЗаказПоставщику.Сумма																КАК СуммаСНДС
	|ИЗ
	|	Документ.ЗаказПоставщику.Товары КАК ЗаказПоставщику
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		РегистрСведений.НоменклатураКонтрагентов КАК НоменклатураПоставщика
	|	ПО
	|		НоменклатураПоставщика.Контрагент 				= ЗаказПоставщику.Ссылка.Контрагент
	|		И НоменклатураПоставщика.Номенклатура 					= ЗаказПоставщику.Номенклатура
	|		И НоменклатураПоставщика.ХарактеристикаНоменклатуры 	= ЗаказПоставщику.ХарактеристикаНоменклатуры
	|ГДЕ
	|	ЗаказПоставщику.Ссылка = &Ссылка
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ
	|	ЕСТЬNULL(НоменклатураПоставщика.Идентификатор, Неопределено),
	|	ЕСТЬNULL(НоменклатураПоставщика.АртикулНоменклатурыКонтрагента, """"),
	|	ЕСТЬNULL(НоменклатураПоставщика.НаименованиеНоменклатурыКонтрагента, """"),
	|	ЗаказПоставщику.Номенклатура,
	|	ЗаказПоставщику.Количество,
	|	ЗаказПоставщику.Сумма,
	|	ЗаказПоставщику.Цена,
	|	Неопределено,
	|	ЗаказПоставщику.Номенклатура.БазоваяЕдиницаИзмерения,
	|	ЗаказПоставщику.Номенклатура.БазоваяЕдиницаИзмерения.Код,
	|	ЗаказПоставщику.Номенклатура.БазоваяЕдиницаИзмерения.Наименование,
	|	ЗаказПоставщику.Номенклатура.БазоваяЕдиницаИзмерения.НаименованиеПолное,
	|	ЗаказПоставщику.Номенклатура.БазоваяЕдиницаИзмерения.МеждународноеСокращение,
	|	ЗаказПоставщику.Номенклатура.БазоваяЕдиницаИзмерения.Код,
	|	ЗаказПоставщику.Номенклатура.БазоваяЕдиницаИзмерения.Наименование,
	|	1,
	|	ЗаказПоставщику.СтавкаНДС,
	|	ЗаказПоставщику.СуммаНДС,
	|	0,
	|	ЗаказПоставщику.Сумма
	|ИЗ
	|	Документ.ЗаказПоставщику.Услуги КАК ЗаказПоставщику
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		РегистрСведений.НоменклатураКонтрагентов КАК НоменклатураПоставщика
	|	ПО
	|		НоменклатураПоставщика.Контрагент 						= ЗаказПоставщику.Ссылка.Контрагент
	|		И НоменклатураПоставщика.Номенклатура 					= ЗаказПоставщику.Номенклатура
	|		И НоменклатураПоставщика.ХарактеристикаНоменклатуры 	= ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|ГДЕ
	|	ЗаказПоставщику.Ссылка = &Ссылка";
		
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаОбъект);
	Выборка = Запрос.Выполнить().Выбрать();
	
	НеУдалосьСопоставитьНоменклатуру = Ложь;
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.Ид = Неопределено Тогда
			ОбщегоНазначения.СообщитьОбОшибке(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось сопоставить номенклатуру ""%1"" с номенклатурой поставщика'"),
				Строка(Выборка.Номенклатура) + ?(ЗначениеЗаполнено(Выборка.Характеристика), "("+Выборка.Характеристика+")", "")));
				
			НеУдалосьСопоставитьНоменклатуру = Истина;
		КонецЕсли;
			
		НоваяСтрока = ТаблицаТоваров.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		
	КонецЦикла;
	
	Если НеУдалосьСопоставитьНоменклатуру Тогда
		// Не удалось сопоставить номенклатуру с номенклатурой поставщики. ЭД сформирован не будет.
		СтруктураПараметров.Вставить("ДанныеПодготовлены", Ложь);
		Возврат;
	КонецЕсли;
	
	СтруктураПараметров.Вставить("Исполнитель",				СтруктураЭД.Отправитель);
	СтруктураПараметров.Вставить("ВерсияСхемы", 			"4.02");
	СтруктураПараметров.Вставить("Роль", 					"Покупатель");
	СтруктураПараметров.Вставить("ТаблицаТоваров", 			ТаблицаТоваров);
	СтруктураПараметров.Вставить("Организация", 			СтруктураЭД.Организация);
	СтруктураПараметров.Вставить("Контрагент", 				СтруктураЭД.Контрагент);
	СтруктураПараметров.Вставить("Ид", 						СтруктураЭД.НомерЭД);
	СтруктураПараметров.Вставить("ДатаФормирования",		ТекущаяДатаСеанса());
	СтруктураПараметров.Вставить("Номер", 					СтруктураЭД.НомерДокументаОтправителя);
	СтруктураПараметров.Вставить("Дата", 					СтруктураЭД.ДатаДокументаОтправителя);
	СтруктураПараметров.Вставить("Валюта",					СсылкаНаОбъект.ВалютаДокумента);
	СтруктураПараметров.Вставить("Курс", 					СсылкаНаОбъект.КурсВзаиморасчетов);
	СтруктураПараметров.Вставить("Сумма",					СсылкаНаОбъект.СуммаДокумента);
	СтруктураПараметров.Вставить("ЦенаВключаетНДС",			СсылкаНаОбъект.СуммаВключаетНДС);
	СтруктураПараметров.Вставить("СуммаНДС", 				СсылкаНаОбъект.Товары.Итог("СуммаНДС"));
	СтруктураПараметров.Вставить("ВидЭД", 					СтруктураЭД.ВидЭД);
	СтруктураПараметров.Вставить("НаправлениеЭД",			СтруктураЭД.НаправлениеЭД);
	СтруктураПараметров.Вставить("Комментарий",				СсылкаНаОбъект.Комментарий);
	СтруктураПараметров.Вставить("НомерПоДаннымКлиента",	СсылкаНаОбъект.Номер);
	СтруктураПараметров.Вставить("ДатаПоДаннымКлиента",		СсылкаНаОбъект.Дата);
	
	Если ЗначениеЗаполнено(СсылкаНаОбъект.НомерПоДаннымПоставщика) Тогда
		СтруктураПараметров.Вставить("НомерПоДаннымПоставщика",	СсылкаНаОбъект.НомерПоДаннымПоставщика);
		СтруктураПараметров.Вставить("ДатаПоДаннымПоставщика",	СсылкаНаОбъект.ДатаПоДаннымПоставщика);
	КонецЕсли;
	
	ИтоговаяСтрока = НСтр("ru='Всего наименований %Количество%, на сумму %Сумма%'");
	ИтоговаяСтрока = СтрЗаменить(ИтоговаяСтрока, "%Количество%", ТаблицаТоваров.Количество());
	ИтоговаяСтрока = СтрЗаменить(ИтоговаяСтрока, "%Сумма%",		 ОбщегоНазначения.ФорматСумм(СсылкаНаОбъект.СуммаДокумента, СсылкаНаОбъект.ВалютаДокумента));
	СуммаПрописью  = ОбщегоНазначения.СформироватьСуммуПрописью(СсылкаНаОбъект.СуммаДокумента, СсылкаНаОбъект.ВалютаДокумента);
	ИтоговаяСтрока = ИтоговаяСтрока + Символы.ПС + СуммаПрописью;
	СтруктураПараметров.Вставить("ИтогиПрописью", ИтоговаяСтрока);
	
	СтруктураПараметров.Вставить("ОбязательныеПоля", "Организация, Контрагент, Ид, ДатаФормирования,
		|Номер, Дата, ВидЭД, НаправлениеЭД, ТаблицаТоваров");
	СтруктураПараметров.Вставить("ОбязательныеПоляТаблицыЗначений", "Ид, Наименование, БазоваяЕдиницаКод, УпаковкаКод");
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа ОтветНаЗаказ.
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
//  СтруктураЭД - Структура, структура данных для формирования электронного документа.
//  СтруктураПараметров - структура - параметры заполнения электронного документа.
//
Процедура ПодготовитьДанныеПоОтветуНаЗаказ(СсылкаНаОбъект, СтруктураЭД, СтруктураПараметров) Экспорт
	
	СтруктураПараметров = Новый Структура;
	
	ОписаниеТиповКоличество = Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(0, 4));
	ОписаниеТиповСумма = Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(18, 2));
	
	ТаблицаТоваров = Новый ТаблицаЗначений;
	ТаблицаТоваров.Колонки.Добавить("Ид");
	ТаблицаТоваров.Колонки.Добавить("Артикул");
	ТаблицаТоваров.Колонки.Добавить("Наименование");
	ТаблицаТоваров.Колонки.Добавить("Номенклатура");
	ТаблицаТоваров.Колонки.Добавить("Характеристика");
	ТаблицаТоваров.Колонки.Добавить("БазоваяЕдиница");
	ТаблицаТоваров.Колонки.Добавить("БазоваяЕдиницаКод");
	ТаблицаТоваров.Колонки.Добавить("БазоваяЕдиницаНаименование");
	ТаблицаТоваров.Колонки.Добавить("БазоваяЕдиницаНаименованиеПолное");
	ТаблицаТоваров.Колонки.Добавить("БазоваяЕдиницаМеждународноеСокращение");
	ТаблицаТоваров.Колонки.Добавить("Количество", ОписаниеТиповКоличество);
	ТаблицаТоваров.Колонки.Добавить("УпаковкаКод");
	ТаблицаТоваров.Колонки.Добавить("УпаковкаНаименование");
	ТаблицаТоваров.Колонки.Добавить("Коэффициент");
	ТаблицаТоваров.Колонки.Добавить("СуммаСНДС", ОписаниеТиповСумма);
	ТаблицаТоваров.Колонки.Добавить("СтавкаНДС");
	ТаблицаТоваров.Колонки.Добавить("СуммаНДС", ОписаниеТиповСумма);
	ТаблицаТоваров.Колонки.Добавить("Цена", ОписаниеТиповСумма);
	ТаблицаТоваров.Колонки.Добавить("СуммаСкидки");
	ТаблицаТоваров.Колонки.Добавить("Сумма", ОписаниеТиповСумма);
	ДопКолонка = Константы.ДополнительнаяКолонкаПечатныхФормДокументов.Получить();
	
	Если ДопКолонка = Перечисления.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул Тогда
		ТоварКод = "Артикул";
	Иначе
		ТоварКод = "Код";
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗаказПокупателя.Номенклатура.Код 												КАК КодТовара,
	|	ЗаказПокупателя.Номенклатура."+ТоварКод+" 										КАК Артикул,
	|	ЗаказПокупателя.Номенклатура.НаименованиеПолное									КАК Наименование,
	|	ЗаказПокупателя.Номенклатура													КАК Номенклатура,
	|	ЗаказПокупателя.Количество 														КАК Количество,
	|	ЗаказПокупателя.Сумма															КАК Сумма,
	|	ЗаказПокупателя.Цена															КАК Цена,
	|	ЗаказПокупателя.ХарактеристикаНоменклатуры 										КАК Характеристика,
	|	ЗаказПокупателя.Номенклатура.БазоваяЕдиницаИзмерения 							КАК БазоваяЕдиница,
	|	ЗаказПокупателя.Номенклатура.БазоваяЕдиницаИзмерения.Код 						КАК БазоваяЕдиницаКод,
	|	ЗаказПокупателя.Номенклатура.БазоваяЕдиницаИзмерения.Наименование 				КАК БазоваяЕдиницаНаименование,
	|	ЗаказПокупателя.Номенклатура.БазоваяЕдиницаИзмерения.НаименованиеПолное 		КАК БазоваяЕдиницаНаименованиеПолное,
	|	ЗаказПокупателя.Номенклатура.БазоваяЕдиницаИзмерения.МеждународноеСокращение	КАК БазоваяЕдиницаМеждународноеСокращение,
	|	ЗаказПокупателя.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код 					КАК УпаковкаКод,
	|	ЗаказПокупателя.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Наименование 			КАК УпаковкаНаименование,
	|	ЗаказПокупателя.Коэффициент														КАК Коэффициент,
	|	ЗаказПокупателя.СтавкаНДС 														КАК СтавкаНДС,
	|	ЗаказПокупателя.СуммаНДС														КАК СуммаНДС,
	|	0 																				КАК СуммаСкидки,
	|	ЗаказПокупателя.Сумма															КАК СуммаСНДС
	|ИЗ
	|	Документ.ЗаказПокупателя.Товары КАК ЗаказПокупателя
	|	
	|ГДЕ
	|	ЗаказПокупателя.Ссылка = &Ссылка
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ
	|	ЗаказПокупателя.Номенклатура.Код,
	|	ЗаказПокупателя.Номенклатура."+ТоварКод+",
	|	ЗаказПокупателя.Номенклатура.НаименованиеПолное,
	|	ЗаказПокупателя.Номенклатура,
	|	ЗаказПокупателя.Количество,
	|	ЗаказПокупателя.Сумма,
	|	ЗаказПокупателя.Цена,
	|	Неопределено,
	|	ЗаказПокупателя.Номенклатура.БазоваяЕдиницаИзмерения,
	|	ЗаказПокупателя.Номенклатура.БазоваяЕдиницаИзмерения.Код,
	|	ЗаказПокупателя.Номенклатура.БазоваяЕдиницаИзмерения.Наименование,
	|	ЗаказПокупателя.Номенклатура.БазоваяЕдиницаИзмерения.НаименованиеПолное,
	|	ЗаказПокупателя.Номенклатура.БазоваяЕдиницаИзмерения.МеждународноеСокращение,
	|	ЗаказПокупателя.Номенклатура.БазоваяЕдиницаИзмерения.Код,
	|	ЗаказПокупателя.Номенклатура.БазоваяЕдиницаИзмерения.Наименование,
	|	1,
	|	ЗаказПокупателя.СтавкаНДС,
	|	ЗаказПокупателя.СуммаНДС,
	|	0,
	|	ЗаказПокупателя.Сумма
	|ИЗ
	|	Документ.ЗаказПокупателя.Услуги КАК ЗаказПокупателя
	|
	|ГДЕ
	|	ЗаказПокупателя.Ссылка = &Ссылка";
		
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаОбъект);
	
	ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(Запрос.Выполнить().Выгрузить(), ТаблицаТоваров);
	ОбработатьТаблицуТоваров(ТаблицаТоваров);
	
	СтруктураПараметров.Вставить("Исполнитель",				СтруктураЭД.Отправитель);
	СтруктураПараметров.Вставить("ВерсияСхемы", 			"4.02");
	СтруктураПараметров.Вставить("Роль", 					"Продавец");
	
	Если ЗначениеЗаполнено(СсылкаНаОбъект.НомерПоДаннымПокупателя) Тогда
		СтруктураПараметров.Вставить("НомерПоДаннымКлиента",	СсылкаНаОбъект.НомерПоДаннымПокупателя);
		СтруктураПараметров.Вставить("ДатаПоДаннымКлиента",		СсылкаНаОбъект.ДатаПоДаннымПокупателя);
	КонецЕсли;	
	
	СтруктураПараметров.Вставить("НомерПоДаннымПоставщика",	СсылкаНаОбъект.Номер);
	СтруктураПараметров.Вставить("ДатаПоДаннымПоставщика",	СсылкаНаОбъект.Дата);
	СтруктураПараметров.Вставить("ТаблицаТоваров", 			ТаблицаТоваров);
	СтруктураПараметров.Вставить("Организация", 			СтруктураЭД.Организация);
	СтруктураПараметров.Вставить("Контрагент", 				СтруктураЭД.Контрагент);
	СтруктураПараметров.Вставить("Ид", 						СтруктураЭД.НомерЭД);
	СтруктураПараметров.Вставить("ДатаФормирования",		ТекущаяДатаСеанса());
	СтруктураПараметров.Вставить("Номер", 					СтруктураЭД.НомерДокументаОтправителя);
	СтруктураПараметров.Вставить("Дата", 					СтруктураЭД.ДатаДокументаОтправителя);
	СтруктураПараметров.Вставить("Валюта",					СсылкаНаОбъект.ВалютаДокумента);
	СтруктураПараметров.Вставить("Курс", 					СсылкаНаОбъект.КурсВзаиморасчетов);
	СтруктураПараметров.Вставить("Сумма",					СсылкаНаОбъект.СуммаДокумента);
	СтруктураПараметров.Вставить("ЦенаВключаетНДС",			СсылкаНаОбъект.СуммаВключаетНДС);
	СтруктураПараметров.Вставить("СуммаНДС", 				СсылкаНаОбъект.Товары.Итог("СуммаНДС"));
	СтруктураПараметров.Вставить("ВидЭД", 					СтруктураЭД.ВидЭД);
	СтруктураПараметров.Вставить("НаправлениеЭД",			СтруктураЭД.НаправлениеЭД);
	СтруктураПараметров.Вставить("Комментарий", 			СсылкаНаОбъект.Комментарий);
	
	ИтоговаяСтрока = НСтр("ru='Всего наименований %Количество%, на сумму %Сумма%'");
	ИтоговаяСтрока = СтрЗаменить(ИтоговаяСтрока, "%Количество%", ТаблицаТоваров.Количество());
	ИтоговаяСтрока = СтрЗаменить(ИтоговаяСтрока, "%Сумма%",		 ОбщегоНазначения.ФорматСумм(СсылкаНаОбъект.СуммаДокумента, СсылкаНаОбъект.ВалютаДокумента));
	СуммаПрописью  = ОбщегоНазначения.СформироватьСуммуПрописью(СсылкаНаОбъект.СуммаДокумента, СсылкаНаОбъект.ВалютаДокумента);
	ИтоговаяСтрока = ИтоговаяСтрока + Символы.ПС + СуммаПрописью;
	СтруктураПараметров.Вставить("ИтогиПрописью", ИтоговаяСтрока);
	
	СтруктураПараметров.Вставить("ОбязательныеПоля", "Организация, Контрагент, Ид, ДатаФормирования,
		|Номер, Дата, ВидЭД, НаправлениеЭД, ТаблицаТоваров");
	СтруктураПараметров.Вставить("ОбязательныеПоляТаблицыЗначений", "Ид, Наименование, БазоваяЕдиницаКод, УпаковкаКод");
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа КаталогТоваров.
//
// Параметры: 
// СсылкаНаОбъект - ДокументСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
// ТоварыКаталога - Массив, список товаров для заполнения каталога.
// СтруктураЭД - Структура, структура данных для формирования электронного документа.
// СтруктураПараметров - структура - параметры заполнения электронного документа.
//
Процедура ПодготовитьДанныеПоКаталогуТоваров(СсылкаНаОбъект, ТоварыКаталога, СтруктураЭД, СтруктураПараметров) Экспорт
	
	ТаблицаТоваров = Новый ТаблицаЗначений;
	
	ТаблицаТоваров.Колонки.Добавить("Ид");
	ТаблицаТоваров.Колонки.Добавить("Артикул");
	ТаблицаТоваров.Колонки.Добавить("Наименование");
	ТаблицаТоваров.Колонки.Добавить("Номенклатура");
	ТаблицаТоваров.Колонки.Добавить("Характеристика");
	ТаблицаТоваров.Колонки.Добавить("БазоваяЕдиница");
	ТаблицаТоваров.Колонки.Добавить("БазоваяЕдиницаКод");
	ТаблицаТоваров.Колонки.Добавить("БазоваяЕдиницаНаименование");
	ТаблицаТоваров.Колонки.Добавить("БазоваяЕдиницаНаименованиеПолное");
	ТаблицаТоваров.Колонки.Добавить("БазоваяЕдиницаМеждународноеСокращение");
	ТаблицаТоваров.Колонки.Добавить("УпаковкаКод");
	ТаблицаТоваров.Колонки.Добавить("УпаковкаНаименование");
	
	ДопКолонка = Константы.ДополнительнаяКолонкаПечатныхФормДокументов.Получить();
	Если ДопКолонка = Перечисления.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул Тогда
		ТоварКод = "Артикул";
	Иначе
		ТоварКод = "Код";
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТоварыКаталога.Номенклатура,
	|	ТоварыКаталога.ХарактеристикаНоменклатуры,
	|	ТоварыКаталога.ЕдиницаИзмерения
	|ПОМЕСТИТЬ ТаблицаТоваров
	|ИЗ
	|	&ТоварыКаталога КАК ТоварыКаталога
	|;
	|
	|//////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Номенклатура."+ТоварКод+" 				КАК Артикул,
	|	ТаблицаТоваров.Номенклатура.НаименованиеПолное 									КАК Наименование,
	|	ТаблицаТоваров.ХарактеристикаНоменклатуры.Наименование 							КАК НаименованиеХарактеристики,
	|	ТаблицаТоваров.Номенклатура														КАК Номенклатура,
	|	ТаблицаТоваров.ХарактеристикаНоменклатуры 										КАК Характеристика,
	|	ТаблицаТоваров.Номенклатура.БазоваяЕдиницаИзмерения 							КАК БазоваяЕдиница,
	|	ТаблицаТоваров.Номенклатура.БазоваяЕдиницаИзмерения.Код 						КАК БазоваяЕдиницаКод,
	|	ТаблицаТоваров.Номенклатура.БазоваяЕдиницаИзмерения.Наименование 				КАК БазоваяЕдиницаНаименование,
	|	ТаблицаТоваров.Номенклатура.БазоваяЕдиницаИзмерения.НаименованиеПолное 			КАК БазоваяЕдиницаНаименованиеПолное,
	|	ТаблицаТоваров.Номенклатура.БазоваяЕдиницаИзмерения.МеждународноеСокращение		КАК БазоваяЕдиницаМеждународноеСокращение,
	|	ТаблицаТоваров.ЕдиницаИзмерения.Код 											КАК УпаковкаКод,
	|	ТаблицаТоваров.ЕдиницаИзмерения.Наименование 									КАК УпаковкаНаименование
	|ИЗ
	|	ТаблицаТоваров КАК ТаблицаТоваров";
	
	Запрос.УстановитьПараметр("ТоварыКаталога", ТоварыКаталога);
	
	ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(Запрос.Выполнить().Выгрузить(), ТаблицаТоваров);
	ОбработатьТаблицуТоваров(ТаблицаТоваров);
	
	СтруктураПараметров.Вставить("Исполнитель", 		СтруктураЭД.Отправитель);
	СтруктураПараметров.Вставить("ВерсияСхемы", 		"4.02");
	СтруктураПараметров.Вставить("ТаблицаТоваров", 		ТаблицаТоваров);
	СтруктураПараметров.Вставить("Организация", 		СтруктураЭД.Организация);
	СтруктураПараметров.Вставить("Контрагент", 			СтруктураЭД.Контрагент);
	СтруктураПараметров.Вставить("Ид", 					СтруктураЭД.НомерЭД);
	СтруктураПараметров.Вставить("ДатаФормирования",	ТекущаяДатаСеанса());
	СтруктураПараметров.Вставить("ВидЭД", 				СтруктураЭД.ВидЭД);
	СтруктураПараметров.Вставить("НаправлениеЭД", 		СтруктураЭД.НаправлениеЭД);
	
	СтруктураПараметров.Вставить("ОбязательныеПоля", 				"Организация, Ид, ДатаФормирования, ТаблицаТоваров");
	СтруктураПараметров.Вставить("ОбязательныеПоляТаблицыЗначений", "Ид, Наименование, Номенклатура, БазоваяЕдиницаКод, УпаковкаКод");
	
КонецПроцедуры

// Заполняет адрес хранилища с таблицей значений - каталога товаров
//
// Параметры:
//  АдресВоВременномХранилище - адрес хранения каталога товаров;
//  ИдентификаторФормы - уникальный  идентификатор формы, вызвавшей функцию.
//
Процедура ПоместитьКаталогТоваровВоВременноеХранилище(АдресВоВременномХранилище, ИдентификаторФормы) Экспорт
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа КаталогТоваров.
//
// Параметры:
//  Организация - СправочникСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
//  ТоварыКаталога - Массив, список товаров для заполнения каталога.
//  ДеревоДанных - дерево значений, дерево данных заполнения электронного документа.
//
Процедура ПодготовитьДанныеПоКаталогуТоваровCML(Организация, ТоварыКаталога, ДеревоДанных) Экспорт
	
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа ПрайсЛист.
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
//  СтруктураЭД - Структура, структура данных для формирования электронного документа.
//  СтруктураПараметров - структура - параметры заполнения электронного документа.
//
Процедура ПодготовитьДанныеПоПрайсЛисту(СсылкаНаОбъект, СтруктураЭД, СтруктураПараметров) Экспорт
	
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа АктВыполненияРабот.
//
// Параметры: 
// СсылкаНаОбъект - ДокументСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
// СтруктураЭД - Структура, структура данных для формирования электронного документа.
//  СтруктураПараметров - структура - параметры заполнения электронного документа.
//
Процедура ПодготовитьДанныеПоАктуВыполненныхРабот(СсылкаНаОбъект, СтруктураЭД, СтруктураПараметров) Экспорт
	
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа ОтчетКомитенту.
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
//  СтруктураЭД - Структура, структура данных для формирования электронного документа.
//  СтруктураПараметров - структура, параметры для заполнения.
//
// Особенность:
//  Параметр ДополнительныеРеквизитыДляТаблицыТоваров в общей структуре параметров предназначен для заполнения
//  колонки ДополнительныеРеквизиты в таблице товаров.
//
Процедура ПодготовитьДанныеПоОтчетуОПродажахКомиссионногоТовара(СсылкаНаОбъект, СтруктураЭД, СтруктураПараметров) Экспорт
	
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа ОтчетКомитентуОСписании.
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
//  СтруктураЭД - Структура, структура данных для формирования электронного документа.
//  СтруктураПараметров - структура, параметры для заполнения.
//
Процедура ПодготовитьДанныеПоОтчетуОСписанииКомиссионногоТовара(СсылкаНаОбъект, СтруктураЭД, СтруктураПараметров) Экспорт
	
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа ПередачаТоваровМеждуОрганизациями.
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
//  СтруктураЭД - Структура, структура данных для формирования электронного документа.
//  СтруктураПараметров - структура, параметры для заполнения.
//
Процедура ПодготовитьДанныеПоПередачеТоваровМеждуОрганизациями(СсылкаНаОбъект, СтруктураЭД, СтруктураПараметров) Экспорт
	
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа ВозвратТоваровМеждуОрганизациями.
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
//  СтруктураЭД - Структура, структура данных для формирования электронного документа.
//  СтруктураПараметров - структура, параметры для заполнения.
//
Процедура ПодготовитьДанныеПоВозвратуТоваровМеждуОрганизациями(СсылкаНаОбъект, СтруктураЭД, СтруктураПараметров) Экспорт
	
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа Платежное поручение.
//
// Параметры: 
// СсылкаНаОбъект - ДокументСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
// СтруктураЭД - Структура, структура данных для формирования электронного документа.
// СтруктураПараметров - структура, параметры для заполнения.
//
Процедура ПодготовитьДанныеПоПлатежномуПоручению(СсылкаНаОбъект, СтруктураЭД, СтруктураПараметров) Экспорт
	
	
КонецПроцедуры

// Заполняет данные для электронного документа типа Акт на передачу прав.
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
//  СтруктураЭД - Структура, структура данных для формирования электронного документа.
//  СтруктураПараметров - структура, параметры для заполнения.
//
Процедура ПодготовитьДанныеПоАктуНаПередачуПрав(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных) Экспорт
	
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа СоглашениеОбИзмененииСтоимостиОтправитель.
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
//  СтруктураЭД - Структура, структура данных для формирования электронного документа.
//  ДеревоДанных - дерево для заполнения.
//
Процедура ПодготовитьДанныеПоКорректировочномуДокументу(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных) Экспорт
	
	ПараметрыПечати = Новый Структура("ВыводитьУслуги", Истина);
	
	ДанныеДляПечати = ПолучитьДанныеСогласованногоИзменения(СсылкаНаОбъект);
	
	ТаблицаТоваров = ДанныеДляПечати.ТаблицаТоваров;
	
	ТаблицаТоваров.Колонки.Удалить("Упаковка");
	ТаблицаТоваров.Колонки.Удалить("УпаковкаКод");
	ТаблицаТоваров.Колонки.Удалить("УпаковкаНаименование");
	
	РеквизитыШапки = ДанныеДляПечати.РеквизитыШапки;
	
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "НомерТоварнойНакладной", РеквизитыШапки.Номер);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаТоварнойНакладной",  РеквизитыШапки.ДатаДокумента);
	
	// Выводим общие реквизиты шапки
	СведенияОПоставщике       = ПолучитьДанныеЮрФизЛица(РеквизитыШапки.Организация);
	СведенияОПокупателе       = ПолучитьДанныеЮрФизЛица(РеквизитыШапки.Контрагент);
	СведенияОГрузополучателе  = ПолучитьДанныеЮрФизЛица(РеквизитыШапки.Грузополучатель);
	СведенияОГрузоотправителе = ПолучитьДанныеЮрФизЛица(РеквизитыШапки.Грузоотправитель);
	
	ЗаполнитьДанныеУчастника(ДеревоДанных, СведенияОПоставщике,       "Поставщик", "Юр");
	ЗаполнитьДанныеУчастника(ДеревоДанных, СведенияОГрузоотправителе, "Грузоотправитель", "Факт");
	
	ЗаполнитьДанныеУчастника(ДеревоДанных, СведенияОПокупателе,       "Плательщик", "Юр");
	ЗаполнитьДанныеУчастника(ДеревоДанных, СведенияОГрузополучателе,  "Грузополучатель", "Факт");
	
	СтруктураДопДанных = Новый Структура;
	СтруктураДопДанных.Вставить("ВалютаКод",   РеквизитыШапки.ВалютаДокумента.Код);
	СтруктураДопДанных.Вставить("ВидОперации", РеквизитыШапки.ВидОперации);
	Если ЗначениеЗаполнено(РеквизитыШапки.АдресДоставки) Тогда
		СтруктураДопДанных.Вставить("АдресДоставки", УправлениеКонтактнойИнформацией.ПолучитьПредставлениеАдресаПоСтрока(РеквизитыШапки.АдресДоставки));
	КонецЕсли;
    ОбщегоНазначенияЭД.ДобавитьДопДанныеВДерево(ДеревоДанных, СтруктураДопДанных, Истина);
	
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
								ДеревоДанных,
								"ДокументыОснования",
								РеквизитыШапки.ДокументОснование);
		
	ИтоговыеСуммы = СтруктураИтоговыеСуммы(ТаблицаТоваров, СтруктураЭД.ВидЭД);
	
	// Добавим данные о товарах
	ТаблицаТоваров.Колонки.Добавить("ДопДанныеПодписанные");
	ТаблицаТоваров.Колонки.Добавить("ДопДанныеНеПодписанные");
	ТаблицаТоваров.Колонки.Добавить("СтавкаНДСЧислом");
	Для Каждого СтрокаТовары Из ТаблицаТоваров Цикл
		ЗаполнитьДопДанныеВСтроке(СтрокаТовары);
	КонецЦикла;
	
	ТаблицаТоваров.Колонки.Удалить("СтавкаНДС");
	КолонкаСтавкаНДССтрокой = ТаблицаТоваров.Колонки.Найти("СтавкаНДСЧислом");
	КолонкаСтавкаНДССтрокой.Имя = "СтавкаНДС";
	
	ОбщегоНазначенияЭД.ЗагрузитьТаблицуВДерево(ДеревоДанных, ТаблицаТоваров, "ТаблицаТоваров");
	
	// Получим данные по ответственным лицам
	Руководитель = ОтветственноеЛицоОрганизации(
		РеквизитыШапки.ДатаДокумента, 
		РеквизитыШапки.Организация, 
		Перечисления.ОтветственныеЛицаОрганизаций.Руководитель);
		
	ГлавныйБухгалтер = ОтветственноеЛицоОрганизации(
		РеквизитыШапки.ДатаДокумента, 
		РеквизитыШапки.Организация, 
		Перечисления.ОтветственныеЛицаОрганизаций.ГлавныйБухгалтер);
	
	ЗаполнитьРеквизитыФизЛица(ДеревоДанных, ГлавныйБухгалтер.ФИО, "СведенияПоОтпускуГруза.Бухгалтер");
	
	ЗаполнитьРеквизитыФизЛица(
			ДеревоДанных,
			Руководитель.ФИО,
			"СведенияПоОтпускуГруза.ОтпускРазрешил",
			Руководитель.Должность);
			
	ЗаполнитьРеквизитыФизЛица(
			ДеревоДанных,
			Руководитель.ФИО,
			"СведенияПоОтпускуГруза.ОтпускПроизвел",
			"");
			
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
								ДеревоДанных,
								"СведенияПоОтпускуГруза.ОтпущеноНаСумму",
								ИтоговыеСуммы.СуммаСНДС);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
								ДеревоДанных,
								"ОбщиеСведенияОТоварнойНакладной.КоличествоПорядковыхНомеровЗаписей",
								ИтоговыеСуммы.КоличествоПорядковыхНомеровЗаписей);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
								ДеревоДанных,
								"ОбщиеСведенияОТоварнойНакладной.ВсегоМест",
								ИтоговыеСуммы.КоличествоМест);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
								ДеревоДанных,
								"ОбщиеСведенияОТоварнойНакладной.МассаГрузаНетто",
								ИтоговыеСуммы.МассаНетто);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
								ДеревоДанных,
								"ОбщиеСведенияОТоварнойНакладной.МассаГрузаБрутто",
								ИтоговыеСуммы.МассаБрутто);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
								ДеревоДанных,
								"ВсегоПоНакладной.КоличествоМест",
								ИтоговыеСуммы.КоличествоМест);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
								ДеревоДанных,
								"ВсегоПоНакладной.МассаБрутто",
								ИтоговыеСуммы.МассаБрутто);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
								ДеревоДанных,
								"ВсегоПоНакладной.МассаНетто",
								ИтоговыеСуммы.МассаНетто);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
								ДеревоДанных,
								"ВсегоПоНакладной.СуммаБезНДС",
								ИтоговыеСуммы.СуммаБезНДС);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
								ДеревоДанных,
								"ВсегоПоНакладной.СуммаНДС",
								ИтоговыеСуммы.СуммаНДС);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
								ДеревоДанных,
								"ВсегоПоНакладной.СуммаСНДС",
								ИтоговыеСуммы.СуммаСНДС);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
								ДеревоДанных,
								"ВсегоПоНакладной.МассаНеттоДоКорректировки",
								ИтоговыеСуммы.МассаНеттоДоКорректировки);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
								ДеревоДанных,
								"ВсегоПоНакладной.СуммаБезНДСДоКорректировки",
								ИтоговыеСуммы.СуммаБезНДСДоКорректировки);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
								ДеревоДанных,
								"ВсегоПоНакладной.СуммаНДСДоКорректировки",
								ИтоговыеСуммы.СуммаНДСДоКорректировки);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
								ДеревоДанных,
								"ВсегоПоНакладной.СуммаСНДСДоКорректировки",
								ИтоговыеСуммы.СуммаСНДСДоКорректировки);
	
	ЭтоФизЛицо = ЭтоФизЛицо(СтруктураЭД.Организация);
	ДанныеЮрФизЛица = ПолучитьДанныеЮрФизЛица(СтруктураЭД.Организация);
		
	Если ЭтоФизЛицо Тогда
		ФИО = ДанныеЮрФизЛица.ПолноеНаименование;
		
		ДанныеСвидетельства = "";
		ДанныеСвидетельстваОРегистрацииИП(СтруктураЭД.Организация, ДанныеСвидетельства);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									"Подписант.ИП.СвидетельствоОРегистрацииИП",
									ДанныеСвидетельства);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "Подписант.ИП.ИНН", ДанныеЮрФизЛица.ИНН);
		ЗаполнитьФИОПодписантаВДереве(ДеревоДанных, "ИП", ФИО);
	Иначе
		ФИО = Руководитель.ФИО;
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "Подписант.ЮЛ.Должность", Руководитель.Должность);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "Подписант.ЮЛ.ИНН",       ДанныеЮрФизЛица.ИНН);
		ЗаполнитьФИОПодписантаВДереве(ДеревоДанных, "ЮЛ", ФИО);
	КонецЕсли;

КонецПроцедуры

// Подготавливает данные для электронного документа типа Торг12 титул покупателя.
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
//  СтруктураЭД - Структура, структура данных для формирования электронного документа.
//  СтруктураПараметров - структура, параметры для заполнения.
//
Функция ПодготовитьДанныеПоКорректировочномуДокументуПолучатель(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных) Экспорт

	ЭтоФизЛицо = ЭтоФизЛицо(СтруктураЭД.Организация);
	ДанныеЮрФизЛица = ПолучитьДанныеЮрФизЛица(СтруктураЭД.Организация);
	
	Если ЭтоФизЛицо Тогда
		ФИО = ДанныеЮрФизЛица.ПолноеНаименование;
		
		ДанныеСвидетельства = "";
		ДанныеСвидетельстваОРегистрацииИП(СтруктураЭД.Организация, ДанныеСвидетельства);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									"Подписант.ИП.СвидетельствоОРегистрацииИП",
									ДанныеСвидетельства);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "Подписант.ИП.ИНН", ДанныеЮрФизЛица.ИНН);
		ЗаполнитьФИОПодписантаВДереве(ДеревоДанных, "ИП", ФИО);
	Иначе
		Руководитель = ОтветственноеЛицоОрганизации(
			СтруктураЭД.ДатаЭД, 
			СтруктураЭД.Организация, 
			Перечисления.ОтветственныеЛицаОрганизаций.Руководитель);
		
		ФИО       = Руководитель.ФИО;
		Должность = Руководитель.Должность;
		
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "Подписант.ЮЛ.Должность", Должность);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "Подписант.ЮЛ.ИНН",       ДанныеЮрФизЛица.ИНН);
		ЗаполнитьФИОПодписантаВДереве(ДеревоДанных, "ЮЛ", ФИО);
	КонецЕсли;

КонецФункции // ()

////////////////////////////////////////////////////////////////////////////////
// Просмотр электронных документов

// Необходимо заполнить соответствие ставок и сумм НДС
//
Процедура ПолучитьСоответствиеСтавокНДС(СоответствиеСтавокНДС) Экспорт
	
	СоответствиеСтавокНДС = Новый Соответствие();
	
	СоответствиеСтавокНДС.Вставить(Перечисления.СтавкиНДС.НДС10,     0);
	СоответствиеСтавокНДС.Вставить(Перечисления.СтавкиНДС.НДС10_110, 0);
	СоответствиеСтавокНДС.Вставить(Перечисления.СтавкиНДС.НДС18,     0);
	СоответствиеСтавокНДС.Вставить(Перечисления.СтавкиНДС.НДС18_118, 0);
	СоответствиеСтавокНДС.Вставить(Перечисления.СтавкиНДС.НДС0,      0);
	
КонецПроцедуры

// Возвращает сумму прописью.
//
// Параметры:
//  СуммаЧислом - Число, преобразуемая сумма.
//  КодВалюты - Число, код используемой валюты.
//
Функция СуммаПрописью(СуммаЧислом, КодВалюты = Неопределено) Экспорт
	
	Если КодВалюты = Неопределено Тогда
		Валюта = глЗначениеПеременной("ВалютаРегламентированногоУчета");
	Иначе
		Валюта = НайтиСсылкуНаОбъект("Валюты",КодВалюты);
		
		Если Не ЗначениеЗаполнено(Валюта) Тогда
			Валюта = глЗначениеПеременной("ВалютаРегламентированногоУчета");
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ОбщегоНазначения.СформироватьСуммуПрописью(СуммаЧислом, Валюта);
	
КонецФункции

// Возвращает текстовое описание организации.
//
// Параметры:
//  СведенияОКонтрагенте - Структура, сведения об организации, по которой надо составить описание.
//  Список - Строка, список запрашиваемых параметров организации.
//  СПрефиксом - Булево, признак вывода префикса параметра организации.
//
Функция ОписаниеОрганизации(СведенияОКонтрагенте, Список = "", СПрефиксом = Истина) Экспорт
	
	Возврат ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОКонтрагенте, Список, СПрефиксом);
	
КонецФункции

// Возвращает текстовое представление суммы.
//
// Параметры:
//  СуммаКПрописи - Число, сумма, по которой надо получить представление.
//  КодВалюты - Число, код используемой валюты.
//  ЧН - Строка, параметр нулевого значения числа.
//  ЧРГ - Строка, разделитель групп целой части числа.
//
Функция ФорматСумм(СуммаКПрописи, КодВалюты = Неопределено, ЧН = "", ЧРГ = "") Экспорт
	
	Валюта = НайтиСсылкуНаОбъект("Валюты",КодВалюты);
	
	Возврат ОбщегоНазначения.ФорматСумм(СуммаКПрописи, Валюта,  ЧН, ЧРГ);
	
КонецФункции

// Формирует текст НДС по ставке для печатной формы счета и заказа
//
// Параметры:
//  СтавкаНДС       - ПеречислениеСсылка.СтавкиНДС - ставка НДС, для которой необходимо сформировать текст
//  ЦенаВключаетНДС - Булево - Признак включения НДС в цену
//
// Возвращаемое значение:
//  Строка
//
Функция ТекстНДСПоСтавке(СтавкаНДС, ЦенаВключаетНДС) Экспорт
	
	ТекстНДСПоСтавке = ?(ЦенаВключаетНДС, НСтр("ru='В т.ч. НДС (%СтавкаНДС%):'"), НСтр("ru='НДС (%СтавкаНДС%):'"));
	ТекстНДСПоСтавке = СтрЗаменить(ТекстНДСПоСтавке, "%СтавкаНДС%", СтавкаНДС);
	
	Возврат ТекстНДСПоСтавке;
	
КонецФункции

// Возвращает числовое значение ставки НДС по значению перечисления
//
// Параметры:
//  СтавкаНДС - ПеречислениеСсылка.СтавкиНДС - значение перечисления СтавкиНДС
//
// Возвращаемое значение:
//  Число - Значение ставки НДС числом
//
Функция ПолучитьСтавкуНДСЧислом(Знач СтавкаНДС) Экспорт
	
	Возврат УчетНДС.ПолучитьСтавкуНДС(СтавкаНДС);
	
КонецФункции

Функция ЗначениеПеречисленияСтавкаНДС(СтавкаЧислом) Экспорт
	
	ЗначениеНДС = Неопределено;
	
	Если ТипЗнч(СтавкаЧислом) = Тип("Строка") Тогда
		ПредставлениеСтавкиНДС = СтрЗаменить(СтавкаЧислом, " ", "");
	ИначеЕсли ТипЗнч(СтавкаЧислом) = Тип("Число") Тогда 
		ПредставлениеСтавкиНДС = СтрЗаменить(Строка(СтавкаЧислом), " ", "");
	Иначе // неправильный тип
		ПредставлениеСтавкиНДС = Неопределено;
	КонецЕсли;
	
	Если ПредставлениеСтавкиНДС = Неопределено 
		ИЛИ ВРег(ПредставлениеСтавкиНДС) = "БЕЗНДС" Тогда
		ЗначениеНДС = Перечисления.СтавкиНДС.БезНДС;
		
	ИначеЕсли Найти("0#0%", ПредставлениеСтавкиНДС) > 0 Тогда
		ЗначениеНДС = Перечисления.СтавкиНДС.НДС0;
		
	ИначеЕсли Найти("10#0.1#0,1#0.10#0,10#10%", ПредставлениеСтавкиНДС) > 0 Тогда
		ЗначениеНДС = Перечисления.СтавкиНДС.НДС10;
		
	ИначеЕсли Найти("20#0.2#0,2#0.20#0,20#20%", ПредставлениеСтавкиНДС) > 0 Тогда
		ЗначениеНДС = Перечисления.СтавкиНДС.НДС20;
		
	ИначеЕсли Найти("18#0.18#0,18#0.18#0,18#18%", ПредставлениеСтавкиНДС) > 0 Тогда
		ЗначениеНДС = Перечисления.СтавкиНДС.НДС18;
		
	ИначеЕсли Найти("10/110#10%/110%", ПредставлениеСтавкиНДС) > 0 Тогда
		ЗначениеНДС = Перечисления.СтавкиНДС.НДС10_110;
		
	ИначеЕсли Найти("18/118#18%/118%", ПредставлениеСтавкиНДС) > 0 Тогда
		ЗначениеНДС = Перечисления.СтавкиНДС.НДС18_118;
		
	КонецЕсли;
	
	Возврат ЗначениеНДС;
	
КонецФункции

// Возвращает ответственного за электронный документооборот по данному соглашению
//
// Параметры:
//  Контрагент - СправочникСсылка.Контрагенты, ссылка на контрагента, по которому надо получить ответственного.
//  Соглашение - СправочникСсылка.СоглашениеОбИспользованииЭД, ссылка на соглашение, по которому надо найти ответственного.
//
Функция ПолучитьОтветственногоПоЭД(Контрагент, Соглашение) Экспорт
	
	ОтветственныйПоЭД = Справочники.Пользователи.ПустаяСсылка();
	
	Если ТипЗнч(Контрагент) = Тип("СправочникСсылка.Контрагенты") Тогда
		ОтветственныйПоЭД = Контрагент.ОсновнойМенеджерПокупателя;
	КонецЕсли;
	
	Возврат ОтветственныйПоЭД;
	
КонецФункции

// Возвращает признак физ. лица.
//
// Параметры:
//  ДанныеКонтрагента - ссылка на элемент справочника.
//
Функция ЭтоФизЛицо(ДанныеКонтрагента) Экспорт
	
	Если ДанныеКонтрагента.Метаданные().Реквизиты.Найти("ЮрФизЛицо") = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ЮрФизЛицо = ДанныеКонтрагента.ЮрФизЛицо;
	
	Если ТипЗнч(ЮрФизЛицо) <> Тип("ПеречислениеСсылка.ЮрФизЛицо") Тогда
		Возврат Ложь;
	КонецЕсли;
		
	ЭтоФизЛицо = Ложь;
	Если ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо Тогда
		ЭтоФизЛицо = Истина;
	КонецЕсли;
	
	Возврат ЭтоФизЛицо;
	
КонецФункции

// Формирует текст НДС по этапу оплаты
//
// Параметры:
//  СоответствиеСтавокНДС - Соответствие - соответствие, полученное с помощью функции ПолучитьСоответствиеСтавокНДС()
//  ПроцентПлатежа       - Число - Процент платежа по этапу
//
// Возвращаемое значение:
//  ТекстНДС - Строка - описание ставки НДС
//
Функция СформироватьТекстНДСЭтапаОплаты(СоответствиеСтавокНДС, ПроцентПлатежа) Экспорт
	
	ТекстНДС = "";
	
	Если СоответствиеСтавокНДС.Количество() > 0 Тогда
		
		Для Каждого ТекСтавкаНДС Из СоответствиеСтавокНДС Цикл
			
			Если ТекСтавкаНДС.Значение <> 0 Тогда
				
				ТекстНДС = ТекстНДС + ?(ПустаяСтрока(ТекстНДС), НСтр("ru='НДС(%СтавкаНДС%) %СуммаНДС%'"), НСтр("ru=', НДС(%СтавкаНДС%) %СуммаНДС%'"));
				ТекстНДС = СтрЗаменить(ТекстНДС, "%СтавкаНДС%", ТекСтавкаНДС.Ключ);
				ТекстНДС = СтрЗаменить(ТекстНДС, "%СуммаНДС%",  Формат(ТекСтавкаНДС.Значение / 100 * ПроцентПлатежа, "ЧЦ=15; ЧДЦ=2"));
			
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекстНДС) Тогда
		ТекстНДС = НСтр("ru='В т.ч. '") + ТекстНДС;
	Иначе
		ТекстНДС = НСтр("ru='Без налога (НДС)'");
	КонецЕсли;
	
	Возврат ТекстНДС;
	
КонецФункции

// Функция возвращает, нужно ли вы водить данные о скидках в печатную форму документа
//
Функция НужноВыводитьСкидки(Знач Товары, ИспользоватьСкидки) Экспорт
	
	Возврат Ложь;
	
КонецФункции

// Получает имя дополнительной колонки.
//
// Возвращаемое значение:
//  ИмяКолонки - строка колонки.
//
Функция ИмяДополнительнойКолонки() Экспорт
	
	Результат = "";
	
	ДопКолонка = Константы.ДополнительнаяКолонкаПечатныхФормДокументов.Получить();
	Если ДопКолонка = Перечисления.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул Тогда
		Результат = "Артикул";
	ИначеЕсли ДопКолонка = Перечисления.ДополнительнаяКолонкаПечатныхФормДокументов.Код Тогда
		Результат = "Код";
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Переопределение поведения электронных документов

// Данное событие возникает при изменении элемента справочника ЭДПрисоединенныеФайлы
// Предназначено для переопределения или добавления изменяемых реквизитов электронного документа
//
// Параметры:
//  Объект - СправочникСсылка.ЭДПрисоединенныеФайлы - изменяемый объект
//  СтруктураПараметров - Структура, содержит структуру изменяемых реквизитов
//
Процедура ПриИзмененииПрисоединенногоФайла(Объект, СтруктураПараметров) Экспорт
	
	Если НЕ СтруктураПараметров.Свойство("Ответственный") Тогда
		СтруктураПараметров.Вставить("Ответственный", ПользователиСервер.АвторизованныйПользователь());
	КонецЕсли;
	
КонецПроцедуры

// Выполняет дополнительную обработку электронного документа, которому назначили статус "Утвержден".
// 
// Параметры:
//  ЭлектронныйДокумент - ссылка на присоединенный файл.
//
Процедура НазначенСтатусУтвержден(ЭлектронныйДокумент) Экспорт
	
КонецПроцедуры

// Выполняет дополнительную обработку электронного документа, которому назначили статус "Подписан".
// 
// Параметры:
//  ЭлектронныйДокумент - ссылка на присоединенный файл.
//
Процедура НазначенСтатусПодписан(ЭлектронныйДокумент) Экспорт
	
	
КонецПроцедуры

// Проверяет, готовность документов ИБ для формирования ЭД, и удаляет из массива не готовые документы
//
// Параметры
//  ДокументыМассив - Массив   - ссылки на документы, которые должны быть проверены перед формированием ЭД.
//
Процедура ПроверитьГотовностьИсточников(ДокументыМассив, ФормаИсточник = Неопределено) Экспорт
	
	ОбщегоНазначенияКлиентСервер.УдалитьВсеВхожденияТипаИзМассива(ДокументыМассив, Тип("СтрокаГруппировкиДинамическогоСписка"));
	
	ДокументыНеВыставляются = Новый Массив();
	ДокументыНеВыставляютсяВЭлектронномВиде = Новый Массив();
	СводныеКорректировочныеСчетаФактуры = Новый Массив();
	ДокументыКорректировкиПоСогласованиюСторон = Новый Массив();
	ДокументыИсправленияКорректировкиПоСогласованиюСторон = Новый Массив();
	
	
	Для каждого Документ из ДокументыМассив Цикл
		
		Если ТипЗнч(Документ) = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
			Если Документ.СчетФактураНеВыставляется Тогда
				// На основании счетов-фактур с признаком СчетФактураНеВыставляется ЭД формировать не требуется
				ДокументыНеВыставляются.Добавить(Документ);
			ИначеЕсли Документ.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.Корректировочный
				И Документ.ДокументыОснования.Количество()>1 Тогда
				СводныеКорректировочныеСчетаФактуры.Добавить(Документ);
			КонецЕсли;
			
		ИначеЕсли ТипЗнч(Документ) = Тип("ДокументСсылка.КорректировкаРеализации") Тогда
			
			// На основании док. "Корректировка реализации", осуществляющего исправление/согласованное изменение 
			// документа отличного от док. "РеализацияТоваровУслуг", "АктОбОказанииПроизводственныхУслуг" ЭД не формируется
			
			ИсходныйИсправляемыйДокумент = УчетНДС.ПолучитьИсправляемыйДокументРеализации(Документ.ДокументРеализации, Истина);
			
			Если ТипЗнч(ИсходныйИсправляемыйДокумент) <> Тип("ДокументСсылка.РеализацияТоваровУслуг")
				 И ТипЗнч(ИсходныйИсправляемыйДокумент) <> Тип("ДокументСсылка.АктОбОказанииПроизводственныхУслуг") Тогда
				 
				ДокументыНеВыставляютсяВЭлектронномВиде.Добавить(Документ);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	УдалитьДокументыНеподходящиеДляФормированияЭД(
		ДокументыМассив,
		ДокументыНеВыставляются,
		НСтр("ru = 'Документ ""%1"" не выставляется. Электронный документ не сформирован.'"));
		
	УдалитьДокументыНеподходящиеДляФормированияЭД(
		ДокументыМассив,
		ДокументыНеВыставляютсяВЭлектронномВиде,
		НСтр("ru = 'Документ ""%1"" не выставляется в электронном виде. Электронный документ не сформирован.'"));
		
	УдалитьДокументыНеподходящиеДляФормированияЭД(
		ДокументыМассив,
		СводныеКорректировочныеСчетаФактуры,
		НСтр("ru = 'Корректировочный счета-фактура ""%1"" содержит несколько документов-оснований. Отправка таких счетов-фактур не поддерживается.'"));
		
	// Перед формированием ЭД документы ИБ должны быть проведены
	МассивПроводимыхДокументов = Новый Массив;
	МассивТиповНепроводимыхДокументов = Новый Массив;
	Для каждого Элемент из ДокументыМассив Цикл
		ИмяДокумента = Элемент.Метаданные().ПолноеИмя();
		Если Метаданные.Документы.Содержит(Метаданные.НайтиПоПолномуИмени(ИмяДокумента)) Тогда
			
			Если Элемент.Метаданные().Проведение = Метаданные.СвойстваОбъектов.Проведение.Запретить Тогда
				Если МассивТиповНепроводимыхДокументов.Найти(ТипЗнч(Элемент)) = Неопределено Тогда
					МассивТиповНепроводимыхДокументов.Добавить(ТипЗнч(Элемент));
				КонецЕсли;
			КонецЕсли;
			
			МассивПроводимыхДокументов.Добавить(Элемент)
		КонецЕсли;
	КонецЦикла;
	
	Для каждого ТипНепроводногоДокумента ИЗ МассивТиповНепроводимыхДокументов Цикл
		ОбщегоНазначенияКлиентСервер.УдалитьВсеВхожденияТипаИзМассива(МассивПроводимыхДокументов, ТипНепроводногоДокумента);
	КонецЦикла;
	
	МассивНепроведенныхДокументов = ОбщегоНазначения.ПроверитьПроведенностьДокументов(МассивПроводимыхДокументов);
	Если МассивНепроведенныхДокументов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ШаблонСообщения = НСтр("ru = 'Документ %1 не проведен. Электронный документ не сформирован.'");
	УдалитьДокументыНеподходящиеДляФормированияЭД(
		ДокументыМассив,
		МассивНепроведенныхДокументов,
		НСтр("ru = 'Документ %1 не проведен. Электронный документ не сформирован.'"));
	
КонецПроцедуры

// Проверяет все ли необходимые подписи установлены перед отправкой контрагенту.
// 
// Параметры:
//  ЭлектронныйДокумент - ссылка на присоединенный файл.
//  ФлагПодписанПолностью - булево - признак полностью подписанного документа.
//
Процедура ЭлектронныйДокументПолностьюПодписан(ЭлектронныйДокумент, ФлагПодписанПолностью) Экспорт
	
	
КонецПроцедуры

// Проверяет выполняются ли необходимые автоматические условия для утверждения документа.
//
// Параметры:
//  ЭлектронныйДокумент - ссылка на присоединенный файл.
//
Функция ЭлектронныйДокументГотовКУтверждению(ЭлектронныйДокумент) Экспорт
	
	Возврат Истина;
	
КонецФункции

// Процедура удаляет из исходного массива документы неподходящие для формирования ЭД
//
// Параметры
//  МассивДокументов - массив документов для формирования ЭД
//  МассивДокументовДляУдаления - массив документов неподходящих для формирования ЭД
//  ШаблонСообщения - шаблон сообщения
//
Процедура УдалитьДокументыНеподходящиеДляФормированияЭД(МассивДокументов, МассивДокументовДляУдаления, ШаблонСообщения)
	
	Для Каждого Документ Из МассивДокументовДляУдаления Цикл
		Найденный = МассивДокументов.Найти(Документ);
		Если Найденный <> Неопределено Тогда
			МассивДокументов.Удалить(Найденный);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, Строка(Документ)), 
				Документ);
		КонецЕсли;
		
	КонецЦикла
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Работа с элементами форм

// Изменяет поведение элементов управляемой или обычной формы.
//
// Параметры:
//  Форма - <Управляемая или обычная форма> - управляемая или обычная форма для изменения.
//  СтруктураПараметров - <Структура> - параметры процедуры
//
Процедура ИзменитьСвойстваЭлементовФормы(Форма, СтруктураПараметров) Экспорт
	
	Если СтруктураПараметров.Свойство("ВидОперации")
		И СтруктураПараметров.Свойство("ЗначениеПараметра") Тогда
		Если ВРег(СтруктураПараметров.ВидОперации) = ВРег("УстановкаГиперссылки")
			И СтруктураПараметров.Свойство("ТекстСостоянияЭД") Тогда
			// Зададим особые условия.
			Если Найти(СтруктураПараметров.ТекстСостоянияЭД, "Не сформирован") > 0 Тогда
				СтруктураПараметров.ЗначениеПараметра = Ложь;
			КонецЕсли;
			// Определим элемент формы.
			НайденныйЭлементФормы = Неопределено;
			Если ТипЗнч(Форма) = Тип("УправляемаяФорма") Тогда // только для управляемой формы
				Если НЕ Форма.Элементы.Найти("СостояниеЭД") = Неопределено Тогда
					НайденныйЭлементФормы = Форма.Элементы.СостояниеЭД;
				КонецЕсли;
				
				// Заполним свойство найденного элемента.
				Если НЕ НайденныйЭлементФормы = Неопределено
					И НайденныйЭлементФормы.Вид = ВидПоляФормы.ПолеНадписи Тогда
					НайденныйЭлементФормы.Гиперссылка = СтруктураПараметров.ЗначениеПараметра;
				КонецЕсли;
			Иначе // для обычной формы
				Если НЕ Форма.ЭлементыФормы.Найти("ТекстСостоянияЭД") = Неопределено Тогда
					НайденныйЭлементФормы = Форма.ЭлементыФормы.ТекстСостоянияЭД;
				КонецЕсли;
				
				// Заполним свойство найденного элемента.
				Если НЕ НайденныйЭлементФормы = Неопределено
					И ТипЗнч(НайденныйЭлементФормы) = Тип("Надпись") Тогда
					НайденныйЭлементФормы.Гиперссылка = СтруктураПараметров.ЗначениеПараметра;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(Форма) = Тип("УправляемаяФорма") Тогда // только для управляемой формы
		Если НЕ ЭлектронныеДокументыСлужебныйВызовСервера.ЕстьПравоЧтенияЭД(Ложь) Тогда
			Если НЕ Форма.Элементы.Найти("ГруппаСостояниеЭД") = Неопределено Тогда
				Форма.Элементы.ГруппаСостояниеЭД.Видимость = Ложь;
			ИначеЕсли НЕ Форма.Элементы.Найти("СостояниеЭД") = Неопределено Тогда
				Форма.Элементы.СостояниеЭД.Видимость = Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Сопоставление номенклатуры

// Возвращает структуру для открытия формы сопоставления номенклатуры
//
// Параметры:
//  СсылкаНаЭД - СправочникСсылка.ЭДПрисоединенныеФайлы
//
// Возвращаемое значение:
//  Структура, содержащая ИмяФормы и ПараметрыОткрытияФормы
//
Функция ПолучитьПараметрыФормыСопоставленияНоменклатуры(СсылкаНаЭД) Экспорт
	
	СтруктураПараметров = Новый Структура();
	
	// Сопоставление номенклатуры вызывается 
	// 	- для входящих документов видов:
	// 		- ТОРГ12
	// 		- ТОРГ12Продавец
	// 		- ОтветНаЗаказ
	// 		- КаталогТоваров
	// 		- АктИсполнитель
	// 		- СчетНаОплату
	// 		- СоглашениеОбИзмененииСтоимостиОтправитель
	// 	- для исходящих документов 
	// 		- ТОРГ12Покупатель
	// 		- АктЗаказчик
	// 		- СоглашениеОбИзмененииСтоимостиПолучатель
	Если (СсылкаНаЭД.НаправлениеЭД = Перечисления.НаправленияЭД.Входящий
		    И (СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.ТОРГ12
				ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.ТОРГ12Продавец
				ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.ОтветНаЗаказ
				ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.КаталогТоваров
				ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.АктИсполнитель
				ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.СчетНаОплату
				ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиОтправитель)) 
		 ИЛИ (СсылкаНаЭД.НаправлениеЭД = Перечисления.НаправленияЭД.Исходящий
			И (СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.ТОРГ12Покупатель
				ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.АктЗаказчик
				ИЛИ СсылкаНаЭД.ВидЭД = Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиПолучатель)) Тогда
		
		СтруктураПараметров.Вставить("ИмяФормы", "ОбщаяФорма.СопоставлениеДанныхПоНоменклатуре");
		ПараметрыОткрытияФормы = Новый Структура("ЭлектронныйДокумент, НеОткрыватьФормуПриОтсутствииНесопоставленнойНоменклатуры", 
			СсылкаНаЭД, Истина);
		СтруктураПараметров.Вставить("ПараметрыОткрытияФормы", ПараметрыОткрытияФормы);
		
	КонецЕсли;
	
	Возврат СтруктураПараметров;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Работа с извещениями

// Выполняет заполнение структуры параметров подписанта для ЭД вида извещение о получении.
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка, ссылка на объект информационной базы, по которому необходимо создать электронный документ.
//  СтруктураЭД - Структура, структура данных для формирования электронного документа.
//  СтруктураПараметровПадписанта - структура - параметры заполнения подписанта электронного документа.
//
Процедура ЗаполнитьСтруктуруДанныхПодписанта(СсылкаНаОбъект, СтруктураЭД, СтруктураПараметровПодписанта) Экспорт
	
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Работа с временными каталогами и файлами

// При необходимости, в функции можно определить каталог для временных файлов,
// отличный от устанавливаемого по умолчанию в библиотеке ЭД.
//
// Параметры:
//  ТекущийКаталог - путь к каталогу временных файлов.
//
Процедура ТекущийКаталогВременныхФайлов(ТекущийКаталог) Экспорт
	
	ТекущийКаталог = КаталогВременныхФайлов();
	
КонецПроцедуры

// Получает имя временного файла.
//
// Параметры:
//  ИмяВременногоФайла - Строка - имя временного файла;
//  Расширение - Строка - расширение для временного файла.
//
Процедура ТекущееИмяВременногоФайла(ИмяВременногоФайла, Расширение = "") Экспорт
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла(Расширение);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Работа со счет-фактурами

// В процедуре выполняется заполнение реквизитов (дата выставления, признак выставления,
// дата получения, признак получения) документов счета-фактуры, по ключевым событиям,
// описанным в приказе от 25 апреля 2011 г. N 50н.: получение ПДО, ПДП, ИП, ПДОИП.
//
// Параметры:
//  ВладелецЭД - документ-ссылка, ссылка на документ ИБ счет-фактура выданный/полученный.
//  ЭД - справочник-ссылка, ссылка на элемент справочника ЭДПрисоединенныеФайлы.
//
Процедура ЗаполнитьРеквизитыЭСФ(ВладелецЭД, ЭД) Экспорт
	
	Если ЭД.ТипЭлементаВерсииЭД = Перечисления.ТипыЭлементовВерсииЭД.ПДПЭСФ Тогда
		
		//  Датой выставления покупателю счета-фактуры в электронном виде по телекоммуникационным
		// каналам связи считается дата поступления файла счета-фактуры Оператору ЭДО от продавца, 
		// указанная в подтверждении (ПДПЭСФ) этого Оператора ЭДО.  ПРИКАЗ от 25 апреля 2011 г. N 50н
		
		ЭСФ = ВладелецЭД.ПолучитьОбъект();
		Если ЭСФ.Дата >= '20130608' Тогда
			ЭСФ.ДатаВыставления = ЭД.ДатаДокументаОтправителя;
			ЭСФ.КодСпособаВыставления = 2;
			ЭСФ.Выставлен = Истина;
			ЭСФ.Записать(?(ЭСФ.Проведен, РежимЗаписиДокумента.Проведение, РежимЗаписиДокумента.Запись));
		КонецЕсли;
		
	ИначеЕсли ЭД.ТипЭлементаВерсииЭД = Перечисления.ТипыЭлементовВерсииЭД.ПДОЭСФ Тогда
		
		//  Датой получения покупателем счета-фактуры в электронном виде по телекоммуникационным 
		// каналам связи считается дата направления покупателю Оператором ЭДО файла счета-фактуры продавца, 
		// указанная в подтверждении (ПДОЭСФ) Оператора ЭДО.  ПРИКАЗ от 25 апреля 2011 г. N 50н
		
		ЭСФ = ВладелецЭД.ПолучитьОбъект();
		Если КонецДня(ЭСФ.Дата) <> КонецДня(ЭД.ДатаДокументаОтправителя)
			ИЛИ ЭСФ.КодСпособаПолучения <> 2 Тогда
			ЭСФ.Дата = ЭД.ДатаДокументаОтправителя + (ЭСФ.Дата - НачалоДня(ЭСФ.Дата)) ;
			ЭСФ.КодСпособаПолучения = 2;
			ЭСФ.Записать(?(ЭСФ.Проведен, РежимЗаписиДокумента.Проведение, РежимЗаписиДокумента.Запись));
		КонецЕсли;
		
	ИначеЕсли ЭД.ТипЭлементаВерсииЭД = Перечисления.ТипыЭлементовВерсииЭД.ИПЭСФ Тогда
		
		// Счет-фактура в электронном виде считается выставленным, если продавцу поступило 
		// соответствующее подтверждение (ПДПЭСФ) Оператора ЭДО, при наличии у продавца извещения покупателя 
		// о получении счета-фактуры (ИПЭСФ), подписанного ЭЦП покупателя и полученного через Оператора ЭДО.
		// ПРИКАЗ от 25 апреля 2011 г. N 50н
		
		СвойстваЭСФ = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(ВладелецЭД, "Выставлен, КодСпособаВыставления, ДатаВыставления");
		ДатаВыставленияЭСФ = ЭлектронныеДокументы.ДатаВыставленияСчетФактуры(ВладелецЭД.Ссылка);
		Если НЕ СвойстваЭСФ.Выставлен ИЛИ СвойстваЭСФ.КодСпособаВыставления <> 2
			ИЛИ СвойстваЭСФ.ДатаВыставления <> ДатаВыставленияЭСФ Тогда
			ЭСФ = ВладелецЭД.ПолучитьОбъект();
			ЭСФ.ДатаВыставления = ДатаВыставленияЭСФ;
			ЭСФ.КодСпособаВыставления = 2;
			ЭСФ.Выставлен = Истина;
			ЭСФ.Записать(?(ЭСФ.Проведен, РежимЗаписиДокумента.Проведение, РежимЗаписиДокумента.Запись));
		КонецЕсли;
		
	ИначеЕсли ЭД.ТипЭлементаВерсииЭД = Перечисления.ТипыЭлементовВерсииЭД.ПДОИПЭСФ Тогда
		
		//  Счет-фактура в электронном виде считается полученным покупателем, если ему поступило 
		// соответствующее подтверждение (ПДОЭСФ) Оператора ЭДО, при наличии извещения покупателя 
		// о получении счета-фактуры (ИПЭСФ), подписанного ЭЦП покупателя и подтвержденного (ПДОИПЭСФ)
		// Оператором ЭДО.  ПРИКАЗ от 25 апреля 2011 г. N 50н
		
		СвойстваЭСФ = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(ВладелецЭД, "КодСпособаПолучения");
		Если СвойстваЭСФ.КодСпособаПолучения <> 2 Тогда
			ЭСФ = ВладелецЭД.ПолучитьОбъект();
			ЭСФ.КодСпособаПолучения = 2;
			ЭСФ.Записать(?(ЭСФ.Проведен, РежимЗаписиДокумента.Проведение, РежимЗаписиДокумента.Запись));
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Работа с правами

// Проверяет наличие прав обрабатывать электронный документы.
//
// Возвращаемое значение:
//  Булево - истина или ложь, в зависимости от установленных прав.
//
Функция ЕстьПравоОбработкиЭД() Экспорт
	
	Возврат (ПользователиСервер.ЭтоПолноправныйПользовательИБ() ИЛИ ПользователиСервер.РолиДоступны("ИспользованиеОбменаЭД"))
	
КонецФункции

// Проверяет наличие прав читать электронный документы.
//
// Возвращаемое значение:
//  Булево - истина или ложь, в зависимости от установленных прав.
//
Функция ЕстьПравоЧтенияЭД() Экспорт
	
	Возврат (ПользователиСервер.ЭтоПолноправныйПользовательИБ() ИЛИ ПользователиСервер.РолиДоступны("ИспользованиеОбменаЭД")) 
	
КонецФункции

// Проверяет наличие прав на открытие жур;нала регистрации.
//
// Возвращаемое значение:
//  Булево - истина или ложь, в зависимости от установленных прав.
//
Функция ЕстьПравоОткрытияЖурналаРегистрации() Экспорт
	
	Возврат ПользователиСервер.ЭтоПолноправныйПользовательИБ()
	
КонецФункции

// Проверяет наличие прав на настройку параметров электронных документов.
//
// Возвращаемое значение:
//  Булево - истина или ложь, в зависимости от установленных прав.
//
Функция ЕстьПравоНастройкиПараметровЭД() Экспорт
	
	Возврат ПользователиСервер.РолиДоступны("НастройкаНСИРегл") ИЛИ ПользователиСервер.ЭтоПолноправныйПользовательИБ();
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Общие процедуры и функции

// Разбирает из переданной строки фамилию, имя и отчество.
//
// Параметры
//  ПолноеНаименование - строка с наименованием;
//  Фамилия - строка с фамилией;
//  Имя - строка с именем;
//  Отчество - строка с отчеством.
//
Процедура РазобратьНаименованиеФизЛица(ПолноеНаименование, Фамилия = " ", Имя = " ", Отчество = " ") Экспорт
	
	ОбщегоНазначения.ФамилияИнициалыФизЛица(ПолноеНаименование, Фамилия, Имя, Отчество);
	
КонецПроцедуры

// Функция формирует прокси по настройкам прокси (передаваемому параметру)
//
// Параметры:
// НастройкаПроксиСервера - Соответствие:
//  ИспользоватьПрокси - использовать ли прокси-сервер
//  НеИспользоватьПроксиДляЛокальныхАдресов - использовать ли прокси-сервер для локальных адресов
//  ИспользоватьСистемныеНастройки - использовать ли системные настройки прокси-сервера
//  Сервер       - адрес прокси-сервера
//  Порт         - порт прокси-сервера
//  Пользователь - имя пользователя для авторизации на прокси-сервере
//  Пароль       - пароль пользователя
//
Функция ПолучитьНастройкиПроксиСервера(НастройкаПроксиСервера) Экспорт
	
	НастройкаПроксиСервера = ПолучениеФайловИзИнтернета.ПолучитьНастройкиПроксиНаСервере1СПредприятие();
	
	Если НастройкаПроксиСервера = Неопределено Тогда
		
		НастройкаПроксиСервера = Новый Соответствие();
		НастройкаПроксиСервера.Вставить("ИспользоватьПрокси", Истина);
		НастройкаПроксиСервера.Вставить("ИспользоватьСистемныеНастройки", Истина);
		НастройкаПроксиСервера.Вставить("НеИспользоватьПроксиДляЛокальныхАдресов", Ложь);
		НастройкаПроксиСервера.Вставить("Сервер", "");
		НастройкаПроксиСервера.Вставить("Порт", "");
		НастройкаПроксиСервера.Вставить("Пользователь", "");
		НастройкаПроксиСервера.Вставить("Пароль", "");
		
	КонецЕсли;
	
КонецФункции

// Выполняется проверка возможности корректного чтения Пакета ЭД.
// Необходимость данной проверки возникает при работе с данными внешней информационной базы (через com-соединение).
//
// Параметры:
//  ПакетЭД - ДокументСсылка.ПакетЭД - исследуемый пакет электронных документов.
//  ЧтениеПакетаВозможно - булево/неопределено - Ложь - чтение пакета не будет выполняться, во всех остальных случаях,
//    (включая пустое значение) пакет будет прочитан.
//
Процедура ОпределитьВозможностьЧтенияДвоичныхДанныхПакетаЭД(ПакетЭД, ЧтениеПакетаВозможно) Экспорт
	
	
	
КонецПроцедуры

// Возвращает структуру, содержащую значения реквизитов прочитанные из информационной базы
// по ссылке на объект.
// 
// Если не задан альтернативный алгоритм получения значений реквизитов (процедура пуста), то используется функция БСП:
// ОбщегоНазначения.ПолучитьЗначениеРеквизита(Ссылка, ИменаРеквизитов).
// 
// Параметры:
//  Ссылка       - ссылка на объект, - элемент справочника, документ, ...
//  ИменаРеквизитов - Строка или Структура - если Строка, то имена реквизитов, 
//               перечисленные через запятую, в формате требований к свойствам структуры.
//               Например, "Код, Наименование, Родитель".
//               Если Структура, то в качестве ключа передается псевдоним поля для
//               возвращаемой структуры с результатом, а в качестве значения (опционально) 
//               - фактическое имя поля в таблице. 
//               Если значение не определено, то имя поля берется из ключа.
//  СтруктураДанных - содержит список свойств, как список имен в строке
//                 ИменаРеквизитов, со значениям реквизитов, прочитанных
//                 из информационной базы.
// 
Процедура ПолучитьСтруктуруЗначенийРеквизитов(Ссылка, ИменаРеквизитов, СтруктураДанных) Экспорт
	
	
	
КонецПроцедуры

// Переопреледеляет выводимое сообщение об ошибке
// КодОшибки - строка
// ТекстОшибки - строка
Процедура ИзменитьСообщениеОбОшибке(КодОшибки, ТекстОшибки) Экспорт
	
	Если КодОшибки = "100" ИЛИ КодОшибки = "110" Тогда
		ТекстОшибки = "Проверьте общие настройки криптографии."
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обмен с банком

// Используется для включения подсистемы Сбербанк в прикладном решении.
//
// Параметры:
//  ФлагИспользования - <Булево> - необходимо присвоить параметру Истина, если используется подсистема Сбербанк.
//
Процедура ПроверитьИспользованиеПодсистемыСбербанк(ФлагИспользования) Экспорт
	
	ФлагИспользования = Ложь;
	
КонецПроцедуры

// Используется для получения номеров счетов в виде массив строк
//
// Параметры:
//  Организация - <СправочникСсылка.Организации> - отбор по организации.
//  Банк - <СправочникСсылка.КлассификаторБанковРФ> - отбор по банку.
//  МассивНомеровБанковскихСчетов - массив возврата, в элементах строки с номерами счетов
//
Процедура ПолучитьНомераБанковскихСчетов(Организация, Банк, МассивНомеровБанковскихСчетов) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	БанковскиеСчета.НомерСчета
	|ИЗ
	|	Справочник.БанковскиеСчета КАК БанковскиеСчета
	|ГДЕ
	|	БанковскиеСчета.Банк = &Банк
	|	И БанковскиеСчета.Владелец = &Организация
	|	И НЕ БанковскиеСчета.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("Банк", Банк);
	Запрос.УстановитьПараметр("Организация", Организация);
	ТабРез = Запрос.Выполнить().Выгрузить();
	МассивНомеровБанковскихСчетов = ТабРез.ВыгрузитьКолонку("НомерСчета");
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Прямой обмен с банком

// Разбирает переданный текстовый файл выписки.
//
// Параметры
//  СоглашениеЭД - СправочникСсылка.СоглашениеОбИспользованииЭД - соглашение, по которому производится обмен.
//  ФайлЗагрузки - Строка, содержит путь к файлу на клиенте
//
Процедура РазобратьФайлВыписки(СоглашениеЭД, ФайлЗагрузки) Экспорт
	
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// Поиск и создание документов

Функция ПолучитьДанныеСтрокиТЧ(ДанныеЗаполнения, ДеревоРазбора)
	
	ДанныеДляЗаполненияСтрокиТЧ = Новый Структура();
	
	Для Каждого ТекСтрока Из ДанныеЗаполнения Цикл
		
		ИмяРеквизитаВБД = ТекСтрока.Реквизит;
		
		Если ВРег(ИмяРеквизитаВБД) = ВРег("Характеристика") Тогда 
			ИмяРеквизитаВБД = "ХарактеристикаНоменклатуры";
		ИначеЕсли ВРег(ИмяРеквизитаВБД) = ВРег("Мест") Тогда
			ИмяРеквизитаВБД = "КоличествоМест";
		ИначеЕсли ВРег(ИмяРеквизитаВБД) = ВРег("Описание") Тогда
			ИмяРеквизитаВБД = "Содержание";
		ИначеЕсли Найти(ИмяРеквизитаВБД, "ДоКорректировки") <> 0 Тогда
			ИмяРеквизитаВБД = СтрЗаменить(ИмяРеквизитаВБД, "ДоКорректировки", "ДоИзменения");
		КонецЕсли;
		
		НайденноеЗначение = ПолучитьЗначениеРеквизитаДерева(ТекСтрока, ТекСтрока.Реквизит, Истина, ДеревоРазбора);
		
		ДанныеДляЗаполненияСтрокиТЧ.Вставить(ИмяРеквизитаВБД, НайденноеЗначение);
		
		Если ТекСтрока.Реквизит = "Номенклатура" И ЗначениеЗаполнено(НайденноеЗначение) Тогда
			
			СтрокаНоменклатура = ДеревоРазбора.Строки.Найти(ТекСтрока.ЗначениеРеквизита,"ИндексСтроки",Истина);
			
			// Получим единицу измерения по классификатору
			ЕдиницаИзмеренияПоКлассификатору = ПолучитьЗначениеРеквизитаДерева(СтрокаНоменклатура, "ЕдиницаИзмерения", Истина, ДеревоРазбора);
			
			// Получим единицу измерния для данной номенклатуры в соответствии с единицей классификатора.
			ЕдиницаИзмерения = НайтиСсылкуНаОбъектПоРеквизиту("ЕдиницыИзмерения", 
					"ЕдиницаПоКлассификатору", 
					ЕдиницаИзмеренияПоКлассификатору, 
					НайденноеЗначение);
					
			ДанныеДляЗаполненияСтрокиТЧ.Вставить("ЕдиницаИзмерения", ЕдиницаИзмерения);
			
			Если ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда
				ДанныеДляЗаполненияСтрокиТЧ.Вставить("Коэффициент", ЕдиницаИзмерения.Коэффициент); 
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ДанныеДляЗаполненияСтрокиТЧ;
	
КонецФункции

Функция ПолучитьЗначениеРеквизитаДерева(СтрокаДерева, ИмяРеквизита, ВключатьПодчиненные = Ложь, ДеревоРазбора = Неопределено)
	
	Результат = Неопределено;
	
	Если СтрокаДерева.Строки.Количество() > 0 Тогда 
		НайденнаяСтрока = СтрокаДерева.Строки.Найти(ИмяРеквизита, "Реквизит", ВключатьПодчиненные);
	Иначе // передали строку с реквизитом
		НайденнаяСтрока = СтрокаДерева;
	КонецЕсли;
	Если НайденнаяСтрока <> Неопределено Тогда
		Результат = НайденнаяСтрока.ЗначениеРеквизита;
		// если реквизит ссылочного типа (передали реквизит ДеревоРазбора), тогда нашли всего лишь индекс строки
			Если ЗначениеЗаполнено(ДеревоРазбора) Тогда 
				НайденнаяСтрока = ДеревоРазбора.Строки.Найти(Результат, "ИндексСтроки", Истина);
				Если НайденнаяСтрока <> Неопределено Тогда
					Результат = НайденнаяСтрока.СсылкаНаОбъект;
				КонецЕсли;
			КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПодготовитьСтруктуруДляПоступленияТоваровУслуг(СтрокаДляЗагрузки, ДеревоРазбора)
	
	ДанныеДляОбъекта      = Новый Структура;
	ДанныеЗаполненияШапки = Новый Структура;
	Товары = Документы.ПоступлениеТоваровУслуг.ПустаяСсылка().Товары.ВыгрузитьКолонки();
	Услуги = Документы.ПоступлениеТоваровУслуг.ПустаяСсылка().Услуги.ВыгрузитьКолонки();
	
	Для Каждого СтрокаРеквизита Из СтрокаДляЗагрузки.Строки Цикл
		
		Если СтрокаРеквизита.Реквизит = "СписокОписаний" Тогда
			 
			СтрокаРеквизитаОписанийРабот = ПолучитьЗначениеРеквизитаДерева(СтрокаРеквизита, СтрокаРеквизита.Реквизит, Истина, ДеревоРазбора);
			
			Для Каждого СтрокаРеквизитаОписания Из СтрокаРеквизитаОписанийРабот.Строки Цикл
				
				Если СтрокаРеквизитаОписания.Строки.Количество() = 0 Тогда
					ЗначениеРеквизита = ПолучитьЗначениеРеквизитаДерева(СтрокаРеквизитаОписания, СтрокаРеквизитаОписания.Реквизит, Истина, ДеревоРазбора);
					Если ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
						ДанныеЗаполненияШапки.Вставить(СтрокаРеквизитаОписания.Реквизит, ЗначениеРеквизита);
					КонецЕсли;
				Иначе
					ДанныеДляЗаполненияСтрокиТЧ = ПолучитьДанныеСтрокиТЧ(СтрокаРеквизитаОписания.Строки, ДеревоРазбора);
					ЗаполнитьЗначенияСвойств(Услуги.Добавить(), ДанныеДляЗаполненияСтрокиТЧ);
				КонецЕсли;
				
			КонецЦикла;
			
		Иначе
			
			Если СтрокаРеквизита.Строки.Количество() = 0 Тогда 
				
				// Заполним реквизит шапки
				ЗначениеРеквизита = ПолучитьЗначениеРеквизитаДерева(СтрокаРеквизита, СтрокаРеквизита.Реквизит, Истина, ДеревоРазбора);
				
				Если СтрокаРеквизита.Реквизит = "ДатаПоДаннымКлиента" Тогда
					ИмяРеквизита = "ДатаПоДаннымПокупателя";
				ИначеЕсли СтрокаРеквизита.Реквизит = "НомерПоДаннымКлиента" Тогда
					ИмяРеквизита = "НомерПоДаннымПокупателя";
				ИначеЕсли СтрокаРеквизита.Реквизит = "Валюта" Тогда
					ИмяРеквизита = "ВалютаДокумента";
				Иначе
					ИмяРеквизита = СтрокаРеквизита.Реквизит;
				КонецЕсли;
				
				Если ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
					ДанныеЗаполненияШапки.Вставить(ИмяРеквизита, ЗначениеРеквизита);
				КонецЕсли;
				
			Иначе 
				// Добавим строку табличной части
				ДанныеДляЗаполненияСтрокиТЧ = ПолучитьДанныеСтрокиТЧ(СтрокаРеквизита.Строки, ДеревоРазбора);
				
				Если ТипЗнч(ДанныеДляЗаполненияСтрокиТЧ.Номенклатура) = Тип("СправочникСсылка.Номенклатура")
					 И ДанныеДляЗаполненияСтрокиТЧ.Номенклатура.Услуга Тогда
					 НоваяСтрока = Услуги.Добавить();
					 НОваяСтрока.Содержание = ДанныеДляЗаполненияСтрокиТЧ.Номенклатура.НаименованиеПолное;
				Иначе
					 НоваяСтрока = Товары.Добавить();
				КонецЕсли;
				
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеДляЗаполненияСтрокиТЧ);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	// спец. значения 
	Если ЗначениеЗаполнено(ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "НомерСчетаФактуры")) Тогда // указана счет-фактура
		ДанныеЗаполненияШапки.Вставить("НомерСчетаФактуры",ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "НомерСчетаФактуры"));
		ДанныеЗаполненияШапки.Вставить("ДатаСчетаФактуры", ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "ДатаСчетаФактуры"));
	Иначе
		ДанныеЗаполненияШапки.Вставить("НомерВходящегоДокумента",ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "Номер"));
		ДанныеЗаполненияШапки.Вставить("ДатаВходящегоДокумента", ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "Дата"));
	КонецЕсли;
	
	// реквизиты для связки "заказ - поступление"
	ДанныеЗаполненияШапки.Вставить("НомерПоДаннымПоставщика", ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "НомерПоДаннымПоставщика"));
	ДанныеЗаполненияШапки.Вставить("ДатаПоДаннымПоставщика",  ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "ДатаПоДаннымПоставщика"));
	
	ДанныеДляОбъекта.Вставить("Шапка", 	ДанныеЗаполненияШапки);
	ДанныеДляОбъекта.Вставить("Товары", Товары);
	ДанныеДляОбъекта.Вставить("Услуги", Услуги);
	
	Возврат ДанныеДляОбъекта;
	
КонецФункции

Функция ПодготовитьСтруктуруДляКорректировкиПоступления(СтрокаДляЗагрузки, ДеревоРазбора)
	
	ДанныеДляОбъекта      = Новый Структура;
	ДанныеЗаполненияШапки = Новый Структура;
	
	Товары = Документы.КорректировкаПоступления.ПустаяСсылка().Товары.ВыгрузитьКолонки();
	Услуги = Документы.КорректировкаПоступления.ПустаяСсылка().Услуги.ВыгрузитьКолонки();
	
	Для Каждого СтрокаРеквизита Из СтрокаДляЗагрузки.Строки Цикл
		
		Если СтрокаРеквизита.Реквизит = "СписокОписаний" Тогда
			 
			СтрокаРеквизитаОписанийРабот = ПолучитьЗначениеРеквизитаДерева(СтрокаРеквизита, СтрокаРеквизита.Реквизит, Истина, ДеревоРазбора);
			
			Для Каждого СтрокаРеквизитаОписания Из СтрокаРеквизитаОписанийРабот.Строки Цикл
				
				Если СтрокаРеквизитаОписания.Строки.Количество() = 0 Тогда
					ЗначениеРеквизита = ПолучитьЗначениеРеквизитаДерева(СтрокаРеквизитаОписания, СтрокаРеквизитаОписания.Реквизит, Истина, ДеревоРазбора);
					Если ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
						ДанныеЗаполненияШапки.Вставить(СтрокаРеквизитаОписания.Реквизит, ЗначениеРеквизита);
					КонецЕсли;
				Иначе
					ДанныеДляЗаполненияСтрокиТЧ = ПолучитьДанныеСтрокиТЧ(СтрокаРеквизитаОписания.Строки, ДеревоРазбора);
					ЗаполнитьЗначенияСвойств(Услуги.Добавить(), ДанныеДляЗаполненияСтрокиТЧ);
				КонецЕсли;
				
			КонецЦикла;
			
		Иначе
			
			Если СтрокаРеквизита.Строки.Количество() = 0 Тогда 
				
				// Заполним реквизит шапки
				ЗначениеРеквизита = ПолучитьЗначениеРеквизитаДерева(СтрокаРеквизита, СтрокаРеквизита.Реквизит, Истина, ДеревоРазбора);
				
				Если СтрокаРеквизита.Реквизит = "ДатаПоДаннымКлиента" Тогда
					ИмяРеквизита = "ДатаПоДаннымПокупателя";
				ИначеЕсли СтрокаРеквизита.Реквизит = "НомерПоДаннымКлиента" Тогда
					ИмяРеквизита = "НомерПоДаннымПокупателя";
				ИначеЕсли СтрокаРеквизита.Реквизит = "Валюта" Тогда
					ИмяРеквизита = "ВалютаДокумента";
				ИначеЕсли СтрокаРеквизита.Реквизит = "ДокументОснования" Тогда
					ИмяРеквизита = "ДокументПоступления";
				Иначе
					ИмяРеквизита = СтрокаРеквизита.Реквизит;
				КонецЕсли;
				
				Если ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
					ДанныеЗаполненияШапки.Вставить(ИмяРеквизита, ЗначениеРеквизита);
				КонецЕсли;
				
			Иначе 
				
				// Добавим строку табличной части
				ДанныеДляЗаполненияСтрокиТЧ = ПолучитьДанныеСтрокиТЧ(СтрокаРеквизита.Строки, ДеревоРазбора);
				
				Если ТипЗнч(ДанныеДляЗаполненияСтрокиТЧ.Номенклатура) = Тип("СправочникСсылка.Номенклатура")
					 И ДанныеДляЗаполненияСтрокиТЧ.Номенклатура.Услуга Тогда
					 НоваяСтрока = Услуги.Добавить();
					 НоваяСтрока.Содержание = ДанныеДляЗаполненияСтрокиТЧ.Номенклатура.НаименованиеПолное;
				Иначе
					 НоваяСтрока = Товары.Добавить();
				КонецЕсли;
				
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеДляЗаполненияСтрокиТЧ);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ДанныеЗаполненияШапки.Вставить("ВидОперации", ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "ВидОперации", Истина, ДеревоРазбора));
	
	// спец. значения 
	Если ЗначениеЗаполнено(ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "НомерСчетаФактуры")) Тогда // указана счет-фактура
		ДанныеЗаполненияШапки.Вставить("НомерСчетаФактуры",ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "НомерСчетаФактуры"));
		ДанныеЗаполненияШапки.Вставить("ДатаСчетаФактуры", ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "ДатаСчетаФактуры"));
	Иначе
		ДанныеЗаполненияШапки.Вставить("НомерВходящегоДокумента",ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "Номер"));
		ДанныеЗаполненияШапки.Вставить("ДатаВходящегоДокумента", ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "Дата"));
	КонецЕсли;
	
	ДанныеДляОбъекта.Вставить("Шапка",  ДанныеЗаполненияШапки);
	ДанныеДляОбъекта.Вставить("Товары", Товары);
	ДанныеДляОбъекта.Вставить("Услуги", Услуги);
	
	Возврат ДанныеДляОбъекта;
	
КонецФункции

Функция ПодготовитьСтруктуруДляСчетаФактуры(СтрокаДляЗагрузки, ДеревоРазбора)
	
	ДанныеЗаполненияШапки = Новый Структура;
	ДанныеЗаполненияШапки.Вставить("НомерВходящегоДокумента", 	ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "Номер"));
	ДанныеЗаполненияШапки.Вставить("ДатаВходящегоДокумента", 	ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "Дата"));
	
	НомерСчетаФактуры = ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "НомерСчетаФактуры");
	
	Если ЗначениеЗаполнено(НомерСчетаФактуры) Тогда
		
		ДанныеЗаполненияШапки.Вставить("ВидСчетаФактуры", Перечисления.ВидСчетаФактурыПолученного.Корректировочный);
		
		НомерИсправления 	= ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "НомерИсправления");
		ДатаИсправления 	= ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "ДатаИсправления");
		ДанныеЗаполненияШапки.Вставить("НомерИсправления", 	НомерИсправления);
		ДанныеЗаполненияШапки.Вставить("ДатаИсправления", 	ДатаИсправления);
		Если ЗначениеЗаполнено(НомерИсправления) И ЗначениеЗаполнено(ДатаИсправления) Тогда
			ДанныеЗаполненияШапки.Вставить("Исправление", Истина);
		КонецЕсли;
		ДанныеЗаполненияШапки.Вставить("НомерИсходногоДокумента", 	НомерСчетаФактуры);
		ДанныеЗаполненияШапки.Вставить("ДатаИсходногоДокумента", 	ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "ДатаСчетаФактуры"));
		
		НомерИсправленияИсходногоДокумента 	= ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "НомерИсправленияСчетаФактуры");
		ДатаИсправленияИсходногоДокумента 	= ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "ДатаИсправленияСчетаФактуры");
		ДанныеЗаполненияШапки.Вставить("НомерИсправленияИсходногоДокумента",	НомерИсправленияИсходногоДокумента);
		ДанныеЗаполненияШапки.Вставить("ДатаИсправленияИсходногоДокумента", 	ДатаИсправленияИсходногоДокумента);
		Если ЗначениеЗаполнено(НомерИсправленияИсходногоДокумента) И ЗначениеЗаполнено(ДатаИсправленияИсходногоДокумента) Тогда
			ДанныеЗаполненияШапки.Вставить("УчитыватьИсправлениеИсходногоДокумента", Истина);
		КонецЕсли;
		
		
		ДанныеЗаполненияШапки.Вставить("СуммаУвеличение", 		ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "СтТовУчНалВсегоУвел"));
		ДанныеЗаполненияШапки.Вставить("СуммаУменьшение", 		ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "СтТовУчНалВсегоУм"));
		ДанныеЗаполненияШапки.Вставить("СуммаНДСУвеличение",	ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "СумНДСУвел"));
		ДанныеЗаполненияШапки.Вставить("СуммаНДСУменьшение", 	ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "СумНДСУм"));
		
	Иначе
		
		ДанныеЗаполненияШапки.Вставить("ВидСчетаФактуры", 	Перечисления.ВидСчетаФактурыПолученного.НаПоступление);
		
		ДанныеЗаполненияШапки.Вставить("СуммаДокумента", 	ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "СтТовУчНал"));
		ДанныеЗаполненияШапки.Вставить("СуммаНДСДокумента", ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "СумНДС"));
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено (ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "НомерИсправления")) Тогда
		ДанныеЗаполненияШапки.Вставить("Исправление", Истина);
		ДанныеЗаполненияШапки.Вставить("НомерИсправления", 	ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "НомерИсправления"));
		ДанныеЗаполненияШапки.Вставить("ДатаИсправления", 	ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "ДатаИсправления"));
	КонецЕсли;
	
	ДанныеЗаполненияШапки.Вставить("ВалютаДокумента", НайтиСсылкуНаОбъект("Валюты", ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "ВалКод")));
	
	// Данные по организации
	ДанныеЗаполненияШапки.Вставить("Организация", СсылкаНаОбъектПоИННКПП("Организации", 
		ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "ПокупательИНН"), 
		ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "ПокупательКПП")));
	
	// Данные по контрагенту
	ПродавецКПП = ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "ПродавецКПП");
	ДанныеЗаполненияШапки.Вставить("КППКонтрагента", ПродавецКПП);
	ДанныеЗаполненияШапки.Вставить("Контрагент", СсылкаНаОбъектПоИННКПП("Контрагенты", 
		ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "ПродавецИНН"), 
		ПродавецКПП));
		
	// Получим документы-основания
	СтрокаДокументыОснования = СтрокаДляЗагрузки.Строки.Найти("ДокументыОснования");
	Если СтрокаДокументыОснования <> Неопределено Тогда
		МассивДокументовОснований = Новый Массив;
		Для Каждого Строка Из СтрокаДокументыОснования.Строки Цикл
			Если ЗначениеЗаполнено(Строка.СсылкаНаОбъект) Тогда
				МассивДокументовОснований.Добавить(Строка.СсылкаНаОбъект);
			КонецЕсли;
		КонецЦикла;
		ДанныеЗаполненияШапки.Вставить("ДокументыОснования", МассивДокументовОснований);
	КонецЕсли;

	Возврат ДанныеЗаполненияШапки;
	
КонецФункции

Функция ПодготовитьСтруктуруДляЗаказаПокупателя(СтрокаДляЗагрузки, ДеревоРазбора)
	
	ДанныеДляОбъекта = Новый Структура;
	ДанныеЗаполненияШапки = Новый Структура;
	Товары = Документы.ЗаказПокупателя.ПустаяСсылка().Товары.ВыгрузитьКолонки();
	Услуги = Документы.ЗаказПокупателя.ПустаяСсылка().Услуги.ВыгрузитьКолонки();
	
	Для Каждого СтрокаРеквизита Из СтрокаДляЗагрузки.Строки Цикл
		
		Если СтрокаРеквизита.Строки.Количество()=0 Тогда // примитивный тип
			
			ЗначениеРеквизита = ПолучитьЗначениеРеквизитаДерева(СтрокаРеквизита, СтрокаРеквизита.Реквизит, Истина, ДеревоРазбора);
			
			Если СтрокаРеквизита.Реквизит = "ЦенаВключаетНДС" Тогда
				ИмяРеквизита = "СуммаВключаетНДС";
			ИначеЕсли СтрокаРеквизита.Реквизит = "Валюта" Тогда
				ИмяРеквизита = "ВалютаДокумента";
			ИначеЕсли СтрокаРеквизита.Реквизит = "Курс" Тогда
				ИмяРеквизита = "КурсВзаиморасчетов";
			ИначеЕсли СтрокаРеквизита.Реквизит = "ДатаПоДаннымКлиента" Тогда
				ИмяРеквизита = "ДатаПоДаннымПокупателя";
			ИначеЕсли СтрокаРеквизита.Реквизит = "НомерПоДаннымКлиента" Тогда
				ИмяРеквизита = "НомерПоДаннымПокупателя";
			Иначе
				ИмяРеквизита = СтрокаРеквизита.Реквизит;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
				ДанныеЗаполненияШапки.Вставить(ИмяРеквизита, ЗначениеРеквизита);
			КонецЕсли;
			
		ИначеЕсли СтрокаРеквизита.Реквизит = "СтрокаТЧ" Тогда
			// Добавим строку табличной части
			ДанныеДляЗаполненияСтрокиТЧ = ПолучитьДанныеСтрокиТЧ(СтрокаРеквизита.Строки, ДеревоРазбора);
			
			Если ТипЗнч(ДанныеДляЗаполненияСтрокиТЧ.Номенклатура) = Тип("СправочникСсылка.Номенклатура")
				 И ДанныеДляЗаполненияСтрокиТЧ.Номенклатура.Услуга Тогда
				НоваяСтрока = Услуги.Добавить();
			Иначе
				НоваяСтрока = Товары.Добавить();
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеДляЗаполненияСтрокиТЧ);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ДанныеДляОбъекта.Вставить("Шапка", 	ДанныеЗаполненияШапки);
	ДанныеДляОбъекта.Вставить("Товары",	Товары);
	ДанныеДляОбъекта.Вставить("Услуги",	Услуги);
	
	Возврат ДанныеДляОбъекта;
	
КонецФункции

Функция ПодготовитьСтруктуруДляЗаказаПоставщику(СтрокаДляЗагрузки, ДеревоРазбора)
	
	ДанныеДляОбъекта = Новый Структура;
	ДанныеЗаполненияШапки = Новый Структура;
	Товары = Документы.ЗаказПоставщику.ПустаяСсылка().Товары.ВыгрузитьКолонки();
	Услуги = Документы.ЗаказПоставщику.ПустаяСсылка().Услуги.ВыгрузитьКолонки();
	
	Для Каждого СтрокаРеквизита Из СтрокаДляЗагрузки.Строки Цикл
		
		Если СтрокаРеквизита.Строки.Количество()=0 Тогда // примитивный тип
			
			ЗначениеРеквизита = ПолучитьЗначениеРеквизитаДерева(СтрокаРеквизита, СтрокаРеквизита.Реквизит, Истина, ДеревоРазбора);
			
			Если СтрокаРеквизита.Реквизит = "ЦенаВключаетНДС" Тогда
				ИмяРеквизита = "СуммаВключаетНДС";
			ИначеЕсли СтрокаРеквизита.Реквизит = "Валюта" Тогда
				ИмяРеквизита = "ВалютаДокумента";
			ИначеЕсли СтрокаРеквизита.Реквизит = "Курс" Тогда
				ИмяРеквизита = "КурсВзаиморасчетов";
			ИначеЕсли СтрокаРеквизита.Реквизит = "ДатаПоДаннымКлиента" Тогда
				ИмяРеквизита = "ДатаПоДаннымПокупателя";
			ИначеЕсли СтрокаРеквизита.Реквизит = "НомерПоДаннымКлиента" Тогда
				ИмяРеквизита = "НомерПоДаннымПокупателя";
			Иначе
				ИмяРеквизита = СтрокаРеквизита.Реквизит;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
				ДанныеЗаполненияШапки.Вставить(ИмяРеквизита, ЗначениеРеквизита);
			КонецЕсли;
			
		ИначеЕсли СтрокаРеквизита.Реквизит = "СтрокаТЧ" Тогда
			// Добавим строку табличной части
			ДанныеДляЗаполненияСтрокиТЧ = ПолучитьДанныеСтрокиТЧ(СтрокаРеквизита.Строки, ДеревоРазбора);
			
			Если ТипЗнч(ДанныеДляЗаполненияСтрокиТЧ.Номенклатура) = Тип("СправочникСсылка.Номенклатура")
				 И ДанныеДляЗаполненияСтрокиТЧ.Номенклатура.Услуга Тогда
				НоваяСтрока = Услуги.Добавить();
			Иначе
				НоваяСтрока = Товары.Добавить();
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеДляЗаполненияСтрокиТЧ);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ДанныеДляОбъекта.Вставить("Шапка",  ДанныеЗаполненияШапки);
	ДанныеДляОбъекта.Вставить("Товары", Товары);
	ДанныеДляОбъекта.Вставить("Услуги", Услуги);
	
	Возврат ДанныеДляОбъекта;
	
КонецФункции

Функция ПодготовитьСтруктуруДляСчетаНаОплатуПоставщика(СтрокаДляЗагрузки, ДеревоРазбора)
	
	ДанныеДляОбъекта      = Новый Структура;
	ДанныеЗаполненияШапки = Новый Структура;
	
	Товары = Документы.СчетНаОплатуПоставщика.ПустаяСсылка().Товары.ВыгрузитьКолонки();
	Услуги = Документы.СчетНаОплатуПоставщика.ПустаяСсылка().Услуги.ВыгрузитьКолонки();
	
	Для Каждого СтрокаРеквизита Из СтрокаДляЗагрузки.Строки Цикл
		
		Если СтрокаРеквизита.Строки.Количество()=0 Тогда // примитивный тип
			
			ЗначениеРеквизита = ПолучитьЗначениеРеквизитаДерева(СтрокаРеквизита, СтрокаРеквизита.Реквизит, Истина, ДеревоРазбора);
			
			Если СтрокаРеквизита.Реквизит = "ЦенаВключаетНДС" Тогда
				ИмяРеквизита = "СуммаВключаетНДС";
			ИначеЕсли СтрокаРеквизита.Реквизит = "Валюта" Тогда
				ИмяРеквизита = "ВалютаДокумента";
			ИначеЕсли СтрокаРеквизита.Реквизит = "Курс" Тогда
				ИмяРеквизита = "КурсВзаиморасчетов";
			Иначе
				ИмяРеквизита = СтрокаРеквизита.Реквизит;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
				ДанныеЗаполненияШапки.Вставить(ИмяРеквизита, ЗначениеРеквизита);
			КонецЕсли;
			
		ИначеЕсли СтрокаРеквизита.Реквизит = "СтрокаТЧ" Тогда
			
			// Добавим строку табличной части
			ДанныеДляЗаполненияСтрокиТЧ = ПолучитьДанныеСтрокиТЧ(СтрокаРеквизита.Строки, ДеревоРазбора);
			
			Если ТипЗнч(ДанныеДляЗаполненияСтрокиТЧ.Номенклатура) = Тип("СправочникСсылка.Номенклатура")
				 И ДанныеДляЗаполненияСтрокиТЧ.Номенклатура.Услуга Тогда
				НоваяСтрока = Услуги.Добавить();
			Иначе
				НоваяСтрока = Товары.Добавить();
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеДляЗаполненияСтрокиТЧ);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ДанныеДляОбъекта.Вставить("Шапка",  ДанныеЗаполненияШапки);
	ДанныеДляОбъекта.Вставить("Товары", Товары);
	ДанныеДляОбъекта.Вставить("Услуги", Услуги);
	
	Возврат ДанныеДляОбъекта;
	
КонецФункции

// Находит документ ИБ по параметрам.
//
// Параметры:
//  ВидЭД - Перечисления.ВидыЭД - Вид электронного документа, по которому ищется документ ИБ,
//  Контрагент - Ссылка на контрагента,
//  РеквизитыИБ - структура параметров информационной базы,
//  РеквизитыИБКонтрагента - структура параметров контрагента в информационной базе.
//
Функция НайтиДокумент(ВидЭД, Контрагент, РеквизитыИБ = Неопределено, РеквизитыИБКонтрагента = Неопределено)
	
	НайденныйДок = Неопределено;
	
	УстановитьПривилегированныйРежим(Истина);
	
	НайденныйДок = Неопределено;
	Если ВидЭД = Перечисления.ВидыЭД.ЗаказТовара Тогда
		Запрос = Новый Запрос;	
		ОсновнойТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ДокументПоиска.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ЗаказПокупателя КАК ДокументПоиска
		|ГДЕ
		|	ДокументПоиска.Контрагент = &Контрагент";
		
		// будем искать по нашим реквизитам
		Если РеквизитыИБ <> Неопределено И РеквизитыИБ.Количество()>0 Тогда
			Запрос.Текст = ОсновнойТекстЗапроса;
			Запрос.УстановитьПараметр("Контрагент", Контрагент);
			Для Каждого ТекЭл Из РеквизитыИБ Цикл
				Если Найти(ВРег(ТекЭл.Ключ), ВРег("Дата"))>0 Тогда
					Запрос.Текст = Запрос.Текст+" И КОНЕЦПЕРИОДА(ДокументПоиска."+ТекЭл.Ключ+", ДЕНЬ) =  КОНЕЦПЕРИОДА(&"+ТекЭл.Ключ+", ДЕНЬ)";
				Иначе
					Запрос.Текст = Запрос.Текст+" И ДокументПоиска."+ТекЭл.Ключ+" = &"+ТекЭл.Ключ;
				КонецЕсли;
				Запрос.УстановитьПараметр(ТекЭл.Ключ, ТекЭл.Значение);
			КонецЦикла;   
			
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				НайденныйДок = Выборка.Ссылка;
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(НайденныйДок) И РеквизитыИБКонтрагента <> Неопределено
			И РеквизитыИБКонтрагента.Количество()>0 Тогда // не нашли по нашим реквизитам, искать будем по данным контрагента
			Запрос.Текст = ОсновнойТекстЗапроса;
			Запрос.УстановитьПараметр("Контрагент", Контрагент);
			Для Каждого ТекЭл Из РеквизитыИБКонтрагента Цикл
				Запрос.Текст = Запрос.Текст+" И ДокументПоиска."+ТекЭл.Ключ+" = &"+ТекЭл.Ключ;
				Запрос.УстановитьПараметр(ТекЭл.Ключ, ТекЭл.Значение);
			КонецЦикла;   
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				НайденныйДок = Выборка.Ссылка;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ВидЭД = Перечисления.ВидыЭД.ОтветНаЗаказ Тогда
		Запрос = Новый Запрос;
		ОсновнойТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ДокументПоиска.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ЗаказПоставщику КАК ДокументПоиска
		|ГДЕ
		|	ДокументПоиска.Контрагент = &Контрагент";
		
		// будем искать по нашим реквизитам
		Если РеквизитыИБ <> Неопределено И РеквизитыИБ.Количество()>0  Тогда
			Запрос.Текст = ОсновнойТекстЗапроса;
			Запрос.УстановитьПараметр("Контрагент", Контрагент);
			Для Каждого ТекЭл Из РеквизитыИБ Цикл
				Если Найти(ВРег(ТекЭл.Ключ), ВРег("Дата"))>0 Тогда
					Запрос.Текст = Запрос.Текст+" И КОНЕЦПЕРИОДА(ДокументПоиска."+ТекЭл.Ключ+", ДЕНЬ) =  КОНЕЦПЕРИОДА(&"+ТекЭл.Ключ+", ДЕНЬ)";
				Иначе
					Запрос.Текст = Запрос.Текст+" И ДокументПоиска."+ТекЭл.Ключ+" = &"+ТекЭл.Ключ;
				КонецЕсли;
				Запрос.УстановитьПараметр(ТекЭл.Ключ, ТекЭл.Значение);
			КонецЦикла;   
			
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				НайденныйДок = Выборка.Ссылка;
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(НайденныйДок) И РеквизитыИБКонтрагента <> Неопределено 
			И РеквизитыИБКонтрагента.Количество()>0 Тогда // не нашли по нашим реквизитам, искать будем по данным контрагента
			Запрос.Текст = ОсновнойТекстЗапроса;
			Запрос.УстановитьПараметр("Контрагент", Контрагент);
			Для Каждого ТекЭл Из РеквизитыИБКонтрагента Цикл
				Запрос.Текст = Запрос.Текст+" И ДокументПоиска."+ТекЭл.Ключ+" = &"+ТекЭл.Ключ;
				Запрос.УстановитьПараметр(ТекЭл.Ключ, ТекЭл.Значение);
			КонецЦикла;   
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				НайденныйДок = Выборка.Ссылка;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат НайденныйДок;
	
КонецФункции

Функция НайтиСоздатьПоступлениеТоваровУслуг(СтрокаДляЗагрузки, ДеревоРазбора, СсылкаНаВладельца = Неопределено, Записывать = Истина)
	
	ВозвращаемоеЗначение = Неопределено;
	
	ДанныеДляЗагрузки = ПодготовитьСтруктуруДляПоступленияТоваровУслуг(СтрокаДляЗагрузки, ДеревоРазбора);
	
	ДанныеЗаполнения = ДанныеДляЗагрузки.Шапка;
	РежимЗаписи      = РежимЗаписиДокумента.Запись;
	
	Попытка
		
		Если ЗначениеЗаполнено(СсылкаНаВладельца) Тогда // получены изменения по существующему документу
			
			Если СсылкаНаВладельца.Проведен Тогда 
				ВызватьИсключение НСтр("ru = 'Заполнение на основании ЭД возможно только для непроведенного документа'");
			КонецЕсли;
			
			ДокументОбъект = СсылкаНаВладельца.ПолучитьОбъект();
			
		Иначе  // создаем новый
			УстановитьПривилегированныйРежим(Истина);
			ДокументОбъект = Документы.ПоступлениеТоваровУслуг.СоздатьДокумент();
			ДокументОбъект.Дата = ТекущаяДатаСеанса();
			ДокументОбъект.Заполнить(ДанныеЗаполнения);
			
		КонецЕсли;
		ДокументОбъект.ОбменДанными.Загрузка = Истина; 
		
		// попробуем найти заказ поставщику
		ЗаказПоставщику = Неопределено;
		Если ДанныеЗаполнения.Свойство("Контрагент") Тогда
			РеквизитыИБКонтрагента = Новый Структура;
			Если ДанныеЗаполнения.Свойство("НомерПоДаннымПоставщика") И ЗначениеЗаполнено(ДанныеЗаполнения.НомерПоДаннымПоставщика) Тогда
				РеквизитыИБКонтрагента.Вставить("НомерПоДаннымПоставщика", ДанныеЗаполнения.НомерПоДаннымПоставщика);
			КонецЕсли;
			Если ДанныеЗаполнения.Свойство("ДатаПоДаннымПоставщика") И ЗначениеЗаполнено(ДанныеЗаполнения.ДатаПоДаннымПоставщика) Тогда
				РеквизитыИБКонтрагента.Вставить("ДатаПоДаннымПоставщика", ДанныеЗаполнения.ДатаПоДаннымПоставщика);
			КонецЕсли;
			РеквизитыИБ = Новый Структура;
			Если ДанныеЗаполнения.Свойство("НомерПоДаннымПокупателя") И ЗначениеЗаполнено(ДанныеЗаполнения.НомерПоДаннымПокупателя) Тогда
				РеквизитыИБ.Вставить("Номер", ДанныеЗаполнения.НомерПоДаннымПокупателя);
				РеквизитыИБ.Вставить("Дата", ДанныеЗаполнения.ДатаПоДаннымПокупателя);
			КонецЕсли;
			Если РеквизитыИБ.Количество()>0 ИЛИ РеквизитыИБКонтрагента.Количество()>0 Тогда // есть еще реквизиты поиска, кроме Контрагента
				ЗаказПоставщику = НайтиДокумент(Перечисления.ВидыЭД.ОтветНаЗаказ, ДанныеЗаполнения.Контрагент, РеквизитыИБ, РеквизитыИБКонтрагента);
			КонецЕсли;
		КонецЕсли;
		
		// Заполним реквизиты шапки на основании структуры данных заполнения
		ПерезаполнениеЗначенийРеквизитовШапки(ДокументОбъект, ДанныеЗаполнения);
		
		// Загрузим табличные части
		ДокументОбъект.Товары.Загрузить(ДанныеДляЗагрузки.Товары);
		ДокументОбъект.Услуги.Загрузить(ДанныеДляЗагрузки.Услуги);
		
		// Установим вручную некоторые реквизиты шапки
		ДокументОбъект.СуммаВключаетНДС = Ложь;
		ДокументОбъект.СуммаДокумента   = УчетНДС.ПолучитьСуммуДокументаСНДС(ДокументОбъект);
		Если НЕ ЗначениеЗаполнено(ДокументОбъект.ВалютаДокумента) Тогда
			ДокументОбъект.ВалютаДокумента = глЗначениеПеременной("ВалютаРегламентированногоУчета");
		КонецЕсли;
		
		// Заполним курс и кратность
		КурсВалюты = МодульВалютногоУчета.ПолучитьКурсВалюты(ДокументОбъект.ВалютаДокумента, ДокументОбъект.Дата);
		ДокументОбъект.КурсВзаиморасчетов      = КурсВалюты.Курс;
		ДокументОбъект.КратностьВзаиморасчетов = КурсВалюты.Кратность;
		
		Если ЗначениеЗаполнено(ЗаказПоставщику) Тогда
			ДокументОбъект.Сделка = ЗаказПоставщику;
		КонецЕсли;
		
		ДокументОбъект.ДополнительныеСвойства.Вставить("ЕстьСоглашение", Истина);
		
		Если ДокументОбъект.ЭтоНовый() Тогда
			ДокументОбъект.УстановитьНовыйНомер();
		КонецЕсли;
		
		Если Записывать ТОгда
			ДокументОбъект.Записать(РежимЗаписи);
			ВозвращаемоеЗначение = ДокументОбъект.Ссылка;
		Иначе
			ВозвращаемоеЗначение = ДокументОбъект;
		КонецЕсли;
		
	Исключение
		ОписаниеОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбщегоНазначения.СообщитьОбОшибке(ОписаниеОшибки);
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Заполнение на основании ЭД'"),
			УровеньЖурналаРегистрации.Ошибка, 
			Метаданные.Документы.ПоступлениеТоваровУслуг, 
			СсылкаНаВладельца, 
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение;
		
	КонецПопытки;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция НайтиСоздатьКорректировкуПоступления(СтрокаДляЗагрузки, ДеревоРазбора, СсылкаНаВладельца = Неопределено, Записывать = Истина)
	
	ВозвращаемоеЗначение = Неопределено;
	
	ДанныеДляЗагрузки = ПодготовитьСтруктуруДляКорректировкиПоступления(СтрокаДляЗагрузки, ДеревоРазбора);
	
	ДанныеЗаполнения = ДанныеДляЗагрузки.Шапка;
	РежимЗаписи      = РежимЗаписиДокумента.Запись;
	
	Попытка
		
		Если ЗначениеЗаполнено(СсылкаНаВладельца) Тогда // получены изменения по существующему документу
			
			Если СсылкаНаВладельца.Проведен Тогда 
				ВызватьИсключение НСтр("ru = 'Заполнение на основании ЭД возможно только для непроведенного документа'");
			КонецЕсли;
			
			ДокументОбъект = СсылкаНаВладельца.ПолучитьОбъект();
			
		Иначе  // создаем новый
			
			УстановитьПривилегированныйРежим(Истина);
			ДокументОбъект = Документы.КорректировкаПоступления.СоздатьДокумент();
			ДокументОбъект.Дата = ТекущаяДатаСеанса();
			
		КонецЕсли;
		
		ДокументОбъект.ОбменДанными.Загрузка = Истина;
		
		ДокументПоступления = ДокументОбъект.ДокументПоступления;
		
		Если Не ЗначениеЗаполнено(ДокументПоступления) И ДанныеЗаполнения.Свойство("Основание") Тогда
			ДокументПоступления = ДанныеЗаполнения.Основание;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ДокументПоступления) Тогда
			// Заполненим корректировку поступления данными основания
			ДокументОбъект.Заполнить(ДокументПоступления);
		Иначе
			// Заполним шапку по данным заполнения.
			ПерезаполнениеЗначенийРеквизитовШапки(ДокументОбъект, ДанныеЗаполнения);
		КонецЕсли;
		
		// Заполним табличные части данными корректировки
		ТабличныеЧастиДляЗаполения = Новый Структура("Товары, Услуги");
		
		Для Каждого ТабличнаяЧасть Из ТабличныеЧастиДляЗаполения Цикл
			
			ИмяТЧ = ТабличнаяЧасть.Ключ;
			
			СтруктураПоиска = Новый Структура("Номенклатура, СтавкаНДС");
			Если ИмяТЧ = "Товары" Тогда
				СтруктураПоиска.Вставить("ХарактеристикаНоменклатуры");
				СтруктураПоиска.Вставить("ЕдиницаИзмерения");
			КонецЕсли;
			
			Для Каждого СтрокаДокумента Из ДокументОбъект[ИмяТЧ] Цикл
				
				ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаДокумента);
				РезультатПоискаДанныхЗаполнения = ДанныеДляЗагрузки[ИмяТЧ].НайтиСтроки(СтруктураПоиска);
				
				Если РезультатПоискаДанныхЗаполнения.Количество() = 0 Тогда
					
					// Такой строки в данных корректировки нет - обнуляем данные строки документа.
					СтрокаДокумента.Количество = 0;
					СтрокаДокумента.Цена       = 0;
					СтрокаДокумента.Сумма      = 0;
					СтрокаДокумента.СуммаНДС   = 0;
					
				Иначе
					
					СтрокаДанныхЗаполнения = РезультатПоискаДанныхЗаполнения[0];
					ЗаполнитьЗначенияСвойств(СтрокаДокумента, СтрокаДанныхЗаполнения, "Количество, Цена, Сумма, СуммаНДС");
					
					// Удалим обработанную строку из данных заполнения
					ДанныеДляЗагрузки[ИмяТЧ].Удалить(СтрокаДанныхЗаполнения);
					
				КонецЕсли;
				
			КонецЦикла;
			
			// Добавим новые строки в документ
			Если ДанныеДляЗагрузки[ИмяТЧ].Количество() > 0 Тогда
				
				Для Каждого СтрокаДанныхЗаполнения Из ДанныеДляЗагрузки[ИмяТЧ] Цикл
					
					НоваяСтрока = ДокументОбъект[ИмяТЧ].Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДанныхЗаполнения);
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЦикла;
		
		Если ДанныеЗаполнения.ВидОперации = Перечисления.ВидыОперацийЭД.Исправление Тогда
			ДокументОбъект.ВидОперации = Перечисления.ВидыОперацийИсправленияПоступленияРеализации.ИсправлениеОшибки;
		Иначе
			ДокументОбъект.ВидОперации = Перечисления.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение;
		КонецЕсли;
		
		ДокументОбъект.СуммаДокумента = УчетНДС.ПолучитьСуммуДокументаСНДС(ДокументОбъект);
		ДокументОбъект.ДополнительныеСвойства.Вставить("ЕстьСоглашение", Истина);
		
		Если ДокументОбъект.ЭтоНовый() Тогда
			ДокументОбъект.УстановитьНовыйНомер();
		КонецЕсли;
		
		Если Записывать ТОгда
			ДокументОбъект.Записать(РежимЗаписи);
			ВозвращаемоеЗначение = ДокументОбъект.Ссылка;
		Иначе
			ВозвращаемоеЗначение = ДокументОбъект;
		КонецЕсли;
		
	Исключение
		
		ОписаниеОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбщегоНазначения.СообщитьОбОшибке(ОписаниеОшибки);
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Заполнение на основании ЭД'"),
			УровеньЖурналаРегистрации.Ошибка, 
			Метаданные.Документы.ПоступлениеТоваровУслуг, 
			СсылкаНаВладельца, 
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение;
		
	КонецПопытки;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция НайтиСоздатьСчетФактуру(СтрокаДляЗагрузки, ДеревоРазбора, СсылкаНаВладельца = Неопределено, Записывать = Истина)
	
	ВозвращаемоеЗначение = Неопределено;
	
	ДанныеДляЗагрузки = ПодготовитьСтруктуруДляСчетаФактуры(СтрокаДляЗагрузки, ДеревоРазбора);
	
	РежимЗаписи = РежимЗаписиДокумента.Запись;
	
	Попытка
		
		Если ЗначениеЗаполнено(СсылкаНаВладельца) Тогда
			
			Если СсылкаНаВладельца.Проведен Тогда 
				ВызватьИсключение НСтр("ru = 'Заполнение на основании ЭД возможно только для непроведенного документа'");
			КонецЕсли;
			
			ДокументОбъект = СсылкаНаВладельца.ПолучитьОбъект();
			
		Иначе
			
			УстановитьПривилегированныйРежим(Истина);
			ДокументОбъект = Документы.СчетФактураПолученный.СоздатьДокумент();
			ДокументОбъект.Заполнить(ДанныеДляЗагрузки);
			ДокументОбъект.Дата = ТекущаяДатаСеанса();
			
		КонецЕсли;
		
		ДокументОбъект.ОбменДанными.Загрузка = Истина; 
		
		ДокументОбъект.ДополнительныеСвойства.Вставить("ЕстьСоглашение", Истина);
		
		// Заполним ТЧ ДокументыОснования
		Если ДанныеДляЗагрузки.Свойство("ДокументыОснования") Тогда
			ДокументОбъект.ДокументыОснования.Очистить();
			Для Каждого ДокументОснование Из ДанныеДляЗагрузки.ДокументыОснования Цикл
				НоваяСтрока = ДокументОбъект.ДокументыОснования.Добавить();
				НоваяСтрока.ДокументОснование = ДокументОснование;
				Если ДанныеДляЗагрузки.ВидСчетаФактуры  = Перечисления.ВидСчетаФактурыПолученного.Корректировочный Тогда
					НоваяСтрока.НомерИсходногоДокумента = ДанныеДляЗагрузки.НомерИсходногоДокумента;
					НоваяСтрока.ДатаИсходногоДокумента  = ДанныеДляЗагрузки.ДатаИсходногоДокумента;
					НоваяСтрока.СуммаУвеличение         = ДанныеДляЗагрузки.СуммаУвеличение;
					НоваяСтрока.СуммаУменьшение         = ДанныеДляЗагрузки.СуммаУменьшение;
					НоваяСтрока.СуммаНДСУвеличение      = ДанныеДляЗагрузки.СуммаНДСУвеличение;
					НоваяСтрока.СуммаНДСУменьшение      = ДанныеДляЗагрузки.СуммаНДСУменьшение;
					НоваяСтрока.УчитыватьИсправлениеИсходногоДокумента = ДанныеДляЗагрузки.Свойство("УчитыватьИсправлениеИсходногоДокумента");
					Если НоваяСтрока.УчитыватьИсправлениеИсходногоДокумента Тогда
						НоваяСтрока.НомерИсправленияИсходногоДокумента = ДанныеДляЗагрузки.НомерИсправленияИсходногоДокумента;
						НоваяСтрока.ДатаИсправленияИсходногоДокумента  = ДанныеДляЗагрузки.ДатаИсправленияИсходногоДокумента;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если ДокументОбъект.ДокументыОснования.Количество() = 0 Тогда
			НоваяСтрока = ДокументОбъект.ДокументыОснования.Добавить();
			Если ДанныеДляЗагрузки.ВидСчетаФактуры  = Перечисления.ВидСчетаФактурыПолученного.Корректировочный Тогда
				НоваяСтрока.НомерИсходногоДокумента = ДанныеДляЗагрузки.НомерИсходногоДокумента;
				НоваяСтрока.ДатаИсходногоДокумента  = ДанныеДляЗагрузки.ДатаИсходногоДокумента;
				НоваяСтрока.СуммаУвеличение         = ДанныеДляЗагрузки.СуммаУвеличение;
				НоваяСтрока.СуммаУменьшение         = ДанныеДляЗагрузки.СуммаУменьшение;
				НоваяСтрока.СуммаНДСУвеличение      = ДанныеДляЗагрузки.СуммаНДСУвеличение;
				НоваяСтрока.СуммаНДСУменьшение      = ДанныеДляЗагрузки.СуммаНДСУменьшение;
				НоваяСтрока.УчитыватьИсправлениеИсходногоДокумента = ДанныеДляЗагрузки.Свойство("УчитыватьИсправлениеИсходногоДокумента");
				Если НоваяСтрока.УчитыватьИсправлениеИсходногоДокумента Тогда
					НоваяСтрока.НомерИсправленияИсходногоДокумента = ДанныеДляЗагрузки.НомерИсправленияИсходногоДокумента;
					НоваяСтрока.ДатаИсправленияИсходногоДокумента  = ДанныеДляЗагрузки.ДатаИсправленияИсходногоДокумента;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если ДанныеДляЗагрузки.ВидСчетаФактуры  = Перечисления.ВидСчетаФактурыПолученного.Корректировочный Тогда
			Если ДанныеДляЗагрузки.Свойство("НомерИсходногоДокумента") Тогда
				ДанныеДляЗагрузки.Удалить("НомерИсходногоДокумента");
			КонецЕсли;
			Если ДанныеДляЗагрузки.Свойство("ДатаИсходногоДокумента") Тогда
				ДанныеДляЗагрузки.Удалить("ДатаИсходногоДокумента");
			КонецЕсли;
			Если ДанныеДляЗагрузки.Свойство("УчитыватьИсправлениеИсходногоДокумента") Тогда
				ДанныеДляЗагрузки.Удалить("УчитыватьИсправлениеИсходногоДокумента");
			КонецЕсли;
			Если ДанныеДляЗагрузки.Свойство("НомерИсправленияИсходногоДокумента") Тогда
				ДанныеДляЗагрузки.Удалить("НомерИсправленияИсходногоДокумента");
			КонецЕсли;
			Если ДанныеДляЗагрузки.Свойство("ДатаИсправленияИсходногоДокумента") Тогда
				ДанныеДляЗагрузки.Удалить("ДатаИсправленияИсходногоДокумента");
			КонецЕсли;
		КонецЕсли;
		
		// Заполним реквизиты шапки на основании структуры данных заполнения
		ПерезаполнениеЗначенийРеквизитовШапки(ДокументОбъект, ДанныеДляЗагрузки);
		Если ДокументОбъект.ЭтоНовый() Тогда
			ДокументОбъект.УстановитьНовыйНомер();
		КонецЕсли;
		
		Если Записывать ТОгда
			ДокументОбъект.Записать(РежимЗаписи);
			ВозвращаемоеЗначение = ДокументОбъект.Ссылка;
		Иначе
			ВозвращаемоеЗначение = ДокументОбъект;
		КонецЕсли;
		
	Исключение
		
		ОписаниеОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбщегоНазначения.СообщитьОбОшибке(ОписаниеОшибки);
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Заполнение на основании ЭД'"),
			УровеньЖурналаРегистрации.Ошибка, 
			Метаданные.Документы.СчетФактураПолученный, 
			СсылкаНаВладельца, 
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		ВызватьИсключение;
		
	КонецПопытки;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция НайтиСоздатьЗаказПокупателя(СтрокаДляЗагрузки, ДеревоРазбора, СсылкаНаВладельца = Неопределено, Записывать = Истина)
	
	ВозвращаемоеЗначение = Неопределено;
	
	ДанныеДляЗагрузки = ПодготовитьСтруктуруДляЗаказаПокупателя(СтрокаДляЗагрузки, ДеревоРазбора);
	
	ДанныеЗаполнения = ДанныеДляЗагрузки.Шапка;
	РежимЗаписи      = РежимЗаписиДокумента.Запись;
	
	Попытка
		
		Если ЗначениеЗаполнено(СсылкаНаВладельца) Тогда // получены изменения по существующему документу
			
			Если СсылкаНаВладельца.Проведен Тогда 
				ВызватьИсключение НСтр("ru = 'Заполнение на основании ЭД возможно только для непроведенного документа'");
			КонецЕсли;
			
			ДокументОбъект = СсылкаНаВладельца.ПолучитьОбъект();
			
		Иначе // попробуем найти по РеквизитыИБ и РеквизитыИБКонтрагента
			
			УстановитьПривилегированныйРежим(Истина);
			
			НайденныйДок = Неопределено;
			Если ДанныеЗаполнения.Свойство("Контрагент") Тогда
				РеквизитыИБКонтрагента = Новый Структура;
				Если ДанныеЗаполнения.Свойство("НомерПоДаннымПокупателя") И ЗначениеЗаполнено(ДанныеЗаполнения.НомерПоДаннымПокупателя) Тогда
					РеквизитыИБКонтрагента.Вставить("НомерПоДаннымПокупателя", ДанныеЗаполнения.НомерПоДаннымПокупателя);
				КонецЕсли;
				Если ДанныеЗаполнения.Свойство("ДатаПоДаннымПокупателя") И ЗначениеЗаполнено(ДанныеЗаполнения.ДатаПоДаннымПокупателя) Тогда
					РеквизитыИБКонтрагента.Вставить("ДатаПоДаннымПокупателя", ДанныеЗаполнения.ДатаПоДаннымПокупателя);
				КонецЕсли;
					РеквизитыИБ = Новый Структура;
				Если ДанныеЗаполнения.Свойство("НомерПоДаннымПоставщика") И ЗначениеЗаполнено(ДанныеЗаполнения.НомерПоДаннымПоставщика) Тогда
					РеквизитыИБ.Вставить("Номер", ДанныеЗаполнения.НомерПоДаннымПоставщика);
				КонецЕсли;
				Если ДанныеЗаполнения.Свойство("ДатаПоДаннымПоставщика") И ЗначениеЗаполнено(ДанныеЗаполнения.ДатаПоДаннымПоставщика) Тогда
					РеквизитыИБ.Вставить("Дата", ДанныеЗаполнения.ДатаПоДаннымПоставщика);
				КонецЕсли;
				Если РеквизитыИБ.Количество()>0 ИЛИ РеквизитыИБКонтрагента.Количество()>0 Тогда // есть еще реквизиты поиска, кроме Контрагента
					НайденныйДок = НайтиДокумент(Перечисления.ВидыЭД.ЗаказТовара, ДанныеЗаполнения.Контрагент, РеквизитыИБ, РеквизитыИБКонтрагента);
				КонецЕсли;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(НайденныйДок) Тогда // нашли документ, вернем ссылку, чтоб просто привязать ЭД
				ДокументОбъект = НайденныйДок.ПолучитьОбъект();
				Возврат ДокументОбъект.Ссылка;
			КонецЕсли;
			
			ДокументОбъект = Документы.ЗаказПокупателя.СоздатьДокумент();
			ДокументОбъект.Дата = ТекущаяДатаСеанса();
			ДокументОбъект.Заполнить(ДанныеЗаполнения);
			ДокументОбъект.Организация = ДанныеЗаполнения.Организация;
			
		КонецЕсли;
		
		ДокументОбъект.ОбменДанными.Загрузка = Истина;
		
		// Заполним значения реквизитов шапки
		ПерезаполнениеЗначенийРеквизитовШапки(ДокументОбъект, ДанныеЗаполнения);
		
		ДокументОбъект.СуммаДокумента 			= ДанныеДляЗагрузки.Шапка.СуммаИтог;
		ДокументОбъект.КратностьВзаиморасчетов 	= 1;
		ДокументОбъект.ВидОперации				= Перечисления.ВидыОперацийЗаказПокупателя.ПродажаКомиссия;
		
		Если ДокументОбъект.ЭтоНовый() И ЗначениеЗаполнено(ДокументОбъект.Организация) Тогда
			ДокументОбъект.УчитыватьНДС = НЕ НалоговыйУчетУСН.ПрименениеУСН(ДокументОбъект.Организация, ДокументОбъект.Дата);
		Иначе
			ДокументОбъект.УчитыватьНДС = Истина;
		КонецЕсли;
		
		// Загрузим табличные части
		ДокументОбъект.Товары.Загрузить(ДанныеДляЗагрузки.Товары);
		ДокументОбъект.Услуги.Загрузить(ДанныеДляЗагрузки.Услуги);
		
		Если ДокументОбъект.СуммаВключаетНДС Тогда
			Для Каждого Строка из ДокументОбъект.Товары Цикл
				Строка.Сумма = Строка.Сумма + Строка.СуммаНДС;
			КонецЦикла;
		КонецЕсли;
		
		Если ДокументОбъект.ЭтоНовый() Тогда
			ДокументОбъект.УстановитьНовыйНомер();
		КонецЕсли;
		
		Если Записывать ТОгда
			ДокументОбъект.Записать(РежимЗаписи);
			ВозвращаемоеЗначение = ДокументОбъект.Ссылка;
		Иначе
			ВозвращаемоеЗначение = ДокументОбъект;
		КонецЕсли;
		
	Исключение
		
		ОписаниеОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбщегоНазначения.СообщитьОбОшибке(ОписаниеОшибки);
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Заполнение на основании ЭД'"), 
			УровеньЖурналаРегистрации.Ошибка, 
			Метаданные.Документы.ЗаказПокупателя, 
			СсылкаНаВладельца, 
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение;
		
	КонецПопытки;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция НайтиСоздатьЗаказПоставщику(СтрокаДляЗагрузки, ДеревоРазбора, СсылкаНаВладельца = Неопределено, Записывать = Истина)
	
	ВозвращаемоеЗначение = Неопределено;
	
	ДанныеДляЗагрузки = ПодготовитьСтруктуруДляЗаказаПоставщику(СтрокаДляЗагрузки, ДеревоРазбора);
	
	ДанныеЗаполнения = ДанныеДляЗагрузки.Шапка;
	РежимЗаписи      = РежимЗаписиДокумента.Запись;
	
	Попытка
		
		Если ЗначениеЗаполнено(СсылкаНаВладельца) Тогда // получены изменения по существующему документу
			
			Если СсылкаНаВладельца.Проведен Тогда 
				ВызватьИсключение НСтр("ru = 'Заполнение на основании ЭД возможно только для непроведенного документа'");
			КонецЕсли;
			
			ДокументОбъект = СсылкаНаВладельца.ПолучитьОбъект();
			
		Иначе // попробуем найти по РеквизитыИБ и РеквизитыИБКонтрагента
			
			УстановитьПривилегированныйРежим(Истина);
			
			НайденныйДок = Неопределено;
			Если ДанныеЗаполнения.Свойство("Контрагент") Тогда
				РеквизитыИБКонтрагента = Новый Структура;
				Если ДанныеЗаполнения.Свойство("НомерПоДаннымПоставщика") И ЗначениеЗаполнено(ДанныеЗаполнения.НомерПоДаннымПоставщика) Тогда
					РеквизитыИБКонтрагента.Вставить("НомерПоДаннымПоставщика", ДанныеЗаполнения.НомерПоДаннымПоставщика);
				КонецЕсли;
				Если ДанныеЗаполнения.Свойство("ДатаПоДаннымПоставщика") И ЗначениеЗаполнено(ДанныеЗаполнения.ДатаПоДаннымПоставщика) Тогда
					РеквизитыИБКонтрагента.Вставить("ДатаПоДаннымПоставщика", ДанныеЗаполнения.ДатаПоДаннымПоставщика);
				КонецЕсли;
				РеквизитыИБ = Новый Структура;
				Если ДанныеЗаполнения.Свойство("НомерПоДаннымПокупателя") И ЗначениеЗаполнено(ДанныеЗаполнения.НомерПоДаннымПокупателя) Тогда
					РеквизитыИБ.Вставить("Номер", ДанныеЗаполнения.НомерПоДаннымПокупателя);
				КонецЕсли;
				Если ДанныеЗаполнения.Свойство("ДатаПоДаннымПокупателя") И ЗначениеЗаполнено(ДанныеЗаполнения.ДатаПоДаннымПокупателя) Тогда
					РеквизитыИБ.Вставить("Дата", ДанныеЗаполнения.ДатаПоДаннымПокупателя);
				КонецЕсли;
				Если РеквизитыИБ.Количество()>0 ИЛИ РеквизитыИБКонтрагента.Количество()>0 Тогда // есть еще реквизиты поиска, кроме Контрагента
					НайденныйДок = НайтиДокумент(Перечисления.ВидыЭД.ОтветНаЗаказ, ДанныеЗаполнения.Контрагент, РеквизитыИБ, РеквизитыИБКонтрагента);
				КонецЕсли;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(НайденныйДок) Тогда // нашли документ, вернем ссылку, чтоб просто привязать ЭД
				ДокументОбъект = НайденныйДок.ПолучитьОбъект();
				Возврат ДокументОбъект.Ссылка;
			КонецЕсли;
			
			ДокументОбъект = Документы.ЗаказПоставщику.СоздатьДокумент();
			ДокументОбъект.Дата = ТекущаяДатаСеанса();
			ДокументОбъект.Заполнить(ДанныеЗаполнения);
			ДокументОбъект.Организация = ДанныеЗаполнения.Организация;
			
		КонецЕсли;
		
		ДокументОбъект.ОбменДанными.Загрузка = Истина;
		
		// Заполним значения реквизитов шапки
		ПерезаполнениеЗначенийРеквизитовШапки(ДокументОбъект, ДанныеЗаполнения);
		
		ДокументОбъект.СуммаДокумента 			= ДанныеДляЗагрузки.Шапка.СуммаИтог;
		ДокументОбъект.КратностьВзаиморасчетов 	= 1;
		ДокументОбъект.ВидОперации				= Перечисления.ВидыОперацийЗаказПоставщику.ПокупкаКомиссия;
		
		Если ДокументОбъект.ЭтоНовый() И ЗначениеЗаполнено(ДокументОбъект.Организация) Тогда
			ДокументОбъект.УчитыватьНДС = НЕ НалоговыйУчетУСН.ПрименениеУСН(ДокументОбъект.Организация, ДокументОбъект.Дата);
		Иначе
			ДокументОбъект.УчитыватьНДС = Истина;
		КонецЕсли;
		
		// Загрузим табличные части
		ДокументОбъект.Товары.Загрузить(ДанныеДляЗагрузки.Товары);
		ДокументОбъект.Услуги.Загрузить(ДанныеДляЗагрузки.Услуги);
		
		Если ДокументОбъект.СуммаВключаетНДС Тогда
			Для Каждого Строка из ДокументОбъект.Товары Цикл
				Строка.Сумма = Строка.Сумма + Строка.СуммаНДС;
			КонецЦикла;
		КонецЕсли;
		
		Если ДокументОбъект.ЭтоНовый() Тогда
			ДокументОбъект.УстановитьНовыйНомер();
		КонецЕсли;
		
		Если Записывать ТОгда
			ДокументОбъект.Записать(РежимЗаписи);
			ВозвращаемоеЗначение = ДокументОбъект.Ссылка;
		Иначе
			ВозвращаемоеЗначение = ДокументОбъект;
		КонецЕсли;
		
	Исключение
		
		ОписаниеОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбщегоНазначения.СообщитьОбОшибке(ОписаниеОшибки);
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Заполнение на основании ЭД'"),
			УровеньЖурналаРегистрации.Ошибка, 
			Метаданные.Документы.ЗаказПоставщику, 
			СсылкаНаВладельца, 
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение;
		
	КонецПопытки;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция НайтиСоздатьСчетНаОплатуПоставщика(СтрокаДляЗагрузки, ДеревоРазбора, СсылкаНаВладельца = Неопределено, Записывать = Истина)
	
	ВозвращаемоеЗначение = Неопределено;
	
	ДанныеДляЗагрузки = ПодготовитьСтруктуруДляСчетаНаОплатуПоставщика(СтрокаДляЗагрузки, ДеревоРазбора);
	
	ДанныеЗаполнения = ДанныеДляЗагрузки.Шапка;
	РежимЗаписи      = РежимЗаписиДокумента.Запись;
	
	Попытка
		
		Если ЗначениеЗаполнено(СсылкаНаВладельца) Тогда // получены изменения по существующему документу
			
			Если СсылкаНаВладельца.Проведен Тогда 
				ВызватьИсключение НСтр("ru = 'Заполнение на основании ЭД возможно только для непроведенного документа'");
			КонецЕсли;
			
			ДокументОбъект = СсылкаНаВладельца.ПолучитьОбъект();
			
		Иначе // попробуем найти по РеквизитыИБ и РеквизитыИБКонтрагента
			
			УстановитьПривилегированныйРежим(Истина);
			
			ДокументОбъект = Документы.СчетНаОплатуПоставщика.СоздатьДокумент();
			ДокументОбъект.Дата = ТекущаяДатаСеанса();
			ДокументОбъект.Организация = ДанныеЗаполнения.Организация;
			ДокументОбъект.Заполнить(ДанныеЗаполнения);
			
		КонецЕсли;
		
		ДокументОбъект.ОбменДанными.Загрузка = Истина;
		
		// Заполним значения реквизитов шапки
		ПерезаполнениеЗначенийРеквизитовШапки(ДокументОбъект, ДанныеЗаполнения);
		
		ДокументОбъект.СуммаДокумента 			= ДанныеДляЗагрузки.Шапка.СуммаИтог;
		ДокументОбъект.КратностьВзаиморасчетов 	= 1;
		
		Если ДокументОбъект.ЭтоНовый() И ЗначениеЗаполнено(ДокументОбъект.Организация) Тогда
			ДокументОбъект.УчитыватьНДС = НЕ НалоговыйУчетУСН.ПрименениеУСН(ДокументОбъект.Организация, ДокументОбъект.Дата);
		Иначе
			ДокументОбъект.УчитыватьНДС = Истина;
		КонецЕсли;
		
		// Загрузим табличные части
		ДокументОбъект.Товары.Загрузить(ДанныеДляЗагрузки.Товары);
		ДокументОбъект.Услуги.Загрузить(ДанныеДляЗагрузки.Услуги);
		
		Если ДокументОбъект.СуммаВключаетНДС Тогда
			Для Каждого Строка из ДокументОбъект.Товары Цикл
				Строка.Сумма = Строка.Сумма + Строка.СуммаНДС;
			КонецЦикла
		КонецЕсли;
		
		ДокументОбъект.СуммаДокумента = УчетНДС.ПолучитьСуммуДокументаСНДС(ДокументОбъект);
		
		Если ДокументОбъект.ЭтоНовый() Тогда
			ДокументОбъект.УстановитьНовыйНомер();
		КонецЕсли;
		
		Если Записывать ТОгда
			ДокументОбъект.Записать(РежимЗаписи);
			ВозвращаемоеЗначение = ДокументОбъект.Ссылка;
		Иначе
			ВозвращаемоеЗначение = ДокументОбъект;
		КонецЕсли;
		
	Исключение
		
		ОписаниеОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбщегоНазначения.СообщитьОбОшибке(ОписаниеОшибки);
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Заполнение на основании ЭД'"), 
			УровеньЖурналаРегистрации.Ошибка, 
			Метаданные.Документы.ЗаказПокупателя, 
			СсылкаНаВладельца, 
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение;
		
	КонецПопытки;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Создание элементов справочников

// Возвращает данные по номенклатуре и характеристике товара на основании данных о номенклатуре поставщика
// 
// Параметры:
//  НоменклатураПоставщика - Струкутра, содержит сведения о номенклатуре поставщика
//
// Возвращаемое значение:
//  Структура - Структура, содержащая поля:
//               Номенклатура
//               ХарактеристикаНоменклатуры
//
Функция ПолучитьДанныеТовараПоНоменклатуреПоставщика(НоменклатураПоставщика)
	
	ДанныеПоТовару = Новый Структура(
		"Номенклатура, ХарактеристикаНоменклатуры",
		Справочники.Номенклатура.ПустаяСсылка(),
		Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка());
		
	Если НЕ НоменклатураПоставщика.Свойство("Идентификатор")
		 ИЛИ НЕ ЗначениеЗаполнено(НоменклатураПоставщика.Идентификатор) Тогда
		Возврат ДанныеПоТовару; 
	КонецЕсли;
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НоменклатураКонтрагентов.Номенклатура,
	|	НоменклатураКонтрагентов.ХарактеристикаНоменклатуры
	|ИЗ
	|	РегистрСведений.НоменклатураКонтрагентов КАК НоменклатураКонтрагентов
	|ГДЕ
	|	НоменклатураКонтрагентов.Контрагент = &Контрагент
	|	И НоменклатураКонтрагентов.Идентификатор = &Идентификатор";
	
	Запрос.УстановитьПараметр("Контрагент", НоменклатураПоставщика.Владелец);
	Запрос.УстановитьПараметр("Идентификатор", НоменклатураПоставщика.Идентификатор);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = Запрос.Выполнить().Выбрать();
		Выборка.Следующий();
		ДанныеПоТовару.Номенклатура 				= Выборка.Номенклатура;
		ДанныеПоТовару.ХарактеристикаНоменклатуры 	= Выборка.ХарактеристикаНоменклатуры; 
	КонецЕсли;
	
	Возврат ДанныеПоТовару;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Работа с деревом

Процедура ЗаполнитьДопДанныеВСтроке(СтрокаТовары)
	
	СтруктураДопДанных = Новый Структура;
	СтруктураДопДанных.Вставить("КодХарактеристики");
	СтруктураДопДанных.Вставить("Характеристика");
	СтруктураДопДанных.Вставить("СрокГодности");
	СтруктураДопДанных.Вставить("Коэффициент");
	СтруктураДопДанных.Вставить("Штрихкод");
	СтруктураДопДанных.Вставить("УпаковкаНаименование");
	СтруктураДопДанных.Вставить("УпаковкаКод");
	ЗаполнитьЗначенияСвойств(СтруктураДопДанных, СтрокаТовары);
		
	СтрокаТовары.ДопДанныеНеПодписанные = СтруктураДопДанных;
	
	СтруктураДопДанныхПодписанные = Новый Структура;
	
	Если СтрокаТовары.СтавкаНДС = Перечисления.СтавкиНДС.НДС18_118
		ИЛИ СтрокаТовары.СтавкаНДС = Перечисления.СтавкиНДС.НДС18 Тогда
		СтруктураДопДанныхПодписанные.Вставить("СтавкаНДС", Строка(СтрокаТовары.СтавкаНДС));
		СтрокаТовары.СтавкаНДСЧислом = 18;
	ИначеЕсли СтрокаТовары.СтавкаНДС = Перечисления.СтавкиНДС.НДС10_110
		ИЛИ СтрокаТовары.СтавкаНДС = Перечисления.СтавкиНДС.НДС10 Тогда
		СтруктураДопДанныхПодписанные.Вставить("СтавкаНДС", Строка(СтрокаТовары.СтавкаНДС));
		СтрокаТовары.СтавкаНДСЧислом = 10;
	Иначе
		СтруктураДопДанныхПодписанные.Вставить("СтавкаНДС", Строка(СтрокаТовары.СтавкаНДС));
		СтрокаТовары.СтавкаНДСЧислом = 0;
	КонецЕсли;

	СтрокаТовары.ДопДанныеПодписанные = СтруктураДопДанныхПодписанные;
	
КонецПроцедуры

Процедура ЗаполнитьРеквизитыФизЛица(ДеревоДанных, ИсточникДанных, ТипЛица, Должность = Неопределено)
	
	Фамилия = ""; Имя = ""; Отчество = "";
	ЭлектронныеДокументы.ФамилияИнициалыФизЛица(ИсточникДанных, Фамилия, Имя, Отчество);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, ТипЛица + ".Фамилия", Фамилия);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, ТипЛица + ".Имя", Имя);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, ТипЛица + ".Отчество", Отчество);
	Если Должность <> Неопределено Тогда
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, ТипЛица + ".Должность", Должность);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьФИОПодписантаВДереве(ДеревоДанных, ТипПодписанта, ИсточникДанных)
	
	Фамилия = ""; Имя = ""; Отчество = "";
	ЭлектронныеДокументы.ФамилияИнициалыФизЛица(ИсточникДанных, Фамилия, Имя, Отчество);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
								ДеревоДанных,
								"Подписант." + ТипПодписанта + ".Фамилия",
								Фамилия);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "Подписант." + ТипПодписанта + ".Имя", Имя);
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
								ДеревоДанных,
								"Подписант." + ТипПодписанта + ".Отчество",
								Отчество);
	
КонецПроцедуры

Процедура ЗаполнитьДанныеУчастника(ДеревоДанных, СведенияОбУчастнике, ВидУчастника, ВидАдреса = "Юр")
	
	Если СведенияОбУчастнике.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо Тогда
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ВидУчастника + ".ТипУчастника.ЮЛ.ИНН",
									СведенияОбУчастнике.ИНН);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ВидУчастника + ".ТипУчастника.ЮЛ.КПП",
									СведенияОбУчастнике.КПП);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ВидУчастника + ".ТипУчастника.ЮЛ.НаименованиеОрганизации",
									СведенияОбУчастнике.ПолноеНаименование);
	Иначе
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ВидУчастника + ".ТипУчастника.ФЛ.ИНН",
									СведенияОбУчастнике.ИНН);
		Фамилия = ""; Имя = ""; Отчество = "";
		ЭлектронныеДокументы.ФамилияИнициалыФизЛица(СведенияОбУчастнике.ПолноеНаименование, Фамилия, Имя, Отчество);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ВидУчастника + ".ТипУчастника.ФЛ.Фамилия",
									Фамилия);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ВидУчастника + ".ТипУчастника.ФЛ.Имя",
									Имя);
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ВидУчастника + ".ТипУчастника.ФЛ.Отчество",
									Отчество);
	КонецЕсли;
	
	АдресУчастника = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьАдресСтруктурой(СведенияОбУчастнике, "Ссылка", ВидАдреса);
	ТипАдреса = ?(АдресУчастника.АдресРФ, "Структурированный", "Иностранный");
	ЗаполнитьАдресВДереве(ДеревоДанных, АдресУчастника, ТипАдреса, ВидУчастника);
	
	ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
								ДеревоДанных,
								ВидУчастника + ".Контакт.Телефон",
								СведенияОбУчастнике.Телефоны);
	
	НомерСчета = "";
	Если СведенияОбУчастнике.Свойство("НомерСчета", НомерСчета) И ЗначениеЗаполнено(НомерСчета) Тогда
		Банк = "";
		БИК = "";
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
				ДеревоДанных,
				ВидУчастника + ".БанковскийСчет.НомерСчета",
				НомерСчета);
		Если СведенияОбУчастнике.Свойство("Банк", Банк) И ЗначениеЗаполнено(Банк) Тогда
			ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
										ДеревоДанных,
										ВидУчастника + ".БанковскийСчет.НаимБанк",
										Банк.Наименование);
		КонецЕсли;
		Если СведенияОбУчастнике.Свойство("БИК", БИК) И ЗначениеЗаполнено(БИК) Тогда
			ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
										ДеревоДанных,
										ВидУчастника + ".БанковскийСчет.БИК",
										БИК);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Заполняет соответствующий тип адреса переданными данными.
// Параметры:
//  СтрокаДерева - СтрокаДереваЗначений - Строка дерева, содержащая данные участника
//  АдресУчастника - Структура - содержит данные адреса участника обмена. Имена полей структуры должны совпадать с
//    именами полей структуры выбранного типа адреса:
//    Структурированный - "Индекс, КодРегион, Район, Город, НаселПункт, Улица, Дом, Корпус, Кварт";
//    Произвольный/Иностранный - "КодСтраны, АдресСтрокой" (вынесены в разные элементы списка для того,
//      чтобы правильно заполнить ЭД).
//  ТипАдреса - Строка - один из 3-х вариантов: Структурированный, Произвольный, Иностранный.
//
Процедура ЗаполнитьАдресВДереве(ДеревоДанных, АдресУчастника, ТипАдреса, ВидУчастника)
	
	Если ТипАдреса = "Произвольный" Тогда
		ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ВидУчастника + ".Адрес.Произвольный",
									АдресУчастника.АдрТекст);
	Иначе
		Если АдресУчастника.АдресРФ Тогда
			АдресУчастника.Удалить("АдресРФ");
			АдресУчастника.Удалить("КодСтраны");
			АдресУчастника.Удалить("АдресСтрокой");
		Иначе
			АдресУчастника.Удалить("АдресРФ");
			АдресУчастника.Удалить("Индекс");
			АдресУчастника.Удалить("КодРегион");
			АдресУчастника.Удалить("Район");
			АдресУчастника.Удалить("Город");
			АдресУчастника.Удалить("НаселПункт");
			АдресУчастника.Удалить("Улица");
			АдресУчастника.Удалить("Дом");
			АдресУчастника.Удалить("Корпус");
			АдресУчастника.Удалить("Кварт");
		КонецЕсли;
		Для Каждого Элемент Из АдресУчастника Цикл
			ОбщегоНазначенияЭД.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ВидУчастника + ".Адрес." + ТипАдреса + "." + Элемент.Ключ,
									Элемент.Значение);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// Формирование данных для электронных документов

// Заполняет соответствующий тип адреса переданными данными.
// Параметры:
//  СписокТиповАдресов - СписокЗначений - Представление элемента - описание типа (Структурированный,
//    Произвольный, Иностранный), значение элемента - структура описывающая поля адреса, пометка - указывает, из какого
//    элемента списка брать данные при заполнении ЭД.
//  АдресУчастника - Структура - содержит данные адреса участника обмена. Имена полей структуры должны совпадать с
//    именами полей структуры выбранного типа адреса:
//    Структурированный - "Индекс, КодРегион, Район, Город, НаселПункт, Улица, Дом, Корпус, Кварт";
//    Произвольный/Иностранный - "КодСтраны, АдресСтрокой" (вынесены в разные элементы списка для того,
//      чтобы правильно заполнить ЭД).
//  ТипАдреса - Строка - один из 3-х вариантов: Структурированный, Произвольный, Иностранный.
//
Процедура ЗаполнитьАдресВСпискеТиповАдресов(СписокТиповАдресов, АдресУчастника, ТипАдреса = "Структурированный")
	
	// Типы адресов представлены списком значений, в котором Представление элемента - описание типа (Структурированный,
	// Произвольный, Иностранный), значение элемента - структура описывающая поля адреса, пометка - указывает, из какого
	// элемента списка брать данные при заполнении ЭД.
	СписокТиповАдресов.ЗаполнитьПометки(Ложь);
	ВыбранныйТипАдреса = Неопределено;
	Для Каждого Элемент Из СписокТиповАдресов Цикл
		Если Элемент.Представление = ТипАдреса Тогда
			ВыбранныйТипАдреса = Элемент;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Если ВыбранныйТипАдреса <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ВыбранныйТипАдреса.Значение, АдресУчастника);
		ВыбранныйТипАдреса.Пометка = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьФИОиДолжность(СтруктураПриемник, ИсточникДанных, Должность = Неопределено)
	
	Если ТипЗнч(ИсточникДанных) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(СтруктураПриемник, ИсточникДанных);
	ИначеЕсли ТипЗнч(ИсточникДанных) = Тип("Строка") Тогда
		Фамилия = ""; 
		Имя = ""; 
		Отчество = "";
		ЭлектронныеДокументы.ФамилияИнициалыФизЛица(ИсточникДанных, Фамилия, Имя, Отчество);
		СтруктураПриемник.Вставить("Фамилия", Фамилия);
		СтруктураПриемник.Вставить("Имя", Имя);
		СтруктураПриемник.Вставить("Отчество", Отчество);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Должность) Тогда
		СтруктураПриемник.Вставить("Должность", Должность);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьСведенияУчастникаОбмена(СтруктураУчастника, СведенияОбУчастнике, ВидАдреса = "Юр")
	
	СтруктураУчастника.Вставить("КодОКПО",                 СведенияОбУчастнике.КодПоОКПО);
	СтруктураУчастника.Вставить("НаименованиеОрганизации", СведенияОбУчастнике.ПолноеНаименование);
	СтруктураУчастника.Вставить("ИНН",                     СведенияОбУчастнике.ИНН);
	СтруктураУчастника.Вставить("КПП",                     СведенияОбУчастнике.КПП);
	СтруктураУчастника.Вставить("КодОКОПФ",                СведенияОбУчастнике.КодОКОПФ);
	СтруктураУчастника.Вставить("ЭтоФизЛицо", СведенияОбУчастнике.ЮрФизЛицо <> Перечисления.ЮрФизЛицо.ЮрЛицо);
	Если СведенияОбУчастнике.ЮрФизЛицо <> Перечисления.ЮрФизЛицо.ЮрЛицо Тогда
		ЗаполнитьФИОиДолжность(СтруктураУчастника, СведенияОбУчастнике.ПолноеНаименование);
	КонецЕсли;
	
	// Типы адресов представлены списком значений, в котором Представление элемента - описание типа (Структурированный,
	// Произвольный, Иностранный), значение элемента - структура описывающая поля адреса, пометка - указывает, из какого
	// элемента списка брать данные при заполнении ЭД.
	АдресУчастника = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьАдресСтруктурой(СведенияОбУчастнике, "Ссылка", ВидАдреса);
	ТипАдреса = ?(АдресУчастника.АдресРФ, "Структурированный", "Иностранный");
	ЗаполнитьАдресВСпискеТиповАдресов(СтруктураУчастника.Адрес, АдресУчастника, ТипАдреса);
	
	СтруктураУчастника.Вставить("Телефон",                 СведенияОбУчастнике.Телефоны);
	СтруктураУчастника.Вставить("Факс");
	
	СтруктураБанковскийСчет = СтруктураУчастника.БанковскийСчет;
	СтруктураБанковскийСчет.Вставить("БИК",         СведенияОбУчастнике.БИК);
	СтруктураБанковскийСчет.Вставить("НаимБанк",    СведенияОбУчастнике.Банк.Наименование);
	СтруктураБанковскийСчет.Вставить("НомерСчета ", СведенияОбУчастнике.НомерСчета);
	
КонецПроцедуры

Процедура ЗаполнитьРеквизитыУчастниковТОРГ12(ДанныеПечати, СтруктураПараметров)
	
	СведенияОПоставщике       = ПолучитьДанныеЮрФизЛица(ДанныеПечати.Организация, ДанныеПечати.БанковскийСчет);
	СведенияОПокупателе       = ПолучитьДанныеЮрФизЛица(ДанныеПечати.Покупатель);
	СведенияОГрузополучателе  = ПолучитьДанныеЮрФизЛица(ДанныеПечати.Грузополучатель);
	СведенияОГрузоотправителе = ПолучитьДанныеЮрФизЛица(ДанныеПечати.Грузоотправитель);
	
	ЗаполнитьСведенияУчастникаОбмена(СтруктураПараметров.Поставщик, СведенияОПоставщике);
	Если ДанныеПечати.Организация <> ДанныеПечати.Грузоотправитель Тогда
		ЗаполнитьСведенияУчастникаОбмена(СтруктураПараметров.СведенияОГрузоотправителе.Грузоотправитель, СведенияОГрузоотправителе, "Факт");
		СтруктураПараметров.СведенияОГрузоотправителе.СтруктурноеПодразделение = ДанныеПечати.Подразделение;
	КонецЕсли;
	
	ЗаполнитьСведенияУчастникаОбмена(СтруктураПараметров.Плательщик,      СведенияОПокупателе);
	ЗаполнитьСведенияУчастникаОбмена(СтруктураПараметров.Грузополучатель, СведенияОГрузополучателе, "Факт");
	
КонецПроцедуры

Процедура ЗаполнитьРеквизитыУчастниковАкт501(ДанныеПечати, СтруктураПараметров)
	
	СведенияОИсполнителе = ПолучитьДанныеЮрФизЛица(ДанныеПечати.Исполнитель);
	СведенияОЗаказчике   = ПолучитьДанныеЮрФизЛица(ДанныеПечати.Заказчик);
	ЗаполнитьСведенияУчастникаОбмена(СтруктураПараметров.Исполнитель, СведенияОИсполнителе);
	ЗаполнитьСведенияУчастникаОбмена(СтруктураПараметров.Заказчик, СведенияОЗаказчике);
	
КонецПроцедуры

// Процедура добавляет новую строку в таблицы данных структуры параметров
//
Процедура ДобавитьСтрокуТаблицуДанных(ТаблицаДанных, ДанныеСтроки, СтруктураПараметров, ИмяЭлементаВладельцаДопДанных, НомерСтроки)
	
	НоваяСтрока = ТаблицаДанных.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеСтроки);
	
	НоваяСтрока.НомерСтроки = НомерСтроки;
	
	// Сформируем доп. параметры
	СтруктураДопДанных = Новый Структура;
	
	ИДТовара = ДанныеСтроки.Номенклатура.УникальныйИдентификатор();
	ИДХарактеристики = ?(ЗначениеЗаполнено(ДанныеСтроки.Характеристика), ДанныеСтроки.Характеристика.УникальныйИдентификатор(), "");
	ИДУпаковки = "";
	СтруктураДопДанных.Вставить("ИД", Строка(ИДТовара) + "#" + Строка(ИДХарактеристики) + "#" + Строка(ИДУпаковки));
	Если ЗначениеЗаполнено(ДанныеСтроки.Характеристика) Тогда
		Наименование = ДанныеСтроки.НаименованиеНоменклатуры + ?(ЗначениеЗаполнено(ДанныеСтроки.Характеристика), " ("+ДанныеСтроки.Характеристика+")", "");
		СтруктураДопДанных.Вставить("Наименование", Наименование);
		СтруктураДопДанных.Вставить("Характеристика", ДанныеСтроки.Характеристика);
	КонецЕсли;
	
	// Из-за особенностей в схеме ФНС некоторые ставки НДС необходимо передавать в доп. параметрах.
	Если ТаблицаДанных.Колонки.Найти("СтавкаНДС") <> Неопределено Тогда
		// В ТОРГ12 не предусмотрена передача 
		// - "дробных" ставок НДС
		// - ставки "Без НДС"
		
		СоответствиеСтавокНДС = Новый Соответствие;
		СоответствиеСтавокНДС.Вставить(Перечисления.СтавкиНДС.НДС18_118, Перечисления.СтавкиНДС.НДС18);
		СоответствиеСтавокНДС.Вставить(Перечисления.СтавкиНДС.НДС10_110, Перечисления.СтавкиНДС.НДС10);
		СоответствиеСтавокНДС.Вставить(Перечисления.СтавкиНДС.НДС20_120, Перечисления.СтавкиНДС.НДС20);
		СоответствиеСтавокНДС.Вставить(Перечисления.СтавкиНДС.БезНДС,    Перечисления.СтавкиНДС.ПустаяСсылка());
		
		Если СоответствиеСтавокНДС[НоваяСтрока.СтавкаНДС] <> Неопределено Тогда
			// Ставку НДС передадим в структуре доп. параметров
			СтруктураДопДанных.Вставить("СтавкаНДС", НоваяСтрока.СтавкаНДС);
			// В схему передадим соответствующую ставку НДС "числом"
			НоваяСтрока.СтавкаНДС = СоответствиеСтавокНДС[НоваяСтрока.СтавкаНДС];
			
		КонецЕсли;
		
	Иначе
		// В Акте не предусмотрена передача ставки НДС
		СтруктураДопДанных.Вставить("СтавкаНДС", ДанныеСтроки.СтавкаНДС);
	КонецЕсли;
	
	Если ИмяЭлементаВладельцаДопДанных = "Услуги" Тогда
		// Для Акта таблица услуг "вкладывается" в строку таблицы описания услуг, 
		// поэтому номер строки для доп. данных определим как составной
		НомерСтрокиДопДанных = "1." + НомерСтроки;
	Иначе
		НомерСтрокиДопДанных = НомерСтроки;
	КонецЕсли;
	
	ЭлектронныеДокументы.ДобавитьДанныеВДеревоДопДанных(СтруктураПараметров, СтруктураДопДанных, ИмяЭлементаВладельцаДопДанных, Истина, НомерСтрокиДопДанных);
	
КонецПроцедуры

Функция СтруктураИтоговыеСуммы(ТаблицаТоваров, ВидЭД)
	
	Структура = Новый Структура;
	
	Структура.Вставить("КоличествоМест", ТаблицаТоваров.Итог("КоличествоМест"));
	Структура.Вставить("МассаБрутто",    ТаблицаТоваров.Итог("МассаБрутто"));
	Структура.Вставить("МассаНетто",     ТаблицаТоваров.Итог("МассаНетто"));
	Структура.Вставить("СуммаБезНДС",    ТаблицаТоваров.Итог("СуммаБезНДС"));
	Структура.Вставить("СуммаНДС",       ТаблицаТоваров.Итог("СуммаНДС"));
	Структура.Вставить("СуммаСНДС",      ТаблицаТоваров.Итог("СуммаБезНДС") + ТаблицаТоваров.Итог("СуммаНДС"));
	Структура.Вставить("КоличествоПорядковыхНомеровЗаписей", ТаблицаТоваров.Количество());
	
	Если ВидЭД = Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиОтправитель Тогда
		Структура.Вставить("МассаНеттоДоКорректировки",  ТаблицаТоваров.Итог("МассаНеттоДоКорректировки"));
		Структура.Вставить("СуммаБезНДСДоКорректировки", ТаблицаТоваров.Итог("СуммаБезНДСДоКорректировки"));
		Структура.Вставить("СуммаНДСДоКорректировки",    ТаблицаТоваров.Итог("СуммаНДСДоКорректировки"));
		Структура.Вставить("СуммаСНДСДоКорректировки",   ТаблицаТоваров.Итог("СуммаБезНДСДоКорректировки") + ТаблицаТоваров.Итог("СуммаНДСДоКорректировки"));
	КонецЕсли;
	
	Возврат Структура;
	
КонецФункции

// Возвращает данные для формирования ЭД "ТОРГ12" на основании документов
//   - РеализацияТоваровИУслуг 
//   - КорректировкаРеализации (исправляющего реализацию)
// 
Функция ПолучитьДанныеРеализации(СсылкаНаОбъект)
	
	ДанныеДокумента = Новый Структура();
	
	// Подготовим данные шапки документа
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	//КорректировкаРеализации ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЭД.Исправление) КАК ВидОперации,
	|	//КорректировкаРеализации ДокументРеализации КАК ДокументОснование,
	|	//РеализацияТоваровУслуг """" КАК ВидОперации,
	|	//РеализацияТоваровУслуг Сделка КАК ДокументОснование,
	|	Номер,
	|	Дата КАК ДатаДокумента,
	|	РеализацияТоваровУслуг.Организация,
	|	РеализацияТоваровУслуг.Организация КАК ЮрФизЛицо,
	|	РеализацияТоваровУслуг.Организация КАК Поставщик,
	|	РеализацияТоваровУслуг.Организация КАК Контрагент,
	|	РеализацияТоваровУслуг.Организация КАК Руководители,
	|	РеализацияТоваровУслуг.АдресДоставки КАК АдресДоставки,
	|	ВЫБОР 
	|		КОГДА Грузополучатель = &ПустойКонтрагент
	|		ТОГДА Контрагент
	|		ИНАЧЕ Грузополучатель 
	|	КОНЕЦ КАК Грузополучатель,
	|	ВЫБОР 
	|		КОГДА Грузоотправитель = &ПустойКонтрагент
	|		ТОГДА РеализацияТоваровУслуг.Организация
	|		ИНАЧЕ Грузоотправитель 
	|	КОНЕЦ КАК Грузоотправитель,
	|	БанковскийСчетОрганизации КАК БанковскийСчет,
	|	Контрагент КАК Покупатель,
	|	Контрагент КАК Плательщик,
	|	Сделка,
	|	ДоговорКонтрагента.Представление  КАК ДоговорНаименование,
	|	ДоговорКонтрагента.Номер          КАК ДоговорНомер,
	|	ДоговорКонтрагента.Дата           КАК ДоговорДата,
	|	ДоговорКонтрагента.ВедениеВзаиморасчетов КАК ВедениеВзаиморасчетов,
	|	ОтветственныеЛица.ФизическоеЛицо КАК ОтветственноеЛицо,
	|	//РеализацияТоваровУслуг ОтпускРазрешил,
	|	//РеализацияТоваровУслуг ОтпускПроизвел,
	|	//КорректировкаРеализации ИсправляемыйДокументРеализации.ОтпускРазрешил КАК ОтпускРазрешил,
	|	//КорректировкаРеализации ИсправляемыйДокументРеализации.ОтпускПроизвел КАК ОтпускПроизвел,
	|	ПРЕДСТАВЛЕНИЕ(
	|		ВЫБОР
	|			КОГДА РеализацияТоваровУслуг.ОтражатьВБухгалтерскомУчете
	|			ТОГДА ЕСТЬNULL(ТаблицаСоответствиеОрганизации.ПодразделениеОрганизации, """")
	|		ИНАЧЕ РеализацияТоваровУслуг.Подразделение
	|	КОНЕЦ) КАК Подразделение,
	|	ВалютаДокумента,
	|	КурсВзаиморасчетов      КАК Курс,
	|	КратностьВзаиморасчетов КАК Кратность,
	|	УчитыватьНДС,
	|	СуммаВключаетНДС
	|
	|ИЗ
	|	//РеализацияТоваровУслуг Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|	//КорректировкаРеализации Документ.КорректировкаРеализации КАК РеализацияТоваровУслуг
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ОтветственныеЛица.СрезПоследних(&ДатаСреза, СтруктурнаяЕдиница = &СтруктурнаяЕдиница) КАК ОтветственныеЛица
	|	ПО
	|		ОтветственныеЛица.СтруктурнаяЕдиница = РеализацияТоваровУслуг.Склад
	| 	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		(ВЫБРАТЬ
	|			СоответсвиеПодразделений.Подразделение,
	|			СоответсвиеПодразделений.Организация,
	|			МИНИМУМ(СоответсвиеПодразделений.ПодразделениеОрганизации) КАК ПодразделениеОрганизации,
	|			МАКСИМУМ(СоответсвиеПодразделений.ПодразделениеОрганизации) КАК ПодразделениеОрг
	|		ИЗ
	|			РегистрСведений.СоответствиеПодразделенийИПодразделенийОрганизаций КАК СоответсвиеПодразделений
	|		ГДЕ
	|			Подразделение = &Подразделение
	|			И Организация = &Организация
	|			И Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			И Подразделение <> ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|		СГРУППИРОВАТЬ ПО
	|			Подразделение,
	|			Организация
	|		ИМЕЮЩИЕ
	|			КОЛИЧЕСТВО(*) = 1
	|		) ТаблицаСоответствиеОрганизации
	|	ПО
	|		ТаблицаСоответствиеОрганизации.Подразделение = РеализацияТоваровУслуг.Подразделение
	|		И ТаблицаСоответствиеОрганизации.Организация = РеализацияТоваровУслуг.Организация
	|
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка = &ТекущийДокумент
	|";
	
	Если ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "//РеализацияТоваровУслуг", "");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "//КорректировкаРеализации", "");
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ДатаСреза",          СсылкаНаОбъект.Дата);
	Запрос.УстановитьПараметр("СтруктурнаяЕдиница", СсылкаНаОбъект.Склад);
	Запрос.УстановитьПараметр("ТекущийДокумент",    СсылкаНаОбъект);
	Запрос.УстановитьПараметр("ПустойКонтрагент",   Справочники.Контрагенты.ПустаяСсылка());
	Запрос.УстановитьПараметр("Организация",        СсылкаНаОбъект.Организация);
	Запрос.УстановитьПараметр("Подразделение",      СсылкаНаОбъект.Подразделение);
	
	Шапка = Запрос.Выполнить().Выбрать();
	Шапка.Следующий();
	
	ДанныеДокумента.Вставить("РеквизитыШапки", Шапка);
	
	// Подготовим данные табличных частей
	
	СтрокаВыборкиПоляСодержания    = ОбработкаТабличныхЧастей.ПолучитьЧастьЗапросаДляВыбораСодержания("РеализацияТоваровУслуг");
	ВалютаРегламентированногоУчета = глЗначениеПеременной("ВалютаРегламентированногоУчета");
	
	ДопКолонка = Константы.ДополнительнаяКолонкаПечатныхФормДокументов.Получить();
	Если ДопКолонка = Перечисления.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул Тогда
		ТоварКод = "Артикул";
	Иначе
		ТоварКод = "Код";
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РеализацияТоваровУслуг.НомерСтроки                                             КАК НомерСтроки,
	|	РеализацияТоваровУслуг.Номенклатура.Код                                        КАК КодТовара,
	|	РеализацияТоваровУслуг.Номенклатура."+ТоварКод+"                               КАК Артикул,
	|	РеализацияТоваровУслуг.Номенклатура.НаименованиеПолное                         КАК Наименование,
	|	РеализацияТоваровУслуг.Номенклатура.НаименованиеПолное                         КАК НаименованиеНоменклатуры,
	|	РеализацияТоваровУслуг.Номенклатура                                            КАК Номенклатура,
	|	РеализацияТоваровУслуг.Количество                                              КАК Количество,
	|	РеализацияТоваровУслуг.Сумма * &Курс / &Кратность                              КАК Сумма,
	|	РеализацияТоваровУслуг.Цена *&Курс / &Кратность                                КАК Цена,
	|	ПРЕДСТАВЛЕНИЕ(РеализацияТоваровУслуг.ХарактеристикаНоменклатуры)               КАК НаименованиеХарактеристики,
	|	РеализацияТоваровУслуг.ХарактеристикаНоменклатуры                              КАК Характеристика,
	|	РеализацияТоваровУслуг.Номенклатура.БазоваяЕдиницаИзмерения                    КАК БазоваяЕдиница,
	|	РеализацияТоваровУслуг.Номенклатура.БазоваяЕдиницаИзмерения.Код                КАК БазоваяЕдиницаКод,
	|	РеализацияТоваровУслуг.Номенклатура.БазоваяЕдиницаИзмерения.Наименование       КАК БазоваяЕдиницаНаименование,
	|	РеализацияТоваровУслуг.Номенклатура.БазоваяЕдиницаИзмерения.НаименованиеПолное КАК БазоваяЕдиницаНаименованиеПолное,
	|	РеализацияТоваровУслуг.Номенклатура.БазоваяЕдиницаИзмерения.МеждународноеСокращение КАК БазоваяЕдиницаМеждународноеСокращение,
	|	РеализацияТоваровУслуг.ЕдиницаИзмерения.ЕдиницаПоКлассификатору                КАК ЕдиницаИзмерения,
	|	РеализацияТоваровУслуг.ЕдиницаИзмерения.ЕдиницаПоКлассификатору                КАК Упаковка,
	|	РеализацияТоваровУслуг.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код            КАК УпаковкаКод,
	|	РеализацияТоваровУслуг.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Наименование   КАК УпаковкаНаименование,
	|	РеализацияТоваровУслуг.Коэффициент                                             КАК Коэффициент,
	|	РеализацияТоваровУслуг.СтавкаНДС                                               КАК СтавкаНДС,
	|	РеализацияТоваровУслуг.СуммаНДС * &Курс / &Кратность                           КАК СуммаНДС,
	|	0                                                                              КАК СуммаСкидки,
	|	РеализацияТоваровУслуг.ЕдиницаИзмеренияМест.Представление                      КАК ВидУпаковки,
	|	РеализацияТоваровУслуг.КоличествоМест                                          КАК КоличествоМест,
	|	РеализацияТоваровУслуг.ЕдиницаИзмеренияМест.Коэффициент 
	|		/ РеализацияТоваровУслуг.Коэффициент                                       КАК КоличествоВОдномМесте,
	|	РеализацияТоваровУслуг.Количество                                              КАК МассаНетто,
	|	ВЫБОР
	|		КОГДА РеализацияТоваровУслуг.КоличествоМест > 0 
	|		ТОГДА РеализацияТоваровУслуг.КоличествоМест * РеализацияТоваровУслуг.ЕдиницаИзмеренияМест.Вес
	|		ИНАЧЕ РеализацияТоваровУслуг.Количество * РеализацияТоваровУслуг.ЕдиницаИзмерения.Вес
	|	КОНЕЦ                                                                          КАК МассаБрутто
	|ИЗ
	|	//РеализацияТоваровУслуг Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслуг
	|	//КорректировкаРеализации Документ.КорректировкаРеализации.Товары КАК РеализацияТоваровУслуг
	|
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка = &Ссылка
	|	
	|	//КорректировкаРеализации И (РеализацияТоваровУслуг.Сумма > 0 ИЛИ РеализацияТоваровУслуг.Количество > 0)
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ
	|	РеализацияТоваровУслуг.НомерСтроки,
	|	РеализацияТоваровУслуг.Номенклатура.Код,
	|	РеализацияТоваровУслуг.Номенклатура."+ТоварКод+",
	|	"+СтрокаВыборкиПоляСодержания+",
	|	"+СтрокаВыборкиПоляСодержания+",
	|	РеализацияТоваровУслуг.Номенклатура,
	|	РеализацияТоваровУслуг.Количество,
	|	РеализацияТоваровУслуг.Сумма * &Курс / &Кратность,
	|	РеализацияТоваровУслуг.Цена * &Курс / &Кратность,
	|	"""",
	|	Неопределено,
	|	РеализацияТоваровУслуг.Номенклатура.БазоваяЕдиницаИзмерения,
	|	РеализацияТоваровУслуг.Номенклатура.БазоваяЕдиницаИзмерения.Код,
	|	РеализацияТоваровУслуг.Номенклатура.БазоваяЕдиницаИзмерения.Наименование,
	|	РеализацияТоваровУслуг.Номенклатура.БазоваяЕдиницаИзмерения.НаименованиеПолное,
	|	РеализацияТоваровУслуг.Номенклатура.БазоваяЕдиницаИзмерения.МеждународноеСокращение,
	|	РеализацияТоваровУслуг.Номенклатура.БазоваяЕдиницаИзмерения,
	|	РеализацияТоваровУслуг.Номенклатура.БазоваяЕдиницаИзмерения,
	|	РеализацияТоваровУслуг.Номенклатура.БазоваяЕдиницаИзмерения.Код,
	|	РеализацияТоваровУслуг.Номенклатура.БазоваяЕдиницаИзмерения.Наименование,
	|	1,
	|	РеализацияТоваровУслуг.СтавкаНДС,
	|	РеализацияТоваровУслуг.СуммаНДС * &Курс / &Кратность,
	|	0,
	|	Неопределено,
	|	0,
	|	0,
	|	РеализацияТоваровУслуг.Количество,
	|	0
	|	
	|ИЗ
	|	//РеализацияТоваровУслуг Документ.РеализацияТоваровУслуг.Услуги КАК РеализацияТоваровУслуг
	|	//КорректировкаРеализации Документ.КорректировкаРеализации.Услуги КАК РеализацияТоваровУслуг
	|	
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка = &Ссылка
	|	
	|	//КорректировкаРеализации И (РеализацияТоваровУслуг.Сумма > 0 ИЛИ РеализацияТоваровУслуг.Количество > 0)";
	
	Если ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "//РеализацияТоваровУслуг", "");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "//КорректировкаРеализации", "");
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Курс",       ЗаполнениеДокументов.КурсДокумента(СсылкаНаОбъект, ВалютаРегламентированногоУчета));
	Запрос.УстановитьПараметр("Кратность",  ЗаполнениеДокументов.КратностьДокумента(СсылкаНаОбъект, ВалютаРегламентированногоУчета));
	Запрос.УстановитьПараметр("Ссылка",     СсылкаНаОбъект);
	
	ТаблицаТоваров = Запрос.Выполнить().Выгрузить();
	
	ТаблицаТоваров.Колонки.Добавить("СуммаБезНДС", Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(18, 2)));
	ТаблицаТоваров.Колонки.Добавить("СуммаСНДС",   Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(18, 2)));
	
	// Вычислим курс документа для печати
	Если СсылкаНаОбъект.ВалютаДокумента <> ВалютаРегламентированногоУчета
		 И (СсылкаНаОбъект.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах
		 ИЛИ СсылкаНаОбъект.Дата >= '20090101000000') Тогда
		
		ЗапросКурсАванса = Новый Запрос;
		ЗапросКурсАванса.УстановитьПараметр("ДокументСсылка", СсылкаНаОбъект);
		ЗапросКурсАванса.Текст =
		"ВЫБРАТЬ
		|	Док.СуммаВзаиморасчетов,
		|	Док.СуммаРегл
		|ИЗ 
		|	Документ.РеализацияТоваровУслуг.ДокументыРасчетовСКонтрагентом КАК Док
		|ГДЕ 
		|	Док.Ссылка = &ДокументСсылка
		|	
		|ИТОГИ 
		|	СУММА(СуммаВзаиморасчетов), 
		|	СУММА(СуммаРегл) 
		|ПО 
		|	ОБЩИЕ";
		
		Выборка = ЗапросКурсАванса.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Если Выборка.Следующий() Тогда
			ВыборкаСуммаВзаиморасчетов = Выборка.СуммаВзаиморасчетов;
			ВыборкаСуммаРегл           = Выборка.СуммаРегл;
		Иначе
			ВыборкаСуммаВзаиморасчетов = 0;
			ВыборкаСуммаРегл           = 0;
		КонецЕсли;
		
		РасчетСуммыНДСПоСтавке =  УчетНДС.РасчетНДСвРубляхПоСтавкеДокумента(Шапка.ДатаДокумента);
		
		СуммаВзаиморасчетов = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(УчетНДС.ПолучитьСуммуДокументаСНДС(СсылкаНаОбъект),
			СсылкаНаОбъект.ВалютаДокумента, СсылкаНаОбъект.ДоговорКонтрагента.ВалютаВзаиморасчетов,
			ЗаполнениеДокументов.КурсДокумента(СсылкаНаОбъект, ВалютаРегламентированногоУчета), СсылкаНаОбъект.КурсВзаиморасчетов,
			ЗаполнениеДокументов.КратностьДокумента(СсылкаНаОбъект, ВалютаРегламентированногоУчета), СсылкаНаОбъект.КратностьВзаиморасчетов);
			
		НеоплаченнаяСумма = СуммаВзаиморасчетов - ВыборкаСуммаВзаиморасчетов;
		
		НеоплаченнаяСуммаРегл = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(НеоплаченнаяСумма,
			СсылкаНаОбъект.ДоговорКонтрагента.ВалютаВзаиморасчетов, ВалютаРегламентированногоУчета,
			СсылкаНаОбъект.КурсВзаиморасчетов, 1,
			СсылкаНаОбъект.КратностьВзаиморасчетов, 1);
			
		СуммаРегл = ВыборкаСуммаРегл + НеоплаченнаяСуммаРегл;
		
		Если НЕ СуммаРегл = 0 Тогда
		
			МассивРаспределения = Новый Массив;
			
			Для Каждого СтрокаТовар Из ТаблицаТоваров Цикл
				СуммаСНДС = СтрокаТовар.Сумма + ?(Шапка.СуммаВключаетНДС, 0, СтрокаТовар.СуммаНДС);
				СуммаБезНДС = СуммаСНДС - СтрокаТовар.СуммаНДС;
				МассивРаспределения.Добавить(СуммаСНДС);
				СтрокаТовар.СуммаБезНДС = СуммаБезНДС;
			КонецЦикла;
			
			ТаблицаТоваров.Колонки.Добавить("СуммаРублевая");
			УчетНДС.РаспределитьСуммуПоСтолбцу(МассивРаспределения, СуммаРегл, ТаблицаТоваров, "СуммаРублевая");
			
			Для Каждого СтрокаТовар Из ТаблицаТоваров Цикл
				
				Если РасчетСуммыНДСПоСтавке Тогда
					//Выделение суммы НДС, Расчет суммы без НДС
					ЗначениеСтавкиНДС = УчетНДС.ПолучитьСтавкуНДС(СтрокаТовар.СтавкаНДС);
					СтрокаТовар.СуммаНДС = ?(ЗначениеСтавкиНДС = 0, 0, Окр(СтрокаТовар.СуммаРублевая * ЗначениеСтавкиНДС / (100+ЗначениеСтавкиНДС), 2));
					СтрокаТовар.Сумма = СтрокаТовар.СуммаРублевая - СтрокаТовар.СуммаНДС;
					
				Иначе
					
					МассивРаспределения.Очистить();
					МассивРаспределения.Добавить(СтрокаТовар.СуммаБезНДС);
					МассивРаспределения.Добавить(СтрокаТовар.СуммаНДС);
					МассивРаспределенныхСумм = ОбщегоНазначения.РаспределитьПропорционально(СтрокаТовар.СуммаРублевая, МассивРаспределения);
					Если МассивРаспределенныхСумм <> Неопределено Тогда
						СтрокаТовар.Сумма = МассивРаспределенныхСумм[0];
						СтрокаТовар.СуммаНДС = МассивРаспределенныхСумм[1];
					КонецЕсли;
					
				КонецЕсли;
				
				Если Шапка.СуммаВключаетНДС Тогда
					СтрокаТовар.Сумма = СтрокаТовар.Сумма + СтрокаТовар.СуммаНДС;
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Для каждого СтрокаТаблицы из ТаблицаТоваров Цикл
		
		СуммаСНДС   = Окр((СтрокаТаблицы.Сумма + ?(Шапка.СуммаВключаетНДС, 0, СтрокаТаблицы.СуммаНДС)), 2);
		СуммаНДС    = Окр(СтрокаТаблицы.СуммаНДС, 2);
		СуммаБезНДС = СуммаСНДС - СуммаНДС;
		
		СтрокаТаблицы.СуммаНДС    = СуммаНДС;
		СтрокаТаблицы.СуммаБезНДС = СуммаБезНДС;
		СтрокаТаблицы.СуммаСНДС   = СуммаСНДС;
		
		Если Шапка.СуммаВключаетНДС Тогда
			СтрокаТаблицы.Цена = ?(СтрокаТаблицы.Количество = 0, 0, Окр(СуммаБезНДС / СтрокаТаблицы.Количество, 2));
		Иначе
			СтрокаТаблицы.Цена = Окр(СтрокаТаблицы.Цена, 2);
		КонецЕсли;
		
	КонецЦикла;
	
	ДанныеДокумента.Вставить("ТаблицаТоваров", ТаблицаТоваров);
	
	Возврат ДанныеДокумента;
	
КонецФункции

// Возвращает данные для формирования ЭД "АктВыполненныхРабот" на основании документов
//   - АктОбОказанииПроизводственныхУслуг
//   - РеализацияТоваровИУслуг (акт выполненных работ), 
//   - КорректировкаРеализации (исправляющего реализацию)
// 
Функция ПодготовитьДанныеАктаОбОказанииУслуг(СсылкаНаОбъект)
	
	ДанныеДокумента = Новый Структура();
	ВалютаРегламентированногоУчета = глЗначениеПеременной("ВалютаРегламентированногоУчета");
	
	СтрокаВыборкиПоляСодержания = ОбработкаТабличныхЧастей.ПолучитьЧастьЗапросаДляВыбораСодержания("Акт");
	
	ТекстЗапросаШапка =
	"ВЫБРАТЬ
	|	//АктОбОказанииПроизводственныхУслуг """"   КАК ВидОперации,
	|	//АктОбОказанииПроизводственныхУслуг Сделка КАК ДокументОснование,
	|	//РеализацияТоваровУслуг """"   КАК ВидОперации,
	|	//РеализацияТоваровУслуг Сделка КАК ДокументОснование,
	|	//КорректировкаРеализации ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЭД.Исправление) КАК ВидОперации,
	|	//КорректировкаРеализации ДокументРеализации КАК ДокументОснование,
	|	Акт.Номер,
	|	Акт.Дата,
	|	Акт.ДоговорКонтрагента,
	|	Акт.Контрагент           КАК Заказчик,
	|	Акт.Организация          КАК Исполнитель,
	|	Акт.Организация,
	|	Акт.СуммаДокумента,
	|	Акт.ВалютаДокумента,
	|	Акт.ВалютаДокумента.Код  КАК КодВалютыДокумента,
	|	Акт.УчитыватьНДС,
	|	Акт.СуммаВключаетНДС,
	|	Акт.КратностьВзаиморасчетов,
	|	Акт.КурсВзаиморасчетов
	|ИЗ
	|	//АктОбОказанииПроизводственныхУслуг Документ.АктОбОказанииПроизводственныхУслуг КАК Акт
	|	//РеализацияТоваровУслуг Документ.РеализацияТоваровУслуг КАК Акт
	|	//КорректировкаРеализации Документ.КорректировкаРеализации КАК Акт
	|	
	|ГДЕ
	|	Акт.Ссылка = &ТекущийДокумент";
	
	ТекстЗапросаТЧ =
	"ВЫБРАТЬ
	|	МИНИМУМ(Акт.НомерСтроки)                     КАК НомерСтроки,
	|	Акт.Номенклатура                             КАК Номенклатура,
	|	НЕОПРЕДЕЛЕНО                                 КАК Характеристика,
	|	ВЫБОР 
	|		КОГДА НЕ (Акт.Номенклатура.НаименованиеПолное ПОДОБНО """")
	|			ТОГДА ВЫРАЗИТЬ (Акт.Номенклатура.НаименованиеПолное КАК Строка(1000))
	|		ИНАЧЕ Акт.Номенклатура.Наименование
	|	КОНЕЦ                                         КАК НаименованиеНоменклатуры,
	|	" + СтрокаВыборкиПоляСодержания + "           КАК Описание,
	|	Акт.Номенклатура.БазоваяЕдиницаИзмерения      КАК БазоваяЕдиница,
	|	Акт.Номенклатура.БазоваяЕдиницаИзмерения.Код  КАК БазоваяЕдиницаКод,
	|	
	|	//АктОбОказанииПроизводственныхУслуг Акт.ЕдиницаИзмерения.ЕдиницаПоКлассификатору КАК ЕдиницаИзмерения,
	|	//РеализацияТоваровУслуг Акт.Номенклатура.ЕдиницаХраненияОстатков.ЕдиницаПоКлассификатору КАК ЕдиницаИзмерения,
	|	//КорректировкаРеализации Акт.Номенклатура.ЕдиницаХраненияОстатков.ЕдиницаПоКлассификатору КАК ЕдиницаИзмерения,
	|	
	|	СУММА(Акт.Количество)                        КАК Количество,
	|	СУММА(Акт.Цена * &Курс / &Кратность)         КАК Цена,
	|	СУММА(Акт.Сумма * &Курс / &Кратность)        КАК Сумма,
	|	Акт.СтавкаНДС                                КАК СтавкаНДС,
	|	СУММА(Акт.СуммаНДС * &Курс / &Кратность)     КАК СуммаНДС
	|ИЗ
	|	
	|	//АктОбОказанииПроизводственныхУслуг Документ.АктОбОказанииПроизводственныхУслуг.Услуги КАК Акт
	|	//РеализацияТоваровУслуг Документ.РеализацияТоваровУслуг.Услуги КАК Акт
	|	//КорректировкаРеализации Документ.КорректировкаРеализации.Услуги КАК Акт
	|
	|ГДЕ
	|	Акт.Ссылка = &ТекущийДокумент
	|	
	|	//КорректировкаРеализации И (Акт.Сумма > 0 ИЛИ Акт.Количество > 0)
	|
	|СГРУППИРОВАТЬ ПО
	|	Акт.Номенклатура,
	|	" + СтрокаВыборкиПоляСодержания + ",
	|	
	|	//АктОбОказанииПроизводственныхУслуг Акт.ЕдиницаИзмерения.ЕдиницаПоКлассификатору,
	|	//РеализацияТоваровУслуг Акт.Номенклатура.ЕдиницаХраненияОстатков.ЕдиницаПоКлассификатору,
	|	//КорректировкаРеализации Акт.Номенклатура.ЕдиницаХраненияОстатков.ЕдиницаПоКлассификатору,
	|	
	|	Акт.СтавкаНДС
	|	
	|УПОРЯДОЧИТЬ ПО 
	|	НомерСтроки";
	
	Если ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.АктОбОказанииПроизводственныхУслуг") Тогда
		ТекстЗапросаШапка = СтрЗаменить(ТекстЗапросаШапка, "//АктОбОказанииПроизводственныхУслуг", "");
		ТекстЗапросаТЧ    = СтрЗаменить(ТекстЗапросаТЧ,    "//АктОбОказанииПроизводственныхУслуг", "");
	ИначеЕсли ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		ТекстЗапросаШапка = СтрЗаменить(ТекстЗапросаШапка, "//РеализацияТоваровУслуг", "");
		ТекстЗапросаТЧ    = СтрЗаменить(ТекстЗапросаТЧ,    "//РеализацияТоваровУслуг", "");
	Иначе
		ТекстЗапросаШапка = СтрЗаменить(ТекстЗапросаШапка, "//КорректировкаРеализации", "");
		ТекстЗапросаТЧ    = СтрЗаменить(ТекстЗапросаТЧ,    "//КорректировкаРеализации", "");
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТекущийДокумент", СсылкаНаОбъект);
	Запрос.Текст = ТекстЗапросаШапка;
	
	// Получим данные шапки документа
	Шапка = Запрос.Выполнить().Выбрать();
	Шапка.Следующий();
	ДанныеДокумента.Вставить("РеквизитыШапки", Шапка);
	
	// Вычислим курс документа для печати
	Если Шапка.ВалютаДокумента <> ВалютаРегламентированногоУчета Тогда
	 
		ЗапросКурсАванса = Новый Запрос;
		ЗапросКурсАванса.УстановитьПараметр("ТекущийДокумент", СсылкаНаОбъект);
		ЗапросКурсАванса.Текст =
		"ВЫБРАТЬ
		|	Док.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
		|	Док.СуммаРегл КАК СуммаРегл
		|ИЗ
		|	Документ.РеализацияТоваровУслуг.ДокументыРасчетовСКонтрагентом КАК Док
		|ГДЕ
		|	Док.Ссылка = &ТекущийДокумент
		|ИТОГИ
		|	СУММА(СуммаВзаиморасчетов),
		|	СУММА(СуммаРегл)
		|ПО
		|	ОБЩИЕ";
		
		Выборка = ЗапросКурсАванса.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Если Выборка.Следующий() Тогда
			КурсОплаты                 = ?(Выборка.СуммаВзаиморасчетов = 0, 0, Выборка.СуммаРегл / Выборка.СуммаВзаиморасчетов);
			ВыборкаСуммаВзаиморасчетов = Выборка.СуммаВзаиморасчетов;
			ВыборкаСуммаРегл           = Выборка.СуммаРегл;
		Иначе
			КурсОплаты                 = 0;
			ВыборкаСуммаВзаиморасчетов = 0;
			ВыборкаСуммаРегл           = 0;
		КонецЕсли;
		
		СуммаВзаиморасчетов = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(УчетНДС.ПолучитьСуммуДокументаСНДС(СсылкаНаОбъект, "Услуги"),
			Шапка.ВалютаДокумента, Шапка.ДоговорКонтрагента.ВалютаВзаиморасчетов,
			ЗаполнениеДокументов.КурсДокумента(СсылкаНаОбъект, ВалютаРегламентированногоУчета), Шапка.КурсВзаиморасчетов,
			ЗаполнениеДокументов.КратностьДокумента(СсылкаНаОбъект, ВалютаРегламентированногоУчета), Шапка.КратностьВзаиморасчетов);
			
		НеоплаченнаяСумма = СуммаВзаиморасчетов - ВыборкаСуммаВзаиморасчетов;
		Если НеоплаченнаяСумма > 0 Тогда
			КурсДляПечати = (НеоплаченнаяСумма * Шапка.КурсВзаиморасчетов + ВыборкаСуммаРегл) / (НеоплаченнаяСумма + ВыборкаСуммаВзаиморасчетов);
		Иначе
			КурсДляПечати = КурсОплаты;
		КонецЕсли;
		
		Если КурсДляПечати = 0 Тогда
			Запрос.УстановитьПараметр("Курс",      СсылкаНаОбъект.КурсВзаиморасчетов);
			Запрос.УстановитьПараметр("Кратность", ?(Шапка.КратностьВзаиморасчетов = 0, 1, Шапка.КратностьВзаиморасчетов));
		Иначе
			Запрос.УстановитьПараметр("Курс",      КурсДляПечати);
			Запрос.УстановитьПараметр("Кратность", 1);
		КонецЕсли;
		
	Иначе
		
		Запрос.УстановитьПараметр("Курс",      1);
		Запрос.УстановитьПараметр("Кратность", 1);
		
	КонецЕсли;
	
	// Получим данные табличной части
	Запрос.Текст = ТекстЗапросаТЧ;
	ТаблицаУслуг = Запрос.Выполнить().Выгрузить();
	ТаблицаУслуг.Колонки.Добавить("СуммаБезНДС", Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(18, 2)));
	ТаблицаУслуг.Колонки.Добавить("СуммаСНДС",   Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(18, 2)));
	ТаблицаУслуг.Колонки.Добавить("ИД");
	
	Для каждого СтрокаТаблицы из ТаблицаУслуг Цикл
		
		СуммаСНДС   = Окр((СтрокаТаблицы.Сумма + ?(Шапка.СуммаВключаетНДС, 0, СтрокаТаблицы.СуммаНДС)), 2);
		СуммаНДС    = Окр(СтрокаТаблицы.СуммаНДС, 2);
		СуммаБезНДС = СуммаСНДС - СуммаНДС;
		
		СтрокаТаблицы.СуммаНДС    = СуммаНДС;
		СтрокаТаблицы.СуммаБезНДС = СуммаБезНДС;
		СтрокаТаблицы.СуммаСНДС   = СуммаСНДС;
		
		Если Шапка.СуммаВключаетНДС Тогда
			СтрокаТаблицы.Цена = ?(СтрокаТаблицы.Количество = 0, 0, Окр(СуммаБезНДС / СтрокаТаблицы.Количество, 2));
		Иначе
			СтрокаТаблицы.Цена = Окр(СтрокаТаблицы.Цена, 2);
		КонецЕсли;
		
		// Заполним ИД номенклатуры
		СтрокаТаблицы.ИД = Строка(СтрокаТаблицы.Номенклатура.УникальныйИдентификатор()) + "##";
		
	КонецЦикла;
	
	ДанныеДокумента.Вставить("ТаблицаУслуг", ТаблицаУслуг);
	
	Возврат ДанныеДокумента;
	
КонецФункции

// Возвращает данные для формирования ЭД "СогласованноеИзменениеСтоимости" на основании документов
//   - КорректировкаРеализации (согласованное изменение)
//   - КорректировкаРеализации (исправление согласованного изменения)
// 
Функция ПолучитьДанныеСогласованногоИзменения(СсылкаНаОбъект)
	
	ДанныеДокумента = Новый Структура();
	
	// Подготовим данные шапки документа
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА КорректировкаРеализации.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИсправленияПоступленияРеализации.ИсправлениеОшибки)
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЭД.Исправление) 
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ВидОперации,
	|	КорректировкаРеализации.ДокументРеализации КАК ДокументОснование,
	|	КорректировкаРеализации.Номер       	КАК Номер,
	|	КорректировкаРеализации.Дата        	КАК ДатаДокумента,
	|	КорректировкаРеализации.Организация 	КАК Организация,
	|	КорректировкаРеализации.Контрагент 		КАК Контрагент,
	|	КорректировкаРеализации.АдресДоставки	КАК АдресДоставки,
	|	ВЫБОР 
	|		КОГДА Грузополучатель = &ПустойКонтрагент
	|		ТОГДА Контрагент
	|		ИНАЧЕ Грузополучатель 
	|	КОНЕЦ КАК Грузополучатель,
	|	ВЫБОР 
	|		КОГДА Грузоотправитель = &ПустойКонтрагент
	|		ТОГДА КорректировкаРеализации.Организация
	|		ИНАЧЕ Грузоотправитель 
	|	КОНЕЦ КАК Грузоотправитель,
	|	БанковскийСчетОрганизации КАК БанковскийСчет,
	|	Контрагент КАК Покупатель,
	|	Контрагент КАК Плательщик,
	|	Сделка,
	|	ДоговорКонтрагента.Представление  КАК ДоговорНаименование,
	|	ДоговорКонтрагента.Номер          КАК ДоговорНомер,
	|	ДоговорКонтрагента.Дата           КАК ДоговорДата,
	|	ДоговорКонтрагента.ВедениеВзаиморасчетов КАК ВедениеВзаиморасчетов,
	|	ОтветственныеЛица.ФизическоеЛицо КАК ОтветственноеЛицо,
	|	ПРЕДСТАВЛЕНИЕ(
	|		ВЫБОР
	|			КОГДА КорректировкаРеализации.ОтражатьВБухгалтерскомУчете
	|			ТОГДА ЕСТЬNULL(ТаблицаСоответствиеОрганизации.ПодразделениеОрганизации, """")
	|		ИНАЧЕ КорректировкаРеализации.Подразделение
	|	КОНЕЦ) КАК Подразделение,
	|	ВалютаДокумента,
	|	КурсВзаиморасчетов      КАК Курс,
	|	КратностьВзаиморасчетов КАК Кратность,
	|	УчитыватьНДС,
	|	СуммаВключаетНДС
	|
	|ИЗ
	|	Документ.КорректировкаРеализации КАК КорректировкаРеализации
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ОтветственныеЛица.СрезПоследних(&ДатаСреза, СтруктурнаяЕдиница = &СтруктурнаяЕдиница) КАК ОтветственныеЛица
	|	ПО
	|		ОтветственныеЛица.СтруктурнаяЕдиница = КорректировкаРеализации.Склад
	| 	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		(ВЫБРАТЬ
	|			СоответсвиеПодразделений.Подразделение,
	|			СоответсвиеПодразделений.Организация,
	|			МИНИМУМ(СоответсвиеПодразделений.ПодразделениеОрганизации) КАК ПодразделениеОрганизации,
	|			МАКСИМУМ(СоответсвиеПодразделений.ПодразделениеОрганизации) КАК ПодразделениеОрг
	|		ИЗ
	|			РегистрСведений.СоответствиеПодразделенийИПодразделенийОрганизаций КАК СоответсвиеПодразделений
	|		ГДЕ
	|			Подразделение = &Подразделение
	|			И Организация = &Организация
	|			И Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			И Подразделение <> ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|		СГРУППИРОВАТЬ ПО
	|			Подразделение,
	|			Организация
	|		ИМЕЮЩИЕ
	|			КОЛИЧЕСТВО(*) = 1
	|		) ТаблицаСоответствиеОрганизации
	|	ПО
	|		ТаблицаСоответствиеОрганизации.Подразделение = КорректировкаРеализации.Подразделение
	|		И ТаблицаСоответствиеОрганизации.Организация = КорректировкаРеализации.Организация
	|
	|ГДЕ
	|	КорректировкаРеализации.Ссылка = &ТекущийДокумент
	|";
	
	Запрос.УстановитьПараметр("ДатаСреза",          СсылкаНаОбъект.Дата);
	Запрос.УстановитьПараметр("СтруктурнаяЕдиница", СсылкаНаОбъект.Склад);
	Запрос.УстановитьПараметр("ТекущийДокумент",    СсылкаНаОбъект);
	Запрос.УстановитьПараметр("ПустойКонтрагент",   Справочники.Контрагенты.ПустаяСсылка());
	Запрос.УстановитьПараметр("Организация",        СсылкаНаОбъект.Организация);
	Запрос.УстановитьПараметр("Подразделение",      СсылкаНаОбъект.Подразделение);
	
	Шапка = Запрос.Выполнить().Выбрать();
	Шапка.Следующий();
	
	ДанныеДокумента.Вставить("РеквизитыШапки", Шапка);
	
	// Подготовим данные табличных частей
	СтрокаВыборкиПоляСодержания    = ОбработкаТабличныхЧастей.ПолучитьЧастьЗапросаДляВыбораСодержания("КорректировкаРеализации");
	ВалютаРегламентированногоУчета = глЗначениеПеременной("ВалютаРегламентированногоУчета");
	
	ДопКолонка = Константы.ДополнительнаяКолонкаПечатныхФормДокументов.Получить();
	Если ДопКолонка = Перечисления.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул Тогда
		ТоварКод = "Артикул";
	Иначе
		ТоварКод = "Код";
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КорректировкаРеализации.НомерСтроки                                             КАК НомерСтроки,
	|	КорректировкаРеализации.Номенклатура.Код                                        КАК КодТовара,
	|	КорректировкаРеализации.Номенклатура."+ТоварКод+"                               КАК Артикул,
	|	КорректировкаРеализации.Номенклатура.НаименованиеПолное                         КАК Наименование,
	|	КорректировкаРеализации.Номенклатура.НаименованиеПолное                         КАК НаименованиеНоменклатуры,
	|	КорректировкаРеализации.Номенклатура                                            КАК Номенклатура,
	|	КорректировкаРеализации.Сумма * &Курс / &Кратность                              КАК Сумма,
	|	КорректировкаРеализации.СуммаДоИзменения * &Курс / &Кратность                   КАК СуммаДоКорректировки,
	|	КорректировкаРеализации.Цена *&Курс / &Кратность                                КАК Цена,
	|	КорректировкаРеализации.ЦенаДоИзменения *&Курс / &Кратность                     КАК ЦенаДоКорректировки,
	|	ПРЕДСТАВЛЕНИЕ(КорректировкаРеализации.ХарактеристикаНоменклатуры)               КАК НаименованиеХарактеристики,
	|	КорректировкаРеализации.ХарактеристикаНоменклатуры                              КАК Характеристика,
	|	КорректировкаРеализации.Номенклатура.БазоваяЕдиницаИзмерения                    КАК БазоваяЕдиница,
	|	КорректировкаРеализации.Номенклатура.БазоваяЕдиницаИзмерения.Код                КАК БазоваяЕдиницаКод,
	|	КорректировкаРеализации.Номенклатура.БазоваяЕдиницаИзмерения.Наименование       КАК БазоваяЕдиницаНаименование,
	|	КорректировкаРеализации.Номенклатура.БазоваяЕдиницаИзмерения.НаименованиеПолное КАК БазоваяЕдиницаНаименованиеПолное,
	|	КорректировкаРеализации.Номенклатура.БазоваяЕдиницаИзмерения.МеждународноеСокращение КАК БазоваяЕдиницаМеждународноеСокращение,
	|	КорректировкаРеализации.ЕдиницаИзмерения.ЕдиницаПоКлассификатору                КАК ЕдиницаИзмерения,
	|	КорректировкаРеализации.ЕдиницаИзмерения.ЕдиницаПоКлассификатору                КАК Упаковка,
	|	КорректировкаРеализации.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код            КАК УпаковкаКод,
	|	КорректировкаРеализации.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Наименование   КАК УпаковкаНаименование,
	|	КорректировкаРеализации.Коэффициент                                             КАК Коэффициент,
	|	КорректировкаРеализации.СтавкаНДС                                               КАК СтавкаНДС,
	|	КорректировкаРеализации.СуммаНДС * &Курс / &Кратность                           КАК СуммаНДС,
	|	КорректировкаРеализации.СуммаНДСДоИзменения * &Курс / &Кратность                КАК СуммаНДСДоКорректировки,
	|	0                                                                               КАК СуммаСкидки,
	|	КорректировкаРеализации.ЕдиницаИзмеренияМест.Представление                      КАК ВидУпаковки,
	|	КорректировкаРеализации.КоличествоМест                                          КАК КоличествоМест,
	|	КорректировкаРеализации.ЕдиницаИзмеренияМест.Коэффициент 
	|		/ КорректировкаРеализации.Коэффициент                                       КАК КоличествоВОдномМесте,
	|	КорректировкаРеализации.Количество                                              КАК МассаНетто,
	|	КорректировкаРеализации.Количество                                              КАК МассаБрутто,
	|	КорректировкаРеализации.КоличествоДоИзменения                                   КАК МассаНеттоДоКорректировки
	|ИЗ
	|	Документ.КорректировкаРеализации.Товары КАК КорректировкаРеализации
	|
	|ГДЕ
	|	КорректировкаРеализации.Ссылка = &Ссылка
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ
	|	КорректировкаРеализации.НомерСтроки,
	|	КорректировкаРеализации.Номенклатура.Код,
	|	КорректировкаРеализации.Номенклатура."+ТоварКод+",
	|	"+СтрокаВыборкиПоляСодержания+",
	|	"+СтрокаВыборкиПоляСодержания+",
	|	КорректировкаРеализации.Номенклатура,
	|	КорректировкаРеализации.Сумма * &Курс / &Кратность,
	|	КорректировкаРеализации.СуммаДоИзменения * &Курс / &Кратность,
	|	КорректировкаРеализации.Цена * &Курс / &Кратность,
	|	КорректировкаРеализации.ЦенаДоИзменения * &Курс / &Кратность,
	|	"""",
	|	Неопределено,
	|	КорректировкаРеализации.Номенклатура.БазоваяЕдиницаИзмерения,
	|	КорректировкаРеализации.Номенклатура.БазоваяЕдиницаИзмерения.Код,
	|	КорректировкаРеализации.Номенклатура.БазоваяЕдиницаИзмерения.Наименование,
	|	КорректировкаРеализации.Номенклатура.БазоваяЕдиницаИзмерения.НаименованиеПолное,
	|	КорректировкаРеализации.Номенклатура.БазоваяЕдиницаИзмерения.МеждународноеСокращение,
	|	КорректировкаРеализации.Номенклатура.БазоваяЕдиницаИзмерения,
	|	КорректировкаРеализации.Номенклатура.БазоваяЕдиницаИзмерения,
	|	КорректировкаРеализации.Номенклатура.БазоваяЕдиницаИзмерения.Код,
	|	КорректировкаРеализации.Номенклатура.БазоваяЕдиницаИзмерения.Наименование,
	|	1,
	|	КорректировкаРеализации.СтавкаНДС,
	|	КорректировкаРеализации.СуммаНДС * &Курс / &Кратность,
	|	КорректировкаРеализации.СуммаНДСДоИзменения * &Курс / &Кратность,
	|	0,
	|	Неопределено,
	|	0,
	|	0,
	|	КорректировкаРеализации.Количество,
	|	КорректировкаРеализации.Количество,
	|	КорректировкаРеализации.КоличествоДоИзменения
	|	
	|ИЗ
	|	Документ.КорректировкаРеализации.Услуги КАК КорректировкаРеализации
	|	
	|ГДЕ
	|	КорректировкаРеализации.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Курс",       ЗаполнениеДокументов.КурсДокумента(СсылкаНаОбъект, ВалютаРегламентированногоУчета));
	Запрос.УстановитьПараметр("Кратность",  ЗаполнениеДокументов.КратностьДокумента(СсылкаНаОбъект, ВалютаРегламентированногоУчета));
	Запрос.УстановитьПараметр("Ссылка",     СсылкаНаОбъект);
	
	ТаблицаТоваров = Запрос.Выполнить().Выгрузить();
	
	ТаблицаТоваров.Колонки.Добавить("СуммаБезНДС", Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(18, 2)));
	ТаблицаТоваров.Колонки.Добавить("СуммаБезНДСДоКорректировки", Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(18, 2)));
	ТаблицаТоваров.Колонки.Добавить("СуммаСНДС",   Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(18, 2)));
	ТаблицаТоваров.Колонки.Добавить("СуммаСНДСДоКорректировки",   Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(18, 2)));
	
	Для каждого СтрокаТаблицы из ТаблицаТоваров Цикл
		
		СуммаСНДС   = Окр((СтрокаТаблицы.Сумма + ?(Шапка.СуммаВключаетНДС, 0, СтрокаТаблицы.СуммаНДС)), 2);
		СуммаНДС    = Окр(СтрокаТаблицы.СуммаНДС, 2);
		СуммаБезНДС = СуммаСНДС - СуммаНДС;
		
		СуммаСНДСДоКорректировки   = Окр((СтрокаТаблицы.СуммаДоКорректировки + ?(Шапка.СуммаВключаетНДС, 0, СтрокаТаблицы.СуммаНДСДоКорректировки)), 2);
		СуммаНДСДоКорректировки    = Окр(СтрокаТаблицы.СуммаНДСДоКорректировки, 2);
		СуммаБезНДСДоКорректировки = СуммаСНДСДоКорректировки - СуммаНДСДоКорректировки;
		
		СтрокаТаблицы.СуммаНДС    = СуммаНДС;
		СтрокаТаблицы.СуммаБезНДС = СуммаБезНДС;
		СтрокаТаблицы.СуммаСНДС   = СуммаСНДС;
		
		СтрокаТаблицы.СуммаНДСДоКорректировки    = СуммаНДСДоКорректировки;
		СтрокаТаблицы.СуммаБезНДСДоКорректировки = СуммаБезНДСДоКорректировки;
		СтрокаТаблицы.СуммаСНДСДоКорректировки   = СуммаСНДСДоКорректировки;
		
		Если Шапка.СуммаВключаетНДС Тогда
			СтрокаТаблицы.Цена = ?(СтрокаТаблицы.МассаНетто = 0, 
				0, Окр(СуммаБезНДС / СтрокаТаблицы.МассаНетто, 2));
			СтрокаТаблицы.ЦенаДоКорректировки = ?(СтрокаТаблицы.МассаНеттоДоКорректировки = 0, 
				0, Окр(СуммаБезНДСДоКорректировки / СтрокаТаблицы.МассаНеттоДоКорректировки, 2));
		Иначе
			СтрокаТаблицы.Цена = Окр(СтрокаТаблицы.Цена, 2);
			СтрокаТаблицы.ЦенаДоКорректировки = Окр(СтрокаТаблицы.ЦенаДоКорректировки, 2);
		КонецЕсли;
		
	КонецЦикла;
	
	ДанныеДокумента.Вставить("ТаблицаТоваров", ТаблицаТоваров);
	
	Возврат ДанныеДокумента;

КонецФункции

Функция ОтветственноеЛицоОрганизации(Период, Организация, ОтветственноеЛицо)
	
	Результат = Новый Структура("ФИО, Должность", "", "");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ОтветственныеЛицаОрганизаций.ФизическоеЛицо) КАК ФИО,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ОтветственныеЛицаОрганизаций.Должность)      КАК Должность
	|ИЗ
	|	РегистрСведений.ОтветственныеЛицаОрганизаций.СрезПоследних(
	|			&Период,
	|			СтруктурнаяЕдиница = &Организация
	|			И ОтветственноеЛицо = &ОтветственноеЛицо) КАК ОтветственныеЛицаОрганизаций";
	Запрос.УстановитьПараметр("Период",            Период);
	Запрос.УстановитьПараметр("Организация",       Организация);
	Запрос.УстановитьПараметр("ОтветственноеЛицо", ОтветственноеЛицо);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Результат.Вставить("ФИО",       Выборка.ФИО);
		Результат.Вставить("Должность", Выборка.Должность);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ТипСтавкиНДС(Ставка)
	Возврат "процент";
КонецФункции

Функция ПредставлениеСтавкиНДС(Ставка)
	
	// Подготовим представление ставки в соответсвии с форматом ФНС
	ПредставлениеСтавки = Строка(Ставка);
	
	Если Ставка = Перечисления.СтавкиНДС.БезНДС Тогда
		ПредставлениеСтавки = СтрЗаменить(ПредставлениеСтавки, "Б", "б");
	Иначе
		ПредставлениеСтавки = СтрЗаменить(ПредставлениеСтавки, "%", "");
		ПредставлениеСтавки = СтрЗаменить(ПредставлениеСтавки, " ", "");
	КонецЕсли;
	
	Возврат ПредставлениеСтавки;
	
КонецФункции

/////////////////////////////////////////////////////////////////////////////////
// Получение данных для формирования электронных документов

Процедура ОбработатьТаблицуТоваров(ТаблицаТоваров)
	
	Для Каждого Строка из ТаблицаТоваров Цикл
		
		// Cформируем идентификатор
		ИДТовара = Строка.Номенклатура.УникальныйИдентификатор();
		ИДХарактеристики = ?(ЗначениеЗаполнено(Строка.Характеристика), Строка.Характеристика.УникальныйИдентификатор(), "");
		ИДУпаковки = "";
		Строка.ИД = Строка(ИДТовара)+"#"+Строка(ИДХарактеристики)+"#"+Строка(ИДУпаковки);
		
		// Сформируем наименование
		Строка.Наименование = Строка.Наименование + ?(ЗначениеЗаполнено(Строка.Характеристика), " ("+Строка.Характеристика+")", "");
		
	КонецЦикла;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Заполнение объектов

// Перезаполняет реквизиты шапки объекта.
//
// Параметры:
//  ТекущийОбъект    - Объект ИБ, реквизиты шапки которого необходимо заполнить,
//  ДанныеЗаполнения - Структура значений, которые необходимо подставить в объект ИБ.
//
Процедура ПерезаполнениеЗначенийРеквизитовШапки(ТекущийОбъект, ДанныеЗаполнения)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Для Каждого Строка Из ДанныеЗаполнения Цикл
		
		Если ЗначениеЗаполнено(Строка.Ключ) И ТекущийОбъект.Метаданные().Реквизиты.Найти(Строка.Ключ) <> Неопределено Тогда
			Если ТекущийОбъект[Строка.Ключ] <> Строка.Значение Тогда
				ТекущийОбъект[Строка.Ключ] = Строка.Значение;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

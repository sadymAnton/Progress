
////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Процедуры, функции объекта

// получает доходы НДФЛ по табличным частям с доходами
// Параметры:
//		ВыборкаПоШапкеДокумента - спозиционированная выборка по шапке документа
//		НаборЗаписей - таблица значений или набор записей рег-ра - "приемник" сведений о доходах
//		
Процедура СформироватьДоходыПоКодамНДФЛ(Начисления, ВыборкаПоШапкеДокумента, НаборЗаписей) 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	// Установим параметры запроса
	Запрос.УстановитьПараметр("парамНачисления", Начисления);
	Запрос.УстановитьПараметр("парамСотрудник", ВыборкаПоШапкеДокумента.Сотрудник);
	Запрос.УстановитьПараметр("парамПодразделениеОрганизации", ВыборкаПоШапкеДокумента.ПодразделениеОрганизации);
	Запрос.УстановитьПараметр("парамПерерассчитываемыйДокумент", ВыборкаПоШапкеДокумента.ПерерассчитываемыйДокумент);
	Запрос.УстановитьПараметр("парамПериодРегистрации", ВыборкаПоШапкеДокумента.ПериодРегистрации);
	Запрос.УстановитьПараметр("парамДатаВыплатыДохода", ?(НачалоМесяца(ВыборкаПоШапкеДокумента.ПериодРегистрации) = НачалоМесяца(ВыборкаПоШапкеДокумента.ДатаУвольнения), ВыборкаПоШапкеДокумента.ДатаУвольнения,КонецМесяца(ВыборкаПоШапкеДокумента.ПериодРегистрации)));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Начисления.НомерСтроки КАК НомерСтроки,
	|	&парамСотрудник КАК Сотрудник,
	|	Начисления.ВидРасчета,
	|	Начисления.Результат,
	|	&парамПериодРегистрации КАК ДатаНачала,
	|	&парамПериодРегистрации КАК ДатаОкончания,
	|	&парамПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	&парамПерерассчитываемыйДокумент КАК СторнируемыйДокумент,
	|	Начисления.Сторно
	|ПОМЕСТИТЬ ВТНачисления
	|ИЗ
	|	&парамНачисления КАК Начисления";
	Запрос.Выполнить();
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	""Таблица"" КАК ИмяТЧ,
	|	Начисления.НомерСтроки КАК НомерСтроки,
	|	Начисления.Сотрудник,
	|	Начисления.Сотрудник.Физлицо КАК Физлицо,
	|	Начисления.ВидРасчета,
	|	ВЫРАЗИТЬ(Начисления.ВидРасчета КАК ПланВидовРасчета.ДополнительныеНачисленияОрганизаций).КодДоходаНДФЛ КАК КодДохода,
	|	Начисления.Результат КАК СуммаДохода,
	|	Начисления.ДатаНачала,
	|	Начисления.ДатаОкончания,
	|	Начисления.ПодразделениеОрганизации,
	|	Начисления.СторнируемыйДокумент,
	|	&парамПериодРегистрации КАК ПериодРегистрации,
	|	&парамДатаВыплатыДохода КАК ДатаВыплатыДохода,
	|	ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.ПустаяСсылка) КАК КодВычета,
	|	0 КАК СуммаВычета,
	|	0 КАК КоличествоДетей,
	|	Начисления.Сторно
	|ПОМЕСТИТЬ ВТСторноНачисления
	|ИЗ
	|	ВТНачисления КАК Начисления
	|ГДЕ
	|	Начисления.ВидРасчета ССЫЛКА ПланВидовРасчета.ДополнительныеНачисленияОрганизаций
	|	И ВЫРАЗИТЬ(Начисления.ВидРасчета КАК ПланВидовРасчета.ДополнительныеНачисленияОрганизаций).КодДоходаНДФЛ <> ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.ПустаяСсылка)";
	Запрос.Выполнить();
	
	ПроведениеРасчетов.СформироватьДоходыПоКодамНДФЛ(НаборЗаписей, Запрос.МенеджерВременныхТаблиц, ВыборкаПоШапкеДокумента, ВыборкаПоШапкеДокумента.ПериодРегистрацииДополнительныхНачислений);
	
КонецПроцедуры // СформироватьДоходыПоКодамНДФЛ()

Процедура ВыполнитьПрочиеДвиженияДокумента(ДокументОбъект, ВыборкаПоШапкеДокумента, Отказ, Заголовок, ВыборкаПоНачислениям = Неопределено) Экспорт 
	
	Если НЕ Отказ Тогда
		
		СформироватьДоходыПоКодамНДФЛ(ДокументОбъект.Начисления, ВыборкаПоШапкеДокумента, ДокументОбъект.Движения.НДФЛСведенияОДоходах);
		
		ВыборкаПоНачислениям.Сбросить();
		Пока ВыборкаПоНачислениям.Следующий() Цикл 

			РасчетПриУвольненииРаботникаОрганизацийДополнительный.ДобавитьСтрокуВДвиженияПоРегистрамНакопления(ДокументОбъект, ВыборкаПоШапкеДокумента, ВыборкаПоНачислениям);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыполнитьДополнительныеПроверкиСтрокиНачислений(ДокументОбъект,ВыборкаПоСтрокамДокумента,Отказ, Заголовок, СтрокаНачалаСообщенияОбОшибке) Экспорт 

	// В этой конфигурации дополнительные действия не выполняются

КонецПроцедуры

Процедура ПереписатьПрочиеДвиженияПриПерерасчете(ДокументОбъект, ВыборкаПоШапкеДокумента) Экспорт 
	
	ДокументОбъект.Движения.НДФЛСведенияОДоходах.Очистить();
	ДокументОбъект.Движения.ВзаиморасчетыСРаботникамиОрганизаций.Очистить();
	
	ВыборкаПоНачислениям = ДокументОбъект.СформироватьЗапросПоНачислениям(ВыборкаПоШапкеДокумента).Выбрать();
	ВыполнитьПрочиеДвиженияДокумента(ДокументОбъект, ВыборкаПоШапкеДокумента, Ложь, "", ВыборкаПоНачислениям);
	
	// записываем обновленные движения
	Для Каждого Набор Из ДокументОбъект.Движения Цикл
		ТипНабораЗаписей = ТипЗнч(Набор);
		Если ТипНабораЗаписей = Тип("РегистрНакопленияНаборЗаписей.НДФЛСведенияОДоходах") 
			Или ТипНабораЗаписей = Тип("РегистрНакопленияНаборЗаписей.ВзаиморасчетыСРаботникамиОрганизаций") Тогда
			Набор.Записать();
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Возвращает признак того, используется ли при расчете средне часовой заработок
//
// Параметры
//  ДокументОбъект - ДокументОбъект.НачислениеОтпускаРаботникамОрганизаций
//
// Возвращаемое значение:
//   булево
//
Функция ИспользуетсяСреднеЧасовойЗаработок(ДокументОбъект) Экспорт

	Возврат ДокументОбъект.ИспользоватьСреднеЧасовойЗаработок

КонецФункции // ИспользуетсяСреднеЧасовойЗаработок()

// Возвращает перечень в.р. среднего заработка, соответствующих годовым премиям
//
// Параметры
//  нет
//
// Возвращаемое значение:
//   массив
//
Функция МассивГодовыхПремий() Экспорт

	ГодовыеПремии = Новый Массив(4);
	ГодовыеПремии[0] = ПланыВидовРасчета.СреднийЗаработок.ПоГодовойПремии;
	ГодовыеПремии[1] = ПланыВидовРасчета.СреднийЗаработок.ПоФиксГодовойПремии;
	ГодовыеПремии[2] = ПланыВидовРасчета.СреднийЗаработок.ПоГодовойПремииНеИндексируемые;
	ГодовыеПремии[3] = ПланыВидовРасчета.СреднийЗаработок.ПоФиксГодовойПремииНеИндексируемые;

КонецФункции // МассивГодовыхПремий()

// Определяет количество месяцев расчетного периода в зависимости от даты начала события
//
// Параметры
//  ДокументОбъект - ДокументОбъект.НачислениеОтпускаРаботникамОрганизаций
//
// Возвращаемое значение:
//   число
//
Функция КоличествоМесяцевРасчетаПоЗакону(ДокументОбъект) Экспорт

	Возврат ?(ДокументОбъект.ДатаУвольнения < ПроведениеРасчетовДополнительный.ПолучитьДатуВступленияВСилуИзмененийТрудовогоКодекса2006(),3,12)

КонецФункции // КоличествоМесяцевРасчетаПоЗакону()

Функция ПолучитьПрочиеПоляЗапросаПоСреднему() Экспорт
	
	Возврат "СтрокаРасчетСреднегоДляОтпуска.ОтработаноЧасов"
	
КонецФункции

Функция ПолучитьСтруктуруРегламентированныхФорм(ДокументОбъект) Экспорт
	
	СтруктураПечатныхФорм = Новый Структура;
	СтруктураПечатныхФорм.Вставить("Т61", "Форма Т-61");
	
	Возврат СтруктураПечатныхФорм
	
КонецФункции // ПолучитьСтруктуруПечатныхФорм()

Функция РасчетСреднегоЗаработка(ДокументОбъект, ПорядокРасчетаСреднего, ПериодРасчетаСреднегоЗаработка, Начало, Окончание) Экспорт 
	
	ТаблицаСреднего = ?(ПорядокРасчетаСреднего = Перечисления.СпособыРасчетаОплатыТруда.ПоСреднемуЗаработку, ДокументОбъект.РасчетСреднего,ДокументОбъект.РасчетСреднегоДляОтпуска);
	Если ТаблицаСреднего.Количество() = 0 Тогда
		Возврат 0;
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ОсновнойЗаработок = Новый Массив(2);
	ОсновнойЗаработок[0] = ПланыВидовРасчета.СреднийЗаработок.ПоЗаработку;
	ОсновнойЗаработок[1] = ПланыВидовРасчета.СреднийЗаработок.ПоЗаработкуИндексируемые;
	ПоФиксПремиям = Новый Массив(1);
	ПоФиксПремиям[0] = ПланыВидовРасчета.СреднийЗаработок.ПоФиксПремиям;
	ПоПремиям = Новый Массив(3);
	ПоПремиям[0] = ПланыВидовРасчета.СреднийЗаработок.ПоПремиям;
	ПоПремиям[1] = ПланыВидовРасчета.СреднийЗаработок.ПоПремиямИндексируемые;
	ПоПремиям[2] = ПланыВидовРасчета.СреднийЗаработок.ПоГодовойПремииИндексируемые;
	ПоФиксПремиямНеИндексируемые = Новый Массив(3);
	ПоФиксПремиямНеИндексируемые[0] = ПланыВидовРасчета.СреднийЗаработок.ПоФиксГодовойПремии;
	ПоФиксПремиямНеИндексируемые[1] = ПланыВидовРасчета.СреднийЗаработок.ПоФиксГодовойПремииНеИндексируемые;
	ПоФиксПремиямНеИндексируемые[2] = ПланыВидовРасчета.СреднийЗаработок.ПоФиксПремиямНеИндексируемые;
	
	Запрос.УстановитьПараметр("ОсновнойЗаработок",				ОсновнойЗаработок); // Основной заработок индексируемый
	Запрос.УстановитьПараметр("ОсновнойЗаработокНеиндексируемый", ПланыВидовРасчета.СреднийЗаработок.ПоЗаработкуНеИндексируемые);
	Запрос.УстановитьПараметр("ПоПремиям",						ПоПремиям);  // Премии полностью учитываемые, индексируемые
	Запрос.УстановитьПараметр("ПоФиксПремиям",					ПоФиксПремиям); // Премии учитываемые частично, индексируемые
	Запрос.УстановитьПараметр("ПоФиксПремиямНеИндексируемые",	ПоФиксПремиямНеИндексируемые); // Премии учитываемые частично, не индексируемые
	Запрос.УстановитьПараметр("ТаблицаСреднего",				ТаблицаСреднего);
	Запрос.УстановитьПараметр("ПериодРасчетаСреднегоЗаработка",	ПериодРасчетаСреднегоЗаработка);
	Запрос.УстановитьПараметр("Начало",							Начало);  
	Запрос.УстановитьПараметр("Окончание",						КонецДня(Окончание));  
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РасчетСреднегоЗаработка.ВидРасчета,
	|	РасчетСреднегоЗаработка.Результат,
	|	РасчетСреднегоЗаработка.ОтработаноДней,
	|	РасчетСреднегоЗаработка.ОтработаноЧасов,
	|	РасчетСреднегоЗаработка.НормаПоПятидневке,
	|	РасчетСреднегоЗаработка.ОтработаноПоПятидневке,
	|	РасчетСреднегоЗаработка.ЧислоМесяцев,
	|	РасчетСреднегоЗаработка.КоэффициентИндексации,
	|	РасчетСреднегоЗаработка.БазовыйПериодНачало,
	|	РасчетСреднегоЗаработка.БазовыйПериодКонец
	|ПОМЕСТИТЬ ВТРасчетСреднегоЗаработка
	|ИЗ
	|	&ТаблицаСреднего КАК РасчетСреднегоЗаработка";
	Запрос.Выполнить(); 
			
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СУММА(ВЫРАЗИТЬ(ВЫБОР
	|				КОГДА РасчетСреднегоЗаработка.ВидРасчета В (&ОсновнойЗаработок)
	|					ТОГДА РасчетСреднегоЗаработка.Результат * ВЫБОР
	|							КОГДА РасчетСреднегоЗаработка.КоэффициентИндексации < 1
	|								ТОГДА 1
	|							ИНАЧЕ РасчетСреднегоЗаработка.КоэффициентИндексации
	|						КОНЕЦ
	|				КОГДА РасчетСреднегоЗаработка.ВидРасчета В (&ОсновнойЗаработокНеиндексируемый)
	|					ТОГДА РасчетСреднегоЗаработка.Результат
	|				КОГДА РасчетСреднегоЗаработка.ЧислоМесяцев = 0
	|					ТОГДА 0
	|				ИНАЧЕ ВЫБОР
	|						КОГДА РасчетСреднегоЗаработка.ВидРасчета В (&ПоПремиям)
	|							ТОГДА РасчетСреднегоЗаработка.Результат * ВЫБОР
	|									КОГДА РасчетСреднегоЗаработка.КоэффициентИндексации < 1
	|										ТОГДА 1
	|									ИНАЧЕ РасчетСреднегоЗаработка.КоэффициентИндексации
	|								КОНЕЦ
	|						КОГДА РасчетСреднегоЗаработка.ВидРасчета В (&ПоФиксПремиям)
	|							ТОГДА РасчетСреднегоЗаработка.Результат * ВЫБОР
	|									КОГДА РасчетСреднегоЗаработка.КоэффициентИндексации < 1
	|										ТОГДА 1
	|									ИНАЧЕ РасчетСреднегоЗаработка.КоэффициентИндексации
	|								КОНЕЦ * ВЫБОР
	|									КОГДА РасчетСреднегоЗаработка.НормаПоПятидневке = 0
	|										ТОГДА 0
	|									ИНАЧЕ РасчетСреднегоЗаработка.ОтработаноПоПятидневке / РасчетСреднегоЗаработка.НормаПоПятидневке
	|								КОНЕЦ
	|						КОГДА РасчетСреднегоЗаработка.ВидРасчета В (&ПоФиксПремиямНеИндексируемые)
	|							ТОГДА РасчетСреднегоЗаработка.Результат * ВЫБОР
	|									КОГДА РасчетСреднегоЗаработка.НормаПоПятидневке = 0
	|										ТОГДА 0
	|									ИНАЧЕ РасчетСреднегоЗаработка.ОтработаноПоПятидневке / РасчетСреднегоЗаработка.НормаПоПятидневке
	|								КОНЕЦ
	|						ИНАЧЕ РасчетСреднегоЗаработка.Результат
	|					КОНЕЦ * ВЫБОР
	|						КОГДА РасчетСреднегоЗаработка.ЧислоМесяцев > &ПериодРасчетаСреднегоЗаработка
	|							ТОГДА &ПериодРасчетаСреднегоЗаработка / РасчетСреднегоЗаработка.ЧислоМесяцев
	|						ИНАЧЕ 1
	|					КОНЕЦ
	|			КОНЕЦ КАК ЧИСЛО(15, 2))) КАК СреднийЗаработок,
 	|	СУММА(ВЫБОР
	|			КОГДА РасчетСреднегоЗаработка.ВидРасчета В (&ОсновнойЗаработок)
	|					ИЛИ РасчетСреднегоЗаработка.ВидРасчета В (&ОсновнойЗаработокНеиндексируемый)
	|				ТОГДА РасчетСреднегоЗаработка.ОтработаноДней
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОтработаноДней,
	|	СУММА(ВЫБОР
	|			КОГДА РасчетСреднегоЗаработка.ВидРасчета В (&ОсновнойЗаработок)
	|					ИЛИ РасчетСреднегоЗаработка.ВидРасчета В (&ОсновнойЗаработокНеиндексируемый)
	|				ТОГДА РасчетСреднегоЗаработка.ОтработаноЧасов
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОтработаноЧасов
	|ИЗ
	|	ВТРасчетСреднегоЗаработка КАК РасчетСреднегоЗаработка
	|ГДЕ
	|	РасчетСреднегоЗаработка.БазовыйПериодНачало МЕЖДУ &Начало И &Окончание";	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Если ПорядокРасчетаСреднего = Перечисления.СпособыРасчетаОплатыТруда.ПоСреднемуЗаработку Тогда
			Если ЗначениеЗаполнено(ДокументОбъект.Сотрудник) Тогда
				Запрос = Новый Запрос;
				Запрос.Текст = 
				"ВЫБРАТЬ
				|	ЕСТЬNULL(ВЫБОР
				|			КОГДА РаботникиОрганизацииСрезПоследних.ПериодЗавершения <= &ДатаАктуальности
				|					И РаботникиОрганизацииСрезПоследних.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
				|				ТОГДА РаботникиОрганизацииСрезПоследних.ГрафикРаботыЗавершения.СуммированныйУчетРабочегоВремени
				|			ИНАЧЕ РаботникиОрганизацииСрезПоследних.ГрафикРаботы.СуммированныйУчетРабочегоВремени
				|		КОНЕЦ, ЛОЖЬ) КАК ИспользоватьСреднеЧасовойЗаработок
				|ИЗ
				|	РегистрСведений.РаботникиОрганизаций.СрезПоследних(&ДатаАктуальности, Сотрудник = &Сотрудник) КАК РаботникиОрганизацииСрезПоследних";
				Запрос.УстановитьПараметр("Сотрудник", ДокументОбъект.Сотрудник);
				Запрос.УстановитьПараметр("ДатаАктуальности", ДокументОбъект.ДатаУвольнения);
				ВыборкаСотрудника = Запрос.Выполнить().Выбрать();
				Если ВыборкаСотрудника.Следующий() Тогда
					Если ВыборкаСотрудника.ИспользоватьСреднеЧасовойЗаработок Тогда
						Возврат ?(Не ЗначениеЗаполнено(Выборка.ОтработаноЧасов), 0, Окр(Выборка.СреднийЗаработок / Выборка.ОтработаноЧасов,2));
					Иначе
						Возврат ?(Не ЗначениеЗаполнено(Выборка.ОтработаноДней), 0, Окр(Выборка.СреднийЗаработок / Выборка.ОтработаноДней,2));
					КонецЕсли;
				Иначе
					Возврат ?(Не ЗначениеЗаполнено(Выборка.ОтработаноДней), 0, Окр(Выборка.СреднийЗаработок / Выборка.ОтработаноДней,2));
				КонецЕсли;
				
			Иначе
				Возврат ?(Не ЗначениеЗаполнено(Выборка.ОтработаноДней), 0, Окр(Выборка.СреднийЗаработок / Выборка.ОтработаноДней,2));
			КонецЕсли;
		Иначе
			Если ДокументОбъект.ИспользоватьСреднеЧасовойЗаработок Тогда
				Возврат ?(Не ЗначениеЗаполнено(Выборка.ОтработаноЧасов), 0, Окр(Выборка.СреднийЗаработок / Выборка.ОтработаноЧасов,2));
			Иначе
				Возврат ?(Не ЗначениеЗаполнено(Выборка.ОтработаноДней), 0, Окр(Выборка.СреднийЗаработок / Выборка.ОтработаноДней,2));
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат 0;

КонецФункции // РасчетСреднедневногоЗаработка()

#Если ТолстыйКлиентОбычноеПриложение Тогда
	
Функция РегламентированнаяПечатнаяФорма(ДокументОбъект, ИмяМакета) Экспорт
	
	Если  ИмяМакета = "Т61" Тогда
		Возврат РасчетПриУвольненииРаботникаОрганизацийДополнительный.ПечатьТ61(ДокументОбъект);
	КонецЕсли;
	
КонецФункции // РегламентированнаяПечатнаяФорма

Функция ОписаниеРегламентированнойПечатнойФормы(ДокументОбъект, ИмяМакета) Экспорт
	
	Возврат " (Форма Т-61)"
		
КонецФункции // ОписаниеРегламентированнойПечатнойФормы

// Функция возвращает массив табличных частей документа, которые необходимо очистить
// перед расчетом
//
Функция ПолучитьМассивТабличныхЧастей(ДокументОбъект, ЧтоРассчитываем) Экспорт

	МассивТаблиц = Новый Массив;
	Если ЧтоРассчитываем = "ПолныйРасчет" или ЧтоРассчитываем = "РасчетСреднего" Тогда
		МассивТаблиц.Добавить(ДокументОбъект.Начисления);
		МассивТаблиц.Добавить(ДокументОбъект.РасчетСреднего);
		МассивТаблиц.Добавить(ДокументОбъект.РасчетСреднегоДляОтпуска);
	ИначеЕсли ЧтоРассчитываем = "РасчетНачислений" Тогда	
		МассивТаблиц.Добавить(ДокументОбъект.Начисления);
	КонецЕсли;	
	
	Возврат МассивТаблиц;

КонецФункции // ПолучитьМассивТабличныхЧастей()

////////////////////////////////////////////////////////////////////////////////
// Процедуры, функции для работы формы документа

Процедура ВыполнитьДополнительныеДействияПриОткрытииФормы(ДокументОбъект, ФормаДокумента) Экспорт

	ЭлементыФормы = ФормаДокумента.ЭлементыФормы;
	ЭлементыФормы.ЗаголовокДополнительнойИнформационнойНадписи.Видимость = Ложь;
	ЭлементыФормы.ДополнительнаяИнформационнаяНадпись.Видимость = Ложь;
	
КонецПроцедуры

Процедура ВыполнитьДополнительныеДействияПриИзмененииПериодаРегистрации(ДокументОбъект, ФормаДокумента) Экспорт

	// В этой конфигурации дополнительные действия не выполняются
	
КонецПроцедуры

Процедура ВыполнитьДействияПриНачалеРедактированияНачисления(ФормаДокумента, Элемент, НоваяСтрока) Экспорт
	
	// В этой конфигурации дополнительные действия не выполняются

КонецПроцедуры

Процедура ВыполнитьДействияПослеРедактированияНачисления(ФормаДокумента, ТекущаяСтрока) Экспорт
	
	// В этой конфигурации дополнительные действия не выполняются
	
КонецПроцедуры

Процедура ВыполнитьДействияПередУдалениемНачисления(Элемент, ДокументОбъект, Отказ) Экспорт
	
	// В этой конфигурации дополнительные действия не выполняются
			
КонецПроцедуры

Процедура ВыполнитьДополнительныеДействияПриУдаленииНачислений(ДокументОбъект, ФормаДокумента) Экспорт
	
	// В этой конфигурации дополнительные действия не выполняются
	
КонецПроцедуры

Процедура НастроитьСпискиВыбораЭлементовУправления(ДокументОбъект, ФормаДокумента) Экспорт

	ЭлементыФормы = ФормаДокумента.ЭлементыФормы;
	 
	ЭтоПорядок2007года = ?(ЗначениеЗаполнено(ДокументОбъект.ДатаУвольнения),ДокументОбъект.ДатаУвольнения,ОбщегоНазначенияЗК.ПолучитьРабочуюДату()) < ПроведениеРасчетовДополнительный.ПолучитьДатуВступленияВСилуИзмененийПоОтпускам2008();
	
	// Список видов записей расчета среднего
	СписокВыбора = ПроведениеРасчетовПереопределяемый.ПолучитьПереченьСоставляющихСреднегоЗаработка(ЭтоПорядок2007года);
	ЭлементыФормы.РасчетСреднего.Колонки.ВидРасчета.ЭлементУправления.СписокВыбора = СписокВыбора;
	ЭлементыФормы.РасчетСреднего.Колонки.ВидРасчета.ЭлементУправления.ВысотаСпискаВыбора = СписокВыбора.Количество();
	ЭлементыФормы.РасчетСреднегоДляОтпуска.Колонки.ВидРасчета.ЭлементУправления.СписокВыбора = СписокВыбора;
	ЭлементыФормы.РасчетСреднегоДляОтпуска.Колонки.ВидРасчета.ЭлементУправления.ВысотаСпискаВыбора = СписокВыбора.Количество();
	
	ЭлементыФормы.РасчетСреднегоДляОтпуска.Колонки.ОтработаноЧасов.Видимость = ЭтоПорядок2007года;
	ЭлементыФормы.РасчетСреднегоДляОтпуска.Колонки.ОтработаноЧасов.ИзменятьНастройку = ЭтоПорядок2007года;

	ЭлементыФормы.РасчетСреднего.Колонки.ОтработаноПоПятидневке.ТекстШапки = ?(ЭтоПорядок2007года,"Отработано по пятидневке","Отработано за расчетный период");
	ЭлементыФормы.РасчетСреднего.Колонки.НормаПоПятидневке.ТекстШапки = ?(ЭтоПорядок2007года,"Норма по пятидневке","Норма за расчетный период");
	ЭлементыФормы.РасчетСреднегоДляОтпуска.Колонки.ОтработаноПоПятидневке.ТекстШапки = ?(ЭтоПорядок2007года,"Отработано по пятидневке","Отработано за расчетный период");
	ЭлементыФормы.РасчетСреднегоДляОтпуска.Колонки.НормаПоПятидневке.ТекстШапки = ?(ЭтоПорядок2007года,"Норма по пятидневке","Норма за расчетный период");
	
	ЭлементыФормы.РасчетСреднего1.Колонки.ВидРасчета.ЭлементУправления.СписокВыбора = СписокВыбора;
	ЭлементыФормы.РасчетСреднего1.Колонки.ВидРасчета.ЭлементУправления.ВысотаСпискаВыбора = СписокВыбора.Количество();
	ЭлементыФормы.РасчетСреднегоДляОтпуска1.Колонки.ВидРасчета.ЭлементУправления.СписокВыбора = СписокВыбора;
	ЭлементыФормы.РасчетСреднегоДляОтпуска1.Колонки.ВидРасчета.ЭлементУправления.ВысотаСпискаВыбора = СписокВыбора.Количество();
	
	ЭлементыФормы.РасчетСреднегоДляОтпуска1.Колонки.ОтработаноЧасов.Видимость = ЭтоПорядок2007года;
	ЭлементыФормы.РасчетСреднегоДляОтпуска1.Колонки.ОтработаноЧасов.ИзменятьНастройку = ЭтоПорядок2007года;

	ЭлементыФормы.РасчетСреднего1.Колонки.ОтработаноПоПятидневке.ТекстШапки = ?(ЭтоПорядок2007года,"Отработано по пятидневке","Отработано за расчетный период");
	ЭлементыФормы.РасчетСреднего1.Колонки.НормаПоПятидневке.ТекстШапки = ?(ЭтоПорядок2007года,"Норма по пятидневке","Норма за расчетный период");
	ЭлементыФормы.РасчетСреднегоДляОтпуска1.Колонки.ОтработаноПоПятидневке.ТекстШапки = ?(ЭтоПорядок2007года,"Отработано по пятидневке","Отработано за расчетный период");
	ЭлементыФормы.РасчетСреднегоДляОтпуска1.Колонки.НормаПоПятидневке.ТекстШапки = ?(ЭтоПорядок2007года,"Норма по пятидневке","Норма за расчетный период");

КонецПроцедуры

Процедура ДополнитьКолонкиТабличногоПоля(ТабличноеПоле) Экспорт 
	
	РаботаСДиалогами.ВставитьКолонкуВТабличноеПоле(4, ТабличноеПоле, "ОтработаноЧасов", "Часов отработано", "ОтработаноЧасов", , , , Тип("ПолеВвода"), ПоложениеКолонки.НаСледующейСтроке, 15);

КонецПроцедуры // ДополнитьКолонкиТабличнойЧасти()

Процедура ОбновитьДополнительнуюИнформационнуюНадпись(ДокументОбъект, ФормаДокумента) Экспорт

	// В этой конфигурации дополнительные действия не выполняются
	
КонецПроцедуры

Процедура ПоказатьДополнительнуюФорму(ДокументОбъект, ФормаДокумента) Экспорт
	
	// В этой конфигурации дополнительные действия не выполняются
	
КонецПроцедуры

#КонецЕсли


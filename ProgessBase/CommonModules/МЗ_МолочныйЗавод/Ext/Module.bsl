Функция ПолучитьКачественныйПоказатель(Номенклатура, Свойство, Дата = Неопределено) Экспорт
	
	Если Дата = Неопределено Тогда
		Дата = ТекущаяДата();
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ПРГ_ПериодическиеСвойстваОбъектовСрезПоследних.Значение
	|ИЗ
	|	РегистрСведений.ПРГ_ПериодическиеСвойстваОбъектов.СрезПоследних(
	|			&ДатаСреза,
	|			Объект = &Номенклатура
	|				И Свойство = &Свойство) КАК ПРГ_ПериодическиеСвойстваОбъектовСрезПоследних");
	
	Запрос.УстановитьПараметр("ДатаСреза", 		Дата);
	Запрос.УстановитьПараметр("Номенклатура", 	Номенклатура);
	Запрос.УстановитьПараметр("Свойство",		Свойство);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Результат = Выборка.Значение;
	Иначе
		Результат = Номенклатура.ПРГ_Базис;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ОпределитьСпецификациюДляПобочногоПродукта(СтрокаПродукции, ТабПродукция, Дата, Подразделение) Экспорт
	
	ПРГ_ИсппользуетсяБлокМолочногоПроизводства = ПРГ_ДопФункцииКлиентСервер.ПРГ_ИсппользуетсяБлокМолочногоПроизводства(Дата);
	
	СпецификацияПобочногоПродукта = СтрокаПродукции.Спецификация;
	
	Если Не ПРГ_ИсппользуетсяБлокМолочногоПроизводства Тогда
		Возврат СпецификацияПобочногоПродукта;
	КонецЕсли;
	
	Если ТабПродукция.Количество() < 2 Тогда
		Возврат СпецификацияПобочногоПродукта;
	КонецЕсли;
	
	Если ТабПродукция.Индекс(СтрокаПродукции) < 1 Тогда
		Возврат СпецификацияПобочногоПродукта;
	КонецЕсли;
	
	СтрокаОсновногоПродукта = ТабПродукция[0];
	
	Если Не ЗначениеЗаполнено(СтрокаОсновногоПродукта.Спецификация)
		ИЛИ Не СтрокаОсновногоПродукта.Спецификация.ВидСпецификации = Перечисления.ВидыСпецификаций.Полная Тогда
	    Возврат СпецификацияПобочногоПродукта;
	КонецЕсли;
	
	СтрокиСпецификации = СтрокаОсновногоПродукта.Спецификация.ВыходныеИзделия.НайтиСтроки(Новый Структура("Номенклатура, ХарактеристикаНоменклатуры", СтрокаПродукции.Номенклатура, СтрокаПродукции.ХарактеристикаНоменклатуры));
	Если СтрокиСпецификации.Количество() > 0 Тогда
		Возврат СтрокаОсновногоПродукта.Спецификация;
	КонецЕсли;
	
	Возврат СпецификацияПобочногоПродукта;
КонецФункции
////////////////////////////////////////////////////////////////////////////////
// Общие процедуры и функции

Функция ФормыОбученияМенеджер() Экспорт
	
	Возврат Справочники.ФормыОбучения;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Процедуры, используемые при первоначальном заполнении ИБ

// Процедура добавляет вопрос в анкету.
// Запускается при обновлении на 1.2.15 и при первоначальном заполнении пустой ИБ.
//
Процедура ДобавитьВопрос(Анкета, Вопрос, Обязательный)
	
	ВопросАнкеты = Анкета.ВопросыАнкеты.Добавить();
	ВопросАнкеты.Вопрос			= Вопрос;
	ВопросАнкеты.Обязательный	= Обязательный;
	
КонецПроцедуры

// Процедура создает элемент справочника "Типовые анкеты".
// Запускается при обновлении на 1.2.15 и при первоначальном заполнении пустой ИБ.
//
Процедура СоздатьАнкетуРезюме() Экспорт
	
	Если НЕ Константы.АнкетаРезюмеКандидата.Получить().Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	НоваяАнкета	= Справочники.ТиповыеАнкеты.СоздатьЭлемент();
	НоваяАнкета.ВидАнкеты					= Перечисления.ВидыАнкет.Опрос;
	НоваяАнкета.Наименование				= "Резюме";
	НоваяАнкета.ЗагружатьОбъекты			= Истина;
	НоваяАнкета.ВидСправочникаДляЗагрузки	= Перечисления.ВидыОбъектовЗагружаемыхИзОпроса.ФизическиеЛица;
	ДобавитьВопрос(НоваяАнкета, ПланыВидовХарактеристик.ВопросыДляАнкетирования.Фамилия, Перечисления.ОбязательностьЗаполненияОтветаНаВопрос.ОбязателенКЗаполнению);
	ДобавитьВопрос(НоваяАнкета, ПланыВидовХарактеристик.ВопросыДляАнкетирования.Имя, Перечисления.ОбязательностьЗаполненияОтветаНаВопрос.ОбязателенКЗаполнению);
	ДобавитьВопрос(НоваяАнкета, ПланыВидовХарактеристик.ВопросыДляАнкетирования.Отчество, Перечисления.ОбязательностьЗаполненияОтветаНаВопрос.ОбязателенКЗаполнению);
	ДобавитьВопрос(НоваяАнкета, ПланыВидовХарактеристик.ВопросыДляАнкетирования.ДатаРождения, Перечисления.ОбязательностьЗаполненияОтветаНаВопрос.ОбязателенКЗаполнению);
	ДобавитьВопрос(НоваяАнкета, ПланыВидовХарактеристик.ВопросыДляАнкетирования.Пол, Перечисления.ОбязательностьЗаполненияОтветаНаВопрос.НеОбязателенКЗаполнению);
	ДобавитьВопрос(НоваяАнкета, ПланыВидовХарактеристик.ВопросыДляАнкетирования.ТОбразование, Перечисления.ОбязательностьЗаполненияОтветаНаВопрос.НеОбязателенКЗаполнению);
	ДобавитьВопрос(НоваяАнкета, ПланыВидовХарактеристик.ВопросыДляАнкетирования.ТТрудоваяДеятельность, Перечисления.ОбязательностьЗаполненияОтветаНаВопрос.НеОбязателенКЗаполнению);
	
	НоваяАнкета.Записать();
	
	Константы.АнкетаРезюмеКандидата.Установить(НоваяАнкета.Ссылка);
	
КонецПроцедуры

// Записываем реквизиты предопределенных вопросов
// Запускается при обновлении на 1.2.15, 1.2.20 и при первоначальном заполнении пустой ИБ.
//
Процедура ЗаполнитьРеквизитыВопросов(ЗаполнятьТабличныеВопросы = Истина) Экспорт

	Менеджер		= ПланыВидовХарактеристик.ВопросыДляАнкетирования;
	ВВидеЧисла		= Перечисления.ТипыОтветаНаВопросАнкеты.Число;
	ВВидеСтроки		= Перечисления.ТипыОтветаНаВопросАнкеты.Строка;
	ВВидеДаты		= Перечисления.ТипыОтветаНаВопросАнкеты.Дата;
	ТабличныйВопрос	= Перечисления.ТипыОтветаНаВопросАнкеты.Табличный;
	ВВидеБулево		= Перечисления.ТипыОтветаНаВопросАнкеты.Булево;
	ВВидеКИ			= Перечисления.ТипыОтветаНаВопросАнкеты.КонтактнаяИнформация;
	ВВидеСсылки		= Перечисления.ТипыОтветаНаВопросАнкеты.Ссылка;
	
	// Описание типов
	КД = Новый КвалификаторыДаты(ЧастиДаты.Дата);
	
	ОписаниеТиповДата	= Новый ОписаниеТипов("Дата", , , КД);
	ОписаниеТиповБулево	= Новый ОписаниеТипов("Булево");
	ОписаниеТиповВОТ	= Новый ОписаниеТипов("СправочникСсылка.ВариантыОтветовОпросов");
	
	УстановитьРеквизитыВопроса(Менеджер.Имя,			"Имя",			ВВидеСтроки,,,, Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(50)), 50);
	УстановитьРеквизитыВопроса(Менеджер.Отчество,		"Отчество",		ВВидеСтроки,,,, Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(50)), 50);
	УстановитьРеквизитыВопроса(Менеджер.Фамилия,		"Фамилия",		ВВидеСтроки,,,, Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(50)), 50);
	УстановитьРеквизитыВопроса(Менеджер.Пол,			"Пол",			ВВидеСсылки,,,, Новый ОписаниеТипов("ПеречислениеСсылка.ПолФизическихЛиц"));
	УстановитьРеквизитыВопроса(Менеджер.ДатаРождения,	"Дата рождения",ВВидеДаты,,,, ОписаниеТиповДата);
	
	УстановитьРеквизитыВопроса(Менеджер.СтраховойНомерПФР,	"Страховой № в ПФР",	ВВидеСтроки,,,, Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(14)), 14);
	УстановитьРеквизитыВопроса(Менеджер.ИНН,				"ИНН",					ВВидеСтроки,,,, Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(12)), 12);
	УстановитьРеквизитыВопроса(Менеджер.КодИМНС,			"Код ИМНС",				ВВидеСтроки,,,, Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(4)), 4);
	УстановитьРеквизитыВопроса(Менеджер.Гражданство,		"Гражданство",			ВВидеСсылки,,,, Новый ОписаниеТипов("СправочникСсылка.КлассификаторСтранМира"));
	УстановитьРеквизитыВопроса(Менеджер.СемейноеПоложение,	"Семейное положение",	ВВидеСсылки,,,, Новый ОписаниеТипов("СправочникСсылка.СемейноеПоложениеФизЛиц"));
	
	// Состав семьи
	УстановитьРеквизитыВопроса(Менеджер.СоставСемьиСтепеньРодства,	"Степень родства",				ВВидеСсылки,,,, Новый ОписаниеТипов("СправочникСсылка.СтепениРодстваФизЛиц"));
	УстановитьРеквизитыВопроса(Менеджер.СоставСемьиРодственник,		"Имя родственника",				ВВидеСтроки,,,, Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(50)), 50);
	УстановитьРеквизитыВопроса(Менеджер.СоставСемьиДатаРождения,	"Дата рождения родственника",	ВВидеДаты,,,,  ОписаниеТиповДата, 4);
	
	// Образование
	УстановитьРеквизитыВопроса(Менеджер.ВидОбразования,		"Образование",		ВВидеСсылки,,,, Новый ОписаниеТипов("СправочникСсылка.ВидыОбразованияФизЛиц"));
	УстановитьРеквизитыВопроса(Менеджер.УчебноеЗаведение,	"Учебное заведение",ВВидеСсылки,,,, Новый ОписаниеТипов("СправочникСсылка.УчебныеЗаведения"));
	УстановитьРеквизитыВопроса(Менеджер.Специальность,		"Специальность",	ВВидеСсылки,,,, Новый ОписаниеТипов("СправочникСсылка.КлассификаторСпециальностейПоОбразованию"));
	УстановитьРеквизитыВопроса(Менеджер.ДипломСерияНомер,	"Диплом",			ВВидеСтроки,,,, Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(50)), 50);
	УстановитьРеквизитыВопроса(Менеджер.ГодОкончания,		"Год окончания",	ВВидеЧисла,,,, Новый ОписаниеТипов("Число", , Новый КвалификаторыЧисла(4, 0)), 4);
	УстановитьРеквизитыВопроса(Менеджер.Квалификация,		"Квалификация",		ВВидеСтроки,,,, Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(100)), 100);
	
	// Трудовая деятельность
	УстановитьРеквизитыВопроса(Менеджер.ТрудоваяДеятельностьОрганизация,	"Наименование организации", ВВидеСтроки,,,, Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(100)), 100);
	УстановитьРеквизитыВопроса(Менеджер.ТрудоваяДеятельностьДолжность,		"Должность",				ВВидеСтроки,,,, Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(100)), 100);
	УстановитьРеквизитыВопроса(Менеджер.ТрудоваяДеятельностьНачалоРаботы,	"Начало работы",			ВВидеДаты,,,, ОписаниеТиповДата);
	УстановитьРеквизитыВопроса(Менеджер.ТрудоваяДеятельностьОкончаниеРаботы,"Окончание работы",			ВВидеДаты,,,, ОписаниеТиповДата);

	// Знание языков
	УстановитьРеквизитыВопроса(Менеджер.СтепеньЗнанияЯзыка,	"Знание языка",		ВВидеСсылки,,,, Новый ОписаниеТипов("СправочникСсылка.СтепениЗнанияЯзыка"));
	УстановитьРеквизитыВопроса(Менеджер.Язык,				"Язык",				ВВидеСсылки,,,, Новый ОписаниеТипов("СправочникСсылка.ЯзыкиНародовМира"));

	// ПаспортныеДанныеФизЛиц
	УстановитьРеквизитыВопроса(Менеджер.ВидДокументаУдостоверяющегоЛичность,"Вид документа",	ВВидеСсылки,,,, Новый ОписаниеТипов("СправочникСсылка.ДокументыУдостоверяющиеЛичность"));
	УстановитьРеквизитыВопроса(Менеджер.НомерДокумента,						"Номер",			ВВидеСтроки,,,, Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(14)), 14);
	УстановитьРеквизитыВопроса(Менеджер.СерияДокумента,						"Серия",			ВВидеСтроки,,,, Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(14)), 14);
	УстановитьРеквизитыВопроса(Менеджер.ДатаВыдачиДокумента,				"Дата выдачи",		ВВидеДаты,,,, ОписаниеТиповДата);
	УстановитьРеквизитыВопроса(Менеджер.КемВыданДокумент,					"Кем выдан",		ВВидеСтроки,,,, Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(1000)));
	УстановитьРеквизитыВопроса(Менеджер.КодПодразделенияДокумента,			"Код подразделения",ВВидеСтроки,,,, Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(15)), 15);

	// ВоинскийУчет
	УстановитьРеквизитыВопроса(Менеджер.ОтношениеКВоинскойОбязанности,	"Воинская обязанность",		ВВидеСсылки,,,, Новый ОписаниеТипов("ПеречислениеСсылка.ОтношениеКВоинскойОбязанности"));
	УстановитьРеквизитыВопроса(Менеджер.ОтношениеКВоинскомуУчету,		"Воинский учет",			ВВидеСсылки,,,, Новый ОписаниеТипов("ПеречислениеСсылка.ОтношениеКВоинскомуУчету"));
	УстановитьРеквизитыВопроса(Менеджер.КатегорияЗапаса,				"Категория запаса",			ВВидеСсылки,,,, Новый ОписаниеТипов("ПеречислениеСсылка.КатегорииЗапасаВоеннообязанных"));
	УстановитьРеквизитыВопроса(Менеджер.Звание,							"Звание",					ВВидеСсылки,,,, Новый ОписаниеТипов("СправочникСсылка.ЗваниеГражданскогоВоинскогоУчета"));
	УстановитьРеквизитыВопроса(Менеджер.Состав,							"Состав (профиль)",			ВВидеСсылки,,,, Новый ОписаниеТипов("СправочникСсылка.СоставыВоеннослужащих"));
	УстановитьРеквизитыВопроса(Менеджер.ВУС,							"ВУС",						ВВидеСтроки,,,, Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(10)), 10);
	УстановитьРеквизитыВопроса(Менеджер.Годность,						"Годность",					ВВидеСсылки,,,, Новый ОписаниеТипов("ПеречислениеСсылка.ГодностьКВоеннойСлужбе"));
	УстановитьРеквизитыВопроса(Менеджер.Военкомат,						"Наименование военкомата",	ВВидеСсылки,,,, Новый ОписаниеТипов("СправочникСсылка.Военкоматы"));
	УстановитьРеквизитыВопроса(Менеджер.НаличиеМобпредписания,			"Есть ли мобпредписание",	ВВидеБулево,,,, ОписаниеТиповБулево);
	УстановитьРеквизитыВопроса(Менеджер.НомерКомандыПартии,				"Номер команды, партии",	ВВидеСтроки,,,, Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(50)), 50);

	// табличные вопросы
	УстановитьРеквизитыВопроса(Менеджер.ТЗнаниеЯзыков,					"Знание языков",			ТабличныйВопрос,, 4,, Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(200)), 200);
	УстановитьРеквизитыВопроса(Менеджер.ТОбразование,					"Образование",				ТабличныйВопрос,, 4,, Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(200)), 200);
	УстановитьРеквизитыВопроса(Менеджер.ТСоставСемьи,					"Состав семьи",				ТабличныйВопрос,, 4,, Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(200)), 200);
	УстановитьРеквизитыВопроса(Менеджер.ТТрудоваяДеятельность,			"Трудовая деятельность",	ТабличныйВопрос,, 4,, Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(200)), 200);
	
	Если ЗаполнятьТабличныеВопросы Тогда
		// табличный вопрос образование
		ОбъектПВХВопросыДляАнкетирования = ПланыВидовХарактеристик.ВопросыДляАнкетирования.ТОбразование.ПолучитьОбъект();
		Если ОбъектПВХВопросыДляАнкетирования.КолонкиТаблицы.Количество() = 0 Тогда
			
			НоваяКолонка = ОбъектПВХВопросыДляАнкетирования.КолонкиТаблицы.Добавить();
			НоваяКолонка.КолонкаТаблицы = ПланыВидовХарактеристик.ВопросыДляАнкетирования.ВидОбразования;
			НоваяКолонка = ОбъектПВХВопросыДляАнкетирования.КолонкиТаблицы.Добавить();
			НоваяКолонка.КолонкаТаблицы = ПланыВидовХарактеристик.ВопросыДляАнкетирования.УчебноеЗаведение;
			НоваяКолонка = ОбъектПВХВопросыДляАнкетирования.КолонкиТаблицы.Добавить();
			НоваяКолонка.КолонкаТаблицы = ПланыВидовХарактеристик.ВопросыДляАнкетирования.Специальность;
			НоваяКолонка = ОбъектПВХВопросыДляАнкетирования.КолонкиТаблицы.Добавить();
			НоваяКолонка.КолонкаТаблицы = ПланыВидовХарактеристик.ВопросыДляАнкетирования.ДипломСерияНомер;
			НоваяКолонка = ОбъектПВХВопросыДляАнкетирования.КолонкиТаблицы.Добавить();
			НоваяКолонка.КолонкаТаблицы = ПланыВидовХарактеристик.ВопросыДляАнкетирования.ГодОкончания;
			НоваяКолонка = ОбъектПВХВопросыДляАнкетирования.КолонкиТаблицы.Добавить();
			НоваяКолонка.КолонкаТаблицы = ПланыВидовХарактеристик.ВопросыДляАнкетирования.Квалификация;
			ОбъектПВХВопросыДляАнкетирования.КоличествоСтрокТаблицы = 4;
			ОбъектПВХВопросыДляАнкетирования.Записать();
			
		КонецЕсли; 
		
		// табличный вопрос знание языков
		ОбъектПВХВопросыДляАнкетирования = ПланыВидовХарактеристик.ВопросыДляАнкетирования.ТЗнаниеЯзыков.ПолучитьОбъект();
		Если ОбъектПВХВопросыДляАнкетирования.КолонкиТаблицы.Количество() = 0 Тогда
			
			НоваяКолонка = ОбъектПВХВопросыДляАнкетирования.КолонкиТаблицы.Добавить();
			НоваяКолонка.КолонкаТаблицы = ПланыВидовХарактеристик.ВопросыДляАнкетирования.Язык;
			НоваяКолонка = ОбъектПВХВопросыДляАнкетирования.КолонкиТаблицы.Добавить();
			НоваяКолонка.КолонкаТаблицы = ПланыВидовХарактеристик.ВопросыДляАнкетирования.СтепеньЗнанияЯзыка;
			ОбъектПВХВопросыДляАнкетирования.КоличествоСтрокТаблицы = 4;
			ОбъектПВХВопросыДляАнкетирования.Записать();
			
		КонецЕсли; 
		
		// табличный вопрос состав семьи
		ОбъектПВХВопросыДляАнкетирования = ПланыВидовХарактеристик.ВопросыДляАнкетирования.ТСоставСемьи.ПолучитьОбъект();
		Если ОбъектПВХВопросыДляАнкетирования.КолонкиТаблицы.Количество() = 0 Тогда
			
			НоваяКолонка = ОбъектПВХВопросыДляАнкетирования.КолонкиТаблицы.Добавить();
			НоваяКолонка.КолонкаТаблицы = ПланыВидовХарактеристик.ВопросыДляАнкетирования.СоставСемьиСтепеньРодства;
			НоваяКолонка = ОбъектПВХВопросыДляАнкетирования.КолонкиТаблицы.Добавить();
			НоваяКолонка.КолонкаТаблицы = ПланыВидовХарактеристик.ВопросыДляАнкетирования.СоставСемьиРодственник;
			НоваяКолонка = ОбъектПВХВопросыДляАнкетирования.КолонкиТаблицы.Добавить();
			НоваяКолонка.КолонкаТаблицы = ПланыВидовХарактеристик.ВопросыДляАнкетирования.СоставСемьиДатаРождения;
			ОбъектПВХВопросыДляАнкетирования.КоличествоСтрокТаблицы = 4;
			ОбъектПВХВопросыДляАнкетирования.Записать();
			
		КонецЕсли; 
		
		// табличный вопрос трудовая деятельность
		ОбъектПВХВопросыДляАнкетирования = ПланыВидовХарактеристик.ВопросыДляАнкетирования.ТТрудоваяДеятельность.ПолучитьОбъект();
		Если ОбъектПВХВопросыДляАнкетирования.КолонкиТаблицы.Количество() = 0 Тогда
			
			НоваяКолонка = ОбъектПВХВопросыДляАнкетирования.КолонкиТаблицы.Добавить();
			НоваяКолонка.КолонкаТаблицы = ПланыВидовХарактеристик.ВопросыДляАнкетирования.ТрудоваяДеятельностьОрганизация;
			НоваяКолонка = ОбъектПВХВопросыДляАнкетирования.КолонкиТаблицы.Добавить();
			НоваяКолонка.КолонкаТаблицы = ПланыВидовХарактеристик.ВопросыДляАнкетирования.ТрудоваяДеятельностьНачалоРаботы;
			НоваяКолонка = ОбъектПВХВопросыДляАнкетирования.КолонкиТаблицы.Добавить();
			НоваяКолонка.КолонкаТаблицы = ПланыВидовХарактеристик.ВопросыДляАнкетирования.ТрудоваяДеятельностьОкончаниеРаботы;
			НоваяКолонка = ОбъектПВХВопросыДляАнкетирования.КолонкиТаблицы.Добавить();
			НоваяКолонка.КолонкаТаблицы = ПланыВидовХарактеристик.ВопросыДляАнкетирования.ТрудоваяДеятельностьДолжность;
			ОбъектПВХВопросыДляАнкетирования.КоличествоСтрокТаблицы = 4;
			ОбъектПВХВопросыДляАнкетирования.Записать();
			
		КонецЕсли; 
		
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьРеквизитыВопросов()

// Устанавливает реквизиты элемента ПВХ "ВопросыДляАнкетирования"
// Запускается при обновлении на 1.2.15 и при первоначальном заполнении пустой ИБ.
//
Процедура УстановитьРеквизитыВопроса(Вопрос, ПолнаяФормулировка, ТипыОтветаНаВопросАнкеты, БолееОдногоОтвета = Ложь, КоличествоСтрок = 0, ВидКонтактнойИнформации = 0, ТипЗначения = Неопределено, Длина = 0)
	
	Объект = Вопрос.ПолучитьОбъект();
	
	Объект.ПолнаяФормулировка		= ПолнаяФормулировка;
	Объект.ТипОтветаНаВопрос		= ТипыОтветаНаВопросАнкеты;
	Объект.КоличествоСтрокТаблицы	= КоличествоСтрок;
	Объект.ВидКонтактнойИнформации	= ВидКонтактнойИнформации;
	
	Если ТипЗначения <> Неопределено Тогда
		Объект.ТипЗначения				= ТипЗначения;
	КонецЕсли;
	Объект.Длина						= Длина;
	
	Объект.Записать();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// 2.5.35

Процедура УточнитьНаименованиеСпособаОтраженияБольничныйЗаСчетРаботодателя() Экспорт

	СпособОтражения = Справочники.СпособыОтраженияЗарплатыВРеглУчете.БольничныйЗаСчетРаботодателя;
	Наименование = СОКРЛП(СпособОтражения.Наименование);
	ПостфиксНаименования = "(не используется с 2011 года)";
	Если Найти(Наименование,ПостфиксНаименования) = 0 Тогда
		Наименование = Наименование + " " +ПостфиксНаименования;
		СпособОтраженияОбъект = СпособОтражения.ПолучитьОбъект();
		СпособОтраженияОбъект.Наименование = Наименование;
		СпособОтраженияОбъект.Записать();
	КонецЕсли;	

КонецПроцедуры
	
////////////////////////////////////////////////////////////////////////////////
// 2.5.84

Процедура ВписатьОснованиеВУчетСтраховыхВзносов() Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СтраховыеВзносыСведенияОДоходах.Регистратор
	|ПОМЕСТИТЬ ВТРегистраторыКОбработке
	|ИЗ
	|	РегистрНакопления.СтраховыеВзносыСведенияОДоходах КАК СтраховыеВзносыСведенияОДоходах
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РасчетСтраховыхВзносов КАК РасчетСтраховыхВзносов
	|		ПО СтраховыеВзносыСведенияОДоходах.Регистратор = РасчетСтраховыхВзносов.Ссылка
	|ГДЕ
	|	СтраховыеВзносыСведенияОДоходах.Период >= ДАТАВРЕМЯ(2014, 1, 1, 0, 0, 0)
	|	И СтраховыеВзносыСведенияОДоходах.ДатаПолученияДохода = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|	И РасчетСтраховыхВзносов.Ссылка ЕСТЬ НЕ NULL 
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Основные.Ссылка.ПериодРегистрации КАК Период,
	|	Основные.Ссылка КАК Регистратор,
	|	Основные.ФизЛицо,
	|	ВЫБОР
	|		КОГДА Основные.Ссылка.Организация.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА Основные.Ссылка.Организация
	|		ИНАЧЕ Основные.Ссылка.Организация.ГоловнаяОрганизация
	|	КОНЕЦ КАК Организация,
	|	Основные.ВидДохода,
	|	Основные.Ссылка.Организация КАК ОбособленноеПодразделение,
	|	НАЧАЛОПЕРИОДА(КОНЕЦПЕРИОДА(Основные.Ссылка.ПериодРегистрации, МЕСЯЦ), ДЕНЬ) КАК ДатаПолученияДохода,
	|	Основные.ОблагаетсяЕНВД,
	|	Основные.ОблагаетсяПоДополнительномуТарифу,
	|	Основные.ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам,
	|	Основные.ЯвляетсяДоходомФармацевта,
	|	Основные.ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ,
	|	Основные.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
	|	Основные.КлассУсловийТруда,
	|	Основные.Результат,
	|	0 КАК Скидка,
	|	Основные.ВидРасчета,
	|	Основные.ДокументОснование
	|ИЗ
	|	Документ.РасчетСтраховыхВзносов.ОсновныеНачисления КАК Основные
	|ГДЕ
	|	Основные.Ссылка В
	|			(ВЫБРАТЬ
	|				РегистраторыКОбработке.Регистратор
	|			ИЗ
	|				ВТРегистраторыКОбработке КАК РегистраторыКОбработке)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Дополнительные.Ссылка.ПериодРегистрации,
	|	Дополнительные.Ссылка,
	|	Дополнительные.ФизЛицо,
	|	ВЫБОР
	|		КОГДА Дополнительные.Ссылка.Организация.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА Дополнительные.Ссылка.Организация
	|		ИНАЧЕ Дополнительные.Ссылка.Организация.ГоловнаяОрганизация
	|	КОНЕЦ,
	|	Дополнительные.ВидДохода,
	|	Дополнительные.Ссылка.Организация,
	|	НАЧАЛОПЕРИОДА(КОНЕЦПЕРИОДА(Дополнительные.Ссылка.ПериодРегистрации, МЕСЯЦ), ДЕНЬ),
	|	Дополнительные.ОблагаетсяЕНВД,
	|	Дополнительные.ОблагаетсяПоДополнительномуТарифу,
	|	Дополнительные.ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам,
	|	Дополнительные.ЯвляетсяДоходомФармацевта,
	|	Дополнительные.ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ,
	|	Дополнительные.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
	|	Дополнительные.КлассУсловийТруда,
	|	Дополнительные.Результат,
	|	0,
	|	Дополнительные.ВидРасчета,
	|	Дополнительные.ДокументОснование
	|ИЗ
	|	Документ.РасчетСтраховыхВзносов.ДополнительныеНачисления КАК Дополнительные
	|ГДЕ
	|	Дополнительные.Ссылка В
	|			(ВЫБРАТЬ
	|				РегистраторыКОбработке.Регистратор
	|			ИЗ
	|				ВТРегистраторыКОбработке КАК РегистраторыКОбработке)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Пособия.Ссылка.ПериодРегистрации,
	|	Пособия.Ссылка,
	|	Пособия.ФизЛицо,
	|	ВЫБОР
	|		КОГДА Пособия.Ссылка.Организация.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА Пособия.Ссылка.Организация
	|		ИНАЧЕ Пособия.Ссылка.Организация.ГоловнаяОрганизация
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА Пособия.ВидПособияСоциальногоСтрахования В (ЗНАЧЕНИЕ(Перечисление.ВидыПособийСоциальногоСтрахования.НетрудоспособностьНесчастныйСлучай), ЗНАЧЕНИЕ(Перечисление.ВидыПособийСоциальногоСтрахования.НетрудоспособностьПрофзаболевание), ЗНАЧЕНИЕ(Перечисление.ВидыПособийСоциальногоСтрахования.ДополнительныйОтпускПослеНесчастныхСлучаев))
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.НеОблагаетсяЦеликом)
	|		КОГДА Пособия.ВидПособияСоциальногоСтрахования = ЗНАЧЕНИЕ(Перечисление.ВидыПособийСоциальногоСтрахования.ДополнительныеВыходныеДниПоУходуЗаДетьмиИнвалидами)
	|			ТОГДА Пособия.ВидРасчета.КодДоходаСтраховыеВзносы
	|		КОГДА Пособия.СпособФинансированияПособий = ЗНАЧЕНИЕ(Перечисление.СпособыФинансированияПособийСоцстрахования.ЗаСчетФСС)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.ПособияЗаСчетФСС)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.НеОблагаетсяЦеликом)
	|	КОНЕЦ,
	|	Пособия.Ссылка.Организация,
	|	НАЧАЛОПЕРИОДА(КОНЕЦПЕРИОДА(Пособия.Ссылка.ПериодРегистрации, МЕСЯЦ), ДЕНЬ),
	|	Пособия.ОблагаетсяЕНВД,
	|	Пособия.ОблагаетсяПоДополнительномуТарифу,
	|	Пособия.ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам,
	|	Пособия.ЯвляетсяДоходомФармацевта,
	|	Пособия.ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ,
	|	Пособия.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
	|	Пособия.КлассУсловийТруда,
	|	Пособия.СуммаВсего,
	|	0,
	|	Пособия.ВидРасчета,
	|	Пособия.ДокументОснование
	|ИЗ
	|	Документ.РасчетСтраховыхВзносов.ПособияПоСоциальномуСтрахованию КАК Пособия
	|ГДЕ
	|	Пособия.Ссылка В
	|			(ВЫБРАТЬ
	|				РегистраторыКОбработке.Регистратор
	|			ИЗ
	|				ВТРегистраторыКОбработке КАК РегистраторыКОбработке)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Пособия.Ссылка.ПериодРегистрации,
	|	Пособия.Ссылка,
	|	Пособия.ФизЛицо,
	|	ВЫБОР
	|		КОГДА Пособия.Ссылка.Организация.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА Пособия.Ссылка.Организация
	|		ИНАЧЕ Пособия.Ссылка.Организация.ГоловнаяОрганизация
	|	КОНЕЦ,
	|	ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.ПособияЗаСчетФСС),
	|	Пособия.Ссылка.Организация,
	|	НАЧАЛОПЕРИОДА(КОНЕЦПЕРИОДА(Пособия.Ссылка.ПериодРегистрации, МЕСЯЦ), ДЕНЬ),
	|	Пособия.ОблагаетсяЕНВД,
	|	Пособия.ОблагаетсяПоДополнительномуТарифу,
	|	Пособия.ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам,
	|	Пособия.ЯвляетсяДоходомФармацевта,
	|	Пособия.ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ,
	|	Пособия.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
	|	Пособия.КлассУсловийТруда,
	|	Пособия.ПособиеПоУходуЗаПервымРебенком + Пособия.ПособиеПоУходуЗаВторымРебенком,
	|	0,
	|	Пособия.ВидРасчета,
	|	Пособия.ДокументОснование
	|ИЗ
	|	Документ.РасчетСтраховыхВзносов.ПособияПоУходуЗаРебенкомДоПолутораЛет КАК Пособия
	|ГДЕ
	|	Пособия.Ссылка В
	|			(ВЫБРАТЬ
	|				РегистраторыКОбработке.Регистратор
	|			ИЗ
	|				ВТРегистраторыКОбработке КАК РегистраторыКОбработке)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СтраховыеВзносыСведенияОДоходах.Период,
	|	СтраховыеВзносыСведенияОДоходах.Регистратор,
	|	СтраховыеВзносыСведенияОДоходах.ФизЛицо,
	|	СтраховыеВзносыСведенияОДоходах.Организация,
	|	СтраховыеВзносыСведенияОДоходах.ВидДохода,
	|	СтраховыеВзносыСведенияОДоходах.ОбособленноеПодразделение,
	|	НАЧАЛОПЕРИОДА(КОНЕЦПЕРИОДА(СтраховыеВзносыСведенияОДоходах.Период, МЕСЯЦ), ДЕНЬ),
	|	СтраховыеВзносыСведенияОДоходах.ОблагаетсяЕНВД,
	|	СтраховыеВзносыСведенияОДоходах.ОблагаетсяПоДополнительномуТарифу,
	|	СтраховыеВзносыСведенияОДоходах.ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам,
	|	СтраховыеВзносыСведенияОДоходах.ЯвляетсяДоходомФармацевта,
	|	СтраховыеВзносыСведенияОДоходах.ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ,
	|	СтраховыеВзносыСведенияОДоходах.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
	|	СтраховыеВзносыСведенияОДоходах.КлассУсловийТруда,
	|	0,
	|	СтраховыеВзносыСведенияОДоходах.Скидка,
	|	СтраховыеВзносыСведенияОДоходах.ВидРасчета,
	|	СтраховыеВзносыСведенияОДоходах.ДокументОснование
	|ИЗ
	|	РегистрНакопления.СтраховыеВзносыСведенияОДоходах КАК СтраховыеВзносыСведенияОДоходах
	|ГДЕ
	|	СтраховыеВзносыСведенияОДоходах.Регистратор В
	|			(ВЫБРАТЬ
	|				РегистраторыКОбработке.Регистратор
	|			ИЗ
	|				ВТРегистраторыКОбработке КАК РегистраторыКОбработке)
	|	И СтраховыеВзносыСведенияОДоходах.Скидка <> 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	Регистратор";
	
	ПроцедурыОбновленияИнформационнойБазы.ПереписатьНаборыЗаписейПоРегистраторам(РегистрыНакопления.СтраховыеВзносыСведенияОДоходах, Запрос, "Обработка учета страховых взносов, часть I");	
	
КонецПроцедуры

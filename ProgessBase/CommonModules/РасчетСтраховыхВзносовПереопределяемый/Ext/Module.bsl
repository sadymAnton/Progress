
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ И ЗАПИСИ ДОКУМЕНТА

Процедура ДополнительныеДействияОбработкиПроведения(ДокументОбъект, ВыборкаПоШапкеДокумента, Заголовок, Отказ) Экспорт

	ДокументОбъект.Движения.ПособияПоУходуЗаРебенкомДоПолутораЛет.мВыполнятьДополнительныеДвижения	= Истина;
	
	Для каждого СтрокаТЧ Из ДокументОбъект.ПособияПоСоциальномуСтрахованию Цикл
		// проверим очередную строку табличной части
		РасчетСтраховыхВзносовДополнительный.ПроверитьЗаполнениеСтрокиПособияСоциальномуСтрахованию(СтрокаТЧ, Отказ, Заголовок);
	КонецЦикла; 
	
	Если НЕ Отказ Тогда
		Выборка = РасчетСтраховыхВзносовДополнительный.СформироватьЗапросПоПособияСоциальномуСтрахованию(ДокументОбъект.Ссылка, ВыборкаПоШапкеДокумента).Выбрать();
		Пока Выборка.Следующий() Цикл 
			ЗаполнитьЗначенияСвойств(ДокументОбъект.Движения.ПособияСоциальномуСтрахованию.Добавить(), Выборка);
		КонецЦикла;
	КонецЕсли;
	
	Для каждого СтрокаТЧ Из ДокументОбъект.ПособияПоУходуЗаРебенкомДоПолутораЛет Цикл
		// проверим очередную строку табличной части
		РасчетСтраховыхВзносовДополнительный.ПроверитьЗаполнениеСтрокиПособияПоУходуЗаРебенкомДоПолутораЛет(СтрокаТЧ, Отказ, Заголовок);
	КонецЦикла; 
	
	Если НЕ Отказ Тогда
		Выборка = РасчетСтраховыхВзносовДополнительный.СформироватьЗапросПоПособияПоУходуЗаРебенкомДоПолутораЛет(ДокументОбъект.Ссылка, ВыборкаПоШапкеДокумента).Выбрать();
		Пока Выборка.Следующий() Цикл 
			ЗаполнитьЗначенияСвойств(ДокументОбъект.Движения.ПособияПоУходуЗаРебенкомДоПолутораЛет.Добавить(),Выборка);
		КонецЦикла;
	КонецЕсли;
	
	СформироватьДвиженияПоДоходам(ДокументОбъект, ВыборкаПоШапкеДокумента, Заголовок, Отказ);

КонецПроцедуры

Процедура СформироватьДвиженияПоДоходам(ДокументОбъект, ВыборкаПоШапкеДокумента, Заголовок, Отказ, НаборЗаписей = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("ГоловнаяОрганизация",	ВыборкаПоШапкеДокумента.Организация);
	Запрос.УстановитьПараметр("Организация",			ВыборкаПоШапкеДокумента.ОбособленноеПодразделение);
	Запрос.УстановитьПараметр("ОсновныеНачисления",		ДокументОбъект.ОсновныеНачисления);
	Запрос.УстановитьПараметр("ПособияСоциальномуСтрахованию", ДокументОбъект.ПособияПоСоциальномуСтрахованию);
	Запрос.УстановитьПараметр("ПособияПоУходуЗаРебенкомДоПолутораЛет", ДокументОбъект.ПособияПоУходуЗаРебенкомДоПолутораЛет);
	Запрос.УстановитьПараметр("ДополнительныеНачисления",ДокументОбъект.ДополнительныеНачисления);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Основные.НомерСтроки,
	|	Основные.ВидРасчета,
	|	Основные.Сотрудник КАК Сотрудник,
	|	Основные.ВидДохода КАК КодДоходаЕСН,
	|	Основные.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
	|	Основные.ОблагаетсяПоДополнительномуТарифу,
	|	Основные.ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам,
	|	Основные.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
	|	Основные.КлассУсловийТруда,
	|	Основные.ЯвляетсяДоходомФармацевта,
	|	Основные.ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ,
	|	Основные.Результат,
	|	Основные.ПериодДействияНачало КАК ПериодДействияНачало,
	|	ВЫБОР
	|		КОГДА Основные.ПериодДействияКонец <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА КОНЕЦПЕРИОДА(Основные.ПериодДействияКонец, ДЕНЬ)
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|	КОНЕЦ КАК ПериодДействияКонец,
	|	Основные.Сторно КАК Сторно,
	|	Основные.ДокументОснование
	|ПОМЕСТИТЬ ВТОсновныеНачисления
	|ИЗ
	|	&ОсновныеНачисления КАК Основные
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник,
	|	ПериодДействияНачало";
	Запрос.Выполнить();
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Дополнительные.НомерСтроки,
	|	Дополнительные.ВидРасчета,
	|	Дополнительные.Сотрудник,
	|	Дополнительные.ВидДохода КАК КодДоходаЕСН,
	|	Дополнительные.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
	|	Дополнительные.ОблагаетсяПоДополнительномуТарифу,
	|	Дополнительные.ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам,
	|	Дополнительные.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
	|	Дополнительные.КлассУсловийТруда,
	|	Дополнительные.ЯвляетсяДоходомФармацевта,
	|	Дополнительные.ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ,
	|	Дополнительные.Результат,
	|	Дополнительные.Сторно,
	|	Дополнительные.ДокументОснование
	|ПОМЕСТИТЬ ВТДополнительныеНачисления
	|ИЗ
	|	&ДополнительныеНачисления КАК Дополнительные";
	Запрос.Выполнить();
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Пособия.НомерСтроки,
	|	Пособия.ВидРасчета,
	|	Пособия.Сотрудник КАК Сотрудник,
	|	Пособия.ВидПособияСоциальногоСтрахования,
	|	Пособия.СпособФинансированияПособий,
	|	Пособия.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
	|	Пособия.ОблагаетсяПоДополнительномуТарифу,
	|	Пособия.ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам,
	|	Пособия.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
	|	Пособия.КлассУсловийТруда,
	|	Пособия.ЯвляетсяДоходомФармацевта,
	|	Пособия.ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ,
	|	Пособия.СуммаВсего,
	|	Пособия.ПериодДействияНачало КАК ПериодДействияНачало,
	|	ВЫБОР
	|		КОГДА Пособия.ПериодДействияКонец <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА КОНЕЦПЕРИОДА(Пособия.ПериодДействияКонец, ДЕНЬ)
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|	КОНЕЦ КАК ПериодДействияКонец,
	|	Пособия.Сторно,
	|	Пособия.ДокументОснование
	|ПОМЕСТИТЬ ВТПособияСоциальномуСтрахованию
	|ИЗ
	|	&ПособияСоциальномуСтрахованию КАК Пособия
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник,
	|	ПериодДействияНачало";
	Запрос.Выполнить();
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПособияПоУходу.НомерСтроки,
	|	ПособияПоУходу.ВидРасчета,
	|	ПособияПоУходу.Сотрудник КАК Сотрудник,
	|	ПособияПоУходу.ОблагаетсяЕНВД ОблагаетсяЕНВД,
	|	ПособияПоУходу.ОблагаетсяПоДополнительномуТарифу,
	|	ПособияПоУходу.ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам,
	|	ПособияПоУходу.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
	|	ПособияПоУходу.КлассУсловийТруда,
	|	ПособияПоУходу.ЯвляетсяДоходомФармацевта,
	|	ПособияПоУходу.ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ,
	|	ПособияПоУходу.ПособиеПоУходуЗаПервымРебенком,
	|	ПособияПоУходу.ПособиеПоУходуЗаВторымРебенком,
	|	ПособияПоУходу.ПериодДействияНачало КАК ПериодДействияНачало,
	|	ВЫБОР
	|		КОГДА ПособияПоУходу.ПериодДействияКонец <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА КОНЕЦПЕРИОДА(ПособияПоУходу.ПериодДействияКонец, ДЕНЬ)
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|	КОНЕЦ КАК ПериодДействияКонец,
	|	ПособияПоУходу.Сторно,
	|	ПособияПоУходу.ДокументОснование
	|ПОМЕСТИТЬ ВТПособияПоУходуЗаРебенкомДоПолутораЛет
	|ИЗ
	|	&ПособияПоУходуЗаРебенкомДоПолутораЛет КАК ПособияПоУходу
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник,
	|	ПериодДействияНачало";
	Запрос.Выполнить();
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Основные.ПериодДействияНачало КАК ПериодДействияНачало,
	|	Основные.Сотрудник КАК Сотрудник
	|ПОМЕСТИТЬ ВТСотрудники
	|ИЗ
	|	ВТОсновныеНачисления КАК Основные
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Пособия.ПериодДействияНачало,
	|	Пособия.Сотрудник
	|ИЗ
	|	ВТПособияСоциальномуСтрахованию КАК Пособия
	|ГДЕ
	|	Пособия.ВидРасчета ССЫЛКА ПланВидовРасчета.ОсновныеНачисленияОрганизаций
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПособияПоУходу.ПериодДействияНачало,
	|	ПособияПоУходу.Сотрудник
	|ИЗ
	|	ВТПособияПоУходуЗаРебенкомДоПолутораЛет КАК ПособияПоУходу
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник,
	|	ПериодДействияНачало
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РаботникиОрганизацииСрез.Сотрудник КАК Сотрудник,
	|	РаботникиОрганизацииСрез.ПериодДействияНачало КАК ПериодДействияНачало,
	|	ВЫБОР
	|		КОГДА РаботникиОрганизации.ПериодЗавершения <= РаботникиОрганизацииСрез.ПериодДействияНачало
	|				И РаботникиОрганизации.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА РаботникиОрганизации.ГрафикРаботыЗавершения.СуммированныйУчетРабочегоВремени
	|		ИНАЧЕ РаботникиОрганизации.ГрафикРаботы.СуммированныйУчетРабочегоВремени
	|	КОНЕЦ КАК СуммированныйУчетРабочегоВремени,
	|	ВЫБОР
	|		КОГДА РаботникиОрганизации.ПериодЗавершения <= РаботникиОрганизацииСрез.ПериодДействияНачало
	|				И РаботникиОрганизации.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА РаботникиОрганизации.ГрафикРаботыЗавершения
	|		ИНАЧЕ РаботникиОрганизации.ГрафикРаботы
	|	КОНЕЦ КАК ГрафикРаботы
	|ПОМЕСТИТЬ ВТДанныеСотрудников
	|ИЗ
	|	(ВЫБРАТЬ
	|		МАКСИМУМ(РаботникиОрганизации.Период) КАК ПериодСреза,
	|		Основные.Сотрудник КАК Сотрудник,
	|		Основные.ПериодДействияНачало КАК ПериодДействияНачало
	|	ИЗ
	|		ВТСотрудники КАК Основные
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизации
	|			ПО Основные.ПериодДействияНачало >= РаботникиОрганизации.Период
	|				И Основные.Сотрудник = РаботникиОрганизации.Сотрудник
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Основные.Сотрудник,
	|		Основные.ПериодДействияНачало) КАК РаботникиОрганизацииСрез
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизации
	|		ПО РаботникиОрганизацииСрез.ПериодСреза = РаботникиОрганизации.Период
	|			И РаботникиОрганизацииСрез.Сотрудник = РаботникиОрганизации.Сотрудник
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник,
	|	ПериодДействияНачало
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	""Основные начисления"" КАК ТипСтроки,
	|	Основные.НомерСтроки,
	|	Основные.ВидРасчета.СпособРасчета КАК СпособРасчета,
	|	Основные.ВидРасчета КАК ВидРасчета,
	|	Основные.Сотрудник КАК Сотрудник,
	|	Основные.Сотрудник.Физлицо КАК Физлицо,
	|	Основные.КодДоходаЕСН,
	|	Основные.КодДоходаЕСН КАК ВидДохода,
	|	Основные.ОблагаетсяЕНВД,
	|	Основные.ОблагаетсяПоДополнительномуТарифу,
	|	Основные.ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам,
	|	Основные.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
	|	Основные.КлассУсловийТруда,
	|	Основные.ЯвляетсяДоходомФармацевта,
	|	Основные.ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ,
	|	Основные.Результат,
	|	0 КАК Скидка,
	|	Основные.ПериодДействияНачало КАК ПериодДействияНачало,
	|	Основные.ПериодДействияКонец,
	|	Основные.ВидРасчета.ВидВремени КАК ВидВремени,
	|	ЕСТЬNULL(РаботникиОрганизацииСрез.СуммированныйУчетРабочегоВремени, ЛОЖЬ) КАК СуммированныйУчетРабочегоВремени,
	|	РаботникиОрганизацииСрез.ГрафикРаботы КАК ГрафикРаботы,
	|	ВЫБОР
	|		КОГДА Основные.Сотрудник.Организация = &ГоловнаяОрганизация
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ОшибкаНеСоответствиеСотрудникаИОрганизации,
	|	Основные.Сторно КАК Сторно,
	|	Основные.ДокументОснование
	|ПОМЕСТИТЬ ВТНачисления
	|ИЗ
	|	ВТОсновныеНачисления КАК Основные
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеСотрудников КАК РаботникиОрганизацииСрез
	|		ПО Основные.Сотрудник = РаботникиОрганизацииСрез.Сотрудник
	|			И Основные.ПериодДействияНачало = РаботникиОрганизацииСрез.ПериодДействияНачало
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""Дополнительные начисления"",
	|	Дополнительные.НомерСтроки,
	|	Дополнительные.ВидРасчета.СпособРасчета,
	|	Дополнительные.ВидРасчета,
	|	Дополнительные.Сотрудник,
	|	Дополнительные.Сотрудник.Физлицо,
	|	Дополнительные.КодДоходаЕСН,
	|	Дополнительные.КодДоходаЕСН,
	|	Дополнительные.ОблагаетсяЕНВД,
	|	Дополнительные.ОблагаетсяПоДополнительномуТарифу,
	|	Дополнительные.ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам,
	|	Дополнительные.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
	|	Дополнительные.КлассУсловийТруда,
	|	Дополнительные.ЯвляетсяДоходомФармацевта,
	|	Дополнительные.ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ,
	|	Дополнительные.Результат,
	|	0,
	|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0),
	|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0),
	|	NULL,
	|	NULL,
	|	NULL,
	|	ВЫБОР
	|		КОГДА Дополнительные.Сотрудник.Организация = &ГоловнаяОрганизация
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ,
	|	Дополнительные.Сторно,
	|	Дополнительные.ДокументОснование
	|ИЗ
	|	ВТДополнительныеНачисления КАК Дополнительные
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""Пособия по социальному страхованию (основные)"",
	|	Пособия.НомерСтроки,
	|	Пособия.ВидРасчета.СпособРасчета,
	|	Пособия.ВидРасчета,
	|	Пособия.Сотрудник,
	|	Пособия.Сотрудник.Физлицо,
	|	ВЫБОР
	|		КОГДА Пособия.ВидПособияСоциальногоСтрахования В (ЗНАЧЕНИЕ(Перечисление.ВидыПособийСоциальногоСтрахования.НетрудоспособностьНесчастныйСлучай), ЗНАЧЕНИЕ(Перечисление.ВидыПособийСоциальногоСтрахования.НетрудоспособностьПрофзаболевание), ЗНАЧЕНИЕ(Перечисление.ВидыПособийСоциальногоСтрахования.ДополнительныйОтпускПослеНесчастныхСлучаев))
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.НеОблагаетсяЦеликом)
	|		КОГДА Пособия.ВидПособияСоциальногоСтрахования = ЗНАЧЕНИЕ(Перечисление.ВидыПособийСоциальногоСтрахования.ДополнительныеВыходныеДниПоУходуЗаДетьмиИнвалидами)
	|			ТОГДА Пособия.ВидРасчета.КодДоходаСтраховыеВзносы
	|		КОГДА Пособия.СпособФинансированияПособий = ЗНАЧЕНИЕ(Перечисление.СпособыФинансированияПособийСоцстрахования.ЗаСчетФСС)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.ПособияЗаСчетФСС)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.НеОблагаетсяЦеликом)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА Пособия.ВидПособияСоциальногоСтрахования В (ЗНАЧЕНИЕ(Перечисление.ВидыПособийСоциальногоСтрахования.НетрудоспособностьНесчастныйСлучай), ЗНАЧЕНИЕ(Перечисление.ВидыПособийСоциальногоСтрахования.НетрудоспособностьПрофзаболевание), ЗНАЧЕНИЕ(Перечисление.ВидыПособийСоциальногоСтрахования.ДополнительныйОтпускПослеНесчастныхСлучаев))
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.НеОблагаетсяЦеликом)
	|		КОГДА Пособия.ВидПособияСоциальногоСтрахования = ЗНАЧЕНИЕ(Перечисление.ВидыПособийСоциальногоСтрахования.ДополнительныеВыходныеДниПоУходуЗаДетьмиИнвалидами)
	|			ТОГДА Пособия.ВидРасчета.КодДоходаСтраховыеВзносы
	|		КОГДА Пособия.СпособФинансированияПособий = ЗНАЧЕНИЕ(Перечисление.СпособыФинансированияПособийСоцстрахования.ЗаСчетФСС)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.ПособияЗаСчетФСС)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.НеОблагаетсяЦеликом)
	|	КОНЕЦ,
	|	Пособия.ОблагаетсяЕНВД,
	|	Пособия.ОблагаетсяПоДополнительномуТарифу,
	|	Пособия.ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам,
	|	Пособия.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
	|	Пособия.КлассУсловийТруда,
	|	Пособия.ЯвляетсяДоходомФармацевта,
	|	Пособия.ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ,
	|	Пособия.СуммаВсего,
	|	0,
	|	Пособия.ПериодДействияНачало,
	|	Пособия.ПериодДействияКонец,
	|	Пособия.ВидРасчета.ВидВремени,
	|	ЕСТЬNULL(РаботникиОрганизацииСрез.СуммированныйУчетРабочегоВремени, ЛОЖЬ),
	|	РаботникиОрганизацииСрез.ГрафикРаботы,
	|	ВЫБОР
	|		КОГДА Пособия.Сотрудник.Организация = &ГоловнаяОрганизация
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ,
	|	Пособия.Сторно,
	|	Пособия.ДокументОснование
	|ИЗ
	|	ВТПособияСоциальномуСтрахованию КАК Пособия
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеСотрудников КАК РаботникиОрганизацииСрез
	|		ПО Пособия.Сотрудник = РаботникиОрганизацииСрез.Сотрудник
	|			И Пособия.ПериодДействияНачало = РаботникиОрганизацииСрез.ПериодДействияНачало
	|ГДЕ
	|	Пособия.ВидРасчета ССЫЛКА ПланВидовРасчета.ОсновныеНачисленияОрганизаций
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""Пособия по социальному страхованию (единовременные)"",
	|	Пособия.НомерСтроки,
	|	Пособия.ВидРасчета.СпособРасчета,
	|	Пособия.ВидРасчета,
	|	Пособия.Сотрудник,
	|	Пособия.Сотрудник.Физлицо,
	|	ВЫБОР
	|		КОГДА Пособия.СпособФинансированияПособий = ЗНАЧЕНИЕ(Перечисление.СпособыФинансированияПособийСоцстрахования.ЗаСчетФСС)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.ПособияЗаСчетФСС)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.НеОблагаетсяЦеликом)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА Пособия.СпособФинансированияПособий = ЗНАЧЕНИЕ(Перечисление.СпособыФинансированияПособийСоцстрахования.ЗаСчетФСС)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.ПособияЗаСчетФСС)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.НеОблагаетсяЦеликом)
	|	КОНЕЦ,
	|	Пособия.ОблагаетсяЕНВД,
	|	Пособия.ОблагаетсяПоДополнительномуТарифу,
	|	Пособия.ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам,
	|	Пособия.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
	|	Пособия.КлассУсловийТруда,
	|	Пособия.ЯвляетсяДоходомФармацевта,
	|	Пособия.ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ,
	|	Пособия.СуммаВсего,
	|	0,
	|	Пособия.ПериодДействияНачало,
	|	Пособия.ПериодДействияКонец,
	|	NULL,
	|	NULL,
	|	NULL,
	|	ВЫБОР
	|		КОГДА Пособия.Сотрудник.Организация = &ГоловнаяОрганизация
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ,
	|	Пособия.Сторно,
	|	Пособия.ДокументОснование
	|ИЗ
	|	ВТПособияСоциальномуСтрахованию КАК Пособия
	|ГДЕ
	|	Пособия.ВидРасчета ССЫЛКА ПланВидовРасчета.ДополнительныеНачисленияОрганизаций
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""Пособия по уходу за ребенком"",
	|	ПособияПоУходу.НомерСтроки,
	|	ПособияПоУходу.ВидРасчета.СпособРасчета,
	|	ПособияПоУходу.ВидРасчета,
	|	ПособияПоУходу.Сотрудник,
	|	ПособияПоУходу.Сотрудник.Физлицо,
	|	ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.ПособияЗаСчетФСС),
	|	ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.ПособияЗаСчетФСС),
	|	ПособияПоУходу.ОблагаетсяЕНВД,
	|	ПособияПоУходу.ОблагаетсяПоДополнительномуТарифу,
	|	ПособияПоУходу.ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам,
	|	ПособияПоУходу.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
	|	ПособияПоУходу.КлассУсловийТруда,
	|	ПособияПоУходу.ЯвляетсяДоходомФармацевта,
	|	ПособияПоУходу.ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ,
	|	ПособияПоУходу.ПособиеПоУходуЗаПервымРебенком + ПособияПоУходу.ПособиеПоУходуЗаВторымРебенком,
	|	0,
	|	ПособияПоУходу.ПериодДействияНачало,
	|	ПособияПоУходу.ПериодДействияКонец,
	|	ПособияПоУходу.ВидРасчета.ВидВремени,
	|	ЕСТЬNULL(РаботникиОрганизацииСрез.СуммированныйУчетРабочегоВремени, ЛОЖЬ),
	|	РаботникиОрганизацииСрез.ГрафикРаботы,
	|	ВЫБОР
	|		КОГДА ПособияПоУходу.Сотрудник.Организация = &ГоловнаяОрганизация
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ,
	|	ПособияПоУходу.Сторно,
	|	ПособияПоУходу.ДокументОснование
	|ИЗ
	|	ВТПособияПоУходуЗаРебенкомДоПолутораЛет КАК ПособияПоУходу
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеСотрудников КАК РаботникиОрганизацииСрез
	|		ПО ПособияПоУходу.Сотрудник = РаботникиОрганизацииСрез.Сотрудник
	|			И ПособияПоУходу.ПериодДействияНачало = РаботникиОрганизацииСрез.ПериодДействияНачало
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Начисления.ТипСтроки,
	|	Начисления.НомерСтроки,
	|	Начисления.СпособРасчета,
	|	Начисления.ВидВремени,
	|	Начисления.ВидРасчета КАК ВидРасчета,
	|	Начисления.Сотрудник КАК Сотрудник,
	|	Начисления.Физлицо,
	|	Начисления.КодДоходаЕСН,
	|	Начисления.ВидДохода,
	|	Начисления.ОблагаетсяЕНВД,
	|	Начисления.ОблагаетсяПоДополнительномуТарифу,
	|	Начисления.ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам,
	|	Начисления.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
	|	Начисления.КлассУсловийТруда,
	|	Начисления.ЯвляетсяДоходомФармацевта,
	|	Начисления.ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ,
	|	Начисления.Результат,
	|	Начисления.ПериодДействияНачало КАК ПериодДействияНачало,
	|	Начисления.ПериодДействияКонец,
	|	Начисления.СуммированныйУчетРабочегоВремени,
	|	Начисления.ГрафикРаботы,
	|	Начисления.ОшибкаНеСоответствиеСотрудникаИОрганизации,
	|	Начисления.Сторно,
	|	Начисления.ДокументОснование
	|ИЗ
	|	ВТНачисления КАК Начисления
	|
	|УПОРЯДОЧИТЬ ПО
	|	Сотрудник,
	|	ВидРасчета,
	|	ПериодДействияНачало";
		
	ВыборкаПоДоходам =  Запрос.Выполнить().Выбрать();
	Пока ВыборкаПоДоходам.Следующий() Цикл
		РасчетСтраховыхВзносовДополнительный.ПроверитьЗаполнениеСтрокиДохода(ВыборкаПоДоходам, Отказ, Заголовок);
		Если Не Отказ Тогда
			Если НаборЗаписей = Неопределено Тогда
				РасчетСтраховыхВзносовДополнительный.ДобавитьСтрокуВДвиженияПоДоходам(ДокументОбъект, ВыборкаПоДоходам, ВыборкаПоШапкеДокумента);
			ИначеЕсли ВыборкаПоШапкеДокумента.ПериодРегистрации < РасчетСтраховыхВзносов.ДатаОбъединенияСтраховойИНакопительнойЧастейВзносовПФР() Тогда
				СтрокаНабора = НаборЗаписей.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаНабора,ВыборкаПоДоходам);
				ЗаполнитьЗначенияСвойств(СтрокаНабора,ВыборкаПоШапкеДокумента);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	Если Не Отказ Тогда
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СторноЗаписи.ТипСтроки,
		|	СторноЗаписи.НомерСтроки,
		|	МАКСИМУМ(ОсновныеНачисленияРаботниковОрганизаций.СторнируемыйДокумент) КАК СторнируемыйДокумент
		|ПОМЕСТИТЬ ВТСторнируемыеДокументы
		|ИЗ
		|	ВТНачисления КАК СторноЗаписи
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрРасчета.ОсновныеНачисленияРаботниковОрганизаций КАК ОсновныеНачисленияРаботниковОрганизаций
		|		ПО СторноЗаписи.ДокументОснование = ОсновныеНачисленияРаботниковОрганизаций.Регистратор
		|			И СторноЗаписи.Сотрудник = ОсновныеНачисленияРаботниковОрганизаций.Сотрудник
		|			И СторноЗаписи.ВидРасчета = ОсновныеНачисленияРаботниковОрганизаций.ВидРасчета
		|			И СторноЗаписи.ПериодДействияНачало >= ОсновныеНачисленияРаботниковОрганизаций.ПериодДействияНачало
		|			И СторноЗаписи.ПериодДействияКонец <= ОсновныеНачисленияРаботниковОрганизаций.ПериодДействияКонец
		|ГДЕ
		|	СторноЗаписи.Сторно
		|
		|СГРУППИРОВАТЬ ПО
		|	СторноЗаписи.ТипСтроки,
		|	СторноЗаписи.НомерСтроки
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СторноЗаписи.ТипСтроки,
		|	СторноЗаписи.НомерСтроки,
		|	МАКСИМУМ(ДополнительныеНачисленияРаботниковОрганизаций.СторнируемыйДокумент)
		|ИЗ
		|	ВТНачисления КАК СторноЗаписи
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрРасчета.ДополнительныеНачисленияРаботниковОрганизаций КАК ДополнительныеНачисленияРаботниковОрганизаций
		|		ПО СторноЗаписи.ДокументОснование = ДополнительныеНачисленияРаботниковОрганизаций.Регистратор
		|			И СторноЗаписи.Сотрудник = ДополнительныеНачисленияРаботниковОрганизаций.Сотрудник
		|			И СторноЗаписи.ВидРасчета = ДополнительныеНачисленияРаботниковОрганизаций.ВидРасчета
		|ГДЕ
		|	СторноЗаписи.Сторно
		|
		|СГРУППИРОВАТЬ ПО
		|	СторноЗаписи.ТипСтроки,
		|	СторноЗаписи.НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Начисления.ТипСтроки,
		|	Начисления.НомерСтроки,
		|	Начисления.ВидРасчета КАК ВидРасчета,
		|	Начисления.Сотрудник КАК Сотрудник,
		|	Начисления.Физлицо,
		|	Начисления.ВидДохода,
		|	Начисления.ОблагаетсяЕНВД,
		|	Начисления.ОблагаетсяПоДополнительномуТарифу,
		|	Начисления.ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам,
		|	Начисления.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
		|	Начисления.КлассУсловийТруда,
		|	Начисления.ЯвляетсяДоходомФармацевта,
		|	Начисления.ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ,
		|	Начисления.Результат,
		|	Начисления.Скидка,
		|	Начисления.ПериодДействияНачало КАК ПериодДействияНачало,
		|	Начисления.ПериодДействияКонец,
		|	Начисления.Сторно,
		|	ЕСТЬNULL(СторнируемыеДокументы.СторнируемыйДокумент, Начисления.ДокументОснование) КАК ДокументОснование
		|ПОМЕСТИТЬ ВТВременнаяТаблица
		|ИЗ
		|	ВТНачисления КАК Начисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСторнируемыеДокументы КАК СторнируемыеДокументы
		|		ПО Начисления.ТипСтроки = СторнируемыеДокументы.ТипСтроки
		|			И Начисления.НомерСтроки = СторнируемыеДокументы.НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТНачисления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТСторнируемыеДокументы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблица.ТипСтроки,
		|	ВременнаяТаблица.НомерСтроки,
		|	ВременнаяТаблица.ВидРасчета,
		|	ВременнаяТаблица.Сотрудник,
		|	ВременнаяТаблица.Физлицо,
		|	ВременнаяТаблица.ВидДохода,
		|	ВременнаяТаблица.ОблагаетсяЕНВД,
		|	ВременнаяТаблица.ОблагаетсяПоДополнительномуТарифу,
		|	ВременнаяТаблица.ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам,
		|	ВременнаяТаблица.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
		|	ВременнаяТаблица.КлассУсловийТруда,
		|	ВременнаяТаблица.ЯвляетсяДоходомФармацевта,
		|	ВременнаяТаблица.ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ,
		|	ВременнаяТаблица.Результат,
		|	ВременнаяТаблица.Скидка,
		|	ВременнаяТаблица.ПериодДействияНачало,
		|	ВременнаяТаблица.ПериодДействияКонец,
		|	ВременнаяТаблица.Сторно,
		|	ВременнаяТаблица.ДокументОснование
		|ПОМЕСТИТЬ ВТНачисления
		|ИЗ
		|	ВТВременнаяТаблица КАК ВременнаяТаблица
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТВременнаяТаблица";
		Запрос.Выполнить();
		РасчетСтраховыхВзносов.СформироватьДоходыВУчетеПоСтраховымВзносам(?(НаборЗаписей = Неопределено, ДокументОбъект.Движения.СтраховыеВзносыСведенияОДоходах, НаборЗаписей), Запрос.МенеджерВременныхТаблиц, ВыборкаПоШапкеДокумента, "ДокументОснование", Истина);
		Если НаборЗаписей = Неопределено Тогда
			Для каждого СтрокаНабора Из ДокументОбъект.Движения.СтраховыеВзносыСведенияОДоходах Цикл
				СтрокаНабора.Скидка = 0
			КонецЦикла;
		Иначе
			НаборЗаписей.ЗаполнитьЗначения(0,"Скидка");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ РАСЧЕТА Страховых взносов

Процедура Автозаполнение(ДокументОбъект, ВыборкаПоШапкеДокумента, ОграничениеНаСотрудников, Отказ, УчитыватьСуществующиеДанные = Истина) Экспорт
	
	// Создадим ссылки на наборы записей о доходах
	Если ДокументОбъект.Проведен Тогда
		НаборДвиженийОсновной = РегистрыРасчета.ЕСНОсновныеНачисления.СоздатьНаборЗаписей();
		НаборДвиженийОсновной.Отбор.Регистратор.Значение = ДокументОбъект.Ссылка;
		НаборДвиженийОсновной.Прочитать();
		НаборДвиженийДополнительный = РегистрыРасчета.ЕСНДополнительныеНачисления.СоздатьНаборЗаписей();
		НаборДвиженийДополнительный.Отбор.Регистратор.Значение = ДокументОбъект.Ссылка;
		НаборДвиженийДополнительный.Прочитать();
	КонецЕсли;
	
	ПособияСоциальномуСтрахованию 		  = ДокументОбъект.ПособияПоСоциальномуСтрахованию;
	ПособияПоУходуЗаРебенкомДоПолутораЛет = ДокументОбъект.ПособияПоУходуЗаРебенкомДоПолутораЛет;
	
	ОсновныеНачисления 		 = ДокументОбъект.ОсновныеНачисления;
	ДополнительныеНачисления = ДокументОбъект.ДополнительныеНачисления;
	
	//подготовим таблицу для регистрации ошибок
	ТаблицаОшибок = Новый ТаблицаЗначений;
	ТаблицаОшибок.Колонки.Добавить("Сотрудник");
	ТаблицаОшибок.Колонки.Добавить("ВидРасчета");
	ТаблицаОшибок.Колонки.Добавить("ПериодДействияНачало");
	ТаблицаОшибок.Колонки.Добавить("ПериодДействияКонец");
	ТаблицаОшибок.Колонки.Добавить("Сторно");
	ТаблицаОшибок.Колонки.Добавить("КодОшибки");
	ТаблицаОшибок.Колонки.Добавить("Регистратор");
	ТаблицаОшибок.Колонки.Добавить("НомерСтроки");
	ТаблицаОшибок.Колонки.Добавить("ВидПособияСоциальногоСтрахования");
	
	НаборЗаписейОсновной = РегистрыРасчета.ЕСНОсновныеНачисления.СоздатьНаборЗаписей();
	НаборЗаписейОсновной.Отбор.Регистратор.Значение = ДокументОбъект.Ссылка;
	НаборЗаписейОсновной.Записать();
	НаборЗаписейДополнительный = РегистрыРасчета.ЕСНДополнительныеНачисления.СоздатьНаборЗаписей();
	НаборЗаписейДополнительный.Отбор.Регистратор.Значение = ДокументОбъект.Ссылка;
	НаборЗаписейДополнительный.Записать();
	
	// Автозаполним наборы записей о доходах
	Отказ = РасчетСтраховыхВзносовДополнительный.АвтозаполнениеНаборовЗаписейОДоходах(ДокументОбъект, ВыборкаПоШапкеДокумента, НаборЗаписейОсновной, НаборЗаписейДополнительный, ОграничениеНаСотрудников, ТаблицаОшибок, УчитыватьСуществующиеДанные);

	ТаблицаПособий = ДокументОбъект.ПособияПоСоциальномуСтрахованию.Выгрузить();
	ТаблицаПособий.Свернуть("ВидРасчета");
	ТаблицаПособийПоУходу = ДокументОбъект.ПособияПоУходуЗаРебенкомДоПолутораЛет.Выгрузить();
	ТаблицаПособийПоУходу.Свернуть("ВидРасчета");
	Для каждого СтрокаТЗ Из ТаблицаПособийПоУходу Цикл
		ЗаполнитьЗначенияСвойств(ТаблицаПособий.Добавить(), СтрокаТЗ);
	КонецЦикла;
	МассивПособий = ТаблицаПособий.ВыгрузитьКолонку("ВидРасчета");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("парамРегистратор", ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("МассивПособий", МассивПособий);
	Запрос.УстановитьПараметр("КонецМесяца", НачалоДня(КонецМесяца(ВыборкаПоШапкеДокумента.ПериодРегистрации)));
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЕСНОсновныеНачисления.ВидРасчета,
	|	ЕСНОсновныеНачисления.ПериодДействияНачало,
	|	ЕСНОсновныеНачисления.ПериодДействияКонец,
	|	ЕСНОсновныеНачисления.Сторно,
	|	ЕСНОсновныеНачисления.Сотрудник,
	|	ЕСНОсновныеНачисления.Сотрудник.Физлицо КАК Физлицо,
	|	СУММА(ЕСНОсновныеНачисления.Результат) КАК Результат,
	|	ЕСНОсновныеНачисления.КодДоходаЕСН КАК ВидДохода,
	|	ЕСНОсновныеНачисления.ОблагаетсяЕНВД,
	|	ЕСНОсновныеНачисления.ДокументОснование
	|ИЗ
	|	РегистрРасчета.ЕСНОсновныеНачисления КАК ЕСНОсновныеНачисления
	|ГДЕ
	|	ЕСНОсновныеНачисления.Регистратор = &парамРегистратор
	|	И (НЕ ЕСНОсновныеНачисления.ВидРасчета В (&МассивПособий))
	|
	|СГРУППИРОВАТЬ ПО
	|	ЕСНОсновныеНачисления.ВидРасчета,
	|	ЕСНОсновныеНачисления.ПериодДействияНачало,
	|	ЕСНОсновныеНачисления.ПериодДействияКонец,
	|	ЕСНОсновныеНачисления.Сторно,
	|	ЕСНОсновныеНачисления.Сотрудник,
	|	ЕСНОсновныеНачисления.Сотрудник.Физлицо,
	|	ЕСНОсновныеНачисления.КодДоходаЕСН,
	|	ЕСНОсновныеНачисления.ОблагаетсяЕНВД,
	|	ЕСНОсновныеНачисления.ДокументОснование
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЕСНОсновныеНачисления.Сотрудник.Наименование,
	|	ЕСНОсновныеНачисления.ВидРасчета.Код,
	|	ЕСНОсновныеНачисления.ПериодДействияНачало";
	
	ДокументОбъект.ОсновныеНачисления.Загрузить(Запрос.Выполнить().Выгрузить());
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СтраховыеВзносыСкидкиКДоходам.ВидДохода КАК ВидДохода
	|ПОМЕСТИТЬ ВТКодыСоСкидками
	|ИЗ
	|	РегистрСведений.СтраховыеВзносыСкидкиКДоходам КАК СтраховыеВзносыСкидкиКДоходам
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВидДохода
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСНДополнительныеНачисления.Сотрудник.Физлицо КАК Физлицо,
	|	ЕСНДополнительныеНачисления.КодДоходаЕСН КАК ВидДохода,
	|	&КонецМесяца КАК ДатаПолученияДохода,
	|	СУММА(ЕСНДополнительныеНачисления.Скидка) КАК Скидка
	|ИЗ
	|	РегистрРасчета.ЕСНДополнительныеНачисления КАК ЕСНДополнительныеНачисления
	|ГДЕ
	|	ЕСНДополнительныеНачисления.Регистратор = &парамРегистратор
	|	И (НЕ ЕСНДополнительныеНачисления.ВидРасчета В (&МассивПособий))
	|	И ЕСНДополнительныеНачисления.КодДоходаЕСН В
	|			(ВЫБРАТЬ
	|				КодыСоСкидками.ВидДохода
	|			ИЗ
	|				ВТКодыСоСкидками КАК КодыСоСкидками)
	|
	|СГРУППИРОВАТЬ ПО
	|	ЕСНДополнительныеНачисления.Сотрудник.Физлицо,
	|	ЕСНДополнительныеНачисления.КодДоходаЕСН";
	
	ДокументОбъект.НеоблагаемыеСуммыДоходов.Загрузить(Запрос.Выполнить().Выгрузить());
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЕСНДополнительныеНачисления.ВидРасчета,
	|	ЕСНДополнительныеНачисления.Сторно,
	|	ЕСНДополнительныеНачисления.Сотрудник,
	|	ЕСНДополнительныеНачисления.Сотрудник.Физлицо КАК Физлицо,
	|	СУММА(ЕСНДополнительныеНачисления.Результат) КАК Результат,
	|	ЕСНДополнительныеНачисления.КодДоходаЕСН КАК ВидДохода,
	|	ЕСНДополнительныеНачисления.ОблагаетсяЕНВД,
	|	ЕСНДополнительныеНачисления.ДокументОснование,
	|	ЕСНДополнительныеНачисления.ДатаНачисления КАК ДатаАктуальности,
	|	ЕСНДополнительныеНачисления.ОблагаетсяПоДополнительномуТарифу,
	|	ЕСНДополнительныеНачисления.ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам,
	|	ЕСНДополнительныеНачисления.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
	|	ЕСНДополнительныеНачисления.КлассУсловийТруда
	|ИЗ
	|	РегистрРасчета.ЕСНДополнительныеНачисления КАК ЕСНДополнительныеНачисления
	|ГДЕ
	|	ЕСНДополнительныеНачисления.Регистратор = &парамРегистратор
	|	И НЕ ЕСНДополнительныеНачисления.ВидРасчета В (&МассивПособий)
	|
	|СГРУППИРОВАТЬ ПО
	|	ЕСНДополнительныеНачисления.ВидРасчета,
	|	ЕСНДополнительныеНачисления.Сторно,
	|	ЕСНДополнительныеНачисления.Сотрудник,
	|	ЕСНДополнительныеНачисления.Сотрудник.Физлицо,
	|	ЕСНДополнительныеНачисления.КодДоходаЕСН,
	|	ЕСНДополнительныеНачисления.ОблагаетсяЕНВД,
	|	ЕСНДополнительныеНачисления.ДокументОснование,
	|	ЕСНДополнительныеНачисления.ДатаНачисления,
	|	ЕСНДополнительныеНачисления.ОблагаетсяПоДополнительномуТарифу,
	|	ЕСНДополнительныеНачисления.ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам,
	|	ЕСНДополнительныеНачисления.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
	|	ЕСНДополнительныеНачисления.КлассУсловийТруда
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЕСНДополнительныеНачисления.Сотрудник.Наименование,
	|	ЕСНДополнительныеНачисления.ВидРасчета.Код";
	
	ИспользуютсяРаботыСДосрочнойПенсией = ПроцедурыУправленияПерсоналом.ЗначениеУчетнойПолитикиПоПерсоналуВсехОрганизаций(глЗначениеПеременной("глУчетнаяПолитикаПоПерсоналуОрганизации"), "ИспользуютсяРаботыСДосрочнойПенсией");
	ИспользуетсяТрудЧленовЛетныхЭкипажей = ПроцедурыУправленияПерсоналом.ЗначениеУчетнойПолитикиПоПерсоналуВсехОрганизаций(глЗначениеПеременной("глУчетнаяПолитикаПоПерсоналуОрганизации"), "ИспользуетсяТрудЧленовЛетныхЭкипажей");
	ИспользуетсяТрудШахтеров = ПроцедурыУправленияПерсоналом.ЗначениеУчетнойПолитикиПоПерсоналуВсехОрганизаций(глЗначениеПеременной("глУчетнаяПолитикаПоПерсоналуОрганизации"), "ИспользуетсяТрудШахтеров");
	ИспользуетсяТрудФармацевтов = ПроцедурыУправленияПерсоналом.ЗначениеУчетнойПолитикиПоПерсоналуВсехОрганизаций(глЗначениеПеременной("глУчетнаяПолитикаПоПерсоналуОрганизации"), "ИспользуетсяТрудФармацевтов");
	ИспользуетсяТрудЧленовЭкипажейМорскихСудов = ПроцедурыУправленияПерсоналом.ЗначениеУчетнойПолитикиПоПерсоналуВсехОрганизаций(глЗначениеПеременной("глУчетнаяПолитикаПоПерсоналуОрганизации"), "ИспользуетсяТрудЧленовЭкипажейМорскихСудов");
	
	Если Не (ИспользуютсяРаботыСДосрочнойПенсией Или ИспользуетсяТрудЧленовЛетныхЭкипажей Или ИспользуетсяТрудШахтеров Или ИспользуетсяТрудФармацевтов Или ИспользуетсяТрудЧленовЭкипажейМорскихСудов) Тогда
		
		ДокументОбъект.ДополнительныеНачисления.Загрузить(Запрос.Выполнить().Выгрузить());
		
	Иначе
		
		ТаблицаДополнительныеНачисления = Запрос.Выполнить().Выгрузить();
		
		ДополнительныйЗапрос = Новый Запрос;
		ДополнительныйЗапрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		ДополнительныйЗапрос.УстановитьПараметр("парамРегистратор", ДокументОбъект.Ссылка);
		ДополнительныйЗапрос.Текст =
		"ВЫБРАТЬ
		|	ЕСНДополнительныеНачисления.Сотрудник
		|ПОМЕСТИТЬ ВТСотрудники
		|ИЗ
		|	РегистрРасчета.ЕСНДополнительныеНачисления КАК ЕСНДополнительныеНачисления
		|ГДЕ
		|	ЕСНДополнительныеНачисления.Регистратор = &парамРегистратор
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЕСНОсновныеНачисления.Сотрудник
		|ИЗ
		|	РегистрРасчета.ЕСНОсновныеНачисления КАК ЕСНОсновныеНачисления
		|ГДЕ
		|	ЕСНОсновныеНачисления.Регистратор = &парамРегистратор";
		ДополнительныйЗапрос.Выполнить();
		ТаблицаДолейДляРаспределенияВредности = РасчетСтраховыхВзносов.ДолиДляРаспределенияВредности(ДополнительныйЗапрос.МенеджерВременныхТаблиц, ДокументОбъект.ПериодРегистрации);
		
		// допишем сведения, вытекающие из должности и подразделения сотрудника
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ДатаАктуальности", КонецМесяца(ДокументОбъект.ПериодРегистрации));
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Сотрудники.Номер КАК НомерСтроки,
		|	Сотрудники.Сотрудник КАК Сотрудник,
		|	ВЫБОР
		|		КОГДА Сотрудники.ДатаАктуальности = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|			ТОГДА &ДатаАктуальности
		|		ИНАЧЕ Сотрудники.ДатаАктуальности
		|	КОНЕЦ КАК ДатаАктуальности
		|ПОМЕСТИТЬ ВТСотрудники
		|ИЗ
		|	&ТаблицаСотрудников КАК Сотрудники
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Сотрудник,
		|	ДатаАктуальности
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МАКСИМУМ(РаботникиОрганизаций.Период) КАК Период,
		|	Сотрудники.Сотрудник КАК Сотрудник,
		|	Сотрудники.ДатаАктуальности,
		|	Сотрудники.НомерСтроки
		|ПОМЕСТИТЬ ВТСтрокиРегистра
		|ИЗ
		|	ВТСотрудники КАК Сотрудники
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизаций
		|		ПО Сотрудники.Сотрудник = РаботникиОрганизаций.Сотрудник
		|			И Сотрудники.ДатаАктуальности >= РаботникиОрганизаций.Период
		|
		|СГРУППИРОВАТЬ ПО
		|	Сотрудники.Сотрудник,
		|	Сотрудники.ДатаАктуальности,
		|	Сотрудники.НомерСтроки
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Сотрудник,
		|	Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СтрокиРегистра.НомерСтроки КАК НомерСтроки,
		|	ЕСТЬNULL(КлассыУсловийТрудаПоДолжностямСрезПоследних.КлассУсловийТруда, ЗНАЧЕНИЕ(Перечисление.КлассыУсловийТрудаПоРезультатамСпециальнойОценки.ПустаяСсылка)) КАК КлассУсловийТруда,
		|	ЕСТЬNULL(ВЫБОР
		|			КОГДА Работники.ПериодЗавершения <= СтрокиРегистра.ДатаАктуальности
		|					И Работники.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|				ТОГДА Работники.ДолжностьЗавершения.ВзимаютсяВзносыЗаЗанятыхНаРаботахСДосрочнойПенсией
		|			ИНАЧЕ Работники.Должность.ВзимаютсяВзносыЗаЗанятыхНаРаботахСДосрочнойПенсией
		|		КОНЕЦ, ЗНАЧЕНИЕ(Перечисление.ВидыРаботСДосрочнойПенсией.ПустаяСсылка)) КАК ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
		|	ЕСТЬNULL(ВЫБОР
		|			КОГДА Работники.ПериодЗавершения <= СтрокиРегистра.ДатаАктуальности
		|					И Работники.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|				ТОГДА Работники.ДолжностьЗавершения.ЯвляетсяДолжностьюЛетногоЭкипажа
		|			ИНАЧЕ Работники.Должность.ЯвляетсяДолжностьюЛетногоЭкипажа
		|		КОНЕЦ, ЛОЖЬ) КАК ОблагаетсяПоДополнительномуТарифу,
		|	ЕСТЬNULL(ВЫБОР
		|			КОГДА Работники.ПериодЗавершения <= СтрокиРегистра.ДатаАктуальности
		|					И Работники.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|				ТОГДА Работники.ДолжностьЗавершения.ЯвляетсяШахтерскойДолжностью
		|			ИНАЧЕ Работники.Должность.ЯвляетсяШахтерскойДолжностью
		|		КОНЕЦ, ЛОЖЬ) КАК ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам,
		|	ЕСТЬNULL(ВЫБОР
		|			КОГДА Работники.ПериодЗавершения <= СтрокиРегистра.ДатаАктуальности
		|					И Работники.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|				ТОГДА Работники.ПодразделениеОрганизацииЗавершения.СоответствуетСудамПодФлагомРФ
		|			ИНАЧЕ Работники.ПодразделениеОрганизации.СоответствуетСудамПодФлагомРФ
		|		КОНЕЦ, ЛОЖЬ) КАК ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ,
		|	ЕСТЬNULL(ВЫБОР
		|			КОГДА Работники.ПериодЗавершения <= СтрокиРегистра.ДатаАктуальности
		|					И Работники.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|				ТОГДА Работники.ДолжностьЗавершения.ЯвляетсяФармацевтическойДолжностью
		|			ИНАЧЕ Работники.Должность.ЯвляетсяФармацевтическойДолжностью
		|		КОНЕЦ, ЛОЖЬ) КАК ЯвляетсяДоходомФармацевта
		|ИЗ
		|	ВТСтрокиРегистра КАК СтрокиРегистра
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК Работники
		|		ПО СтрокиРегистра.Сотрудник = Работники.Сотрудник
		|			И СтрокиРегистра.Период = Работники.Период
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КлассыУсловийТрудаПоДолжностям.СрезПоследних(&ДатаАктуальности, ДатаРегистрацииИзменений <= &ДатаАктуальности) КАК КлассыУсловийТрудаПоДолжностямСрезПоследних
		|		ПО (ВЫБОР
		|				КОГДА Работники.ПериодЗавершения <= СтрокиРегистра.ДатаАктуальности
		|						И Работники.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|					ТОГДА Работники.ДолжностьЗавершения
		|				ИНАЧЕ Работники.Должность
		|			КОНЕЦ = КлассыУсловийТрудаПоДолжностямСрезПоследних.Должность)
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
		
		// таблица основных начислений
		ТаблицаСотрудников = ДокументОбъект.ОсновныеНачисления.Выгрузить(,"НомерСтроки, Сотрудник,ПериодДействияНачало");
		ТаблицаСотрудников.Колонки.ПериодДействияНачало.Имя = "ДатаАктуальности";
		ТаблицаСотрудников.Колонки.НомерСтроки.Имя = "Номер";
		Запрос.УстановитьПараметр("ТаблицаСотрудников", ТаблицаСотрудников);
		ТаблицаСотрудников = Неопределено;
		
		ВременнаяТЗ = Запрос.Выполнить().Выгрузить();
		ДокументОбъект.ОсновныеНачисления.ЗагрузитьКолонку(ВременнаяТЗ.ВыгрузитьКолонку("ОблагаетсяПоДополнительномуТарифу"),"ОблагаетсяПоДополнительномуТарифу");
		ДокументОбъект.ОсновныеНачисления.ЗагрузитьКолонку(ВременнаяТЗ.ВыгрузитьКолонку("ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам"),"ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам");
		ДокументОбъект.ОсновныеНачисления.ЗагрузитьКолонку(ВременнаяТЗ.ВыгрузитьКолонку("КлассУсловийТруда"),"КлассУсловийТруда");
		ДокументОбъект.ОсновныеНачисления.ЗагрузитьКолонку(ВременнаяТЗ.ВыгрузитьКолонку("ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией"),"ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией");
		ДокументОбъект.ОсновныеНачисления.ЗагрузитьКолонку(ВременнаяТЗ.ВыгрузитьКолонку("ЯвляетсяДоходомФармацевта"),"ЯвляетсяДоходомФармацевта");
		ДокументОбъект.ОсновныеНачисления.ЗагрузитьКолонку(ВременнаяТЗ.ВыгрузитьКолонку("ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ"),"ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ");
		ВременнаяТЗ = Неопределено;
		
		ДополнительныйЗапрос = Новый Запрос;
		ДополнительныйЗапрос.Текст = 
		"ВЫБРАТЬ
		|	ОсновныеНачисленияОрганизаций.Ссылка КАК ВидРасчета,
		|	ОсновныеНачисленияОрганизаций.ВидСтажаСЗВ4,
		|	ВЫБОР
		|		КОГДА НЕ ОсновныеНачисленияОрганизаций.ЗачетНормыВремени
		|				И ОсновныеНачисленияОрганизаций.НачисляетсяВЦеломЗаМесяц
		|				И ОсновныеНачисленияОрганизаций.ВидСтажаСЗВ4 <> ЗНАЧЕНИЕ(Перечисление.ВидыСтажаСЗВ4.ВключаетсяВСтажДляДосрочногоНазначенияПенсии)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК РаспределяетсяВоВсехСлучаях
		|ИЗ
		|	ПланВидовРасчета.ОсновныеНачисленияОрганизаций КАК ОсновныеНачисленияОрганизаций";
		Выборка = ДополнительныйЗапрос.Выполнить().Выбрать();
		ОписаниеНачислений = Новый Соответствие;
		Пока Выборка.Следующий() Цикл
			ОписаниеНачислений.Вставить(Выборка.ВидРасчета, Новый Структура("ВидСтажаСЗВ4,РаспределяетсяВоВсехСлучаях",Выборка.ВидСтажаСЗВ4,Выборка.РаспределяетсяВоВсехСлучаях))
		КонецЦикла;
		
		РасчетСтраховыхВзносов.УточнитьДанныеОРаботахСДосрочнойПенсией(ДокументОбъект.ОсновныеНачисления, ТаблицаДолейДляРаспределенияВредности, Истина, ДокументОбъект.ПериодРегистрации, ОписаниеНачислений);
		
		// таблица дополнительных начислений
		ТаблицаДополнительныеНачисления.Колонки.Добавить("Номер",Новый ОписаниеТипов("Число"));
		ТаблицаДополнительныеНачисления.Колонки.Добавить("ЯвляетсяДоходомФармацевта",Новый ОписаниеТипов("Булево"));
		ТаблицаДополнительныеНачисления.Колонки.Добавить("ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ",Новый ОписаниеТипов("Булево"));
		Номер = 0;
		Для каждого СтрокаТЗ Из ТаблицаДополнительныеНачисления Цикл
			Номер = Номер + 1;
			СтрокаТЗ.Номер = Номер
		КонецЦикла; 
		Запрос.УстановитьПараметр("ТаблицаСотрудников", ТаблицаДополнительныеНачисления);
		
		ВременнаяТЗ = Запрос.Выполнить().Выгрузить();
		ТаблицаДополнительныеНачисления.ЗагрузитьКолонку(ВременнаяТЗ.ВыгрузитьКолонку("ОблагаетсяПоДополнительномуТарифу"),"ОблагаетсяПоДополнительномуТарифу");
		ТаблицаДополнительныеНачисления.ЗагрузитьКолонку(ВременнаяТЗ.ВыгрузитьКолонку("ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам"),"ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам");
		ТаблицаДополнительныеНачисления.ЗагрузитьКолонку(ВременнаяТЗ.ВыгрузитьКолонку("ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией"),"ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией");
		ТаблицаДополнительныеНачисления.ЗагрузитьКолонку(ВременнаяТЗ.ВыгрузитьКолонку("КлассУсловийТруда"),"КлассУсловийТруда");
		ТаблицаДополнительныеНачисления.ЗагрузитьКолонку(ВременнаяТЗ.ВыгрузитьКолонку("ЯвляетсяДоходомФармацевта"),"ЯвляетсяДоходомФармацевта");
		ТаблицаДополнительныеНачисления.ЗагрузитьКолонку(ВременнаяТЗ.ВыгрузитьКолонку("ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ"),"ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ");
		ВременнаяТЗ = Неопределено;
		
		РасчетСтраховыхВзносов.УточнитьДанныеОРаботахСДосрочнойПенсией(ТаблицаДополнительныеНачисления, ТаблицаДолейДляРаспределенияВредности, Ложь);
		ДокументОбъект.ДополнительныеНачисления.Загрузить(ТаблицаДополнительныеНачисления);
		ТаблицаДополнительныеНачисления = Неопределено;
		
		// таблицы пособий
		ДополнительныйЗапрос = Новый Запрос;
		ДополнительныйЗапрос.Текст =
		"ВЫБРАТЬ
		|	Сотрудники.НомерСтроки КАК НомерСтроки,
		|	Сотрудники.Сотрудник КАК Сотрудник,
		|	ВЫБОР
		|		КОГДА Сотрудники.ДатаАктуальности = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|			ТОГДА &ДатаАктуальности
		|		ИНАЧЕ Сотрудники.ДатаАктуальности
		|	КОНЕЦ КАК ДатаАктуальности
		|ПОМЕСТИТЬ ВТТаблицаСотрудников
		|ИЗ
		|	&ТаблицаСотрудников КАК Сотрудники
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаСотрудников.НомерСтроки КАК Номер,
		|	ТаблицаСотрудников.Сотрудник,
		|	ТаблицаСотрудников.ДатаАктуальности
		|ИЗ
		|	ВТТаблицаСотрудников КАК ТаблицаСотрудников
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
		ТаблицаСотрудников = ДокументОбъект.ПособияПоСоциальномуСтрахованию.Выгрузить(,"НомерСтроки, Сотрудник,ПериодДействияНачало");
		ТаблицаСотрудников.Колонки.ПериодДействияНачало.Имя = "ДатаАктуальности";
		ДополнительныйЗапрос.УстановитьПараметр("ТаблицаСотрудников", ТаблицаСотрудников);
		ТаблицаСотрудников = Неопределено;
		ДополнительныйЗапрос.УстановитьПараметр("ДатаАктуальности", КонецМесяца(ДокументОбъект.ПериодРегистрации));
		Запрос.УстановитьПараметр("ТаблицаСотрудников", ДополнительныйЗапрос.Выполнить().Выгрузить());
		
		ВременнаяТЗ = Запрос.Выполнить().Выгрузить();
		ДокументОбъект.ПособияПоСоциальномуСтрахованию.ЗагрузитьКолонку(ВременнаяТЗ.ВыгрузитьКолонку("ОблагаетсяПоДополнительномуТарифу"),"ОблагаетсяПоДополнительномуТарифу");
		ДокументОбъект.ПособияПоСоциальномуСтрахованию.ЗагрузитьКолонку(ВременнаяТЗ.ВыгрузитьКолонку("ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам"),"ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам");
		ДокументОбъект.ПособияПоСоциальномуСтрахованию.ЗагрузитьКолонку(ВременнаяТЗ.ВыгрузитьКолонку("ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией"),"ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией");
		ДокументОбъект.ПособияПоСоциальномуСтрахованию.ЗагрузитьКолонку(ВременнаяТЗ.ВыгрузитьКолонку("КлассУсловийТруда"),"КлассУсловийТруда");
		ДокументОбъект.ПособияПоСоциальномуСтрахованию.ЗагрузитьКолонку(ВременнаяТЗ.ВыгрузитьКолонку("ЯвляетсяДоходомФармацевта"),"ЯвляетсяДоходомФармацевта");
		ДокументОбъект.ПособияПоСоциальномуСтрахованию.ЗагрузитьКолонку(ВременнаяТЗ.ВыгрузитьКолонку("ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ"),"ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ");
		ВременнаяТЗ = Неопределено;
		
		ТаблицаСотрудников = ДокументОбъект.ПособияПоУходуЗаРебенкомДоПолутораЛет.Выгрузить(,"НомерСтроки, Сотрудник,ПериодДействияНачало");
		ТаблицаСотрудников.Колонки.ПериодДействияНачало.Имя = "ДатаАктуальности";
		ТаблицаСотрудников.Колонки.НомерСтроки.Имя = "Номер";
		Запрос.УстановитьПараметр("ТаблицаСотрудников", ТаблицаСотрудников);
		ТаблицаСотрудников = Неопределено;
		
		ВременнаяТЗ = Запрос.Выполнить().Выгрузить();
		ДокументОбъект.ПособияПоУходуЗаРебенкомДоПолутораЛет.ЗагрузитьКолонку(ВременнаяТЗ.ВыгрузитьКолонку("ОблагаетсяПоДополнительномуТарифу"),"ОблагаетсяПоДополнительномуТарифу");
		ДокументОбъект.ПособияПоУходуЗаРебенкомДоПолутораЛет.ЗагрузитьКолонку(ВременнаяТЗ.ВыгрузитьКолонку("ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам"),"ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам");
		ДокументОбъект.ПособияПоУходуЗаРебенкомДоПолутораЛет.ЗагрузитьКолонку(ВременнаяТЗ.ВыгрузитьКолонку("ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией"),"ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией");
		ДокументОбъект.ПособияПоУходуЗаРебенкомДоПолутораЛет.ЗагрузитьКолонку(ВременнаяТЗ.ВыгрузитьКолонку("КлассУсловийТруда"),"КлассУсловийТруда");
		ДокументОбъект.ПособияПоУходуЗаРебенкомДоПолутораЛет.ЗагрузитьКолонку(ВременнаяТЗ.ВыгрузитьКолонку("ЯвляетсяДоходомФармацевта"),"ЯвляетсяДоходомФармацевта");
		ДокументОбъект.ПособияПоУходуЗаРебенкомДоПолутораЛет.ЗагрузитьКолонку(ВременнаяТЗ.ВыгрузитьКолонку("ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ"),"ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ");
		ВременнаяТЗ = Неопределено;
		
		СтруктураОтбора = Новый Структура("Сотрудник");
		Для каждого СтрокаТЧ Из ДокументОбъект.ПособияПоУходуЗаРебенкомДоПолутораЛет Цикл
			СтруктураОтбора.Сотрудник = СтрокаТЧ.Сотрудник;
			СтрокиРаспределения = ТаблицаДолейДляРаспределенияВредности.НайтиСтроки(СтруктураОтбора);
			Если СтрокиРаспределения.Количество() = 1 Тогда
				СтрокаТЧ.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией = СтрокиРаспределения[0].ВзимаютсяВзносыЗаЗанятыхНаРаботахСДосрочнойПенсией;
				СтрокаТЧ.КлассУсловийТруда = СтрокиРаспределения[0].КлассУсловийТруда
			КонецЕсли;
		КонецЦикла;
		
		Если ДокументОбъект.ПериодРегистрации < РасчетСтраховыхВзносов.ДатаОбъединенияСтраховойИНакопительнойЧастейВзносовПФР() Тогда
			Для каждого СтрокаТЗ Из ДокументОбъект.ДополнительныеНачисления Цикл
				СтрокаТЗ.КлассУсловийТруда = Перечисления.КлассыУсловийТрудаПоРезультатамСпециальнойОценки.ПустаяСсылка()
			КонецЦикла;
			Для каждого СтрокаТЗ Из ДокументОбъект.ОсновныеНачисления Цикл
				СтрокаТЗ.КлассУсловийТруда = Перечисления.КлассыУсловийТрудаПоРезультатамСпециальнойОценки.ПустаяСсылка()
			КонецЦикла;
			Для каждого СтрокаТЗ Из ДокументОбъект.ПособияПоСоциальномуСтрахованию Цикл
				СтрокаТЗ.КлассУсловийТруда = Перечисления.КлассыУсловийТрудаПоРезультатамСпециальнойОценки.ПустаяСсылка()
			КонецЦикла;
			Для каждого СтрокаТЗ Из ДокументОбъект.ПособияПоУходуЗаРебенкомДоПолутораЛет Цикл
				СтрокаТЗ.КлассУсловийТруда = Перечисления.КлассыУсловийТрудаПоРезультатамСпециальнойОценки.ПустаяСсылка()
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
	ДокументОбъект.РасчетСкидок(ВыборкаПоШапкеДокумента);
	
	Если Отказ Тогда
		
		// есть ошибки в сборе данных по отражению начислений
		                                            
		ТекстСообщения = "Расчет страховых взносов не произведен! Для автоматического учета начислений при расчете страховых взносов не хватает данных.";
		ОбщегоНазначенияЗК.КомментарийРасчета(ТекстСообщения, , , , Перечисления.ВидыСообщений.Ошибка);
		
		Если ТаблицаОшибок.Количество() > 0 Тогда
			// сообщим пользователю об ошибках
			
			//отсортируем таблицу ошибок по кодам
			ТаблицаОшибок.Сортировать("КодОшибки, ВидРасчета");
			
			НовыйУчетПособий = НачалоМесяца(ВыборкаПоШапкеДокумента.ПериодРегистрации) >= НачалоМесяца(ПроведениеРасчетовДополнительный.ПолучитьДатуВступленияВСилуИзмененийПоСоциальнымПособиям2006());
			
			СтруктураПоискаНачисления = Новый Структура("Сотрудник,ВидРасчета,ПериодДействияНачало,ПериодДействияКонец,ДокументОснование,Сторно");
			СтруктураПоискаДополнительныеНачисления = Новый Структура("Сотрудник,ВидРасчета,ДокументОснование,Сторно");
			
			// коды ошибок
			// 1 - сторно, нет данных отражения в учете начисления в прошлых периодах
			// 2 - нет доли ЕНВД для пособий, доля ЕНВД по базовым начислениям
			// 3 - нет данных по базе, нужен код страховых взносов и доля ЕНВД
			// 4 - нет данных по базе, нужен код страховых взносов 
			// 5 - нет данных по базе, нужна доля ЕНВД
			// 6 - не заполнен код дохода страховых взносов у вида расчета
			// 7 - не заполнен код дохода страховых взносов у вида расчета, нужна доля ЕНВД
			
			ТекущийКодОшибки = 0;
			ТекущийВидРасчета = ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.ПустаяСсылка();
			
			Для каждого СтрокаТаблицыОшибок Из ТаблицаОшибок Цикл
				
				Если ТекущийКодОшибки <> СтрокаТаблицыОшибок.КодОшибки или ТекущийВидРасчета <> СтрокаТаблицыОшибок.ВидРасчета Тогда
					//выведем описание ошибки
					ТекстСообщенияРекомендации = "";
					ТекстСообщения = "Начисление: """ + СтрокаТаблицыОшибок.ВидРасчета.Наименование + """. ";
					
					Если СтрокаТаблицыОшибок.КодОшибки = 1 Тогда
						ТекстСообщения = ТекстСообщения + "Нет данных для учета сторно записи. Отсутствуют данные отражения в учете для исчисления страховых взносов этого начисления в прошлых периодах";
						ТекстСообщенияРекомендации = "Необходимо зарегистрировать данные вручную";
					ИначеЕсли СтрокаТаблицыОшибок.КодОшибки = 2 Тогда
						ТекстСообщения = ТекстСообщения + "Нет данных для определения доли ЕНВД пособия. Стратегия определения доли ЕНВД - ""по базовым начислениям "" задана в начислении. ";
						Если СтрокаТаблицыОшибок.ВидРасчета.ВидПособияСоциальногоСтрахования <> Перечисления.ВидыПособийСоциальногоСтрахования.ПоУходуЗаРебенкомДоПолутораЛет Тогда
							ТекстСообщенияРекомендации = "Рекомендуется зарегистрировать отражение пособия в учете документом ""Начисление по больничному листу"".
							|На закладке ""Отражение пособия в учете"" установите ""Отражать в учете по данным текущего документа""
							|и заполните таблицу ""Проводки и данные для страховых взносов""";
						КонецЕсли;
					ИначеЕсли СтрокаТаблицыОшибок.КодОшибки = 3 Тогда
						ТекстСообщения = ТекстСообщения + "Нет данных для определения вида дохода в учете для исчисления страховых взносов и доли ЕНВД. Отсутствуют данные о базовых начислениях";
						ТекстСообщенияРекомендации = "Необходимо зарегистрировать данные вручную";
					ИначеЕсли СтрокаТаблицыОшибок.КодОшибки = 4 Тогда
						ТекстСообщения = ТекстСообщения + "Нет данных для определения вида дохода в учете для исчисления страховых взносов. Отсутствуют данные о базовых начислениях";
						ТекстСообщенияРекомендации = "Необходимо зарегистрировать данные вручную";
					ИначеЕсли СтрокаТаблицыОшибок.КодОшибки = 5 Тогда
						ТекстСообщения = ТекстСообщения + "Нет данных для определения доли ЕНВД. Отсутствуют данные о базовых начислениях";
						ТекстСообщенияРекомендации = "Необходимо зарегистрировать данные вручную";
					ИначеЕсли СтрокаТаблицыОшибок.КодОшибки = 6 Тогда
						ТекстСообщения = ТекстСообщения + "Не задан вид дохода по страховым взносам у начисления";
						ТекстСообщенияРекомендации = "Необходимо зарегистрировать данные вручную. Рекомендуется указать порядок учета
													|начисления для целей исчисления страховых взносов в форме начисления на закладке ""Взносы""";
					ИначеЕсли СтрокаТаблицыОшибок.КодОшибки = 7 Тогда
						ТекстСообщения = ТекстСообщения + "Не задан вид дохода по страховым взносам у начисления. Нет данных для определения доли ЕНВД. Отсутствуют данные о базовых начислениях";
						ТекстСообщенияРекомендации = "Необходимо зарегистрировать данные вручную. Рекомендуется указать порядок учета
													|начисления для целей исчисления страховых взносов в форме начисления на закладке ""Взносы""";
					Иначе //для этого кода нет текста сообщения
						Продолжить;
					КонецЕсли;	
					
					РодительскаяСтрока = ОбщегоНазначенияЗК.КомментарийРасчета(ТекстСообщения, , , , Перечисления.ВидыСообщений.ВажнаяИнформация);
					Если ЗначениеЗаполнено(ТекстСообщенияРекомендации) Тогда
						ОбщегоНазначенияЗК.КомментарийРасчета(ТекстСообщенияРекомендации, РодительскаяСтрока);	
					КонецЕсли;
					
					ТекущийКодОшибки  = СтрокаТаблицыОшибок.КодОшибки;
					ТекущийВидРасчета = СтрокаТаблицыОшибок.ВидРасчета;
					
				КонецЕсли;	
				
				// выведем информацию о том, в каких строках табличных частей проблемы
				Если НовыйУчетПособий Тогда
					//поиск пособий делаем в "новых" табличных частях
					
					Если СтрокаТаблицыОшибок.ВидПособияСоциальногоСтрахования = Перечисления.ВидыПособийСоциальногоСтрахования.ПоУходуЗаРебенкомДоПолутораЛет Тогда
						ТекстТабличнаяЧасть = """Пособия по уходу за ребенком"", ";
						СтруктураПоискаНачисления.Сотрудник  = СтрокаТаблицыОшибок.Сотрудник;	
						СтруктураПоискаНачисления.ВидРасчета = СтрокаТаблицыОшибок.ВидРасчета;
						СтруктураПоискаНачисления.ПериодДействияНачало = СтрокаТаблицыОшибок.ПериодДействияНачало;
						СтруктураПоискаНачисления.ПериодДействияКонец  = НачалоДня(СтрокаТаблицыОшибок.ПериодДействияКонец);
						СтруктураПоискаНачисления.ДокументОснование    = СтрокаТаблицыОшибок.Регистратор;
						СтруктураПоискаНачисления.Сторно     = СтрокаТаблицыОшибок.Сторно;
						НайденныеСтроки = ПособияПоУходуЗаРебенкомДоПолутораЛет.НайтиСтроки(СтруктураПоискаНачисления);
					ИначеЕсли ЗначениеЗаполнено(СтрокаТаблицыОшибок.ВидПособияСоциальногоСтрахования)Тогда
						ТекстТабличнаяЧасть = """Пособия по социальному страхованию"", ";
						СтруктураПоискаНачисления.Сотрудник  = СтрокаТаблицыОшибок.Сотрудник;	
						СтруктураПоискаНачисления.ВидРасчета = СтрокаТаблицыОшибок.ВидРасчета;
						СтруктураПоискаНачисления.ПериодДействияНачало = СтрокаТаблицыОшибок.ПериодДействияНачало;
						СтруктураПоискаНачисления.ПериодДействияКонец  = НачалоДня(СтрокаТаблицыОшибок.ПериодДействияКонец);
						СтруктураПоискаНачисления.ДокументОснование    = СтрокаТаблицыОшибок.Регистратор;
						СтруктураПоискаНачисления.Сторно     = СтрокаТаблицыОшибок.Сторно;
						НайденныеСтроки = ПособияСоциальномуСтрахованию.НайтиСтроки(СтруктураПоискаНачисления);
					ИначеЕсли ЗначениеЗаполнено(СтрокаТаблицыОшибок.ПериодДействияНачало) Тогда
						ТекстТабличнаяЧасть = """Основные начисления"", ";
                        СтруктураПоискаНачисления.Сотрудник  = СтрокаТаблицыОшибок.Сотрудник;	
						СтруктураПоискаНачисления.ВидРасчета = СтрокаТаблицыОшибок.ВидРасчета;
						СтруктураПоискаНачисления.ПериодДействияНачало = СтрокаТаблицыОшибок.ПериодДействияНачало;
						СтруктураПоискаНачисления.ПериодДействияКонец  = НачалоДня(СтрокаТаблицыОшибок.ПериодДействияКонец);
						СтруктураПоискаНачисления.ДокументОснование    = СтрокаТаблицыОшибок.Регистратор;
						СтруктураПоискаНачисления.Сторно     = СтрокаТаблицыОшибок.Сторно;
						НайденныеСтроки = ОсновныеНачисления.НайтиСтроки(СтруктураПоискаНачисления);
					Иначе
						ТекстТабличнаяЧасть = """Дополнительные начисления"", ";
						СтруктураПоискаДополнительныеНачисления.Сотрудник  = СтрокаТаблицыОшибок.Сотрудник;	
						СтруктураПоискаДополнительныеНачисления.ВидРасчета = СтрокаТаблицыОшибок.ВидРасчета;
						СтруктураПоискаДополнительныеНачисления.ДокументОснование = СтрокаТаблицыОшибок.Регистратор;
						СтруктураПоискаДополнительныеНачисления.Сторно     = СтрокаТаблицыОшибок.Сторно;
						НайденныеСтроки = ДополнительныеНачисления.НайтиСтроки(СтруктураПоискаДополнительныеНачисления);
					КонецЕсли;	
					
				Иначе
					
					Если ЗначениеЗаполнено(СтрокаТаблицыОшибок.ПериодДействияНачало) Тогда
						СтруктураПоискаНачисления.Сотрудник  = СтрокаТаблицыОшибок.Сотрудник;	
						СтруктураПоискаНачисления.ВидРасчета = СтрокаТаблицыОшибок.ВидРасчета;
						СтруктураПоискаНачисления.ПериодДействияНачало = СтрокаТаблицыОшибок.ПериодДействияНачало;
						СтруктураПоискаНачисления.ПериодДействияКонец  = НачалоДня(СтрокаТаблицыОшибок.ПериодДействияКонец);
						СтруктураПоискаНачисления.ДокументОснование    = СтрокаТаблицыОшибок.Регистратор;
						СтруктураПоискаНачисления.Сторно     = СтрокаТаблицыОшибок.Сторно;
						НайденныеСтроки = ОсновныеНачисления.НайтиСтроки(СтруктураПоискаНачисления);
						ТекстТабличнаяЧасть = """Основные начисления"", ";
					Иначе
						СтруктураПоискаДополнительныеНачисления.Сотрудник  = СтрокаТаблицыОшибок.Сотрудник;	
						СтруктураПоискаДополнительныеНачисления.ВидРасчета = СтрокаТаблицыОшибок.ВидРасчета;
						СтруктураПоискаДополнительныеНачисления.ДокументОснование = СтрокаТаблицыОшибок.Регистратор;
						СтруктураПоискаДополнительныеНачисления.Сторно     = СтрокаТаблицыОшибок.Сторно;
						НайденныеСтроки = ДополнительныеНачисления.НайтиСтроки(СтруктураПоискаДополнительныеНачисления);
						ТекстТабличнаяЧасть = """Дополнительные начисления"", ";
					КонецЕсли;
					
				КонецЕсли;
				
				Для каждого СтрокаТЧ Из НайденныеСтроки Цикл
					ТекстСообщенияПоСтрокеТЧ = ТекстТабличнаяЧасть + "строка: " + СтрокаТЧ.НомерСтроки;
					ОбщегоНазначенияЗК.КомментарийРасчета(ТекстСообщенияПоСтрокеТЧ, РодительскаяСтрока);
				КонецЦикла;
				
			КонецЦикла;
			
		КонецЕсли;	
		
	КонецЕсли;
	
	// восстановление движений документа
	Если ДокументОбъект.Проведен Тогда
		
		Для Каждого Набор Из ДокументОбъект.Движения Цикл
			Если ТипЗнч(Набор)=Тип("РегистрРасчетаНаборЗаписей.ЕСНОсновныеНачисления")  Тогда
				
				НаборДвиженийОсновной.Записать();
				
			ИначеЕсли  ТипЗнч(Набор)=Тип("РегистрРасчетаНаборЗаписей.ЕСНДополнительныеНачисления") Тогда
				
				НаборДвиженийДополнительный.Записать();
				
			КонецЕсли;
		КонецЦикла;
		
	Иначе
		
		Для Каждого Набор Из ДокументОбъект.Движения Цикл
			Если ТипЗнч(Набор)=Тип("РегистрРасчетаНаборЗаписей.ЕСНОсновныеНачисления") 
				или ТипЗнч(Набор)=Тип("РегистрРасчетаНаборЗаписей.ЕСНДополнительныеНачисления") Тогда
				
				// Удаляем движения
				Набор.Очистить();
				Набор.Записать();
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры // Автозаполнение()

Функция ПолучитьДополнительноеУсловиеЗапросаБазаФСС_НС() Экспорт

	Возврат "";

КонецФункции // ПолучитьДополнительноеУсловиеЗапросаБазаФСС_НС



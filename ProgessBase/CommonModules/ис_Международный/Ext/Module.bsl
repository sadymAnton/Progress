
// is ЯннуровВФ нач 20140522 ЗР-0И-001219
// Контроль соответствия остатков ОС МСФО
// Параметры:
//   ОсновныеСредства - массив - список основных средств
//   Отказ - булево - при возврате "ИСТИНА" были найдены несоответстия
Процедура КонтрольОстатковОСМСФО(ОсновныеСредства=Неопределено, Отказ=Неопределено) Экспорт 
	
	лЗапрос = Новый Запрос;
	лЗапрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ОсновныеСредстваМеждународныйУчетСрезПоследних.ОсновноеСредство КАК ОсновноеСредство,
	|	ВЫБОР
	|		КОГДА ОсновныеСредстваМеждународныйУчетСрезПоследних.СчетУчета В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Международный.ПриобретениеИСтроительствоОС))
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоСчет08
	|ПОМЕСТИТЬ втСчетаОС
	|ИЗ
	|	РегистрСведений.ОсновныеСредстваМеждународныйУчет.СрезПоследних(
	|			,
	|			&ОтборОС = ЛОЖЬ
	|				ИЛИ ОсновноеСредство В (&ОсновныеСредства)) КАК ОсновныеСредстваМеждународныйУчетСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НоменклатураОсновныхСредств.ОсновноеСредство КАК ОсновноеСредство,
	|	НоменклатураОсновныхСредств.Номенклатура
	|ПОМЕСТИТЬ втНоменклатураОС
	|ИЗ
	|	РегистрСведений.НоменклатураОсновныхСредств КАК НоменклатураОсновныхСредств
	|ГДЕ
	|	(&ОтборОС = ЛОЖЬ
	|			ИЛИ НоменклатураОсновныхСредств.ОсновноеСредство В (&ОсновныеСредства))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	МеждународныйОстатки.Субконто1 КАК ОсновноеСредство,
	|	МеждународныйОстатки.СуммаОстаток,
	|	0 КАК КоличествоОстаток
	|ПОМЕСТИТЬ втОстатки
	|ИЗ
	|	РегистрБухгалтерии.Международный.Остатки(
	|			,
	|			Счет В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Международный.ПриобретениеИСтроительствоОС)),
	|			&ВидСубконтоОсновныеСредства,
	|			&ОтборОС = ЛОЖЬ
	|				ИЛИ Субконто1 В (&ОсновныеСредства)) КАК МеждународныйОстатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСчетаОС КАК втСчетаОС
	|		ПО МеждународныйОстатки.Субконто1 = втСчетаОС.ОсновноеСредство
	|ГДЕ
	|	втСчетаОС.ЭтоСчет08 = ЛОЖЬ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	МеждународныйОстатки.Субконто1,
	|	МеждународныйОстатки.СуммаОстаток,
	|	МеждународныйОстатки.КоличествоОстаток
	|ИЗ
	|	РегистрБухгалтерии.Международный.Остатки(
	|			,
	|			Счет В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Международный.ПриобретениеИСтроительствоОС)),
	|			&ВидСубконтоНоменклатура,
	|			&ОтборОС = ЛОЖЬ
	|				ИЛИ Субконто1 В
	|					(ВЫБРАТЬ
	|						втНоменклатураОС.Номенклатура
	|					ИЗ
	|						втНоменклатураОС)) КАК МеждународныйОстатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втНоменклатураОС КАК втНоменклатураОС
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСчетаОС КАК втСчетаОС
	|			ПО втНоменклатураОС.ОсновноеСредство = втСчетаОС.ОсновноеСредство
	|		ПО МеждународныйОстатки.Субконто1 = втНоменклатураОС.Номенклатура
	|ГДЕ
	|	втСчетаОС.ЭтоСчет08 = ЛОЖЬ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	втОстатки.ОсновноеСредство КАК ОсновноеСредство,
	|	втОстатки.СуммаОстаток
	|ИЗ
	|	втОстатки КАК втОстатки
	|ГДЕ
	|	втОстатки.КоличествоОстаток = 0
	|	И втОстатки.СуммаОстаток <> 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	ОсновноеСредство
	|АВТОУПОРЯДОЧИВАНИЕ";
	лЗапрос.УстановитьПараметр("ВидСубконтоОсновныеСредства", ПланыВидовХарактеристик.ВидыСубконтоМеждународные.ОсновныеСредства);
	лЗапрос.УстановитьПараметр("ВидСубконтоНоменклатура", ПланыВидовХарактеристик.ВидыСубконтоМеждународные.Номенклатура);
	Если ОсновныеСредства <> Неопределено Тогда 
		лЗапрос.УстановитьПараметр("ОтборОС", Истина);
		лЗапрос.УстановитьПараметр("ОсновныеСредства", ОсновныеСредства);
	Иначе
		лЗапрос.УстановитьПараметр("ОтборОС", Ложь);
		лЗапрос.УстановитьПараметр("ОсновныеСредства", Новый Массив);
	КонецЕсли;
	лВыборка = лЗапрос.Выполнить().Выбрать();
	Если лВыборка.Количество() > 0 Тогда 
		Если ТипЗнч(Отказ) = Тип("Булево") Тогда 
			Отказ = Истина;
		КонецЕсли;
		Сообщить("На счетах 08.* есть следующие остатки:");
		Пока лВыборка.Следующий() Цикл 
			Сообщить(Строка(лВыборка.ОсновноеСредство)+" на сумму "+лВыборка.СуммаОстаток);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Функция СформироватьТекстЗапросаПоЗатратам()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	//ДляРеглУчета Затраты.Организация,
	|	Затраты.Подразделение,
	|	Затраты.СтатьяЗатрат,
	|	Затраты.СтатьяЗатрат.ХарактерЗатрат КАК ХарактерЗатрат,
	|	Затраты.СтатьяЗатрат.ВидРасходовНУ КАК ВидРасходовНУ,
	|	Затраты.НоменклатурнаяГруппа,
	|	Затраты.Заказ,
	|	//ДляУпрУчета Затраты.Проект,
	|	//ДляРеглУчета Затраты.СчетУчета,
	|	//ДляБухУчета ЕСТЬNULL(СчетаУчетаЕНВД.ЕНВД, Ложь) КАК ЕНВД,
	|	//ДляБухУчета ЕСТЬNULL(СчетаУчетаЕНВД.ПодлежитРаспределению, Ложь) КАК ПодлежитРаспределению,
	|
	|	//ДляНалУчета Ложь КАК ЕНВД,
	|	//ДляНалУчета ВЫБОР КОГДА Затраты.СчетУчета В (&МассивРаспределяемыхСчетов) ТОГДА
	|	//ДляНалУчета 	Истина
	|	//ДляНалУчета ИНАЧЕ
	|	//ДляНалУчета 	Ложь
	|	//ДляНалУчета КОНЕЦ КАК ПодлежитРаспределению,
	|	
	|	//ДляНалУчета Затраты.ПостояннаяРазницаОстаток,
	|	//ДляНалУчета Затраты.ВременнаяРазницаОстаток,
	|
	|	Затраты.СуммаОстаток
	|
	|ИЗ
	|	РегистрНакопления.Затраты%СуффиксУчета%.Остатки(&КонГраница,
	|		//ДляУпрУчета СтатьяЗатрат.ХарактерЗатрат В (&МассивХарактеровЗатрат)
	|		//ДляРеглУчета Организация = &Организация 
	|		//ДляРеглУчета И СчетУчета В ИЕРАРХИИ (&МассивСчетовЗатрат)
	|
	|		//ДляНалУчета И СчетУчета Не В (&МассивСчетовКосвенныхРасходов)
	|		//ДляНалУчета И (СтатьяЗатрат.ВидРасходовНУ Не В (&НормируемыеВидыРасходов)
	|		//ДляНалУчета 	ИЛИ СчетУчета Не В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Налоговый.РасходыНаПродажу))
	|		//ДляНалУчета	)
	|
	|		//РавноВидРасходовНУ И СтатьяЗатрат.ВидРасходовНУ = ЗНАЧЕНИЕ(Перечисление.ВидыРасходовНУ.ТранспортныеРасходы)
	|		//НеРавноВидРасходовНУ И СтатьяЗатрат.ВидРасходовНУ <> ЗНАЧЕНИЕ(Перечисление.ВидыРасходовНУ.ТранспортныеРасходы)
	|
	|		) КАК Затраты
	|	
	|	//ДляБухУчета	ЛЕВОЕ СОЕДИНЕНИЕ (												
	|	//ДляБухУчета		ВЫБРАТЬ
	|	//ДляБухУчета			СчетаУчетаЕНВД.Счет,
	|	//ДляБухУчета			СчетаУчетаЕНВД.ПодлежитРаспределению,
	|	//ДляБухУчета			(Не СчетаУчетаЕНВД.ПодлежитРаспределению) КАК ЕНВД
	|	//ДляБухУчета		ИЗ
	|	//ДляБухУчета			РегистрСведений.СчетаУчетаПоДеятельностиЕНВД КАК СчетаУчетаЕНВД
	|	//ДляБухУчета		) КАК СчетаУчетаЕНВД
	|	//ДляБухУчета	ПО
	|	//ДляБухУчета		Затраты.СчетУчета = СчетаУчетаЕНВД.Счет
	|
	|УПОРЯДОЧИТЬ ПО
	|	//ДляРеглУчета Затраты.Организация,
	|	//ДляРеглУчета Затраты.СчетУчета,
	|	//ДляБухУчета ЕСТЬNULL(СчетаУчетаЕНВД.ЕНВД, Ложь),
	|	//ДляБухУчета ЕСТЬNULL(СчетаУчетаЕНВД.ПодлежитРаспределению, Ложь),
	|	Затраты.Подразделение,
	|	Затраты.НоменклатурнаяГруппа,
	|	Затраты.Заказ,
	|	Затраты.СтатьяЗатрат
	|";
		
	Возврат ТекстЗапроса;
	
КонецФункции // СформироватьТекстЗапросаПоЗатратам()

// is ЯннуровВФ нач 20140609
Функция ПолучитьЭлементРБП(Организация, Подразделение, НоменклатурнаяГруппа, СтатьяЗатрат, Создавать=Ложь, РБП=Неопределено) Экспорт 
	лРасходыБудущихПериодов = Справочники.РасходыБудущихПериодов.ПустаяСсылка();
	
	Если РБП <> Неопределено Тогда 
		
		лРасходыБудущихПериодов = РБП;
	 	лЗапись = РегистрыСведений.ис_ЗатратыБудущихПериодовМеждународный.СоздатьМенеджерЗаписи();
		лЗапись.Организация = Организация;
		лЗапись.Подразделение = Подразделение;
		лЗапись.НоменклатурнаяГруппа = НоменклатурнаяГруппа;
		лЗапись.СтатьяЗатрат = СтатьяЗатрат;
		лЗапись.РасходыБудущихПериодов = лРасходыБудущихПериодов;
		лЗапись.Записать();
		
	Иначе
		
		лЗапрос = Новый Запрос;
		лЗапрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ис_ЗатратыБудущихПериодовМеждународный.РасходыБудущихПериодов
		|ИЗ
		|	РегистрСведений.ис_ЗатратыБудущихПериодовМеждународный КАК ис_ЗатратыБудущихПериодовМеждународный
		|ГДЕ
		|	ис_ЗатратыБудущихПериодовМеждународный.Организация = &Организация
		|	И ис_ЗатратыБудущихПериодовМеждународный.Подразделение = &Подразделение
		|	И ис_ЗатратыБудущихПериодовМеждународный.НоменклатурнаяГруппа = &НоменклатурнаяГруппа
		|	И ис_ЗатратыБудущихПериодовМеждународный.СтатьяЗатрат = &СтатьяЗатрат";
		лЗапрос.УстановитьПараметр("Организация", Организация);
		лЗапрос.УстановитьПараметр("Подразделение", Подразделение);
		лЗапрос.УстановитьПараметр("НоменклатурнаяГруппа", НоменклатурнаяГруппа);
		лЗапрос.УстановитьПараметр("СтатьяЗатрат", СтатьяЗатрат);
		лВыборка = лЗапрос.Выполнить().Выбрать();
		Если лВыборка.Следующий() Тогда 
			лРасходыБудущихПериодов = лВыборка.РасходыБудущихПериодов;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(лРасходыБудущихПериодов) 
		 И Создавать Тогда 
		 	лРасходыБудущихПериодов = Справочники.РасходыБудущихПериодов.СоздатьЭлемент();
			лРасходыБудущихПериодов.Наименование  = Строка(Подразделение)+" / "+Строка(НоменклатурнаяГруппа)+" / "+Строка(СтатьяЗатрат);
			//>>061115 Степанов
			лРасходыБудущихПериодов.ОбменДанными.Загрузка = Истина;
			//<<
			лРасходыБудущихПериодов.Записать();
			лРасходыБудущихПериодов = лРасходыБудущихПериодов.Ссылка;
		 	лЗапись = РегистрыСведений.ис_ЗатратыБудущихПериодовМеждународный.СоздатьМенеджерЗаписи();
			лЗапись.Организация = Организация;
			лЗапись.Подразделение = Подразделение;
			лЗапись.НоменклатурнаяГруппа = НоменклатурнаяГруппа;
			лЗапись.СтатьяЗатрат = СтатьяЗатрат;
			лЗапись.РасходыБудущихПериодов = лРасходыБудущихПериодов;
			лЗапись.Записать();
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат лРасходыБудущихПериодов;
КонецФункции

// Функция формирует запрос по регистру "Затраты".
//
// Параметры
//  СтруктураШапкиДокумента – Структура - Реквизиты документа "Расчет себестоимости"
//	ХарактерЗатрат - ПеречислениеСсылка.ХарактерЗатрат - Характер затрат для выборки остатков
//	ВидРасходовНУ - ПеречислениеСсылка.ВидыРасходовНУ - Вид расхода НУ для выборки остатков 
//	УсловиеВидРасходовНУ - ВидСравнения - Условие для вида расходов НУ
//
// Возвращаемое значение:
//   Запрос – Запрос по регистру "Затраты".
//
Функция СформироватьЗапросПоЗатратам(
	СтруктураШапкиДокумента,
	ХарактерЗатрат,
	ВидРасходовНУ,
	УсловиеВидРасходовНУ,
	ВидЗатрат = Неопределено,
	ТолькоЗатраты = Ложь
	) Экспорт 
	
	//>>031215 Степанов 46607 исправление ситуации, когда закрытие отложенных затрат в части ОПР одновременно и переносилось из РБУ,
	//         и вычислялось пропорционально объемам выпуска здесь. При этом вычисления в сумме верные, но в проводках 
	//         проставлялся не 25/97, а 20/97 и субконто РБП выбиралось одно взамен многих. В итоге оставлен один способ - способ переноса х/97
	//         По статье затрат "Амортизация ОС" оставлен расчет по объему выпуска
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДС_ОбъемВыпускаДляРасчетаАмортизацииОС.ПроизводственнаяЛиния КАК ПроизводственнаяЛиния,
	|	ДС_ОбъемВыпускаДляРасчетаАмортизацииОС.ПериодАмортизации,
	|	ДС_ОбъемВыпускаДляРасчетаАмортизацииОС.КоличествоВыпущеннойПродукции
	|ПОМЕСТИТЬ втГрафикиВыпуска
	|ИЗ
	|	РегистрСведений.ДС_ОбъемВыпускаДляРасчетаАмортизацииОС.СрезПоследних(&КонДата, ) КАК ДС_ОбъемВыпускаДляРасчетаАмортизацииОС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПроизводственнаяЛиния
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	втГрафикиВыпуска.ПроизводственнаяЛиния КАК ПроизводственнаяЛиния,
	|	МАКСИМУМ(втГрафикиВыпуска.КоличествоВыпущеннойПродукции) КАК ОбъемВПериоде,
	|	ЕСТЬNULL(МАКСИМУМ(втГрафикиВыпускаДо.ПериодАмортизации), ДАТАВРЕМЯ(1, 1, 1)) КАК НачалоВыпуска,
	|	ЕСТЬNULL(МИНИМУМ(втГрафикиВыпускаПосле.ПериодАмортизации), ДАТАВРЕМЯ(3999, 12, 31)) КАК ОкончаниеВыпуска
	|ПОМЕСТИТЬ втПериодыВыпуска
	|ИЗ
	|	втГрафикиВыпуска КАК втГрафикиВыпуска
	|		ЛЕВОЕ СОЕДИНЕНИЕ втГрафикиВыпуска КАК втГрафикиВыпускаДо
	|		ПО втГрафикиВыпуска.ПроизводственнаяЛиния = втГрафикиВыпускаДо.ПроизводственнаяЛиния
	|			И (втГрафикиВыпускаДо.ПериодАмортизации < &НачДата)
	|			И (втГрафикиВыпускаДо.КоличествоВыпущеннойПродукции = 0)
	|		ЛЕВОЕ СОЕДИНЕНИЕ втГрафикиВыпуска КАК втГрафикиВыпускаПосле
	|		ПО втГрафикиВыпуска.ПроизводственнаяЛиния = втГрафикиВыпускаПосле.ПроизводственнаяЛиния
	|			И (втГрафикиВыпускаПосле.ПериодАмортизации > &КонДата)
	|			И (втГрафикиВыпускаПосле.КоличествоВыпущеннойПродукции = 0)
	|ГДЕ
	|	втГрафикиВыпуска.ПериодАмортизации МЕЖДУ &НачДата И &КонДата
	|	И втГрафикиВыпуска.КоличествоВыпущеннойПродукции > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	втГрафикиВыпуска.ПроизводственнаяЛиния
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПроизводственнаяЛиния
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	втПериодыВыпуска.ПроизводственнаяЛиния КАК ПроизводственнаяЛиния,
	|	МАКСИМУМ(втПериодыВыпуска.ОбъемВПериоде) КАК ОбъемВПериоде,
	|	СУММА(втГрафикиВыпуска.КоличествоВыпущеннойПродукции) КАК ОбъемВсего,
	|	ЕСТЬNULL(СУММА(ВЫБОР
	|				КОГДА втГрафикиВыпуска.ПериодАмортизации > &КонДата
	|					ТОГДА втГрафикиВыпуска.КоличествоВыпущеннойПродукции
	|				ИНАЧЕ 0
	|			КОНЕЦ), 0) КАК ОбъемБудущий
	|ПОМЕСТИТЬ втОбъемыВыпуска
	|ИЗ
	|	втПериодыВыпуска КАК втПериодыВыпуска
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втГрафикиВыпуска КАК втГрафикиВыпуска
	|		ПО втПериодыВыпуска.ПроизводственнаяЛиния = втГрафикиВыпуска.ПроизводственнаяЛиния
	|			И втПериодыВыпуска.НачалоВыпуска < втГрафикиВыпуска.ПериодАмортизации
	|			И втПериодыВыпуска.ОкончаниеВыпуска > втГрафикиВыпуска.ПериодАмортизации
	|
	|СГРУППИРОВАТЬ ПО
	|	втПериодыВыпуска.ПроизводственнаяЛиния
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПроизводственнаяЛиния
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Затраты.Организация,
	|	Затраты.Подразделение КАК Подразделение,
	|	Затраты.СтатьяЗатрат КАК СтатьяЗатрат,
	|	Затраты.СтатьяЗатрат.ХарактерЗатрат КАК ХарактерЗатрат,
	|	Затраты.СтатьяЗатрат.ВидРасходовНУ КАК ВидРасходовНУ,
	|	Затраты.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	Затраты.Заказ КАК Заказ,
	|	Затраты.СчетУчета,
	|	Затраты.СуммаОстаток
	|ИЗ
	|	РегистрНакопления.ЗатратыМеждународныйУчет.Остатки(
	|			&КонГраница,
	|			Организация = &Организация
	|				И (СтатьяЗатрат.ВидЗатрат = &ВидЗатрат
	|					ИЛИ &ВидЗатрат = НЕОПРЕДЕЛЕНО)
	|				И СчетУчета В ИЕРАРХИИ (&МассивСчетовЗатрат)) КАК Затраты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РасходыБП.Организация,
	|	РасходыБП.Подразделение,
	|	РасходыБП.СтатьяЗатрат,
	|	РасходыБП.СтатьяЗатрат.ХарактерЗатрат,
	|	РасходыБП.СтатьяЗатрат.ВидРасходовНУ,
	|	РасходыБП.НоменклатурнаяГруппа,
	|	НЕОПРЕДЕЛЕНО,
	|	ЗНАЧЕНИЕ(ПланСчетов.Международный._ПрочиеРасходыБудущихПериодов),
	|	ВЫБОР
	|		КОГДА втОбъемыВыпуска.ОбъемБудущий > 0
	|			ТОГДА втОбъемыВыпуска.ОбъемВПериоде / втОбъемыВыпуска.ОбъемВсего
	|		ИНАЧЕ 1
	|	КОНЕЦ * РасходыБП.СуммаОстаток
	|ИЗ
	|	втОбъемыВыпуска КАК втОбъемыВыпуска
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ис_РасходыБудущихПериодовМеждународныйУчет.Остатки(
	|				&КонГраница,
	|				Организация = &Организация
//>>	|					И (СтатьяЗатрат.ВидЗатрат = &ВидЗатрат
	|				    	И СтатьяЗатрат = &СтатьяЗатратАмортизацияОС
//>>	|						ИЛИ &ВидЗатрат = НЕОПРЕДЕЛЕНО)
	|					И &ТолькоЗатраты = ЛОЖЬ) КАК РасходыБП
	|		ПО втОбъемыВыпуска.ПроизводственнаяЛиния = РасходыБП.Подразделение
	|
	|УПОРЯДОЧИТЬ ПО
	|	Подразделение,
	|	НоменклатурнаяГруппа,
	|	Заказ,
	|	СтатьяЗатрат";
	//<<031215
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("НачДата", СтруктураШапкиДокумента.мНачДата);
	Запрос.УстановитьПараметр("КонДата", СтруктураШапкиДокумента.мКонДата);
	Запрос.УстановитьПараметр("НачГраница", СтруктураШапкиДокумента.мНачГраница);
	Запрос.УстановитьПараметр("КонГраница", СтруктураШапкиДокумента.мКонГраница);
	Запрос.УстановитьПараметр("Организация", СтруктураШапкиДокумента.Организация);
	Запрос.УстановитьПараметр("ВидЗатрат", ВидЗатрат);
	Запрос.УстановитьПараметр("ТолькоЗатраты", ТолькоЗатраты);
	//>>031215
	Запрос.УстановитьПараметр("СтатьяЗатратАмортизацияОС", Справочники.СтатьиЗатрат.НайтиПоКоду("000010474"));
	//<<031215
	
	МассивСчетовЗатрат = ПроцедурыРасчетаСебестоимостиВыпуска.ПолучитьМассивСчетовЗатрат(
		СтруктураШапкиДокумента,
		ХарактерЗатрат
	);
	Запрос.УстановитьПараметр("МассивСчетовЗатрат", МассивСчетовЗатрат);
	
	Возврат Запрос;
КонецФункции // СформироватьЗапросПоЗатратам()

Функция ПолучитьРегистрПоСчетуЗатрат(СчетЗатрат) Экспорт 
	лРегистр = Неопределено;
	
	Если Не ЗначениеЗаполнено(СчетЗатрат) Тогда 
		Возврат лРегистр;
	КонецЕсли;
	
	Если СчетЗатрат = ПланыСчетов.Международный.ОсновноеПроизводство Или СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Международный.ОсновноеПроизводство) 
	 Или СчетЗатрат = ПланыСчетов.Международный.ВспомогательныеПроизводства Или СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Международный.ВспомогательныеПроизводства)  
	 Или СчетЗатрат = ПланыСчетов.Международный.ОбслуживающиеПроизводства Или СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Международный.ОбслуживающиеПроизводства) Тогда 
		лРегистр = "НезавершенноеПроизводствоМеждународныйУчет";
	ИначеЕсли СчетЗатрат = ПланыСчетов.Международный.БракВПроизводстве Или СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Международный.БракВПроизводстве) Тогда 
		лРегистр = "БракВПроизводствеМеждународныйУчет";
	ИначеЕсли СчетЗатрат = ПланыСчетов.Международный.ОбщепроизводственныеРасходы Или СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Международный.ОбщепроизводственныеРасходы) 
	 Или СчетЗатрат = ПланыСчетов.Международный.АдминистративныеРасходы Или СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Международный.АдминистративныеРасходы) 
	 Или СчетЗатрат = ПланыСчетов.Международный.РасходыНаПродажу Или СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Международный.РасходыНаПродажу) Тогда 
		лРегистр = "ЗатратыМеждународныйУчет";
	КонецЕсли;
	
	Возврат лРегистр;
КонецФункции

// Договор лизинга
Функция ПолучитьДоговорЛизингаОС(ОсновноеСредство) Экспорт 
	лДоговор = Неопределено;

	лЗапрос = Новый Запрос;
	лЗапрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ДоговорыКонтрагентов.Ссылка КАК Договор,
	|	ДоговорыКонтрагентов.Дата КАК ДатаДоговора
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	ДоговорыКонтрагентов.ПометкаУдаления = ЛОЖЬ
	|	И ДоговорыКонтрагентов.ДС_ОбъектДоговора <> ЗНАЧЕНИЕ(Справочник.ОсновныеСредства.ПустаяСсылка)
	|	И ДоговорыКонтрагентов.ВидВзаиморасчетов В (ЗНАЧЕНИЕ(Справочник.ВидыВзаиморасчетов.ЛизингМашин), ЗНАЧЕНИЕ(Справочник.ВидыВзаиморасчетов.ЛизингОборудования))
	|	И ДоговорыКонтрагентов.ДС_ОбъектДоговора = &ОсновноеСредство
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаДоговора УБЫВ
	|АВТОУПОРЯДОЧИВАНИЕ";
	лЗапрос.УстановитьПараметр("ОсновноеСредство", ОсновноеСредство);
	лВыборка = лЗапрос.Выполнить().Выбрать();
	Если лВыборка.Следующий() Тогда 
		лДоговор = лВыборка.Договор;
	КонецЕсли;
	
	Возврат лДоговор;
КонецФункции

// Счет учета основного средства по умолчанию
Функция ПолучитьСчетУчетаОСПоУмолчанию(ОсновноеСредство) Экспорт 
	лСчетУчета = ПланыСчетов.Международный.ОсновныеСредстваВСобственности;
	
	лДоговор = ПолучитьДоговорЛизингаОС(ОсновноеСредство);
	Если ЗначениеЗаполнено(лДоговор) Тогда 
		Если лДоговор.ВидВзаиморасчетов = Справочники.ВидыВзаиморасчетов.ЛизингМашин Тогда 
			лСчетУчета = ПланыСчетов.Международный._ЛизингТранспортныеСредства;
		ИначеЕсли лДоговор.ВидВзаиморасчетов = Справочники.ВидыВзаиморасчетов.ЛизингОборудования Тогда 
			лСчетУчета = ПланыСчетов.Международный.ВложенияВЛизинговоеОборудование;
		КонецЕсли;
	КонецЕсли;
	
	Возврат лСчетУчета;
КонецФункции

// Счет амортизации основного средства по умолчанию
Функция ПолучитьСчетАмортизацииОСПоУмолчанию(ОсновноеСредство) Экспорт 
	лСчетАмортизации = ПланыСчетов.Международный.АмортизацияСобственныхОсновныхСредств;
	
	лДоговор = ПолучитьДоговорЛизингаОС(ОсновноеСредство);
	Если ЗначениеЗаполнено(лДоговор) Тогда 
		Если лДоговор.ВидВзаиморасчетов = Справочники.ВидыВзаиморасчетов.ЛизингМашин Тогда 
			лСчетАмортизации = ПланыСчетов.Международный._АмортизацияЛизингТранспортныеСредства;
		ИначеЕсли лДоговор.ВидВзаиморасчетов = Справочники.ВидыВзаиморасчетов.ЛизингОборудования Тогда 
			лСчетАмортизации = ПланыСчетов.Международный.АмортизацияВложенияВЛизинговоеОборудование;
		КонецЕсли;
	КонецЕсли;
	
	Возврат лСчетАмортизации;
КонецФункции

// Распределенние НЗП вспомогательных производств при расчете себестоимости
Процедура РаспределитьНЗПВспомогательныхПроизводств(СтруктураШапкиДокумента) Экспорт 
	
	лНаборЗаписейНПЗ = РегистрыНакопления.НезавершенноеПроизводствоМеждународныйУчет.СоздатьНаборЗаписей();
	лНаборЗаписейЗатраты = РегистрыНакопления.ЗатратыМеждународныйУчет.СоздатьНаборЗаписей();
	лНаборЗаписейПроводки = РегистрыБухгалтерии.Международный.СоздатьНаборЗаписей();
	
	лЗапрос = Новый Запрос;
	лЗапрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВыпускПродукцииМеждународныйУчетОбороты.Подразделение КАК Подразделение,
	|	СУММА(ВыпускПродукцииМеждународныйУчетОбороты.КоличествоОборот) КАК Количество
	|ПОМЕСТИТЬ втВыпускПродукции
	|ИЗ
	|	РегистрНакопления.ВыпускПродукцииМеждународныйУчет.Обороты(
	|			&НачалоПериода,
	|			&ОкончаниеПериода,
	|			Период,
	|			Организация = &Организация
	|				И Подразделение.ВидПодразделения = ЗНАЧЕНИЕ(Перечисление.ВидыПодразделений.ОсновноеПроизводство)) КАК ВыпускПродукцииМеждународныйУчетОбороты
	|
	|СГРУППИРОВАТЬ ПО
	|	ВыпускПродукцииМеждународныйУчетОбороты.Подразделение
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВыпускПродукцииМеждународныйУчетОбороты.КоличествоОборот) > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Подразделение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПРГ_СтатьиДляРасчетаБазыПрямыхЗатратСрезПоследних.СтатьяЗатрат КАК СтатьяЗатрат
	|ПОМЕСТИТЬ втСтатьиЗатрат
	|ИЗ
	|	РегистрСведений.ПРГ_СтатьиДляРасчетаБазыПрямыхЗатрат.СрезПоследних(&ОкончаниеПериода, Организация = &Организация) КАК ПРГ_СтатьиДляРасчетаБазыПрямыхЗатратСрезПоследних
	|ГДЕ
	|	ПРГ_СтатьиДляРасчетаБазыПрямыхЗатратСрезПоследних.Использование = ИСТИНА
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СтатьяЗатрат
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НезавершенноеПроизводствоМеждународныйУчетОбороты.Подразделение КАК Подразделение,
	|	НезавершенноеПроизводствоМеждународныйУчетОбороты.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	НезавершенноеПроизводствоМеждународныйУчетОбороты.СтоимостьОборот КАК База
	|ПОМЕСТИТЬ втБазаРаспределения
	|ИЗ
	|	РегистрНакопления.НезавершенноеПроизводствоМеждународныйУчет.Обороты(&НачалоПериода, &ОкончаниеПериода, Период, Организация = &Организация) КАК НезавершенноеПроизводствоМеждународныйУчетОбороты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втВыпускПродукции КАК втВыпускПродукции
	|		ПО НезавершенноеПроизводствоМеждународныйУчетОбороты.Подразделение = втВыпускПродукции.Подразделение
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСтатьиЗатрат КАК втСтатьиЗатрат
	|		ПО НезавершенноеПроизводствоМеждународныйУчетОбороты.СтатьяЗатрат = втСтатьиЗатрат.СтатьяЗатрат
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗатратыМеждународныйУчетОбороты.Подразделение,
	|	ЗатратыМеждународныйУчетОбороты.НоменклатурнаяГруппа,
	|	ЗатратыМеждународныйУчетОбороты.СуммаОборот
	|ИЗ
	|	РегистрНакопления.ЗатратыМеждународныйУчет.Обороты(
	|			&НачалоПериода,
	|			&ОкончаниеПериода,
	|			Период,
	|			Организация = &Организация
	|				И СчетУчета В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Международный.ОбщепроизводственныеРасходы))) КАК ЗатратыМеждународныйУчетОбороты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втВыпускПродукции КАК втВыпускПродукции
	|		ПО ЗатратыМеждународныйУчетОбороты.Подразделение = втВыпускПродукции.Подразделение
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСтатьиЗатрат КАК втСтатьиЗатрат
	|		ПО ЗатратыМеждународныйУчетОбороты.СтатьяЗатрат = втСтатьиЗатрат.СтатьяЗатрат
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Подразделение,
	|	НоменклатурнаяГруппа
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	втБазаРаспределения.Подразделение,
	|	втБазаРаспределения.НоменклатурнаяГруппа,
	|	СУММА(втБазаРаспределения.База) КАК База
	|ИЗ
	|	втБазаРаспределения КАК втБазаРаспределения
	|
	|СГРУППИРОВАТЬ ПО
	|	втБазаРаспределения.Подразделение,
	|	втБазаРаспределения.НоменклатурнаяГруппа
	|
	|ИМЕЮЩИЕ
	|	СУММА(втБазаРаспределения.База) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НезавершенноеПроизводствоМеждународныйУчетОбороты.Подразделение,
	|	НезавершенноеПроизводствоМеждународныйУчетОбороты.СчетУчета,
	|	НезавершенноеПроизводствоМеждународныйУчетОбороты.СтатьяЗатрат,
	|	НезавершенноеПроизводствоМеждународныйУчетОбороты.НоменклатурнаяГруппа,
	|	НезавершенноеПроизводствоМеждународныйУчетОбороты.Заказ,
	|	НезавершенноеПроизводствоМеждународныйУчетОбороты.Затрата,
	|	НезавершенноеПроизводствоМеждународныйУчетОбороты.ХарактеристикаЗатраты,
	|	НезавершенноеПроизводствоМеждународныйУчетОбороты.СерияЗатраты,
	|	НезавершенноеПроизводствоМеждународныйУчетОбороты.КоличествоОборот КАК Количество,
	|	НезавершенноеПроизводствоМеждународныйУчетОбороты.СтоимостьОборот КАК Сумма,
	|	ИСТИНА КАК НПЗ
	|ИЗ
	|	РегистрНакопления.НезавершенноеПроизводствоМеждународныйУчет.Обороты(&НачалоПериода, &ОкончаниеПериода, Период, Организация = &Организация) КАК НезавершенноеПроизводствоМеждународныйУчетОбороты
	|ГДЕ
	|	НезавершенноеПроизводствоМеждународныйУчетОбороты.Подразделение.ВидПодразделения = ЗНАЧЕНИЕ(Перечисление.ВидыПодразделений.ВспомогательноеПроизводство)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗатратыМеждународныйУчетОбороты.Подразделение,
	|	ЗатратыМеждународныйУчетОбороты.СчетУчета,
	|	ЗатратыМеждународныйУчетОбороты.СтатьяЗатрат,
	|	ЗатратыМеждународныйУчетОбороты.НоменклатурнаяГруппа,
	|	ЗатратыМеждународныйУчетОбороты.Заказ,
	|	NULL,
	|	NULL,
	|	NULL,
	|	0,
	|	ЗатратыМеждународныйУчетОбороты.СуммаОборот,
	|	ЛОЖЬ
	|ИЗ
	|	РегистрНакопления.ЗатратыМеждународныйУчет.Обороты(
	|			&НачалоПериода,
	|			&ОкончаниеПериода,
	|			Период,
	|			Организация = &Организация
	|				И СчетУчета В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Международный.ОбщепроизводственныеРасходы))) КАК ЗатратыМеждународныйУчетОбороты
	|ГДЕ
	|	ЗатратыМеждународныйУчетОбороты.Подразделение.ВидПодразделения = ЗНАЧЕНИЕ(Перечисление.ВидыПодразделений.ВспомогательноеПроизводство)";
	лЗапрос.УстановитьПараметр("НачалоПериода", СтруктураШапкиДокумента.мНачГраница);
	лЗапрос.УстановитьПараметр("ОкончаниеПериода", СтруктураШапкиДокумента.мКонГраница);
	лЗапрос.УстановитьПараметр("Организация", СтруктураШапкиДокумента.Организация);
	лРезультатыЗапроса = лЗапрос.ВыполнитьПакет();
	
	лБазаРаспределения = лРезультатыЗапроса[3].Выгрузить();
	лВсегоБаза = лБазаРаспределения.Итог("База");
	
	лВыборкаЗатраты = лРезультатыЗапроса[4].Выбрать();
	Пока лВыборкаЗатраты.Следующий() Цикл 
		Если лБазаРаспределения.Количество() = 0
		 Или лВсегоБаза = 0 Тогда 
			Продолжить;
		КонецЕсли;
		
		// Расход с вспомогательного производства
		Если лВыборкаЗатраты.НПЗ Тогда 
			// Запись расхода с НПЗ
			лЗаписьНПЗ = лНаборЗаписейНПЗ.Добавить();
			лЗаписьНПЗ.ВидДвижения = ВидДвиженияНакопления.Расход;
			лЗаписьНПЗ.Период = СтруктураШапкиДокумента.Период;
			лЗаписьНПЗ.Регистратор = СтруктураШапкиДокумента.Ссылка;
			лЗаписьНПЗ.Организация = СтруктураШапкиДокумента.Организация;
			лЗаписьНПЗ.Подразделение = лВыборкаЗатраты.Подразделение;
			лЗаписьНПЗ.СчетУчета = лВыборкаЗатраты.СчетУчета;
			лЗаписьНПЗ.СтатьяЗатрат = лВыборкаЗатраты.СтатьяЗатрат;
			лЗаписьНПЗ.НоменклатурнаяГруппа = лВыборкаЗатраты.НоменклатурнаяГруппа;
			лЗаписьНПЗ.Заказ = лВыборкаЗатраты.Заказ;
			лЗаписьНПЗ.Затрата = лВыборкаЗатраты.Затрата;
			лЗаписьНПЗ.ХарактеристикаЗатраты = лВыборкаЗатраты.ХарактеристикаЗатраты;
			лЗаписьНПЗ.СерияЗатраты = лВыборкаЗатраты.СерияЗатраты;
			лЗаписьНПЗ.Количество = лВыборкаЗатраты.Количество;
			лЗаписьНПЗ.Стоимость = лВыборкаЗатраты.Сумма;
		Иначе
			// Запись расхода с затрат
			лЗаписьЗатраты = лНаборЗаписейЗатраты.Добавить();
			лЗаписьЗатраты.ВидДвижения = ВидДвиженияНакопления.Расход;
			лЗаписьЗатраты.Период = СтруктураШапкиДокумента.Период;
			лЗаписьЗатраты.Регистратор = СтруктураШапкиДокумента.Ссылка;
			лЗаписьЗатраты.Организация = СтруктураШапкиДокумента.Организация;
			лЗаписьЗатраты.Подразделение = лВыборкаЗатраты.Подразделение;
			лЗаписьЗатраты.СчетУчета = лВыборкаЗатраты.СчетУчета;
			лЗаписьЗатраты.СтатьяЗатрат = лВыборкаЗатраты.СтатьяЗатрат;
			лЗаписьЗатраты.НоменклатурнаяГруппа = лВыборкаЗатраты.НоменклатурнаяГруппа;
			лЗаписьЗатраты.Заказ = лВыборкаЗатраты.Заказ;
			лЗаписьЗатраты.Сумма = лВыборкаЗатраты.Сумма;
		КонецЕсли;
		
		// Приход на основное производство
		лКоличествоОстаток = лВыборкаЗатраты.Количество;
		лСуммаОстаток = лВыборкаЗатраты.Сумма;
		Для лИндекс=0 По лБазаРаспределения.Количество()-1 Цикл 
			лСтрокаБазы = лБазаРаспределения[лИндекс];
			
			// Расчет количества и суммы
			Если лИндекс < лБазаРаспределения.Количество()-1 Тогда 
				лКоэффициент = лСтрокаБазы.База / лВсегоБаза;
				лКоличество = Окр(лВыборкаЗатраты.Количество*лКоэффициент, 3);
				лСумма = Окр(лВыборкаЗатраты.Сумма*лКоэффициент, 2);
			Иначе
				лКоличество = лКоличествоОстаток;
				лСумма = лСуммаОстаток;
			КонецЕсли;
			лКоличествоОстаток = лКоличествоОстаток - лКоличество;
			лСуммаОстаток = лСуммаОстаток - лСумма;
			
			Если лВыборкаЗатраты.НПЗ Тогда 
				// Приход на НПЗ
				лЗаписьНПЗ = лНаборЗаписейНПЗ.Добавить();
				лЗаписьНПЗ.ВидДвижения = ВидДвиженияНакопления.Приход;
				лЗаписьНПЗ.Период = СтруктураШапкиДокумента.Период;
				лЗаписьНПЗ.Регистратор = СтруктураШапкиДокумента.Ссылка;
				лЗаписьНПЗ.Организация = СтруктураШапкиДокумента.Организация;
				лЗаписьНПЗ.Подразделение = лСтрокаБазы.Подразделение;
				лЗаписьНПЗ.СчетУчета = лВыборкаЗатраты.СчетУчета;
				лЗаписьНПЗ.СтатьяЗатрат = лВыборкаЗатраты.СтатьяЗатрат;
				лЗаписьНПЗ.НоменклатурнаяГруппа = лСтрокаБазы.НоменклатурнаяГруппа;
				лЗаписьНПЗ.Заказ = лВыборкаЗатраты.Заказ;
				лЗаписьНПЗ.Затрата = лВыборкаЗатраты.Затрата;
				лЗаписьНПЗ.ХарактеристикаЗатраты = лВыборкаЗатраты.ХарактеристикаЗатраты;
				лЗаписьНПЗ.СерияЗатраты = лВыборкаЗатраты.СерияЗатраты;
				лЗаписьНПЗ.Количество = лКоличество;
				лЗаписьНПЗ.Стоимость = лСумма;
				// Проводка
				Если лСтрокаБазы.Подразделение <> лВыборкаЗатраты.Подразделение
				 Или лСтрокаБазы.НоменклатурнаяГруппа <> лВыборкаЗатраты.НоменклатурнаяГруппа Тогда 
					лПроводка = лНаборЗаписейПроводки.Добавить();
					лПроводка.Период = СтруктураШапкиДокумента.Период;
					лПроводка.Регистратор = СтруктураШапкиДокумента.Ссылка;
					лПроводка.Организация = СтруктураШапкиДокумента.Организация;
					лПроводка.СчетДт = лВыборкаЗатраты.СчетУчета;
					лПроводка.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконтоМеждународные.Подразделения] = лСтрокаБазы.Подразделение;
					лПроводка.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконтоМеждународные.НоменклатурныеГруппы] = лСтрокаБазы.НоменклатурнаяГруппа;
					лПроводка.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконтоМеждународные.СтатьиЗатрат] = лВыборкаЗатраты.СтатьяЗатрат;
					лПроводка.СчетКт = лВыборкаЗатраты.СчетУчета;
					лПроводка.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконтоМеждународные.Подразделения] = лВыборкаЗатраты.Подразделение;
					лПроводка.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконтоМеждународные.НоменклатурныеГруппы] = лВыборкаЗатраты.НоменклатурнаяГруппа;
					лПроводка.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконтоМеждународные.СтатьиЗатрат] = лВыборкаЗатраты.СтатьяЗатрат;
					лПроводка.Сумма = лСумма;
				КонецЕсли;
			Иначе
				// Приход на затраты
				лЗаписьЗатраты = лНаборЗаписейЗатраты.Добавить();
				лЗаписьЗатраты.ВидДвижения = ВидДвиженияНакопления.Расход;
				лЗаписьЗатраты.Период = СтруктураШапкиДокумента.Период;
				лЗаписьЗатраты.Регистратор = СтруктураШапкиДокумента.Ссылка;
				лЗаписьЗатраты.Организация = СтруктураШапкиДокумента.Организация;
				лЗаписьЗатраты.Подразделение = лСтрокаБазы.Подразделение;
				лЗаписьЗатраты.СчетУчета = лВыборкаЗатраты.СчетУчета;
				лЗаписьЗатраты.СтатьяЗатрат = лВыборкаЗатраты.СтатьяЗатрат;
				лЗаписьЗатраты.НоменклатурнаяГруппа = лСтрокаБазы.НоменклатурнаяГруппа;
				лЗаписьЗатраты.Заказ = лВыборкаЗатраты.Заказ;
				лЗаписьЗатраты.Сумма = лСумма;
				// Проводка
				Если лСтрокаБазы.Подразделение <> лВыборкаЗатраты.Подразделение Тогда 
					лПроводка = лНаборЗаписейПроводки.Добавить();
					лПроводка.Период = СтруктураШапкиДокумента.Период;
					лПроводка.Регистратор = СтруктураШапкиДокумента.Ссылка;
					лПроводка.Организация = СтруктураШапкиДокумента.Организация;
					лПроводка.СчетДт = лВыборкаЗатраты.СчетУчета;
					лПроводка.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконтоМеждународные.Подразделения] = лСтрокаБазы.Подразделение;
					лПроводка.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконтоМеждународные.СтатьиЗатрат] = лВыборкаЗатраты.СтатьяЗатрат;
					лПроводка.СчетКт = лВыборкаЗатраты.СчетУчета;
					лПроводка.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконтоМеждународные.Подразделения] = лВыборкаЗатраты.Подразделение;
					лПроводка.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконтоМеждународные.СтатьиЗатрат] = лВыборкаЗатраты.СтатьяЗатрат;
					лПроводка.Сумма = лСумма;
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Если лНаборЗаписейНПЗ.Количество() > 0 Тогда 
		лНаборЗаписейНПЗ.Отбор.Регистратор.Установить(СтруктураШапкиДокумента.Ссылка);
		лНаборЗаписейНПЗ.Записать(Ложь);
	КонецЕсли;
	Если лНаборЗаписейЗатраты.Количество() > 0 Тогда 
		лНаборЗаписейЗатраты.Отбор.Регистратор.Установить(СтруктураШапкиДокумента.Ссылка);
		лНаборЗаписейЗатраты.Записать(Ложь);
	КонецЕсли;
	Если лНаборЗаписейПроводки.Количество() > 0 Тогда 
		лНаборЗаписейПроводки.Отбор.Регистратор.Установить(СтруктураШапкиДокумента.Ссылка);
		лНаборЗаписейПроводки.Записать(Ложь);
	КонецЕсли;
	
КонецПроцедуры

// Распределенние общепроизвоственных затрат при расчете себестоимости
Процедура РаспределитьОбщепроизводственныеЗатраты(СтруктураШапкиДокумента) Экспорт 
	
	лНаборЗаписейЗатраты = РегистрыНакопления.ЗатратыМеждународныйУчет.СоздатьНаборЗаписей();
	лНаборЗаписейНПЗ = РегистрыНакопления.НезавершенноеПроизводствоМеждународныйУчет.СоздатьНаборЗаписей();
	лНаборЗаписейПроводки = РегистрыБухгалтерии.Международный.СоздатьНаборЗаписей();
	
	лЗапрос = Новый Запрос;
	лЗапрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ВыпускПродукцииМеждународныйУчетОбороты.Подразделение,
	|	ВыпускПродукцииМеждународныйУчетОбороты.НоменклатурнаяГруппа,
	|	СУММА(ВыпускПродукцииМеждународныйУчетОбороты.КоличествоОборот) КАК База
	|ИЗ
	|	РегистрНакопления.ВыпускПродукцииМеждународныйУчет.Обороты(
	|			&НачалоПериода,
	|			&ОкончаниеПериода,
	|			Период,
	|			Организация = &Организация
	|				И Подразделение.ВидПодразделения = ЗНАЧЕНИЕ(Перечисление.ВидыПодразделений.ОсновноеПроизводство)) КАК ВыпускПродукцииМеждународныйУчетОбороты
	|
	|СГРУППИРОВАТЬ ПО
	|	ВыпускПродукцииМеждународныйУчетОбороты.Подразделение,
	|	ВыпускПродукцииМеждународныйУчетОбороты.НоменклатурнаяГруппа
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВыпускПродукцииМеждународныйУчетОбороты.КоличествоОборот) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗатратыМеждународныйУчетОбороты.Подразделение,
	|	ЗатратыМеждународныйУчетОбороты.СчетУчета,
	|	ЗатратыМеждународныйУчетОбороты.СтатьяЗатрат,
	|	ЗатратыМеждународныйУчетОбороты.НоменклатурнаяГруппа,
	|	ЗатратыМеждународныйУчетОбороты.Заказ,
	|	ЗатратыМеждународныйУчетОбороты.СуммаОборот КАК Сумма
	|ИЗ
	|	РегистрНакопления.ЗатратыМеждународныйУчет.Обороты(
	|			&НачалоПериода,
	|			&ОкончаниеПериода,
	|			Период,
	|			СчетУчета В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Международный.ОбщепроизводственныеРасходы))
	|				И Организация = &Организация) КАК ЗатратыМеждународныйУчетОбороты
	|ГДЕ
	|	ЗатратыМеждународныйУчетОбороты.Подразделение.ВидПодразделения = ЗНАЧЕНИЕ(Перечисление.ВидыПодразделений.ОсновноеПроизводство)";
	лЗапрос.УстановитьПараметр("НачалоПериода", СтруктураШапкиДокумента.мНачГраница);
	лЗапрос.УстановитьПараметр("ОкончаниеПериода", СтруктураШапкиДокумента.мКонГраница);
	лЗапрос.УстановитьПараметр("Организация", СтруктураШапкиДокумента.Организация);
	лРезультатыЗапроса = лЗапрос.ВыполнитьПакет();
	
	лБазаРаспределения = лРезультатыЗапроса[0].Выгрузить();
	лВсегоБаза = лБазаРаспределения.Итог("База");
	
	лВыборкаЗатраты = лРезультатыЗапроса[1].Выбрать();
	Пока лВыборкаЗатраты.Следующий() Цикл 
		Если лБазаРаспределения.Количество() = 0
		 Или лВсегоБаза = 0 Тогда 
			Продолжить;
		КонецЕсли;
		
		// Расход с общепроизводственных затрат
		лЗаписьЗатраты = лНаборЗаписейЗатраты.Добавить();
		лЗаписьЗатраты.ВидДвижения = ВидДвиженияНакопления.Расход;
		лЗаписьЗатраты.Период = СтруктураШапкиДокумента.Период;
		лЗаписьЗатраты.Регистратор = СтруктураШапкиДокумента.Ссылка;
		лЗаписьЗатраты.Организация = СтруктураШапкиДокумента.Организация;
		лЗаписьЗатраты.Подразделение = лВыборкаЗатраты.Подразделение;
		лЗаписьЗатраты.СчетУчета = лВыборкаЗатраты.СчетУчета;
		лЗаписьЗатраты.СтатьяЗатрат = лВыборкаЗатраты.СтатьяЗатрат;
		лЗаписьЗатраты.НоменклатурнаяГруппа = лВыборкаЗатраты.НоменклатурнаяГруппа;
		лЗаписьЗатраты.Заказ = лВыборкаЗатраты.Заказ;
		лЗаписьЗатраты.Сумма = лВыборкаЗатраты.Сумма;
		
		// Приход на основное производство
		лСуммаОстаток = лВыборкаЗатраты.Сумма;
		Для лИндекс=0 По лБазаРаспределения.Количество()-1 Цикл 
			лСтрокаБазы = лБазаРаспределения[лИндекс];
			
			// Расчет количества и суммы
			Если лИндекс < лБазаРаспределения.Количество()-1 Тогда 
				лКоэффициент = лСтрокаБазы.База / лВсегоБаза;
				лСумма = Окр(лВыборкаЗатраты.Сумма*лКоэффициент, 2);
			Иначе
				лСумма = лСуммаОстаток;
			КонецЕсли;
			лСуммаОстаток = лСуммаОстаток - лСумма;
			
			// Приход на НПЗ
			лЗаписьНПЗ = лНаборЗаписейНПЗ.Добавить();
			лЗаписьНПЗ.ВидДвижения = ВидДвиженияНакопления.Приход;
			лЗаписьНПЗ.Период = СтруктураШапкиДокумента.Период;
			лЗаписьНПЗ.Регистратор = СтруктураШапкиДокумента.Ссылка;
			лЗаписьНПЗ.Организация = СтруктураШапкиДокумента.Организация;
			лЗаписьНПЗ.Подразделение = лСтрокаБазы.Подразделение;
			лЗаписьНПЗ.СчетУчета = лВыборкаЗатраты.СчетУчета;
			лЗаписьНПЗ.СтатьяЗатрат = лВыборкаЗатраты.СтатьяЗатрат;
			лЗаписьНПЗ.НоменклатурнаяГруппа = лСтрокаБазы.НоменклатурнаяГруппа;
			лЗаписьНПЗ.Заказ = лВыборкаЗатраты.Заказ;
			лЗаписьНПЗ.Стоимость = лСумма;
			// Проводка
			лПроводка = лНаборЗаписейПроводки.Добавить();
			лПроводка.Период = СтруктураШапкиДокумента.Период;
			лПроводка.Регистратор = СтруктураШапкиДокумента.Ссылка;
			лПроводка.Организация = СтруктураШапкиДокумента.Организация;
			лПроводка.СчетДт = ПланыСчетов.Международный._ОсновноеПроизводство;
			лПроводка.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконтоМеждународные.Подразделения] = лСтрокаБазы.Подразделение;
			лПроводка.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконтоМеждународные.НоменклатурныеГруппы] = лСтрокаБазы.НоменклатурнаяГруппа;
			лПроводка.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконтоМеждународные.СтатьиЗатрат] = лВыборкаЗатраты.СтатьяЗатрат;
			лПроводка.СчетКт = лВыборкаЗатраты.СчетУчета;
			лПроводка.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконтоМеждународные.Подразделения] = лВыборкаЗатраты.Подразделение;
			лПроводка.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконтоМеждународные.СтатьиЗатрат] = лВыборкаЗатраты.СтатьяЗатрат;
			лПроводка.Сумма = лСумма;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Если лНаборЗаписейЗатраты.Количество() > 0 Тогда 
		лНаборЗаписейЗатраты.Отбор.Регистратор.Установить(СтруктураШапкиДокумента.Ссылка);
		лНаборЗаписейЗатраты.Записать(Ложь);
	КонецЕсли;
	Если лНаборЗаписейНПЗ.Количество() > 0 Тогда 
		лНаборЗаписейНПЗ.Отбор.Регистратор.Установить(СтруктураШапкиДокумента.Ссылка);
		лНаборЗаписейНПЗ.Записать(Ложь);
	КонецЕсли;
	Если лНаборЗаписейПроводки.Количество() > 0 Тогда 
		лНаборЗаписейПроводки.Отбор.Регистратор.Установить(СтруктураШапкиДокумента.Ссылка);
		лНаборЗаписейПроводки.Записать(Ложь);
	КонецЕсли;
	
КонецПроцедуры

// Распределенние общехозяйственных затрат при расчете себестоимости
Процедура РаспределитьОбщехозяйственныеЗатраты(СтруктураШапкиДокумента) Экспорт 
	
	лНаборЗаписейЗатраты = РегистрыНакопления.ЗатратыМеждународныйУчет.СоздатьНаборЗаписей();
	лНаборЗаписейНПЗ = РегистрыНакопления.НезавершенноеПроизводствоМеждународныйУчет.СоздатьНаборЗаписей();
	лНаборЗаписейПроводки = РегистрыБухгалтерии.Международный.СоздатьНаборЗаписей();
	
	лЗапрос = Новый Запрос;
	лЗапрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ВыпускПродукцииМеждународныйУчетОбороты.Подразделение КАК Подразделение,
	|	СУММА(ВыпускПродукцииМеждународныйУчетОбороты.КоличествоОборот) КАК Количество
	|ПОМЕСТИТЬ втВыпускПродукции
	|ИЗ
	|	РегистрНакопления.ВыпускПродукцииМеждународныйУчет.Обороты(
	|			&НачалоПериода,
	|			&ОкончаниеПериода,
	|			Период,
	|			Организация = &Организация
	|				И Подразделение.ВидПодразделения = ЗНАЧЕНИЕ(Перечисление.ВидыПодразделений.ОсновноеПроизводство)) КАК ВыпускПродукцииМеждународныйУчетОбороты
	|
	|СГРУППИРОВАТЬ ПО
	|	ВыпускПродукцииМеждународныйУчетОбороты.Подразделение
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВыпускПродукцииМеждународныйУчетОбороты.КоличествоОборот) > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Подразделение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПРГ_СтатьиДляРасчетаБазыПрямыхЗатратСрезПоследних.СтатьяЗатрат КАК СтатьяЗатрат
	|ПОМЕСТИТЬ втСтатьиЗатрат
	|ИЗ
	|	РегистрСведений.ПРГ_СтатьиДляРасчетаБазыПрямыхЗатрат.СрезПоследних(&ОкончаниеПериода, Организация = &Организация) КАК ПРГ_СтатьиДляРасчетаБазыПрямыхЗатратСрезПоследних
	|ГДЕ
	|	ПРГ_СтатьиДляРасчетаБазыПрямыхЗатратСрезПоследних.Использование = ИСТИНА
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СтатьяЗатрат
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НезавершенноеПроизводствоМеждународныйУчетОбороты.Подразделение КАК Подразделение,
	|	НезавершенноеПроизводствоМеждународныйУчетОбороты.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	НезавершенноеПроизводствоМеждународныйУчетОбороты.СтоимостьОборот КАК База
	|ПОМЕСТИТЬ втБазаРаспределения
	|ИЗ
	|	РегистрНакопления.НезавершенноеПроизводствоМеждународныйУчет.Обороты(&НачалоПериода, &ОкончаниеПериода, Период, Организация = &Организация) КАК НезавершенноеПроизводствоМеждународныйУчетОбороты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втВыпускПродукции КАК втВыпускПродукции
	|		ПО НезавершенноеПроизводствоМеждународныйУчетОбороты.Подразделение = втВыпускПродукции.Подразделение
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСтатьиЗатрат КАК втСтатьиЗатрат
	|		ПО НезавершенноеПроизводствоМеждународныйУчетОбороты.СтатьяЗатрат = втСтатьиЗатрат.СтатьяЗатрат
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗатратыМеждународныйУчетОбороты.Подразделение,
	|	ЗатратыМеждународныйУчетОбороты.НоменклатурнаяГруппа,
	|	ЗатратыМеждународныйУчетОбороты.СуммаОборот
	|ИЗ
	|	РегистрНакопления.ЗатратыМеждународныйУчет.Обороты(
	|			&НачалоПериода,
	|			&ОкончаниеПериода,
	|			Период,
	|			Организация = &Организация
	|				И СчетУчета В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Международный.ОбщепроизводственныеРасходы))) КАК ЗатратыМеждународныйУчетОбороты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втВыпускПродукции КАК втВыпускПродукции
	|		ПО ЗатратыМеждународныйУчетОбороты.Подразделение = втВыпускПродукции.Подразделение
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСтатьиЗатрат КАК втСтатьиЗатрат
	|		ПО ЗатратыМеждународныйУчетОбороты.СтатьяЗатрат = втСтатьиЗатрат.СтатьяЗатрат
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Подразделение,
	|	НоменклатурнаяГруппа
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	втБазаРаспределения.Подразделение,
	|	втБазаРаспределения.НоменклатурнаяГруппа,
	|	СУММА(втБазаРаспределения.База) КАК База
	|ИЗ
	|	втБазаРаспределения КАК втБазаРаспределения
	|
	|СГРУППИРОВАТЬ ПО
	|	втБазаРаспределения.Подразделение,
	|	втБазаРаспределения.НоменклатурнаяГруппа
	|
	|ИМЕЮЩИЕ
	|	СУММА(втБазаРаспределения.База) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗатратыМеждународныйУчетОбороты.Подразделение,
	|	ЗатратыМеждународныйУчетОбороты.СчетУчета,
	|	ЗатратыМеждународныйУчетОбороты.СтатьяЗатрат,
	|	ЗатратыМеждународныйУчетОбороты.НоменклатурнаяГруппа,
	|	ЗатратыМеждународныйУчетОбороты.Заказ,
	|	ЗатратыМеждународныйУчетОбороты.СуммаОборот КАК Сумма
	|ИЗ
	|	РегистрНакопления.ЗатратыМеждународныйУчет.Обороты(
	|			&НачалоПериода,
	|			&ОкончаниеПериода,
	|			Период,
	|			Организация = &Организация
	|				И СчетУчета В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Международный.ОбщепроизводственныеРасходы))) КАК ЗатратыМеждународныйУчетОбороты
	|ГДЕ
	|	ЗатратыМеждународныйУчетОбороты.Подразделение.ВидПодразделения = ЗНАЧЕНИЕ(Перечисление.ВидыПодразделений.Прочее)";
	лЗапрос.УстановитьПараметр("НачалоПериода", СтруктураШапкиДокумента.мНачГраница);
	лЗапрос.УстановитьПараметр("ОкончаниеПериода", СтруктураШапкиДокумента.мКонГраница);
	лЗапрос.УстановитьПараметр("Организация", СтруктураШапкиДокумента.Организация);
	лРезультатыЗапроса = лЗапрос.ВыполнитьПакет();
	
	лБазаРаспределения = лРезультатыЗапроса[3].Выгрузить();
	лВсегоБаза = лБазаРаспределения.Итог("База");
	
	лВыборкаЗатраты = лРезультатыЗапроса[4].Выбрать();
	Пока лВыборкаЗатраты.Следующий() Цикл 
		Если лБазаРаспределения.Количество() = 0
		 Или лВсегоБаза = 0 Тогда 
			Продолжить;
		КонецЕсли;
		
		// Расход с общепроизводственных затрат
		лЗаписьЗатраты = лНаборЗаписейЗатраты.Добавить();
		лЗаписьЗатраты.ВидДвижения = ВидДвиженияНакопления.Расход;
		лЗаписьЗатраты.Период = СтруктураШапкиДокумента.Период;
		лЗаписьЗатраты.Регистратор = СтруктураШапкиДокумента.Ссылка;
		лЗаписьЗатраты.Организация = СтруктураШапкиДокумента.Организация;
		лЗаписьЗатраты.Подразделение = лВыборкаЗатраты.Подразделение;
		лЗаписьЗатраты.СчетУчета = лВыборкаЗатраты.СчетУчета;
		лЗаписьЗатраты.СтатьяЗатрат = лВыборкаЗатраты.СтатьяЗатрат;
		лЗаписьЗатраты.НоменклатурнаяГруппа = лВыборкаЗатраты.НоменклатурнаяГруппа;
		лЗаписьЗатраты.Заказ = лВыборкаЗатраты.Заказ;
		лЗаписьЗатраты.Сумма = лВыборкаЗатраты.Сумма;
		
		// Приход на основное производство
		лСуммаОстаток = лВыборкаЗатраты.Сумма;
		Для лИндекс=0 По лБазаРаспределения.Количество()-1 Цикл 
			лСтрокаБазы = лБазаРаспределения[лИндекс];
			
			// Расчет количества и суммы
			Если лИндекс < лБазаРаспределения.Количество()-1 Тогда 
				лКоэффициент = лСтрокаБазы.База / лВсегоБаза;
				лСумма = Окр(лВыборкаЗатраты.Сумма*лКоэффициент, 2);
			Иначе
				лСумма = лСуммаОстаток;
			КонецЕсли;
			лСуммаОстаток = лСуммаОстаток - лСумма;
			
			// Приход на НПЗ
			лЗаписьНПЗ = лНаборЗаписейНПЗ.Добавить();
			лЗаписьНПЗ.ВидДвижения = ВидДвиженияНакопления.Приход;
			лЗаписьНПЗ.Период = СтруктураШапкиДокумента.Период;
			лЗаписьНПЗ.Регистратор = СтруктураШапкиДокумента.Ссылка;
			лЗаписьНПЗ.Организация = СтруктураШапкиДокумента.Организация;
			лЗаписьНПЗ.Подразделение = лСтрокаБазы.Подразделение;
			лЗаписьНПЗ.СчетУчета = лВыборкаЗатраты.СчетУчета;
			лЗаписьНПЗ.СтатьяЗатрат = лВыборкаЗатраты.СтатьяЗатрат;
			лЗаписьНПЗ.НоменклатурнаяГруппа = лСтрокаБазы.НоменклатурнаяГруппа;
			лЗаписьНПЗ.Заказ = лВыборкаЗатраты.Заказ;
			лЗаписьНПЗ.Стоимость = лСумма;
			// Проводка
			лПроводка = лНаборЗаписейПроводки.Добавить();
			лПроводка.Период = СтруктураШапкиДокумента.Период;
			лПроводка.Регистратор = СтруктураШапкиДокумента.Ссылка;
			лПроводка.Организация = СтруктураШапкиДокумента.Организация;
			лПроводка.СчетДт = ПланыСчетов.Международный._ОсновноеПроизводство;
			лПроводка.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконтоМеждународные.Подразделения] = лСтрокаБазы.Подразделение;
			лПроводка.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконтоМеждународные.НоменклатурныеГруппы] = лСтрокаБазы.НоменклатурнаяГруппа;
			лПроводка.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконтоМеждународные.СтатьиЗатрат] = лВыборкаЗатраты.СтатьяЗатрат;
			лПроводка.СчетКт = лВыборкаЗатраты.СчетУчета;
			лПроводка.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконтоМеждународные.Подразделения] = лВыборкаЗатраты.Подразделение;
			лПроводка.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконтоМеждународные.СтатьиЗатрат] = лВыборкаЗатраты.СтатьяЗатрат;
			лПроводка.Сумма = лСумма;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Если лНаборЗаписейЗатраты.Количество() > 0 Тогда 
		лНаборЗаписейЗатраты.Отбор.Регистратор.Установить(СтруктураШапкиДокумента.Ссылка);
		лНаборЗаписейЗатраты.Записать(Ложь);
	КонецЕсли;
	Если лНаборЗаписейНПЗ.Количество() > 0 Тогда 
		лНаборЗаписейНПЗ.Отбор.Регистратор.Установить(СтруктураШапкиДокумента.Ссылка);
		лНаборЗаписейНПЗ.Записать(Ложь);
	КонецЕсли;
	Если лНаборЗаписейПроводки.Количество() > 0 Тогда 
		лНаборЗаписейПроводки.Отбор.Регистратор.Установить(СтруктураШапкиДокумента.Ссылка);
		лНаборЗаписейПроводки.Записать(Ложь);
	КонецЕсли;
	
КонецПроцедуры

// is ЯннуровВФ нач 20150608
Функция ПолучитьКомплектыПродукции(Организация, ПериодРегистрации) Экспорт 
	
	лЗапрос = Новый Запрос;
	лЗапрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Номенклатура.Ссылка
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	/// Кунов О.В., 24.11.2016 - 
	|	Номенклатура.ВидНоменклатуры В (&ВидНоменклатуры43, &ВидНоменклатуры21, &ВидНоменклатуры21М)";
	лЗапрос.УстановитьПараметр("ВидНоменклатуры43", Справочники.ВидыНоменклатуры.НайтиПоКоду("000000003"));
	лЗапрос.УстановитьПараметр("ВидНоменклатуры21", Справочники.ВидыНоменклатуры.НайтиПоКоду("000000004"));
	лЗапрос.УстановитьПараметр("ВидНоменклатуры21М", Справочники.ВидыНоменклатуры.НайтиПоКоду("000000043"));
	///
	лКомплекты = лЗапрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	Возврат лКомплекты;
КонецФункции




Функция ОписаниеДоходовОрганизации(Организация, ГоловнаяОрганизация, ОрганизацияЭтоЮрЛицо, НалоговыйПериод, ДатаСведений, ФизЛицо = Неопределено, Ставка = Неопределено, ПоказыватьКодыОКТМО = Ложь) Экспорт

	ОтбиратьПоФизлицу = ЗначениеЗаполнено(ФизЛицо);
	ОтбиратьПоСтавке = ЗначениеЗаполнено(Ставка);
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Организация",Организация);
	Запрос.УстановитьПараметр("ГоловнаяОрганизация",ГоловнаяОрганизация);
	Запрос.УстановитьПараметр("ГодНП",НалоговыйПериод);
	Запрос.УстановитьПараметр("ДатаПодачиСведений", ДатаСведений);
	Запрос.УстановитьПараметр("НеОтбиратьПоФизлицу", Не ОтбиратьПоФизлицу);
	Запрос.УстановитьПараметр("НеОтбиратьПоСтавке", Не ОтбиратьПоСтавке);
	Запрос.УстановитьПараметр("ФизЛицо", ФизЛицо);
	Запрос.УстановитьПараметр("Ставка", Ставка);
	Запрос.УстановитьПараметр("ПустойКодПоОКАТО","");
	Запрос.УстановитьПараметр("ПустойКодПоОКТМО","");
	Запрос.УстановитьПараметр("ПустойКПП","");

	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫБОР
	|		КОГДА НДФЛСведенияОДоходах.КодПоОКАТО <> &ПустойКодПоОКАТО
	|			ТОГДА НДФЛСведенияОДоходах.КодПоОКАТО
	|		КОГДА ЕСТЬNULL(НДФЛСведенияОДоходах.ПодразделениеОрганизации.КодПоОКАТО, &ПустойКодПоОКАТО) <> &ПустойКодПоОКАТО
	|			ТОГДА НДФЛСведенияОДоходах.ПодразделениеОрганизации.КодПоОКАТО
	|		ИНАЧЕ ЕСТЬNULL(НДФЛСведенияОДоходах.ОбособленноеПодразделение.КодПоОКАТО, &ПустойКодПоОКАТО)
	|	КОНЕЦ КАК Код,
	|	ВЫБОР
	|		КОГДА НДФЛСведенияОДоходах.КПП <> &ПустойКПП
	|			ТОГДА НДФЛСведенияОДоходах.КПП
	|		КОГДА ЕСТЬNULL(НДФЛСведенияОДоходах.ПодразделениеОрганизации.КПП, &ПустойКПП) <> &ПустойКПП
	|			ТОГДА НДФЛСведенияОДоходах.ПодразделениеОрганизации.КПП
	|		ИНАЧЕ ЕСТЬNULL(НДФЛСведенияОДоходах.ОбособленноеПодразделение.КПП, &ПустойКПП)
	|	КОНЕЦ КАК КПП
	|ИЗ
	|	РегистрНакопления.НДФЛСведенияОДоходах КАК НДФЛСведенияОДоходах
	|ГДЕ
	|	НДФЛСведенияОДоходах.Организация = &ГоловнаяОрганизация
	|	И НДФЛСведенияОДоходах.ОбособленноеПодразделение = &Организация
	|	И ГОД(НДФЛСведенияОДоходах.Период) = &ГодНП
	|	И НДФЛСведенияОДоходах.ПериодРегистрации < &ДатаПодачиСведений
	|	И НДФЛСведенияОДоходах.СуммаДохода <> 0
	|	И (&НеОтбиратьПоФизлицу
	|			ИЛИ НДФЛСведенияОДоходах.ФизЛицо = &ФизЛицо)
	|	И (&НеОтбиратьПоСтавке
	|			ИЛИ НДФЛСведенияОДоходах.КодДохода.СтавкаНалогообложенияРезидента = &Ставка)";
	
	Если ПоказыватьКодыОКТМО Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,".КодПоОКАТО",".КодПоОКТМО");
	КонецЕсли;
	Запрос.Текст = ТекстЗапроса;
	Выборка = Запрос.Выполнить().Выбрать();
    ТекстКодов = "";
	Пока Выборка.Следующий() Цикл
		ТекстКодов = ТекстКодов + ?(ЗначениеЗаполнено(ТекстКодов),", ","") + ?(ОрганизацияЭтоЮрЛицо, СокрЛП(СправкиПоНДФЛ.СуммаОКАТОиКПП(Выборка.Код, Выборка.КПП)), СокрЛП(Выборка.Код))
	КонецЦикла;
	
	Возврат "За " + Формат(НалоговыйПериод,"ЧЦ=4; ЧДЦ=0; ЧГ=0") + " год " + ?(ЗначениеЗаполнено(ТекстКодов), "зарегистрированы доходы по следующим кодам " + ?(ПоказыватьКодыОКТМО, "ОКТМО", "ОКАТО") + ?(ОрганизацияЭтоЮрЛицо, " и КПП","") + ": " + ТекстКодов, "не зарегистрировано доходов")

КонецФункции // ОписаниеДоходов()

Функция МестаРегистрацииВНалоговыхОрганах(Организация, НалоговыйПериод) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", ОбщегоНазначенияЗК.ГоловнаяОрганизация(Организация));
	Запрос.УстановитьПараметр("ПустойКодПоОКАТО","");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	НЕОПРЕДЕЛЕНО КАК Представление,
	|	Организации.КодПоОКАТО,
	|	Организации.КодПоОКТМО,
	|	Организации.КПП,
	|	ВЫБОР
	|		КОГДА Организации.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА 1
	|		ИНАЧЕ 2
	|	КОНЕЦ КАК Порядок
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	ВЫБОР
	|			КОГДА Организации.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|				ТОГДА Организации.Ссылка
	|			ИНАЧЕ Организации.ГоловнаяОрганизация
	|		КОНЕЦ = &Организация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НЕОПРЕДЕЛЕНО,
	|	ПодразделенияОрганизаций.КодПоОКАТО,
	|	ПодразделенияОрганизаций.КодПоОКТМО,
	|	ПодразделенияОрганизаций.КПП,
	|	ВЫБОР
	|		КОГДА ПодразделенияОрганизаций.Владелец.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА 3
	|		ИНАЧЕ 4
	|	КОНЕЦ
	|ИЗ
	|	Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
	|ГДЕ
	|	ВЫБОР
	|			КОГДА ПодразделенияОрганизаций.Владелец.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|				ТОГДА ПодразделенияОрганизаций.Владелец
	|			ИНАЧЕ ПодразделенияОрганизаций.Владелец.ГоловнаяОрганизация
	|		КОНЕЦ = &Организация
	|	И (ПодразделенияОрганизаций.КодПоОКАТО <> &ПустойКодПоОКАТО
	|			ИЛИ ПодразделенияОрганизаций.КодПоОКТМО <> &ПустойКодПоОКАТО)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок";
	
	ГодПереходаНаКодыОКТМО = Год(ПроведениеРасчетов.ДатаПереходаНаКодыОКТМО());
	Если НалоговыйПериод >= ГодПереходаНаКодыОКТМО Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "НЕОПРЕДЕЛЕНО КАК Представление,", "Организации.Наименование + "": "" + Организации.КодПоОКТМО + "", "" + Организации.КПП КАК Представление,");
		Запрос.Текст = СтрЗаменить(ТекстЗапроса, "НЕОПРЕДЕЛЕНО,", "ПодразделенияОрганизаций.Владелец.Наименование + "", "" + ПодразделенияОрганизаций.Наименование + "": "" + ПодразделенияОрганизаций.КодПоОКТМО + "", "" + ПодразделенияОрганизаций.КПП,");
	ИначеЕсли НалоговыйПериод = ГодПереходаНаКодыОКТМО - 1 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "НЕОПРЕДЕЛЕНО КАК Представление,", "Организации.Наименование + "": "" + Организации.КодПоОКАТО + "" (по ОКТМО - "" + Организации.КодПоОКТМО + ""), "" + Организации.КПП КАК Представление,");
		Запрос.Текст = СтрЗаменить(ТекстЗапроса, "НЕОПРЕДЕЛЕНО,", "ПодразделенияОрганизаций.Владелец.Наименование + "", "" + ПодразделенияОрганизаций.Наименование + "": "" + ПодразделенияОрганизаций.КодПоОКАТО + "" (по ОКТМО - "" + ПодразделенияОрганизаций.КодПоОКТМО + ""), "" + ПодразделенияОрганизаций.КПП,");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "НЕОПРЕДЕЛЕНО КАК Представление,", "Организации.Наименование + "": "" + Организации.КодПоОКАТО + "", "" + Организации.КПП КАК Представление,");
		Запрос.Текст = СтрЗаменить(ТекстЗапроса, "НЕОПРЕДЕЛЕНО,", "ПодразделенияОрганизаций.Владелец.Наименование + "", "" + ПодразделенияОрганизаций.Наименование + "": "" + ПодразделенияОрганизаций.КодПоОКАТО + "", "" + ПодразделенияОрганизаций.КПП,");
	КонецЕсли;
	
	Возврат Запрос.Выполнить().Выгрузить(); 
	
КонецФункции 

Функция ДанныеОДоходахСПомесячнымУчетомСтатусаФЛ(ТаблицаСотрудников, НалоговыйПериод, Организация, ГоловнаяОрганизация, КодОП, Дата, ПоказыватьПоКодуОКТМО = Ложь, ИсключатьДоходыПоЦеннымБумагам = Ложь) Экспорт

	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылки", ТаблицаСотрудников);
	Запрос.УстановитьПараметр("ДатаЗакона229ФЗ", ПроведениеРасчетов.ДатаЗакона229ФЗ());
	Запрос.УстановитьПараметр("ВыделятьДивидендыРезидентов", НалоговыйПериод >= Год(ПроведениеРасчетов.ДатаИзмененияНалогообложенияДивидендов()));
	Запрос.УстановитьПараметр("ЗаменятьБеженцаНерезидентом", НалоговыйПериод < Год(ПроведениеРасчетов.ДатаЗакона285ФЗ())); // беженцы до начала действия Федерального закона 285-ФЗ соответствуют обычным нерезидентам
	Запрос.УстановитьПараметр("Организация",Организация);
	Запрос.УстановитьПараметр("ГоловнаяОрганизация",ГоловнаяОрганизация);
	Запрос.УстановитьПараметр("ГодНП",НалоговыйПериод);
	НачалоНалоговогоПериода = Дата(НалоговыйПериод, 1, 1);
	КонецНалоговогоПериода = КонецГода(НачалоНалоговогоПериода);
	Запрос.УстановитьПараметр("КонецНП",КонецНалоговогоПериода);
	Запрос.УстановитьПараметр("НачалоНП", НачалоНалоговогоПериода);
	Запрос.УстановитьПараметр("НачалоСледующегоНП", КонецНалоговогоПериода + 1); // начало следующего налогового периода
	Запрос.УстановитьПараметр("ДатаПодачиСведений", Дата);
	Запрос.УстановитьПараметр("ДатаДоПодачиСведений", Дата - 1);
	ЧитатьСводныеДанные = КодОП = Неопределено;
	Запрос.УстановитьПараметр("ЧитатьСводныеДанные", ЧитатьСводныеДанные);
	Запрос.УстановитьПараметр("ИсключатьДоходыПоЦеннымБумагам", ИсключатьДоходыПоЦеннымБумагам);
	Запрос.УстановитьПараметр("ЧитатьСводныеДанныеПоСтавкам", ЧитатьСводныеДанные Или НалоговыйПериод > 2010);
	Запрос.УстановитьПараметр("РазделятьИтогиПоСтавкам", НалоговыйПериод > 2010);
	Запрос.УстановитьПараметр("ОКАТО_КПП", КодОП);
	Запрос.УстановитьПараметр("ОКТМО_КПП", КодОП);
	Если ЧитатьСводныеДанные Тогда
		Запрос.УстановитьПараметр("КодПоОКАТО", Неопределено);
		Запрос.УстановитьПараметр("КПП", Неопределено);
		Запрос.УстановитьПараметр("КодПоОКТМО", Неопределено);
		Запрос.УстановитьПараметр("КПП", Неопределено);
	Иначе
		Запрос.УстановитьПараметр("КодПоОКАТО",СправкиПоНДФЛ.КодОКАТОизСуммыОКАТОиКПП(КодОП));
		Запрос.УстановитьПараметр("КПП",СправкиПоНДФЛ.КППизСуммыОКАТОиКПП(КодОП));
		Запрос.УстановитьПараметр("КодПоОКТМО",СправкиПоНДФЛ.КодОКАТОизСуммыОКАТОиКПП(КодОП));
		Запрос.УстановитьПараметр("КПП",СправкиПоНДФЛ.КППизСуммыОКАТОиКПП(КодОП));
	КонецЕсли;
	Запрос.УстановитьПараметр("ПустойКодПоОКАТО","");
	Запрос.УстановитьПараметр("ПустойКодПоОКТМО","");
	Запрос.УстановитьПараметр("ПустойКПП","");
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СотрудникиОрганизаций.Сотрудник КАК Физлицо,
	|	СотрудникиОрганизаций.НомерСправки,
	|	СотрудникиОрганизаций.Ставка
	|ПОМЕСТИТЬ ВТФизлица
	|ИЗ
	|	&Ссылки КАК СотрудникиОрганизаций";
	Если НалоговыйПериод > 2010 Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"СотрудникиОрганизаций.Ставка","ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.ПустаяСсылка) КАК Ставка")
	КонецЕсли;
	Запрос.Выполнить();
	НачМесяца = НачалоНалоговогоПериода;
	ДатыПоМесяцамТекст = "ВЫБРАТЬ ДАТАВРЕМЯ(" + Формат(НачМесяца,"ДФ=гггг,М,д,Ч,м,с") + ") КАК Период";
	ДатыПоМесяцамТекст = ДатыПоМесяцамТекст + "
	|ПОМЕСТИТЬ ВТПериодыНалогаНаДоходы";
	Пока НачМесяца < НачалоМесяца(КонецНалоговогоПериода) Цикл
		НачМесяца = НачалоМесяца(КонецМесяца(НачМесяца) + 1);
		ДатыПоМесяцамТекст = ДатыПоМесяцамТекст +"
		|ОБЪЕДИНИТЬ ВСЕ ВЫБРАТЬ ДАТАВРЕМЯ(" + Формат(НачМесяца,"ДФ=гггг,М,д,Ч,м,с") + ")";
	КонецЦикла;
	Запрос.Текст = ДатыПоМесяцамТекст;
	Запрос.Выполнить();
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Статусы.ФизЛицо,
	|	ВЫБОР
	|		КОГДА &ЗаменятьБеженцаНерезидентом
	|				И Статусы.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.Беженец)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|		ИНАЧЕ Статусы.Статус
	|	КОНЕЦ КАК Статус
	|ПОМЕСТИТЬ ВТРезидентствоФизлицНаКонецГода
	|ИЗ
	|	РегистрСведений.СтатусФизЛицКакНалогоплательщиковНДФЛ.СрезПоследних(
	|			&КонецНП,
	|			ФизЛицо В
	|				(ВЫБРАТЬ
	|					ФизическиеЛица.ФизЛицо
	|				ИЗ
	|					ВТФизлица КАК ФизическиеЛица)) КАК Статусы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПериодыРегистра.ФизЛицо КАК ФизЛицо,
	|	ПериодыРегистра.МесяцНалоговогоПериода,
	|	ВЫБОР
	|		КОГДА &ЗаменятьБеженцаНерезидентом
	|				И СтатусНалогоплательщика.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.Беженец)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|		ИНАЧЕ ЕСТЬNULL(СтатусНалогоплательщика.Статус, ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.Резидент))
	|	КОНЕЦ КАК Статус
	|ПОМЕСТИТЬ ВТРезидентствоФизлицНаКонецМесяца
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		Физлица.ФизЛицо КАК ФизЛицо,
	|		Периоды.Период КАК МесяцНалоговогоПериода,
	|		МАКСИМУМ(СтатусНалогоплательщика.Период) КАК Период
	|	ИЗ
	|		ВТФизлица КАК Физлица
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТПериодыНалогаНаДоходы КАК Периоды
	|			ПО (ИСТИНА)
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусФизЛицКакНалогоплательщиковНДФЛ КАК СтатусНалогоплательщика
	|			ПО Физлица.ФизЛицо = СтатусНалогоплательщика.ФизЛицо
	|				И (СтатусНалогоплательщика.Период <= КОНЕЦПЕРИОДА(Периоды.Период, МЕСЯЦ))
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Физлица.ФизЛицо,
	|		Периоды.Период) КАК ПериодыРегистра
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусФизЛицКакНалогоплательщиковНДФЛ КАК СтатусНалогоплательщика
	|		ПО ПериодыРегистра.ФизЛицо = СтатусНалогоплательщика.ФизЛицо
	|			И ПериодыРегистра.Период = СтатусНалогоплательщика.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Физлица.ФизЛицо,
	|	Периоды.Период КАК МесяцНалоговогоПериода,
	|	ВЫБОР
	|		КОГДА Периоды.Период < &ДатаЗакона229ФЗ
	|			ТОГДА ЕСТЬNULL(РезидентствоФизлицНаКонецГода.Статус, ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.Резидент))
	|		ИНАЧЕ РезидентствоФизлицНаКонецМесяца.Статус
	|	КОНЕЦ КАК ТекущийСтатус,
	|	ЕСТЬNULL(РезидентствоФизлицНаКонецГода.Статус, ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.Резидент)) КАК Статус
	|ПОМЕСТИТЬ ВТСтатусы
	|ИЗ
	|	ВТФизлица КАК Физлица
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПериодыНалогаНаДоходы КАК Периоды
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТРезидентствоФизлицНаКонецМесяца КАК РезидентствоФизлицНаКонецМесяца
	|		ПО Физлица.ФизЛицо = РезидентствоФизлицНаКонецМесяца.ФизЛицо
	|			И (Периоды.Период = РезидентствоФизлицНаКонецМесяца.МесяцНалоговогоПериода)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТРезидентствоФизлицНаКонецГода КАК РезидентствоФизлицНаКонецГода
	|		ПО Физлица.ФизЛицо = РезидентствоФизлицНаКонецГода.ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СотрудникиОрганизаций.НомерСправки,
	|	СотрудникиОрганизаций.ФизЛицо,
	|	МИНИМУМ(НДФЛИмущественныеВычетыФизлиц.Регистратор) КАК Регистратор
	|ПОМЕСТИТЬ ВТДокументыУведомлений
	|ИЗ
	|	ВТФизлица КАК СотрудникиОрганизаций
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДФЛИмущественныеВычетыФизлиц КАК НДФЛИмущественныеВычетыФизлиц
	|		ПО СотрудникиОрганизаций.ФизЛицо = НДФЛИмущественныеВычетыФизлиц.ФизЛицо
	|			И (&ЧитатьСводныеДанныеПоСтавкам
	|				ИЛИ СотрудникиОрганизаций.Ставка = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13))
	|			И (НДФЛИмущественныеВычетыФизлиц.Период < &ДатаПодачиСведений)
	|			И (НДФЛИмущественныеВычетыФизлиц.Организация = &ГоловнаяОрганизация)
	|			И (НДФЛИмущественныеВычетыФизлиц.Год = &ГодНП)
	|			И (НДФЛИмущественныеВычетыФизлиц.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход))
	|ГДЕ
	|	НДФЛИмущественныеВычетыФизлиц.Регистратор ССЫЛКА Документ.НДФЛПодтверждениеПраваНаИмущественныйВычет
	|
	|СГРУППИРОВАТЬ ПО
	|	СотрудникиОрганизаций.НомерСправки,
	|	СотрудникиОрганизаций.ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументыУведомлений.НомерСправки,
	|	ЕСТЬNULL(НДФЛПодтверждениеПраваНаИмущественныйВычетРаботникиОрганизации.ДатаУведомления, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаУведомления,
	|	ЕСТЬNULL(НДФЛПодтверждениеПраваНаИмущественныйВычетРаботникиОрганизации.КодНалоговогоОргана, """") КАК КодНалоговогоОрганаУведомления,
	|	ЕСТЬNULL(НДФЛПодтверждениеПраваНаИмущественныйВычетРаботникиОрганизации.НомерВходящегоДокумента, """") КАК НомерУведомления
	|ПОМЕСТИТЬ ВТУведомления
	|ИЗ
	|	ВТДокументыУведомлений КАК ДокументыУведомлений
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.НДФЛПодтверждениеПраваНаИмущественныйВычет.РаботникиОрганизации КАК НДФЛПодтверждениеПраваНаИмущественныйВычетРаботникиОрганизации
	|		ПО ДокументыУведомлений.Регистратор = НДФЛПодтверждениеПраваНаИмущественныйВычетРаботникиОрганизации.Ссылка
	|			И ДокументыУведомлений.ФизЛицо = НДФЛПодтверждениеПраваНаИмущественныйВычетРаботникиОрганизации.ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СотрудникиОрганизаций.НомерСправки,
	|	МЕСЯЦ(НДФЛСведенияОДоходах.Период) КАК Месяц,
	|	НДФЛСведенияОДоходах.КодДохода,
	|	НДФЛСведенияОДоходах.КодВычета,
	|	СУММА(НДФЛСведенияОДоходах.СуммаДохода) КАК СуммаДохода,
	|	СУММА(ВЫБОР
	|			КОГДА НДФЛСведенияОДоходах.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.ПустаяСсылка)
	|				ТОГДА 0
	|			ИНАЧЕ НДФЛСведенияОДоходах.СуммаВычета
	|		КОНЕЦ) КАК СуммаВычета,
	|	Статусы.ТекущийСтатус,
	|	Статусы.Статус
	|ПОМЕСТИТЬ ВТДоходы
	|ИЗ
	|	ВТФизлица КАК СотрудникиОрганизаций
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДФЛСведенияОДоходах КАК НДФЛСведенияОДоходах
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатусы КАК Статусы
	|			ПО НДФЛСведенияОДоходах.ФизЛицо = Статусы.ФизЛицо
	|				И (НАЧАЛОПЕРИОДА(НДФЛСведенияОДоходах.Период, МЕСЯЦ) = Статусы.МесяцНалоговогоПериода)
	|		ПО СотрудникиОрганизаций.ФизЛицо = НДФЛСведенияОДоходах.ФизЛицо
	|			И (НДФЛСведенияОДоходах.Организация = &ГоловнаяОрганизация)
	|			И (НДФЛСведенияОДоходах.ОбособленноеПодразделение = &Организация)
	|			И (ГОД(НДФЛСведенияОДоходах.Период) = &ГодНП)
	|			И (НДФЛСведенияОДоходах.ПериодРегистрации < &ДатаПодачиСведений)
	|			И (&ЧитатьСводныеДанные
	|				ИЛИ ВЫБОР
	|					КОГДА НДФЛСведенияОДоходах.КодПоОКАТО <> &ПустойКодПоОКАТО
	|						ТОГДА НДФЛСведенияОДоходах.КодПоОКАТО
	|					КОГДА ЕСТЬNULL(НДФЛСведенияОДоходах.ПодразделениеОрганизации.КодПоОКАТО, &ПустойКодПоОКАТО) <> &ПустойКодПоОКАТО
	|						ТОГДА НДФЛСведенияОДоходах.ПодразделениеОрганизации.КодПоОКАТО
	|					ИНАЧЕ ЕСТЬNULL(НДФЛСведенияОДоходах.ОбособленноеПодразделение.КодПоОКАТО, &ПустойКодПоОКАТО)
	|				КОНЕЦ = &КодПоОКАТО)
	|			И (&ЧитатьСводныеДанные
	|				ИЛИ ВЫБОР
	|					КОГДА НДФЛСведенияОДоходах.КПП <> &ПустойКПП
	|						ТОГДА НДФЛСведенияОДоходах.КПП
	|					КОГДА ЕСТЬNULL(НДФЛСведенияОДоходах.ПодразделениеОрганизации.КПП, &ПустойКПП) <> &ПустойКПП
	|						ТОГДА НДФЛСведенияОДоходах.ПодразделениеОрганизации.КПП
	|					ИНАЧЕ ЕСТЬNULL(НДФЛСведенияОДоходах.ОбособленноеПодразделение.КПП, &ПустойКПП)
	|				КОНЕЦ = &КПП)
	|			И (&ЧитатьСводныеДанныеПоСтавкам
	|				ИЛИ СотрудникиОрганизаций.Ставка = ВЫБОР
	|					КОГДА НДФЛСведенияОДоходах.КодДохода.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
	|						ТОГДА ВЫБОР
	|								КОГДА Статусы.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|										И Статусы.ТекущийСтатус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|									ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|								ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|							КОНЕЦ
	|					КОГДА Статусы.ТекущийСтатус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.Резидент)
	|						ТОГДА ВЫБОР
	|								КОГДА &ВыделятьДивидендыРезидентов
	|										И НДФЛСведенияОДоходах.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
	|									ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|								КОГДА НДФЛСведенияОДоходах.КодДохода.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка09)
	|									ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка09)
	|								КОГДА НДФЛСведенияОДоходах.КодДохода.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка35)
	|									ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка35)
	|								ИНАЧЕ """"
	|							КОНЕЦ
	|					КОГДА НДФЛСведенияОДоходах.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка15)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|				КОНЕЦ)
	|			И (НЕ НДФЛСведенияОДоходах.КодДохода.НеОтражаетсяВОтчетности2010)
	|ГДЕ
	|	НДФЛСведенияОДоходах.КодДохода ЕСТЬ НЕ NULL 
	|	И НЕ(НДФЛСведенияОДоходах.ВключатьВДекларациюПоНалогуНаПрибыль
	|				И &ИсключатьДоходыПоЦеннымБумагам)
	|
	|СГРУППИРОВАТЬ ПО
	|	СотрудникиОрганизаций.НомерСправки,
	|	НДФЛСведенияОДоходах.КодДохода,
	|	НДФЛСведенияОДоходах.КодВычета,
	|	МЕСЯЦ(НДФЛСведенияОДоходах.Период),
	|	Статусы.ТекущийСтатус,
	|	Статусы.Статус
	|
	|ИМЕЮЩИЕ
	|	СУММА(НДФЛСведенияОДоходах.СуммаДохода) <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СотрудникиОрганизаций.НомерСправки КАК НомерСправки,
	|	НДФЛПредоставленныеСтандартныеВычетыФизЛиц.КодВычета КАК КодВычета,
	|	НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ПримененныйВычет КАК СуммаВычета,
	|	НДФЛПредоставленныеСтандартныеВычетыФизЛиц.МесяцНалоговогоПериода,
	|	НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ФизЛицо
	|ПОМЕСТИТЬ ВТВычеты
	|ИЗ
	|	ВТФизлица КАК СотрудникиОрганизаций
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДФЛПредоставленныеСтандартныеВычетыФизЛиц КАК НДФЛПредоставленныеСтандартныеВычетыФизЛиц
	|		ПО СотрудникиОрганизаций.ФизЛицо = НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ФизЛицо
	|			И (&ЧитатьСводныеДанныеПоСтавкам
	|				ИЛИ СотрудникиОрганизаций.Ставка = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13))
	|			И (НДФЛПредоставленныеСтандартныеВычетыФизЛиц.Период < &ДатаПодачиСведений)
	|			И (НДФЛПредоставленныеСтандартныеВычетыФизЛиц.Организация = &ГоловнаяОрганизация)
	|			И (НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ОбособленноеПодразделение = &Организация)
	|			И (ГОД(НДФЛПредоставленныеСтандартныеВычетыФизЛиц.МесяцНалоговогоПериода) = &ГодНП)
	|			И (&ЧитатьСводныеДанные
	|				ИЛИ ВЫБОР
	|					КОГДА НДФЛПредоставленныеСтандартныеВычетыФизЛиц.КодПоОКАТО <> &ПустойКодПоОКАТО
	|						ТОГДА НДФЛПредоставленныеСтандартныеВычетыФизЛиц.КодПоОКАТО
	|					КОГДА ЕСТЬNULL(НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ПодразделениеОрганизации.КодПоОКАТО, &ПустойКодПоОКАТО) <> &ПустойКодПоОКАТО
	|						ТОГДА НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ПодразделениеОрганизации.КодПоОКАТО
	|					ИНАЧЕ ЕСТЬNULL(НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ОбособленноеПодразделение.КодПоОКАТО, &ПустойКодПоОКАТО)
	|				КОНЕЦ = &КодПоОКАТО)
	|			И (&ЧитатьСводныеДанные
	|				ИЛИ ВЫБОР
	|					КОГДА НДФЛПредоставленныеСтандартныеВычетыФизЛиц.КПП <> &ПустойКПП
	|						ТОГДА НДФЛПредоставленныеСтандартныеВычетыФизЛиц.КПП
	|					КОГДА ЕСТЬNULL(НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ПодразделениеОрганизации.КПП, &ПустойКПП) <> &ПустойКПП
	|						ТОГДА НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ПодразделениеОрганизации.КПП
	|					ИНАЧЕ ЕСТЬNULL(НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ОбособленноеПодразделение.КПП, &ПустойКПП)
	|				КОНЕЦ = &КПП)
	|ГДЕ
	|	НДФЛПредоставленныеСтандартныеВычетыФизЛиц.КодВычета ЕСТЬ НЕ NULL 
	|	И НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ПримененныйВычет <> 0
	|	И НЕ(НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ВключатьВДекларациюПоНалогуНаПрибыль
	|				И &ИсключатьДоходыПоЦеннымБумагам)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СотрудникиОрганизаций.НомерСправки,
	|	НДФЛИмущественныеВычетыФизлиц.КодВычетаИмущественный,
	|	НДФЛИмущественныеВычетыФизлиц.Размер,
	|	НДФЛИмущественныеВычетыФизлиц.Период,
	|	НДФЛИмущественныеВычетыФизлиц.ФизЛицо
	|ИЗ
	|	ВТФизлица КАК СотрудникиОрганизаций
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДФЛИмущественныеВычетыФизлиц КАК НДФЛИмущественныеВычетыФизлиц
	|		ПО СотрудникиОрганизаций.ФизЛицо = НДФЛИмущественныеВычетыФизлиц.ФизЛицо
	|			И (&ЧитатьСводныеДанныеПоСтавкам
	|				ИЛИ СотрудникиОрганизаций.Ставка = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13))
	|			И (НДФЛИмущественныеВычетыФизлиц.Период < &ДатаПодачиСведений)
	|			И (НДФЛИмущественныеВычетыФизлиц.Организация = &ГоловнаяОрганизация)
	|			И (НДФЛИмущественныеВычетыФизлиц.ОбособленноеПодразделение = &Организация)
	|			И (НДФЛИмущественныеВычетыФизлиц.Год = &ГодНП)
	|			И (&ЧитатьСводныеДанные
	|				ИЛИ ВЫБОР
	|					КОГДА НДФЛИмущественныеВычетыФизлиц.КодПоОКАТО <> &ПустойКодПоОКАТО
	|						ТОГДА НДФЛИмущественныеВычетыФизлиц.КодПоОКАТО
	|					КОГДА ЕСТЬNULL(НДФЛИмущественныеВычетыФизлиц.ПодразделениеОрганизации.КодПоОКАТО, &ПустойКодПоОКАТО) <> &ПустойКодПоОКАТО
	|						ТОГДА НДФЛИмущественныеВычетыФизлиц.ПодразделениеОрганизации.КодПоОКАТО
	|					ИНАЧЕ ЕСТЬNULL(НДФЛИмущественныеВычетыФизлиц.ОбособленноеПодразделение.КодПоОКАТО, &ПустойКодПоОКАТО)
	|				КОНЕЦ = &КодПоОКАТО)
	|			И (&ЧитатьСводныеДанные
	|				ИЛИ ВЫБОР
	|					КОГДА НДФЛИмущественныеВычетыФизлиц.КПП <> &ПустойКПП
	|						ТОГДА НДФЛИмущественныеВычетыФизлиц.КПП
	|					КОГДА ЕСТЬNULL(НДФЛИмущественныеВычетыФизлиц.ПодразделениеОрганизации.КПП, &ПустойКПП) <> &ПустойКПП
	|						ТОГДА НДФЛИмущественныеВычетыФизлиц.ПодразделениеОрганизации.КПП
	|					ИНАЧЕ ЕСТЬNULL(НДФЛИмущественныеВычетыФизлиц.ОбособленноеПодразделение.КПП, &ПустойКПП)
	|				КОНЕЦ = &КПП)
	|			И (НДФЛИмущественныеВычетыФизлиц.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход))
	|ГДЕ
	|	НДФЛИмущественныеВычетыФизлиц.КодВычетаИмущественный ЕСТЬ НЕ NULL 
	|	И НДФЛИмущественныеВычетыФизлиц.Размер <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДоходыИВычеты.НомерСправки,
	|	СУММА(ДоходыИВычеты.ОбщаяСуммаДохода) КАК ОбщаяСуммаДохода,
	|	СУММА(ДоходыИВычеты.ОблагаемаяСуммаДохода) КАК ОблагаемаяСуммаДохода,
	|	ВЫБОР
	|		КОГДА НЕ &РазделятьИтогиПоСтавкам
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|		КОГДА ДоходыИВычеты.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
	|			ТОГДА ВЫБОР
	|					КОГДА ДоходыИВычеты.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|				КОНЕЦ
	|		КОГДА ДоходыИВычеты.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.Резидент)
	|			ТОГДА ВЫБОР
	|					КОГДА &ВыделятьДивидендыРезидентов
	|							И ДоходыИВычеты.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|					КОГДА ДоходыИВычеты.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка09)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка09)
	|					КОГДА ДоходыИВычеты.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка35)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка35)
	|					ИНАЧЕ """"
	|				КОНЕЦ
	|		КОГДА ДоходыИВычеты.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка15)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|	КОНЕЦ КАК Ставка
	|ПОМЕСТИТЬ ВТИтогиДоходов
	|ИЗ
	|	(ВЫБРАТЬ
	|		Доходы.НомерСправки КАК НомерСправки,
	|		Доходы.СуммаДохода КАК ОбщаяСуммаДохода,
	|		Доходы.СуммаДохода - Доходы.СуммаВычета КАК ОблагаемаяСуммаДохода,
	|		Доходы.КодДохода.СтавкаНалогообложенияРезидента КАК СтавкаНалогообложенияРезидента,
	|		Доходы.КодДохода КАК КодДохода,
	|		Доходы.Статус КАК Статус
	|	ИЗ
	|		ВТДоходы КАК Доходы
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Вычеты.НомерСправки,
	|		0,
	|		-Вычеты.СуммаВычета,
	|		ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13),
	|		ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.КодДоходаПоУмолчанию),
	|		Статусы.Статус
	|	ИЗ
	|		ВТВычеты КАК Вычеты
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатусы КАК Статусы
	|			ПО Вычеты.ФизЛицо = Статусы.ФизЛицо
	|				И (НАЧАЛОПЕРИОДА(Вычеты.МесяцНалоговогоПериода, МЕСЯЦ) = Статусы.МесяцНалоговогоПериода)) КАК ДоходыИВычеты
	|
	|СГРУППИРОВАТЬ ПО
	|	ДоходыИВычеты.НомерСправки,
	|	ВЫБОР
	|		КОГДА НЕ &РазделятьИтогиПоСтавкам
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|		КОГДА ДоходыИВычеты.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
	|			ТОГДА ВЫБОР
	|					КОГДА ДоходыИВычеты.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|				КОНЕЦ
	|		КОГДА ДоходыИВычеты.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.Резидент)
	|			ТОГДА ВЫБОР
	|					КОГДА &ВыделятьДивидендыРезидентов
	|							И ДоходыИВычеты.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|					КОГДА ДоходыИВычеты.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка09)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка09)
	|					КОГДА ДоходыИВычеты.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка35)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка35)
	|					ИНАЧЕ """"
	|				КОНЕЦ
	|		КОГДА ДоходыИВычеты.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка15)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СправкиОДоходах.НомерСправки КАК НомерСправки,
	|	ЕСТЬNULL(СУММА(ВЫБОР
	|				КОГДА НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.Начисление)
	|					ТОГДА НДФЛРасчетыСБюджетом.Налог
	|				ИНАЧЕ 0
	|			КОНЕЦ), 0) КАК НачисленоЗаТекущийГод,
	|	ЕСТЬNULL(СУММА(ВЫБОР
	|				КОГДА НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.Удержание)
	|					ТОГДА НДФЛРасчетыСБюджетом.Налог
	|				ИНАЧЕ 0
	|			КОНЕЦ), 0) КАК УдержаноЗаТекущийГод,
	|	ЕСТЬNULL(СУММА(ВЫБОР
	|				КОГДА НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.ВозвратНалога)
	|					ТОГДА -НДФЛРасчетыСБюджетом.Налог
	|				ИНАЧЕ 0
	|			КОНЕЦ), 0) КАК ВозвращеноЗаТекущийГод,
	|	ЕСТЬNULL(СУММА(ВЫБОР
	|				КОГДА НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.ПереданоНаВзыскание)
	|						И НДФЛРасчетыСБюджетом.Период < &НачалоСледующегоНП
	|					ТОГДА НДФЛРасчетыСБюджетом.Налог
	|				ИНАЧЕ 0
	|			КОНЕЦ), 0) КАК ПереданоНаВзысканиеЗаТекущийГод,
	|	ВЫБОР
	|		КОГДА НЕ &РазделятьИтогиПоСтавкам
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|		КОГДА НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
	|			ТОГДА ВЫБОР
	|					КОГДА Статусы.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|				КОНЕЦ
	|		КОГДА Статусы.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.Резидент)
	|			ТОГДА ВЫБОР
	|					КОГДА &ВыделятьДивидендыРезидентов
	|							И НДФЛРасчетыСБюджетом.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|					КОГДА НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка09)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка09)
	|					КОГДА НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка35)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка35)
	|					ИНАЧЕ """"
	|				КОНЕЦ
	|		КОГДА НДФЛРасчетыСБюджетом.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка15)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|	КОНЕЦ КАК Ставка
	|ПОМЕСТИТЬ ВТНалоги
	|ИЗ
	|	ВТФизлица КАК СправкиОДоходах
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДФЛРасчетыСБюджетом КАК НДФЛРасчетыСБюджетом
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатусы КАК Статусы
	|			ПО НДФЛРасчетыСБюджетом.ФизЛицо = Статусы.ФизЛицо
	|				И (НАЧАЛОПЕРИОДА(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода, МЕСЯЦ) = Статусы.МесяцНалоговогоПериода)
	|		ПО СправкиОДоходах.ФизЛицо = НДФЛРасчетыСБюджетом.ФизЛицо
	|			И (НДФЛРасчетыСБюджетом.Период < &ДатаПодачиСведений)
	|			И (НДФЛРасчетыСБюджетом.Организация = &ГоловнаяОрганизация)
	|			И (НДФЛРасчетыСБюджетом.ОбособленноеПодразделение = &Организация)
	|			И (НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода МЕЖДУ &НачалоНП И &КонецНП)
	|			И (&ЧитатьСводныеДанные
	|				ИЛИ &КодПоОКАТО = ВЫБОР
	|					КОГДА НДФЛРасчетыСБюджетом.КодПоОКАТО <> &ПустойКодПоОКАТО
	|						ТОГДА НДФЛРасчетыСБюджетом.КодПоОКАТО
	|					КОГДА ЕСТЬNULL(НДФЛРасчетыСБюджетом.ПодразделениеОрганизации.КодПоОКАТО, &ПустойКодПоОКАТО) <> &ПустойКодПоОКАТО
	|						ТОГДА НДФЛРасчетыСБюджетом.ПодразделениеОрганизации.КодПоОКАТО
	|					ИНАЧЕ ЕСТЬNULL(НДФЛРасчетыСБюджетом.ОбособленноеПодразделение.КодПоОКАТО, &ПустойКодПоОКАТО)
	|				КОНЕЦ)
	|			И (&ЧитатьСводныеДанные
	|				ИЛИ &КПП = ВЫБОР
	|					КОГДА НДФЛРасчетыСБюджетом.КПП <> &ПустойКПП
	|						ТОГДА НДФЛРасчетыСБюджетом.КПП
	|					КОГДА ЕСТЬNULL(НДФЛРасчетыСБюджетом.ПодразделениеОрганизации.КПП, &ПустойКПП) <> &ПустойКПП
	|						ТОГДА НДФЛРасчетыСБюджетом.ПодразделениеОрганизации.КПП
	|					ИНАЧЕ ЕСТЬNULL(НДФЛРасчетыСБюджетом.ОбособленноеПодразделение.КПП, &ПустойКПП)
	|				КОНЕЦ)
	|			И (&ЧитатьСводныеДанныеПоСтавкам
	|				ИЛИ СправкиОДоходах.Ставка = ВЫБОР
	|					КОГДА НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
	|						ТОГДА ВЫБОР
	|								КОГДА Статусы.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|										И Статусы.ТекущийСтатус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|									ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|								ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|							КОНЕЦ
	|					КОГДА Статусы.ТекущийСтатус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.Резидент)
	|						ТОГДА ВЫБОР
	|								КОГДА &ВыделятьДивидендыРезидентов
	|										И НДФЛРасчетыСБюджетом.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
	|									ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|								КОГДА НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка09)
	|									ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка09)
	|								КОГДА НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка35)
	|									ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка35)
	|								ИНАЧЕ """"
	|							КОНЕЦ
	|					КОГДА НДФЛРасчетыСБюджетом.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка15)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|				КОНЕЦ)
	|ГДЕ
	|	НЕ(НДФЛРасчетыСБюджетом.ВключатьВДекларациюПоНалогуНаПрибыль
	|				И &ИсключатьДоходыПоЦеннымБумагам)
	|
	|СГРУППИРОВАТЬ ПО
	|	СправкиОДоходах.НомерСправки,
	|	ВЫБОР
	|		КОГДА НЕ &РазделятьИтогиПоСтавкам
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|		КОГДА НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
	|			ТОГДА ВЫБОР
	|					КОГДА Статусы.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|				КОНЕЦ
	|		КОГДА Статусы.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.Резидент)
	|			ТОГДА ВЫБОР
	|					КОГДА &ВыделятьДивидендыРезидентов
	|							И НДФЛРасчетыСБюджетом.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|					КОГДА НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка09)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка09)
	|					КОГДА НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка35)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка35)
	|					ИНАЧЕ """"
	|				КОНЕЦ
	|		КОГДА НДФЛРасчетыСБюджетом.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка15)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА &РазделятьИтогиПоСтавкам
	|			ТОГДА ВЫБОР
	|					КОГДА НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
	|						ТОГДА ВЫБОР
	|								КОГДА Статусы.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|									ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|								ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|							КОНЕЦ
	|					КОГДА Статусы.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.Резидент)
	|						ТОГДА ВЫБОР
	|								КОГДА &ВыделятьДивидендыРезидентов
	|										И НДФЛРасчетыСБюджетом.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
	|									ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|								КОГДА НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка09)
	|									ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка09)
	|								КОГДА НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка35)
	|									ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка35)
	|								ИНАЧЕ """"
	|							КОНЕЦ
	|					КОГДА НДФЛРасчетыСБюджетом.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка15)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|				КОНЕЦ
	|		КОГДА НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
	|			ТОГДА ВЫБОР
	|					КОГДА Статусы.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|							И Статусы.ТекущийСтатус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|				КОНЕЦ
	|		КОГДА Статусы.ТекущийСтатус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.Резидент)
	|			ТОГДА ВЫБОР
	|					КОГДА &ВыделятьДивидендыРезидентов
	|							И НДФЛРасчетыСБюджетом.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|					КОГДА НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка09)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка09)
	|					КОГДА НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка35)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка35)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.ПустаяСсылка)
	|				КОНЕЦ
	|		КОГДА НДФЛРасчетыСБюджетом.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка15)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|	КОНЕЦ КАК Ставка,
	|	НАЧАЛОПЕРИОДА(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода, МЕСЯЦ) КАК МесяцНалоговогоПериода,
	|	НДФЛРасчетыСБюджетом.ФизЛицо,
	|	СУММА(НДФЛРасчетыСБюджетом.Налог) КАК Налог
	|ПОМЕСТИТЬ ВТУдержаноНДФЛПоМесяцам
	|ИЗ
	|	РегистрНакопления.НДФЛРасчетыСБюджетом КАК НДФЛРасчетыСБюджетом
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатусы КАК Статусы
	|		ПО НДФЛРасчетыСБюджетом.ФизЛицо = Статусы.ФизЛицо
	|			И (НАЧАЛОПЕРИОДА(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода, МЕСЯЦ) = Статусы.МесяцНалоговогоПериода)
	|ГДЕ
	|	НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.Удержание)
	|	И НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода МЕЖДУ &НачалоНП И &КонецНП
	|	И НДФЛРасчетыСБюджетом.Период < &ДатаПодачиСведений
	|	И НДФЛРасчетыСБюджетом.Организация = &ГоловнаяОрганизация
	|	И НДФЛРасчетыСБюджетом.ОбособленноеПодразделение = &Организация
	|	И НДФЛРасчетыСБюджетом.ФизЛицо В
	|			(ВЫБРАТЬ
	|				ФизическиеЛица.ФизЛицо
	|			ИЗ
	|				ВТФизлица КАК ФизическиеЛица)
	|	И &КодПоОКАТО = ВЫБОР
	|			КОГДА НДФЛРасчетыСБюджетом.КодПоОКАТО <> &ПустойКодПоОКАТО
	|				ТОГДА НДФЛРасчетыСБюджетом.КодПоОКАТО
	|			КОГДА ЕСТЬNULL(НДФЛРасчетыСБюджетом.ПодразделениеОрганизации.КодПоОКАТО, &ПустойКодПоОКАТО) <> &ПустойКодПоОКАТО
	|				ТОГДА НДФЛРасчетыСБюджетом.ПодразделениеОрганизации.КодПоОКАТО
	|			ИНАЧЕ ЕСТЬNULL(НДФЛРасчетыСБюджетом.ОбособленноеПодразделение.КодПоОКАТО, &ПустойКодПоОКАТО)
	|		КОНЕЦ
	|	И &КПП = ВЫБОР
	|			КОГДА НДФЛРасчетыСБюджетом.КПП <> &ПустойКПП
	|				ТОГДА НДФЛРасчетыСБюджетом.КПП
	|			КОГДА ЕСТЬNULL(НДФЛРасчетыСБюджетом.ПодразделениеОрганизации.КПП, &ПустойКПП) <> &ПустойКПП
	|				ТОГДА НДФЛРасчетыСБюджетом.ПодразделениеОрганизации.КПП
	|			ИНАЧЕ ЕСТЬNULL(НДФЛРасчетыСБюджетом.ОбособленноеПодразделение.КПП, &ПустойКПП)
	|		КОНЕЦ
	|	И ВЫБОР
	|			КОГДА НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
	|				ТОГДА ВЫБОР
	|						КОГДА Статусы.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|								И Статусы.ТекущийСтатус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|							ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|						ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|					КОНЕЦ
	|			КОГДА Статусы.ТекущийСтатус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.Резидент)
	|				ТОГДА ВЫБОР
	|						КОГДА &ВыделятьДивидендыРезидентов
	|								И НДФЛРасчетыСБюджетом.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
	|							ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|						КОГДА НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка09)
	|							ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка09)
	|						КОГДА НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка35)
	|							ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка35)
	|						ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.ПустаяСсылка)
	|					КОНЕЦ
	|			КОГДА НДФЛРасчетыСБюджетом.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка15)
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|		КОНЕЦ В (ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13), ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30))
	|	И НЕ(НДФЛРасчетыСБюджетом.ВключатьВДекларациюПоНалогуНаПрибыль
	|				И &ИсключатьДоходыПоЦеннымБумагам)
	|
	|СГРУППИРОВАТЬ ПО
	|	НАЧАЛОПЕРИОДА(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода, МЕСЯЦ),
	|	НДФЛРасчетыСБюджетом.ФизЛицо,
	|	ВЫБОР
	|		КОГДА &РазделятьИтогиПоСтавкам
	|			ТОГДА ВЫБОР
	|					КОГДА НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
	|						ТОГДА ВЫБОР
	|								КОГДА Статусы.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|									ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|								ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|							КОНЕЦ
	|					КОГДА Статусы.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.Резидент)
	|						ТОГДА ВЫБОР
	|								КОГДА &ВыделятьДивидендыРезидентов
	|										И НДФЛРасчетыСБюджетом.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
	|									ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|								КОГДА НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка09)
	|									ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка09)
	|								КОГДА НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка35)
	|									ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка35)
	|								ИНАЧЕ """"
	|							КОНЕЦ
	|					КОГДА НДФЛРасчетыСБюджетом.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка15)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|				КОНЕЦ
	|		КОГДА НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
	|			ТОГДА ВЫБОР
	|					КОГДА Статусы.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|							И Статусы.ТекущийСтатус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|				КОНЕЦ
	|		КОГДА Статусы.ТекущийСтатус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.Резидент)
	|			ТОГДА ВЫБОР
	|					КОГДА &ВыделятьДивидендыРезидентов
	|							И НДФЛРасчетыСБюджетом.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|					КОГДА НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка09)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка09)
	|					КОГДА НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка35)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка35)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.ПустаяСсылка)
	|				КОНЕЦ
	|		КОГДА НДФЛРасчетыСБюджетом.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка15)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.Ставка,
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.ФизЛицо,
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.СуммаРасход,
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.МесяцНалоговогоПериода
	|ПОМЕСТИТЬ ВТПеречисленоНДФЛПоМесяцам
	|ИЗ
	|	РегистрНакопления.РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.Обороты(
	|			&НачалоНП,
	|			&ДатаДоПодачиСведений,
	|			,
	|			Организация = &Организация
	|				И ФизЛицо В
	|					(ВЫБРАТЬ
	|						ФизическиеЛица.ФизЛицо
	|					ИЗ
	|						ВТФизлица КАК ФизическиеЛица)
	|				И НЕ(ВключатьВДекларациюПоНалогуНаПрибыль
	|						И &ИсключатьДоходыПоЦеннымБумагам)
	|				И (МесяцНалоговогоПериода МЕЖДУ &НачалоНП И &КонецНП)
	|				И (&ЧитатьСводныеДанные
	|					ИЛИ ОКАТО_КПП = &ОКАТО_КПП)) КАК РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА НЕ &РазделятьИтогиПоСтавкам
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|		КОГДА Статусы.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|	КОНЕЦ КАК СтавкаПоСтатусу,
	|	ЕСТЬNULL(УдержаноНДФЛПоСтавке30.Налог, 0) КАК УдержаноНДФЛПоСтавке30,
	|	ЕСТЬNULL(УдержаноНДФЛПоСтавке13.Налог, 0) КАК УдержаноНДФЛПоСтавке13,
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.ФизЛицо,
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.СуммаРасход
	|ПОМЕСТИТЬ ВТПеречисленоПоСтавке13_30
	|ИЗ
	|	ВТПеречисленоНДФЛПоМесяцам КАК РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТУдержаноНДФЛПоМесяцам КАК УдержаноНДФЛПоСтавке13
	|		ПО РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.ФизЛицо = УдержаноНДФЛПоСтавке13.ФизЛицо
	|			И РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.МесяцНалоговогоПериода = УдержаноНДФЛПоСтавке13.МесяцНалоговогоПериода
	|			И (УдержаноНДФЛПоСтавке13.Ставка = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13))
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТУдержаноНДФЛПоМесяцам КАК УдержаноНДФЛПоСтавке30
	|		ПО РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.ФизЛицо = УдержаноНДФЛПоСтавке30.ФизЛицо
	|			И РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.МесяцНалоговогоПериода = УдержаноНДФЛПоСтавке30.МесяцНалоговогоПериода
	|			И (УдержаноНДФЛПоСтавке30.Ставка = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30))
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатусы КАК Статусы
	|		ПО РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.ФизЛицо = Статусы.ФизЛицо
	|			И РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.МесяцНалоговогоПериода = Статусы.МесяцНалоговогоПериода
	|ГДЕ
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.Ставка В (ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30), ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Перечислено.Ставка,
	|	Перечислено.ФизЛицо,
	|	СУММА(Перечислено.Сумма) КАК Сумма
	|ПОМЕСТИТЬ ВТПеречисленоНДФЛ
	|ИЗ
	|	(ВЫБРАТЬ
	|		РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.Ставка КАК Ставка,
	|		РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.ФизЛицо КАК ФизЛицо,
	|		РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.СуммаРасход КАК Сумма
	|	ИЗ
	|		ВТПеречисленоНДФЛПоМесяцам КАК РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты
	|	ГДЕ
	|		НЕ РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.Ставка В (ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30), ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30),
	|		ПеречисленоПоСтавке13_30.ФизЛицо,
	|		ВЫБОР
	|			КОГДА ПеречисленоПоСтавке13_30.УдержаноНДФЛПоСтавке30 + ПеречисленоПоСтавке13_30.УдержаноНДФЛПоСтавке13 = 0
	|				ТОГДА 0
	|			КОГДА ПеречисленоПоСтавке13_30.УдержаноНДФЛПоСтавке13 = 0
	|				ТОГДА ПеречисленоПоСтавке13_30.СуммаРасход
	|			ИНАЧЕ ВЫРАЗИТЬ(ПеречисленоПоСтавке13_30.СуммаРасход * ПеречисленоПоСтавке13_30.УдержаноНДФЛПоСтавке30 / (ПеречисленоПоСтавке13_30.УдержаноНДФЛПоСтавке30 + ПеречисленоПоСтавке13_30.УдержаноНДФЛПоСтавке13) КАК ЧИСЛО(22, 0))
	|		КОНЕЦ
	|	ИЗ
	|		ВТПеречисленоПоСтавке13_30 КАК ПеречисленоПоСтавке13_30
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13),
	|		ПеречисленоПоСтавке13_30.ФизЛицо,
	|		ВЫБОР
	|			КОГДА ПеречисленоПоСтавке13_30.УдержаноНДФЛПоСтавке30 + ПеречисленоПоСтавке13_30.УдержаноНДФЛПоСтавке13 = 0
	|				ТОГДА 0
	|			КОГДА ПеречисленоПоСтавке13_30.УдержаноНДФЛПоСтавке30 = 0
	|				ТОГДА ПеречисленоПоСтавке13_30.СуммаРасход
	|			ИНАЧЕ ПеречисленоПоСтавке13_30.СуммаРасход - (ВЫРАЗИТЬ(ПеречисленоПоСтавке13_30.СуммаРасход * ПеречисленоПоСтавке13_30.УдержаноНДФЛПоСтавке30 / (ПеречисленоПоСтавке13_30.УдержаноНДФЛПоСтавке30 + ПеречисленоПоСтавке13_30.УдержаноНДФЛПоСтавке13) КАК ЧИСЛО(22, 0)))
	|		КОНЕЦ
	|	ИЗ
	|		ВТПеречисленоПоСтавке13_30 КАК ПеречисленоПоСтавке13_30
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ПеречисленоПоСтавке13_30.СтавкаПоСтатусу,
	|		ПеречисленоПоСтавке13_30.ФизЛицо,
	|		ВЫБОР
	|			КОГДА ПеречисленоПоСтавке13_30.УдержаноНДФЛПоСтавке30 + ПеречисленоПоСтавке13_30.УдержаноНДФЛПоСтавке13 = 0
	|				ТОГДА ПеречисленоПоСтавке13_30.СуммаРасход
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	ИЗ
	|		ВТПеречисленоПоСтавке13_30 КАК ПеречисленоПоСтавке13_30) КАК Перечислено
	|
	|СГРУППИРОВАТЬ ПО
	|	Перечислено.Ставка,
	|	Перечислено.ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Доходы.НомерСправки КАК НомерСправки,
	|	Доходы.Месяц КАК Месяц,
	|	Доходы.КодДохода,
	|	ВЫБОР
	|		КОГДА Доходы.СуммаВычета = 0
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.ПустаяСсылка)
	|		ИНАЧЕ Доходы.КодВычета
	|	КОНЕЦ КАК КодВычета,
	|	СУММА(Доходы.СуммаДохода) КАК СуммаДохода,
	|	СУММА(Доходы.СуммаВычета) КАК СуммаВычета
	|ПОМЕСТИТЬ ВТДоходыПоМесяцам
	|ИЗ
	|	ВТДоходы КАК Доходы
	|ГДЕ
	|	Доходы.Месяц ЕСТЬ НЕ NULL 
	|
	|СГРУППИРОВАТЬ ПО
	|	Доходы.Месяц,
	|	Доходы.НомерСправки,
	|	Доходы.КодДохода,
	|	ВЫБОР
	|		КОГДА Доходы.СуммаВычета = 0
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.ПустаяСсылка)
	|		ИНАЧЕ Доходы.КодВычета
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Доходы.НомерСправки,
	|	Доходы.КодДохода,
	|	Доходы.Месяц
	|ПОМЕСТИТЬ ВТОсобыеМесяцы
	|ИЗ
	|	ВТДоходыПоМесяцам КАК Доходы
	|
	|СГРУППИРОВАТЬ ПО
	|	Доходы.НомерСправки,
	|	Доходы.КодДохода,
	|	Доходы.Месяц
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВЫБОР
	|			КОГДА Доходы.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.ПустаяСсылка)
	|				ТОГДА 0
	|			ИНАЧЕ 1
	|		КОНЕЦ) = 2
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МесяцыОсобыхВычетов.НомерСправки,
	|	МесяцыОсобыхВычетов.КодДохода,
	|	МесяцыОсобыхВычетов.Месяц,
	|	МАКСИМУМ(Доходы.КодВычета) КАК КодВычета
	|ПОМЕСТИТЬ ВТСтрокиСУказаннымВычетом
	|ИЗ
	|	ВТОсобыеМесяцы КАК МесяцыОсобыхВычетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДоходыПоМесяцам КАК Доходы
	|		ПО МесяцыОсобыхВычетов.НомерСправки = Доходы.НомерСправки
	|			И МесяцыОсобыхВычетов.КодДохода = Доходы.КодДохода
	|			И МесяцыОсобыхВычетов.Месяц = Доходы.Месяц
	|ГДЕ
	|	Доходы.КодВычета <> ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	МесяцыОсобыхВычетов.НомерСправки,
	|	МесяцыОсобыхВычетов.КодДохода,
	|	МесяцыОсобыхВычетов.Месяц
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МесяцыОсобыхВычетов.НомерСправки,
	|	МесяцыОсобыхВычетов.КодДохода,
	|	МесяцыОсобыхВычетов.Месяц,
	|	СУММА(Доходы.СуммаДохода) КАК СуммаДохода
	|ПОМЕСТИТЬ ВТДоходыИзСтрокБезВычетов
	|ИЗ
	|	ВТОсобыеМесяцы КАК МесяцыОсобыхВычетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДоходыПоМесяцам КАК Доходы
	|		ПО МесяцыОсобыхВычетов.НомерСправки = Доходы.НомерСправки
	|			И МесяцыОсобыхВычетов.КодДохода = Доходы.КодДохода
	|			И МесяцыОсобыхВычетов.Месяц = Доходы.Месяц
	|ГДЕ
	|	Доходы.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	МесяцыОсобыхВычетов.НомерСправки,
	|	МесяцыОсобыхВычетов.КодДохода,
	|	МесяцыОсобыхВычетов.Месяц
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Доходы.НомерСправки КАК НомерСправки,
	|	Доходы.Месяц КАК Месяц,
	|	Доходы.КодДохода,
	|	Доходы.КодДохода.КодДляОтчетности КАК КодДоходаКодДляОтчетности,
	|	Доходы.КодВычета КАК КодВычета,
	|	Доходы.СуммаДохода + ВЫБОР
	|		КОГДА СтрокиСУказаннымВычетом.НомерСправки ЕСТЬ NULL 
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(ДоходыИзСтрокБезВычетов.СуммаДохода, 0)
	|	КОНЕЦ КАК СуммаДохода,
	|	Доходы.СуммаВычета КАК СуммаВычета
	|ИЗ
	|	ВТДоходыПоМесяцам КАК Доходы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОсобыеМесяцы КАК СтрокиБезКодаВычета
	|		ПО Доходы.НомерСправки = СтрокиБезКодаВычета.НомерСправки
	|			И Доходы.КодДохода = СтрокиБезКодаВычета.КодДохода
	|			И Доходы.Месяц = СтрокиБезКодаВычета.Месяц
	|			И (Доходы.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.ПустаяСсылка))
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДоходыИзСтрокБезВычетов КАК ДоходыИзСтрокБезВычетов
	|		ПО Доходы.НомерСправки = ДоходыИзСтрокБезВычетов.НомерСправки
	|			И Доходы.Месяц = ДоходыИзСтрокБезВычетов.Месяц
	|			И Доходы.КодДохода = ДоходыИзСтрокБезВычетов.КодДохода
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтрокиСУказаннымВычетом КАК СтрокиСУказаннымВычетом
	|		ПО Доходы.НомерСправки = СтрокиСУказаннымВычетом.НомерСправки
	|			И Доходы.Месяц = СтрокиСУказаннымВычетом.Месяц
	|			И Доходы.КодДохода = СтрокиСУказаннымВычетом.КодДохода
	|			И Доходы.КодВычета = СтрокиСУказаннымВычетом.КодВычета
	|ГДЕ
	|	СтрокиБезКодаВычета.НомерСправки ЕСТЬ NULL 
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСправки,
	|	Месяц,
	|	КодДоходаКодДляОтчетности
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Вычеты.НомерСправки КАК НомерСправки,
	|	Вычеты.КодВычета,
	|	СУММА(Вычеты.СуммаВычета) КАК СуммаВычета,
	|	Вычеты.КодВычета.КодДляОтчетности2011 + Вычеты.КодВычета.КодДляОтчетности2010 КАК КодВычетаКод
	|ИЗ
	|	ВТВычеты КАК Вычеты
	|ГДЕ
	|	Вычеты.КодВычета ЕСТЬ НЕ NULL 
	|
	|СГРУППИРОВАТЬ ПО
	|	Вычеты.НомерСправки,
	|	Вычеты.КодВычета,
	|	Вычеты.КодВычета.КодДляОтчетности2011 + Вычеты.КодВычета.КодДляОтчетности2010
	|
	|ИМЕЮЩИЕ
	|	СУММА(Вычеты.СуммаВычета) <> 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСправки,
	|	КодВычетаКод
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СотрудникиОрганизаций.НомерСправки КАК НомерСправки,
	|	ВЫБОР
	|		КОГДА &РазделятьИтогиПоСтавкам
	|			ТОГДА ЕСТЬNULL(Ставки.Ссылка, ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13))
	|		ИНАЧЕ СотрудникиОрганизаций.Ставка
	|	КОНЕЦ КАК Ставка,
	|	ИтогиДоходов.ОбщаяСуммаДохода,
	|	ИтогиДоходов.ОблагаемаяСуммаДохода,
	|	Налоги.НачисленоЗаТекущийГод КАК Исчислено,
	|	Налоги.УдержаноЗаТекущийГод - Налоги.ВозвращеноЗаТекущийГод КАК Удержано,
	|	ЕСТЬNULL(ПеречисленоНДФЛ.Сумма, 0) КАК Перечислено,
	|	ВЫБОР
	|		КОГДА Налоги.НачисленоЗаТекущийГод - Налоги.УдержаноЗаТекущийГод + Налоги.ВозвращеноЗаТекущийГод - Налоги.ПереданоНаВзысканиеЗаТекущийГод > 0
	|			ТОГДА Налоги.НачисленоЗаТекущийГод - Налоги.УдержаноЗаТекущийГод + Налоги.ВозвращеноЗаТекущийГод - Налоги.ПереданоНаВзысканиеЗаТекущийГод
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Задолженность,
	|	ВЫБОР
	|		КОГДА Налоги.НачисленоЗаТекущийГод - Налоги.УдержаноЗаТекущийГод + Налоги.ВозвращеноЗаТекущийГод - Налоги.ПереданоНаВзысканиеЗаТекущийГод < 0
	|			ТОГДА -(Налоги.НачисленоЗаТекущийГод - Налоги.УдержаноЗаТекущийГод + Налоги.ВозвращеноЗаТекущийГод - Налоги.ПереданоНаВзысканиеЗаТекущийГод)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ИзлишнеУдержано,
	|	Уведомления.ДатаУведомления,
	|	Уведомления.КодНалоговогоОрганаУведомления,
	|	Уведомления.НомерУведомления
	|ИЗ
	|	ВТФизлица КАК СотрудникиОрганизаций
	|		ЛЕВОЕ СОЕДИНЕНИЕ Перечисление.НДФЛСтавки КАК Ставки
	|		ПО (&РазделятьИтогиПоСтавкам)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТУведомления КАК Уведомления
	|		ПО СотрудникиОрганизаций.НомерСправки = Уведомления.НомерСправки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТИтогиДоходов КАК ИтогиДоходов
	|		ПО СотрудникиОрганизаций.НомерСправки = ИтогиДоходов.НомерСправки
	|			И (НЕ &РазделятьИтогиПоСтавкам
	|				ИЛИ ЕСТЬNULL(Ставки.Ссылка, ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)) = ИтогиДоходов.Ставка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНалоги КАК Налоги
	|		ПО СотрудникиОрганизаций.НомерСправки = Налоги.НомерСправки
	|			И (НЕ &РазделятьИтогиПоСтавкам
	|				ИЛИ ЕСТЬNULL(Ставки.Ссылка, ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)) = Налоги.Ставка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПеречисленоНДФЛ КАК ПеречисленоНДФЛ
	|		ПО СотрудникиОрганизаций.ФизЛицо = ПеречисленоНДФЛ.ФизЛицо
	|			И (ВЫБОР
	|				КОГДА &РазделятьИтогиПоСтавкам
	|					ТОГДА ЕСТЬNULL(Ставки.Ссылка, ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13))
	|				ИНАЧЕ СотрудникиОрганизаций.Ставка
	|			КОНЕЦ = ПеречисленоНДФЛ.Ставка)
	|ГДЕ
	|	(НЕ &РазделятьИтогиПоСтавкам
	|			ИЛИ ИтогиДоходов.НомерСправки ЕСТЬ НЕ NULL 
	|				И (ИтогиДоходов.ОбщаяСуммаДохода <> 0
	|					ИЛИ ИтогиДоходов.ОблагаемаяСуммаДохода <> 0)
	|			ИЛИ Налоги.НомерСправки ЕСТЬ НЕ NULL 
	|				И (Налоги.НачисленоЗаТекущийГод <> 0
	|					ИЛИ Налоги.НачисленоЗаТекущийГод - Налоги.УдержаноЗаТекущийГод + Налоги.ВозвращеноЗаТекущийГод - Налоги.ПереданоНаВзысканиеЗаТекущийГод <> 0)
	|			ИЛИ ЕСТЬNULL(ПеречисленоНДФЛ.Сумма, 0) <> 0)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСправки";
	
	Если ПоказыватьПоКодуОКТМО Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ОКАТО", "ОКТМО")
	КонецЕсли;
	Запрос.Текст = ТекстЗапроса;
	Результаты = Запрос.ВыполнитьПакет();
	ВсегоЗапросов = Результаты.Количество();
	
	Возврат Новый Структура("Доходы, Вычеты, Итоги", Результаты[ВсегоЗапросов - 3].Выгрузить(), Результаты[ВсегоЗапросов - 2].Выгрузить(), Результаты[ВсегоЗапросов - 1].Выгрузить());
	
КонецФункции

Функция ДанныеОДоходах(ТаблицаСотрудников, НалоговыйПериод, Организация, ГоловнаяОрганизация, КодОП, Дата, ПоказыватьПоКодуОКТМО = Ложь, ИсключатьДоходыПоЦеннымБумагам = Ложь) Экспорт
	
	Если НалоговыйПериод < Год(ПроведениеРасчетов.ДатаИзмененияПорядкаИсчисленияНалогаДляИностранцев()) Тогда
		Возврат ДанныеОДоходахСПомесячнымУчетомСтатусаФЛ(ТаблицаСотрудников, НалоговыйПериод, Организация, ГоловнаяОрганизация, КодОП, Дата, ПоказыватьПоКодуОКТМО, ИсключатьДоходыПоЦеннымБумагам)
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылки", ТаблицаСотрудников);
	Запрос.УстановитьПараметр("Организация",Организация);
	Запрос.УстановитьПараметр("ГоловнаяОрганизация",ГоловнаяОрганизация);
	Запрос.УстановитьПараметр("ГодНП",НалоговыйПериод);
	НачалоНалоговогоПериода = Дата(НалоговыйПериод, 1, 1);
	КонецНалоговогоПериода = КонецГода(НачалоНалоговогоПериода);
	Запрос.УстановитьПараметр("КонецНП",КонецНалоговогоПериода);
	Запрос.УстановитьПараметр("НачалоНП", НачалоНалоговогоПериода);
	Запрос.УстановитьПараметр("НачалоСледующегоНП", КонецНалоговогоПериода + 1); // начало следующего налогового периода
	Запрос.УстановитьПараметр("ДатаПодачиСведений", Дата);
	Запрос.УстановитьПараметр("ДатаДоПодачиСведений", Дата - 1);
	Запрос.УстановитьПараметр("НачалоПредыдущегоГода", ДобавитьМесяц(НачалоНалоговогоПериода, -12));
	ЧитатьСводныеДанные = КодОП = Неопределено;
	Запрос.УстановитьПараметр("ЧитатьСводныеДанные", ЧитатьСводныеДанные);
	Запрос.УстановитьПараметр("ИсключатьДоходыПоЦеннымБумагам", ИсключатьДоходыПоЦеннымБумагам);
	Запрос.УстановитьПараметр("ОКТМО_КПП", КодОП);
	Если ЧитатьСводныеДанные Тогда
		Запрос.УстановитьПараметр("КодПоОКТМО", Неопределено);
		Запрос.УстановитьПараметр("КПП", Неопределено);
	Иначе
		Запрос.УстановитьПараметр("КодПоОКТМО",СправкиПоНДФЛ.КодОКАТОизСуммыОКАТОиКПП(КодОП));
		Запрос.УстановитьПараметр("КПП",СправкиПоНДФЛ.КППизСуммыОКАТОиКПП(КодОП));
	КонецЕсли;
	Запрос.УстановитьПараметр("ПустойКодПоОКТМО","");
	Запрос.УстановитьПараметр("ПустойКПП","");
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СотрудникиОрганизаций.Сотрудник КАК Физлицо,
	|	СотрудникиОрганизаций.НомерСправки
	|ПОМЕСТИТЬ ВТФизлица
	|ИЗ
	|	&Ссылки КАК СотрудникиОрганизаций";
	Запрос.Выполнить();
	НачМесяца = НачалоНалоговогоПериода;
	ДатыПоМесяцамТекст = "ВЫБРАТЬ ДАТАВРЕМЯ(" + Формат(НачМесяца,"ДФ=гггг,М,д,Ч,м,с") + ") КАК Период";
	ДатыПоМесяцамТекст = ДатыПоМесяцамТекст + "
	|ПОМЕСТИТЬ ВТПериодыНалогаНаДоходы";
	Пока НачМесяца < НачалоМесяца(КонецНалоговогоПериода) Цикл
		НачМесяца = НачалоМесяца(КонецМесяца(НачМесяца) + 1);
		ДатыПоМесяцамТекст = ДатыПоМесяцамТекст +"
		|ОБЪЕДИНИТЬ ВСЕ ВЫБРАТЬ ДАТАВРЕМЯ(" + Формат(НачМесяца,"ДФ=гггг,М,д,Ч,м,с") + ")";
	КонецЦикла;
	Запрос.Текст = ДатыПоМесяцамТекст;
	Запрос.Выполнить();
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СотрудникиОрганизаций.НомерСправки,
	|	СотрудникиОрганизаций.ФизЛицо,
	|	МИНИМУМ(НДФЛИмущественныеВычетыФизлиц.Регистратор) КАК Регистратор,
	|	МИНИМУМ(НДФЛПодтверждениеПраваНаИмущественныйВычет.НомерСтроки) КАК НомерСтроки
	|ПОМЕСТИТЬ ВТДокументыУведомлений
	|ИЗ
	|	ВТФизлица КАК СотрудникиОрганизаций
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДФЛИмущественныеВычетыФизлиц КАК НДФЛИмущественныеВычетыФизлиц
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.НДФЛПодтверждениеПраваНаИмущественныйВычет.РаботникиОрганизации КАК НДФЛПодтверждениеПраваНаИмущественныйВычет
	|			ПО НДФЛИмущественныеВычетыФизлиц.Регистратор = НДФЛПодтверждениеПраваНаИмущественныйВычет.Ссылка
	|				И НДФЛИмущественныеВычетыФизлиц.ФизЛицо = НДФЛПодтверждениеПраваНаИмущественныйВычет.ФизЛицо
	|		ПО СотрудникиОрганизаций.ФизЛицо = НДФЛИмущественныеВычетыФизлиц.ФизЛицо
	|			И (НДФЛИмущественныеВычетыФизлиц.Период < &ДатаПодачиСведений)
	|			И (НДФЛИмущественныеВычетыФизлиц.Организация = &ГоловнаяОрганизация)
	|			И (НДФЛИмущественныеВычетыФизлиц.Год = &ГодНП)
	|			И (НДФЛИмущественныеВычетыФизлиц.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход))
	|ГДЕ
	|	НДФЛПодтверждениеПраваНаИмущественныйВычет.Ссылка ЕСТЬ НЕ NULL 
	|	И НДФЛПодтверждениеПраваНаИмущественныйВычет.Ссылка.ГруппаВычета = ЗНАЧЕНИЕ(Перечисление.ГруппыВычетовПоНДФЛ.Имущественные)
	|
	|СГРУППИРОВАТЬ ПО
	|	СотрудникиОрганизаций.НомерСправки,
	|	СотрудникиОрганизаций.ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СотрудникиОрганизаций.НомерСправки,
	|	СотрудникиОрганизаций.ФизЛицо,
	|	МИНИМУМ(НДФЛИмущественныеВычетыФизлиц.Регистратор) КАК Регистратор,
	|	МИНИМУМ(НДФЛПодтверждениеПраваНаИмущественныйВычет.НомерСтроки) КАК НомерСтроки
	|ПОМЕСТИТЬ ВТДокументыУведомленийСоц
	|ИЗ
	|	ВТФизлица КАК СотрудникиОрганизаций
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДФЛИмущественныеВычетыФизлиц КАК НДФЛИмущественныеВычетыФизлиц
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.НДФЛПодтверждениеПраваНаИмущественныйВычет.РаботникиОрганизации КАК НДФЛПодтверждениеПраваНаИмущественныйВычет
	|			ПО НДФЛИмущественныеВычетыФизлиц.Регистратор = НДФЛПодтверждениеПраваНаИмущественныйВычет.Ссылка
	|				И НДФЛИмущественныеВычетыФизлиц.ФизЛицо = НДФЛПодтверждениеПраваНаИмущественныйВычет.ФизЛицо
	|		ПО СотрудникиОрганизаций.ФизЛицо = НДФЛИмущественныеВычетыФизлиц.ФизЛицо
	|			И (НДФЛИмущественныеВычетыФизлиц.Период < &ДатаПодачиСведений)
	|			И (НДФЛИмущественныеВычетыФизлиц.Организация = &ГоловнаяОрганизация)
	|			И (НДФЛИмущественныеВычетыФизлиц.Год = &ГодНП)
	|			И (НДФЛИмущественныеВычетыФизлиц.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход))
	|ГДЕ
	|	НДФЛПодтверждениеПраваНаИмущественныйВычет.Ссылка ЕСТЬ НЕ NULL 
	|	И НДФЛПодтверждениеПраваНаИмущественныйВычет.Ссылка.ГруппаВычета = ЗНАЧЕНИЕ(Перечисление.ГруппыВычетовПоНДФЛ.СоциальныеПоУведомлениюНО)
	|
	|СГРУППИРОВАТЬ ПО
	|	СотрудникиОрганизаций.НомерСправки,
	|	СотрудникиОрганизаций.ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СотрудникиОрганизаций.ФизЛицо,
	|	АвансовыеПлатежиИностранцевПоНДФЛ.Период,
	|	АвансовыеПлатежиИностранцевПоНДФЛ.Регистратор
	|ПОМЕСТИТЬ ВТСтрокиАвансовыхПлатежей
	|ИЗ
	|	ВТФизлица КАК СотрудникиОрганизаций
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.АвансовыеПлатежиИностранцевПоНДФЛ КАК АвансовыеПлатежиИностранцевПоНДФЛ
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.АвансовыйПлатежИностранцаПоНДФЛ КАК АвансовыйПлатежИностранцаПоНДФЛ
	|			ПО АвансовыеПлатежиИностранцевПоНДФЛ.Регистратор = АвансовыйПлатежИностранцаПоНДФЛ.Ссылка
	|		ПО СотрудникиОрганизаций.ФизЛицо = АвансовыеПлатежиИностранцевПоНДФЛ.ФизЛицо
	|			И (АвансовыеПлатежиИностранцевПоНДФЛ.Период < &ДатаПодачиСведений)
	|			И (АвансовыеПлатежиИностранцевПоНДФЛ.Организация = &ГоловнаяОрганизация)
	|			И (АвансовыеПлатежиИностранцевПоНДФЛ.ОбособленноеПодразделение = &Организация)
	|			И (АвансовыеПлатежиИностранцевПоНДФЛ.Год = &ГодНП)
	|			И (АвансовыеПлатежиИностранцевПоНДФЛ.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход))
	|ГДЕ
	|	АвансовыйПлатежИностранцаПоНДФЛ.Ссылка ЕСТЬ НЕ NULL 
	|	И АвансовыйПлатежИностранцаПоНДФЛ.НалоговыйПериод = &ГодНП
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СотрудникиОрганизаций.НомерСправки,
	|	СотрудникиОрганизаций.ФизЛицо,
	|	МИНИМУМ(АвансовыеПлатежиИностранцевПоНДФЛ.Период) КАК Период
	|ПОМЕСТИТЬ ВТДатыРегистрацииУведомленийАвансовыеПлатежи
	|ИЗ
	|	ВТФизлица КАК СотрудникиОрганизаций
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтрокиАвансовыхПлатежей КАК АвансовыеПлатежиИностранцевПоНДФЛ
	|		ПО СотрудникиОрганизаций.ФизЛицо = АвансовыеПлатежиИностранцевПоНДФЛ.ФизЛицо
	|
	|СГРУППИРОВАТЬ ПО
	|	СотрудникиОрганизаций.НомерСправки,
	|	СотрудникиОрганизаций.ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СотрудникиОрганизаций.НомерСправки,
	|	СотрудникиОрганизаций.ФизЛицо,
	|	МИНИМУМ(АвансовыеПлатежиИностранцевПоНДФЛ.Регистратор) КАК Регистратор
	|ПОМЕСТИТЬ ВТДокументыУведомленийАвансовыеПлатежи
	|ИЗ
	|	ВТДатыРегистрацииУведомленийАвансовыеПлатежи КАК СотрудникиОрганизаций
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтрокиАвансовыхПлатежей КАК АвансовыеПлатежиИностранцевПоНДФЛ
	|		ПО СотрудникиОрганизаций.ФизЛицо = АвансовыеПлатежиИностранцевПоНДФЛ.ФизЛицо
	|			И СотрудникиОрганизаций.Период = АвансовыеПлатежиИностранцевПоНДФЛ.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	СотрудникиОрганизаций.НомерСправки,
	|	СотрудникиОрганизаций.ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументыУведомлений.НомерСправки,
	|	ЕСТЬNULL(НДФЛПодтверждениеПраваНаИмущественныйВычетРаботникиОрганизации.ДатаУведомления, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаУведомления,
	|	ЕСТЬNULL(НДФЛПодтверждениеПраваНаИмущественныйВычетРаботникиОрганизации.КодНалоговогоОргана, """") КАК КодНалоговогоОрганаУведомления,
	|	ЕСТЬNULL(НДФЛПодтверждениеПраваНаИмущественныйВычетРаботникиОрганизации.НомерВходящегоДокумента, """") КАК НомерУведомления
	|ПОМЕСТИТЬ ВТУведомления
	|ИЗ
	|	ВТДокументыУведомлений КАК ДокументыУведомлений
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.НДФЛПодтверждениеПраваНаИмущественныйВычет.РаботникиОрганизации КАК НДФЛПодтверждениеПраваНаИмущественныйВычетРаботникиОрганизации
	|		ПО ДокументыУведомлений.Регистратор = НДФЛПодтверждениеПраваНаИмущественныйВычетРаботникиОрганизации.Ссылка
	|			И ДокументыУведомлений.ФизЛицо = НДФЛПодтверждениеПраваНаИмущественныйВычетРаботникиОрганизации.ФизЛицо
	|			И ДокументыУведомлений.НомерСтроки = НДФЛПодтверждениеПраваНаИмущественныйВычетРаботникиОрганизации.НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументыУведомлений.НомерСправки,
	|	ЕСТЬNULL(НДФЛПодтверждениеПраваНаИмущественныйВычетРаботникиОрганизации.ДатаУведомления, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаУведомления,
	|	ЕСТЬNULL(НДФЛПодтверждениеПраваНаИмущественныйВычетРаботникиОрганизации.КодНалоговогоОргана, """") КАК КодНалоговогоОрганаУведомления,
	|	ЕСТЬNULL(НДФЛПодтверждениеПраваНаИмущественныйВычетРаботникиОрганизации.НомерВходящегоДокумента, """") КАК НомерУведомления
	|ПОМЕСТИТЬ ВТУведомленияСоц
	|ИЗ
	|	ВТДокументыУведомленийСоц КАК ДокументыУведомлений
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.НДФЛПодтверждениеПраваНаИмущественныйВычет.РаботникиОрганизации КАК НДФЛПодтверждениеПраваНаИмущественныйВычетРаботникиОрганизации
	|		ПО ДокументыУведомлений.Регистратор = НДФЛПодтверждениеПраваНаИмущественныйВычетРаботникиОрганизации.Ссылка
	|			И ДокументыУведомлений.ФизЛицо = НДФЛПодтверждениеПраваНаИмущественныйВычетРаботникиОрганизации.ФизЛицо
	|			И ДокументыУведомлений.НомерСтроки = НДФЛПодтверждениеПраваНаИмущественныйВычетРаботникиОрганизации.НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументыУведомлений.НомерСправки,
	|	ЕСТЬNULL(АвансовыйПлатежИностранцаПоНДФЛ.ДатаУведомления, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаУведомления,
	|	ЕСТЬNULL(АвансовыйПлатежИностранцаПоНДФЛ.КодНалоговогоОргана, """") КАК КодНалоговогоОрганаУведомления,
	|	ЕСТЬNULL(АвансовыйПлатежИностранцаПоНДФЛ.НомерВходящегоДокумента, """") КАК НомерУведомления
	|ПОМЕСТИТЬ ВТУведомленияАвансовыеПлатежи
	|ИЗ
	|	ВТДокументыУведомленийАвансовыеПлатежи КАК ДокументыУведомлений
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.АвансовыйПлатежИностранцаПоНДФЛ КАК АвансовыйПлатежИностранцаПоНДФЛ
	|		ПО ДокументыУведомлений.Регистратор = АвансовыйПлатежИностранцаПоНДФЛ.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СотрудникиОрганизаций.НомерСправки КАК НомерСправки,
	|	НДФЛПредоставленныеСтандартныеВычетыФизЛиц.КодВычета КАК КодВычета,
	|	НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ПримененныйВычет КАК СуммаВычета,
	|	НДФЛПредоставленныеСтандартныеВычетыФизЛиц.МесяцНалоговогоПериода,
	|	НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ФизЛицо
	|ПОМЕСТИТЬ ВТВычеты
	|ИЗ
	|	ВТФизлица КАК СотрудникиОрганизаций
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДФЛПредоставленныеСтандартныеВычетыФизЛиц КАК НДФЛПредоставленныеСтандартныеВычетыФизЛиц
	|		ПО СотрудникиОрганизаций.ФизЛицо = НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ФизЛицо
	|			И (НДФЛПредоставленныеСтандартныеВычетыФизЛиц.Период < &ДатаПодачиСведений)
	|			И (НДФЛПредоставленныеСтандартныеВычетыФизЛиц.Организация = &ГоловнаяОрганизация)
	|			И (НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ОбособленноеПодразделение = &Организация)
	|			И (ГОД(НДФЛПредоставленныеСтандартныеВычетыФизЛиц.МесяцНалоговогоПериода) = &ГодНП)
	|			И (&ЧитатьСводныеДанные
	|				ИЛИ ВЫБОР
	|					КОГДА НДФЛПредоставленныеСтандартныеВычетыФизЛиц.КодПоОКТМО <> &ПустойКодПоОКТМО
	|						ТОГДА НДФЛПредоставленныеСтандартныеВычетыФизЛиц.КодПоОКТМО
	|					КОГДА ЕСТЬNULL(НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ПодразделениеОрганизации.КодПоОКТМО, &ПустойКодПоОКТМО) <> &ПустойКодПоОКТМО
	|						ТОГДА НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ПодразделениеОрганизации.КодПоОКТМО
	|					ИНАЧЕ ЕСТЬNULL(НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ОбособленноеПодразделение.КодПоОКТМО, &ПустойКодПоОКТМО)
	|				КОНЕЦ = &КодПоОКТМО)
	|			И (&ЧитатьСводныеДанные
	|				ИЛИ ВЫБОР
	|					КОГДА НДФЛПредоставленныеСтандартныеВычетыФизЛиц.КПП <> &ПустойКПП
	|						ТОГДА НДФЛПредоставленныеСтандартныеВычетыФизЛиц.КПП
	|					КОГДА ЕСТЬNULL(НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ПодразделениеОрганизации.КПП, &ПустойКПП) <> &ПустойКПП
	|						ТОГДА НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ПодразделениеОрганизации.КПП
	|					ИНАЧЕ ЕСТЬNULL(НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ОбособленноеПодразделение.КПП, &ПустойКПП)
	|				КОНЕЦ = &КПП)
	|ГДЕ
	|	НДФЛПредоставленныеСтандартныеВычетыФизЛиц.КодВычета ЕСТЬ НЕ NULL 
	|	И НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ПримененныйВычет <> 0
	|	И НЕ(НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ВключатьВДекларациюПоНалогуНаПрибыль
	|				И &ИсключатьДоходыПоЦеннымБумагам)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СотрудникиОрганизаций.НомерСправки,
	|	НДФЛИмущественныеВычетыФизлиц.КодВычетаИмущественный,
	|	НДФЛИмущественныеВычетыФизлиц.Размер,
	|	НДФЛИмущественныеВычетыФизлиц.Период,
	|	НДФЛИмущественныеВычетыФизлиц.ФизЛицо
	|ИЗ
	|	ВТФизлица КАК СотрудникиОрганизаций
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДФЛИмущественныеВычетыФизлиц КАК НДФЛИмущественныеВычетыФизлиц
	|		ПО СотрудникиОрганизаций.ФизЛицо = НДФЛИмущественныеВычетыФизлиц.ФизЛицо
	|			И (НДФЛИмущественныеВычетыФизлиц.Период < &ДатаПодачиСведений)
	|			И (НДФЛИмущественныеВычетыФизлиц.Организация = &ГоловнаяОрганизация)
	|			И (НДФЛИмущественныеВычетыФизлиц.ОбособленноеПодразделение = &Организация)
	|			И (НДФЛИмущественныеВычетыФизлиц.Год = &ГодНП)
	|			И (&ЧитатьСводныеДанные
	|				ИЛИ ВЫБОР
	|					КОГДА НДФЛИмущественныеВычетыФизлиц.КодПоОКТМО <> &ПустойКодПоОКТМО
	|						ТОГДА НДФЛИмущественныеВычетыФизлиц.КодПоОКТМО
	|					КОГДА ЕСТЬNULL(НДФЛИмущественныеВычетыФизлиц.ПодразделениеОрганизации.КодПоОКТМО, &ПустойКодПоОКТМО) <> &ПустойКодПоОКТМО
	|						ТОГДА НДФЛИмущественныеВычетыФизлиц.ПодразделениеОрганизации.КодПоОКТМО
	|					ИНАЧЕ ЕСТЬNULL(НДФЛИмущественныеВычетыФизлиц.ОбособленноеПодразделение.КодПоОКТМО, &ПустойКодПоОКТМО)
	|				КОНЕЦ = &КодПоОКТМО)
	|			И (&ЧитатьСводныеДанные
	|				ИЛИ ВЫБОР
	|					КОГДА НДФЛИмущественныеВычетыФизлиц.КПП <> &ПустойКПП
	|						ТОГДА НДФЛИмущественныеВычетыФизлиц.КПП
	|					КОГДА ЕСТЬNULL(НДФЛИмущественныеВычетыФизлиц.ПодразделениеОрганизации.КПП, &ПустойКПП) <> &ПустойКПП
	|						ТОГДА НДФЛИмущественныеВычетыФизлиц.ПодразделениеОрганизации.КПП
	|					ИНАЧЕ ЕСТЬNULL(НДФЛИмущественныеВычетыФизлиц.ОбособленноеПодразделение.КПП, &ПустойКПП)
	|				КОНЕЦ = &КПП)
	|			И (НДФЛИмущественныеВычетыФизлиц.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход))
	|ГДЕ
	|	НДФЛИмущественныеВычетыФизлиц.КодВычетаИмущественный ЕСТЬ НЕ NULL 
	|	И НДФЛИмущественныеВычетыФизлиц.Размер <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СотрудникиОрганизаций.НомерСправки,
	|	СотрудникиОрганизаций.ФизЛицо,
	|	НАЧАЛОПЕРИОДА(НДФЛСведенияОДоходах.Период, МЕСЯЦ) КАК Период,
	|	МЕСЯЦ(НДФЛСведенияОДоходах.Период) КАК Месяц,
	|	НДФЛСведенияОДоходах.КодДохода,
	|	НДФЛСведенияОДоходах.КодВычета,
	|	СУММА(НДФЛСведенияОДоходах.СуммаДохода) КАК СуммаДохода,
	|	СУММА(ВЫБОР
	|			КОГДА НДФЛСведенияОДоходах.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.ПустаяСсылка)
	|				ТОГДА 0
	|			ИНАЧЕ НДФЛСведенияОДоходах.СуммаВычета
	|		КОНЕЦ) КАК СуммаВычета,
	|	НДФЛСведенияОДоходах.ДатаПолученияДохода
	|ПОМЕСТИТЬ ВТДоходы
	|ИЗ
	|	ВТФизлица КАК СотрудникиОрганизаций
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДФЛСведенияОДоходах КАК НДФЛСведенияОДоходах
	|		ПО СотрудникиОрганизаций.ФизЛицо = НДФЛСведенияОДоходах.ФизЛицо
	|			И (НДФЛСведенияОДоходах.Организация = &ГоловнаяОрганизация)
	|			И (НДФЛСведенияОДоходах.ОбособленноеПодразделение = &Организация)
	|			И (НДФЛСведенияОДоходах.Период МЕЖДУ &НачалоНП И &КонецНП)
	|			И (НДФЛСведенияОДоходах.ПериодРегистрации < &ДатаПодачиСведений)
	|			И (&ЧитатьСводныеДанные
	|				ИЛИ ВЫБОР
	|					КОГДА НДФЛСведенияОДоходах.КодПоОКТМО <> &ПустойКодПоОКТМО
	|						ТОГДА НДФЛСведенияОДоходах.КодПоОКТМО
	|					КОГДА ЕСТЬNULL(НДФЛСведенияОДоходах.ПодразделениеОрганизации.КодПоОКТМО, &ПустойКодПоОКТМО) <> &ПустойКодПоОКТМО
	|						ТОГДА НДФЛСведенияОДоходах.ПодразделениеОрганизации.КодПоОКТМО
	|					ИНАЧЕ ЕСТЬNULL(НДФЛСведенияОДоходах.ОбособленноеПодразделение.КодПоОКТМО, &ПустойКодПоОКТМО)
	|				КОНЕЦ = &КодПоОКТМО)
	|			И (&ЧитатьСводныеДанные
	|				ИЛИ ВЫБОР
	|					КОГДА НДФЛСведенияОДоходах.КПП <> &ПустойКПП
	|						ТОГДА НДФЛСведенияОДоходах.КПП
	|					КОГДА ЕСТЬNULL(НДФЛСведенияОДоходах.ПодразделениеОрганизации.КПП, &ПустойКПП) <> &ПустойКПП
	|						ТОГДА НДФЛСведенияОДоходах.ПодразделениеОрганизации.КПП
	|					ИНАЧЕ ЕСТЬNULL(НДФЛСведенияОДоходах.ОбособленноеПодразделение.КПП, &ПустойКПП)
	|				КОНЕЦ = &КПП)
	|			И (НЕ НДФЛСведенияОДоходах.КодДохода.НеОтражаетсяВОтчетности2010)
	|ГДЕ
	|	НДФЛСведенияОДоходах.КодДохода ЕСТЬ НЕ NULL 
	|	И НЕ(НДФЛСведенияОДоходах.ВключатьВДекларациюПоНалогуНаПрибыль
	|				И &ИсключатьДоходыПоЦеннымБумагам)
	|
	|СГРУППИРОВАТЬ ПО
	|	СотрудникиОрганизаций.НомерСправки,
	|	СотрудникиОрганизаций.ФизЛицо,
	|	НДФЛСведенияОДоходах.КодДохода,
	|	НДФЛСведенияОДоходах.КодВычета,
	|	НАЧАЛОПЕРИОДА(НДФЛСведенияОДоходах.Период, МЕСЯЦ),
	|	МЕСЯЦ(НДФЛСведенияОДоходах.Период),
	|	НДФЛСведенияОДоходах.ДатаПолученияДохода
	|
	|ИМЕЮЩИЕ
	|	СУММА(НДФЛСведенияОДоходах.СуммаДохода) <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СправкиОДоходах.НомерСправки КАК НомерСправки,
	|	НДФЛРасчетыСБюджетом.ВидСтроки,
	|	НДФЛРасчетыСБюджетом.ФизЛицо,
	|	НДФЛРасчетыСБюджетом.Период,
	|	НДФЛРасчетыСБюджетом.Налог,
	|	НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента,
	|	НДФЛРасчетыСБюджетом.КодДохода,
	|	НАЧАЛОПЕРИОДА(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода, МЕСЯЦ) КАК МесяцНалоговогоПериода,
	|	НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода КАК ДатаПолученияДохода
	|ПОМЕСТИТЬ ВТСтрокиНалогов
	|ИЗ
	|	ВТФизлица КАК СправкиОДоходах
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДФЛРасчетыСБюджетом КАК НДФЛРасчетыСБюджетом
	|		ПО СправкиОДоходах.ФизЛицо = НДФЛРасчетыСБюджетом.ФизЛицо
	|			И (НДФЛРасчетыСБюджетом.Период МЕЖДУ &НачалоПредыдущегоГода И &ДатаДоПодачиСведений)
	|			И (НДФЛРасчетыСБюджетом.Организация = &ГоловнаяОрганизация)
	|			И (НДФЛРасчетыСБюджетом.ОбособленноеПодразделение = &Организация)
	|			И (НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода МЕЖДУ &НачалоНП И &КонецНП)
	|			И (&ЧитатьСводныеДанные
	|				ИЛИ &КодПоОКТМО = ВЫБОР
	|					КОГДА НДФЛРасчетыСБюджетом.КодПоОКТМО <> &ПустойКодПоОКТМО
	|						ТОГДА НДФЛРасчетыСБюджетом.КодПоОКТМО
	|					КОГДА ЕСТЬNULL(НДФЛРасчетыСБюджетом.ПодразделениеОрганизации.КодПоОКТМО, &ПустойКодПоОКТМО) <> &ПустойКодПоОКТМО
	|						ТОГДА НДФЛРасчетыСБюджетом.ПодразделениеОрганизации.КодПоОКТМО
	|					ИНАЧЕ ЕСТЬNULL(НДФЛРасчетыСБюджетом.ОбособленноеПодразделение.КодПоОКТМО, &ПустойКодПоОКТМО)
	|				КОНЕЦ)
	|			И (&ЧитатьСводныеДанные
	|				ИЛИ &КПП = ВЫБОР
	|					КОГДА НДФЛРасчетыСБюджетом.КПП <> &ПустойКПП
	|						ТОГДА НДФЛРасчетыСБюджетом.КПП
	|					КОГДА ЕСТЬNULL(НДФЛРасчетыСБюджетом.ПодразделениеОрганизации.КПП, &ПустойКПП) <> &ПустойКПП
	|						ТОГДА НДФЛРасчетыСБюджетом.ПодразделениеОрганизации.КПП
	|					ИНАЧЕ ЕСТЬNULL(НДФЛРасчетыСБюджетом.ОбособленноеПодразделение.КПП, &ПустойКПП)
	|				КОНЕЦ)
	|ГДЕ
	|	НЕ(НДФЛРасчетыСБюджетом.ВключатьВДекларациюПоНалогуНаПрибыль
	|				И &ИсключатьДоходыПоЦеннымБумагам)
	|	И НДФЛРасчетыСБюджетом.Налог <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Статусы.ФизЛицо,
	|	Статусы.Статус КАК Статус
	|ПОМЕСТИТЬ ВТРезидентствоФизлицНаКонецГода
	|ИЗ
	|	РегистрСведений.СтатусФизЛицКакНалогоплательщиковНДФЛ.СрезПоследних(
	|			&КонецНП,
	|			ФизЛицо В
	|				(ВЫБРАТЬ
	|					ФизическиеЛица.ФизЛицо
	|				ИЗ
	|					ВТФизлица КАК ФизическиеЛица)) КАК Статусы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Доходы.ФизЛицо КАК ФизЛицо,
	|	Доходы.ДатаПолученияДохода КАК Период
	|ПОМЕСТИТЬ ВТДатыПолученияДохода
	|ИЗ
	|	ВТДоходы КАК Доходы
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СтрокиНалогов.ФизЛицо,
	|	СтрокиНалогов.ДатаПолученияДохода
	|ИЗ
	|	ВТСтрокиНалогов КАК СтрокиНалогов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПериодыРегистра.ФизЛицо КАК ФизЛицо,
	|	ПериодыРегистра.ДатаПолученияДохода,
	|	СтатусНалогоплательщика.Статус КАК Статус
	|ПОМЕСТИТЬ ВТРезидентствоФизлицНаДатыПолученияДохода
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		Физлица.ФизЛицо КАК ФизЛицо,
	|		ДатыПолученияДохода.Период КАК ДатаПолученияДохода,
	|		МАКСИМУМ(СтатусНалогоплательщика.Период) КАК Период
	|	ИЗ
	|		ВТФизлица КАК Физлица
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТДатыПолученияДохода КАК ДатыПолученияДохода
	|			ПО Физлица.ФизЛицо = ДатыПолученияДохода.ФизЛицо
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусФизЛицКакНалогоплательщиковНДФЛ КАК СтатусНалогоплательщика
	|			ПО Физлица.ФизЛицо = СтатусНалогоплательщика.ФизЛицо
	|				И (СтатусНалогоплательщика.Период <= КОНЕЦПЕРИОДА(ДатыПолученияДохода.Период, МЕСЯЦ))
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Физлица.ФизЛицо,
	|		ДатыПолученияДохода.Период) КАК ПериодыРегистра
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусФизЛицКакНалогоплательщиковНДФЛ КАК СтатусНалогоплательщика
	|		ПО ПериодыРегистра.ФизЛицо = СтатусНалогоплательщика.ФизЛицо
	|			И ПериодыРегистра.Период = СтатусНалогоплательщика.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Физлица.ФизЛицо,
	|	ДатыПолученияДохода.Период КАК ДатаПолученияДохода,
	|	ЕСТЬNULL(ВЫБОР
	|			КОГДА РезидентствоФизлицНаКонецГода.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.Резидент)
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.Резидент)
	|			КОГДА РезидентствоФизлицНаКонецГода.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|				ТОГДА ВЫБОР
	|						КОГДА РезидентствоФизлицНаДатыПолученияДохода.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.Резидент)
	|							ТОГДА ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|						ИНАЧЕ РезидентствоФизлицНаДатыПолученияДохода.Статус
	|					КОНЕЦ
	|			КОГДА РезидентствоФизлицНаДатыПолученияДохода.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.Резидент)
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|			ИНАЧЕ РезидентствоФизлицНаДатыПолученияДохода.Статус
	|		КОНЕЦ, ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.Резидент)) КАК ТекущийСтатус
	|ПОМЕСТИТЬ ВТСтатусы
	|ИЗ
	|	ВТФизлица КАК Физлица
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДатыПолученияДохода КАК ДатыПолученияДохода
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТРезидентствоФизлицНаДатыПолученияДохода КАК РезидентствоФизлицНаДатыПолученияДохода
	|			ПО ДатыПолученияДохода.ФизЛицо = РезидентствоФизлицНаДатыПолученияДохода.ФизЛицо
	|				И ДатыПолученияДохода.Период = РезидентствоФизлицНаДатыПолученияДохода.ДатаПолученияДохода
	|		ПО Физлица.ФизЛицо = ДатыПолученияДохода.ФизЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТРезидентствоФизлицНаКонецГода КАК РезидентствоФизлицНаКонецГода
	|		ПО Физлица.ФизЛицо = РезидентствоФизлицНаКонецГода.ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Доходы.НомерСправки КАК НомерСправки,
	|	Доходы.СуммаДохода КАК ОбщаяСуммаДохода,
	|	Доходы.КодВычета,
	|	Доходы.СуммаДохода,
	|	Доходы.СуммаВычета,
	|	Доходы.КодДохода КАК КодДохода,
	|	Доходы.ФизЛицо,
	|	Доходы.Период,
	|	Доходы.Месяц,
	|	Доходы.ДатаПолученияДохода,
	|	ВЫБОР
	|		КОГДА Доходы.КодДохода.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
	|			ТОГДА ВЫБОР
	|					КОГДА Статусы.ТекущийСтатус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|				КОНЕЦ
	|		КОГДА Статусы.ТекущийСтатус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.Резидент)
	|			ТОГДА ВЫБОР
	|					КОГДА Доходы.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|					КОГДА Доходы.КодДохода.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка09)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка09)
	|					КОГДА Доходы.КодДохода.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка35)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка35)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.ПустаяСсылка)
	|				КОНЕЦ
	|		КОГДА Доходы.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка15)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|	КОНЕЦ КАК Ставка
	|ПОМЕСТИТЬ ВТДоходыПоСтавкам
	|ИЗ
	|	ВТДоходы КАК Доходы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатусы КАК Статусы
	|		ПО Доходы.ФизЛицо = Статусы.ФизЛицо
	|			И Доходы.ДатаПолученияДохода = Статусы.ДатаПолученияДохода
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтрокиНалогов.НомерСправки КАК НомерСправки,
	|	СтрокиНалогов.ВидСтроки,
	|	СтрокиНалогов.ФизЛицо,
	|	СтрокиНалогов.Период,
	|	СтрокиНалогов.Налог,
	|	СтрокиНалогов.СтавкаНалогообложенияРезидента,
	|	СтрокиНалогов.КодДохода,
	|	СтрокиНалогов.МесяцНалоговогоПериода,
	|	СтрокиНалогов.ДатаПолученияДохода,
	|	ВЫБОР
	|		КОГДА СтрокиНалогов.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
	|			ТОГДА ВЫБОР
	|					КОГДА Статусы.ТекущийСтатус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|				КОНЕЦ
	|		КОГДА Статусы.ТекущийСтатус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.Резидент)
	|			ТОГДА ВЫБОР
	|					КОГДА СтрокиНалогов.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|					КОГДА СтрокиНалогов.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка09)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка09)
	|					КОГДА СтрокиНалогов.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка35)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка35)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.ПустаяСсылка)
	|				КОНЕЦ
	|		КОГДА СтрокиНалогов.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка15)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|	КОНЕЦ КАК Ставка
	|ПОМЕСТИТЬ ВТСтрокиНалоговПоСтавкам
	|ИЗ
	|	ВТСтрокиНалогов КАК СтрокиНалогов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатусы КАК Статусы
	|		ПО СтрокиНалогов.ФизЛицо = Статусы.ФизЛицо
	|			И СтрокиНалогов.ДатаПолученияДохода = Статусы.ДатаПолученияДохода
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДоходыИВычеты.НомерСправки,
	|	СУММА(ДоходыИВычеты.ОбщаяСуммаДохода) КАК ОбщаяСуммаДохода,
	|	СУММА(ДоходыИВычеты.ОблагаемаяСуммаДохода) КАК ОблагаемаяСуммаДохода,
	|	ДоходыИВычеты.Ставка
	|ПОМЕСТИТЬ ВТИтогиДоходов
	|ИЗ
	|	(ВЫБРАТЬ
	|		Доходы.НомерСправки КАК НомерСправки,
	|		Доходы.СуммаДохода КАК ОбщаяСуммаДохода,
	|		Доходы.СуммаДохода - Доходы.СуммаВычета КАК ОблагаемаяСуммаДохода,
	|		Доходы.Ставка КАК Ставка
	|	ИЗ
	|		ВТДоходыПоСтавкам КАК Доходы
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Вычеты.НомерСправки,
	|		0,
	|		-Вычеты.СуммаВычета,
	|		ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|	ИЗ
	|		ВТВычеты КАК Вычеты) КАК ДоходыИВычеты
	|
	|СГРУППИРОВАТЬ ПО
	|	ДоходыИВычеты.НомерСправки,
	|	ДоходыИВычеты.Ставка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтрокиНалогов.НомерСправки КАК НомерСправки,
	|	СтрокиНалогов.Ставка,
	|	ЕСТЬNULL(СУММА(ВЫБОР
	|				КОГДА СтрокиНалогов.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.Начисление)
	|					ТОГДА СтрокиНалогов.Налог
	|				ИНАЧЕ 0
	|			КОНЕЦ), 0) КАК НачисленоЗаТекущийГод,
	|	ЕСТЬNULL(СУММА(ВЫБОР
	|				КОГДА СтрокиНалогов.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.ЗачетАвансовыхПлатежей)
	|					ТОГДА СтрокиНалогов.Налог
	|				ИНАЧЕ 0
	|			КОНЕЦ), 0) КАК ЗачтеноАвансовыхПлатежейЗаТекущийГод,
	|	ЕСТЬNULL(СУММА(ВЫБОР
	|				КОГДА СтрокиНалогов.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.Удержание)
	|					ТОГДА СтрокиНалогов.Налог
	|				ИНАЧЕ 0
	|			КОНЕЦ), 0) КАК УдержаноЗаТекущийГод,
	|	ЕСТЬNULL(СУММА(ВЫБОР
	|				КОГДА СтрокиНалогов.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.ВозвратНалога)
	|					ТОГДА -СтрокиНалогов.Налог
	|				ИНАЧЕ 0
	|			КОНЕЦ), 0) КАК ВозвращеноЗаТекущийГод,
	|	ЕСТЬNULL(СУММА(ВЫБОР
	|				КОГДА СтрокиНалогов.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.ПереданоНаВзыскание)
	|						И СтрокиНалогов.Период < &НачалоСледующегоНП
	|					ТОГДА СтрокиНалогов.Налог
	|				ИНАЧЕ 0
	|			КОНЕЦ), 0) КАК ПереданоНаВзысканиеЗаТекущийГод
	|ПОМЕСТИТЬ ВТНалоги
	|ИЗ
	|	ВТСтрокиНалоговПоСтавкам КАК СтрокиНалогов
	|
	|СГРУППИРОВАТЬ ПО
	|	СтрокиНалогов.НомерСправки,
	|	СтрокиНалогов.Ставка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДФЛРасчетыСБюджетом.Ставка,
	|	НАЧАЛОПЕРИОДА(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода, МЕСЯЦ) КАК МесяцНалоговогоПериода,
	|	НДФЛРасчетыСБюджетом.ФизЛицо,
	|	СУММА(НДФЛРасчетыСБюджетом.Налог) КАК Налог
	|ПОМЕСТИТЬ ВТУдержаноНДФЛПоМесяцам
	|ИЗ
	|	ВТСтрокиНалоговПоСтавкам КАК НДФЛРасчетыСБюджетом
	|ГДЕ
	|	НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.Удержание)
	|	И НДФЛРасчетыСБюджетом.Ставка В (ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13), ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30))
	|
	|СГРУППИРОВАТЬ ПО
	|	НАЧАЛОПЕРИОДА(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода, МЕСЯЦ),
	|	НДФЛРасчетыСБюджетом.Ставка,
	|	НДФЛРасчетыСБюджетом.ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.Ставка,
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.ФизЛицо,
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.СуммаРасход,
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.МесяцНалоговогоПериода,
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.ИсчисленоПоДивидендам
	|ПОМЕСТИТЬ ВТПеречисленоНДФЛПоМесяцам
	|ИЗ
	|	РегистрНакопления.РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.Обороты(
	|			&НачалоПредыдущегоГода,
	|			&ДатаДоПодачиСведений,
	|			,
	|			Организация = &Организация
	|				И ФизЛицо В
	|					(ВЫБРАТЬ
	|						ФизическиеЛица.ФизЛицо
	|					ИЗ
	|						ВТФизлица КАК ФизическиеЛица)
	|				И НЕ(ВключатьВДекларациюПоНалогуНаПрибыль
	|						И &ИсключатьДоходыПоЦеннымБумагам)
	|				И (МесяцНалоговогоПериода МЕЖДУ &НачалоНП И &КонецНП)
	|				И (&ЧитатьСводныеДанные
	|					ИЛИ ОКТМО_КПП = &ОКТМО_КПП)) КАК РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(УдержаноНДФЛПоСтавке30.Налог, 0) КАК УдержаноНДФЛПоСтавке30,
	|	ЕСТЬNULL(УдержаноНДФЛПоСтавке13.Налог, 0) КАК УдержаноНДФЛПоСтавке13,
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.Ставка,
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.ФизЛицо,
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.СуммаРасход
	|ПОМЕСТИТЬ ВТПеречисленоПоСтавке13_30
	|ИЗ
	|	ВТПеречисленоНДФЛПоМесяцам КАК РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТУдержаноНДФЛПоМесяцам КАК УдержаноНДФЛПоСтавке13
	|		ПО РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.ФизЛицо = УдержаноНДФЛПоСтавке13.ФизЛицо
	|			И РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.МесяцНалоговогоПериода = УдержаноНДФЛПоСтавке13.МесяцНалоговогоПериода
	|			И (УдержаноНДФЛПоСтавке13.Ставка = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13))
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТУдержаноНДФЛПоМесяцам КАК УдержаноНДФЛПоСтавке30
	|		ПО РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.ФизЛицо = УдержаноНДФЛПоСтавке30.ФизЛицо
	|			И РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.МесяцНалоговогоПериода = УдержаноНДФЛПоСтавке30.МесяцНалоговогоПериода
	|			И (УдержаноНДФЛПоСтавке30.Ставка = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30))
	|ГДЕ
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.Ставка В (ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30), ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13))
	|	И НЕ РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.ИсчисленоПоДивидендам
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Перечислено.Ставка,
	|	Перечислено.ФизЛицо,
	|	СУММА(Перечислено.Сумма) КАК Сумма
	|ПОМЕСТИТЬ ВТПеречисленоНДФЛ
	|ИЗ
	|	(ВЫБРАТЬ
	|		РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.Ставка КАК Ставка,
	|		РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.ФизЛицо КАК ФизЛицо,
	|		РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.СуммаРасход КАК Сумма
	|	ИЗ
	|		ВТПеречисленоНДФЛПоМесяцам КАК РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты
	|	ГДЕ
	|		НЕ РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.Ставка В (ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30), ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13))
	|		И НЕ РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.ИсчисленоПоДивидендам
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА ЕСТЬNULL(Статусы.Статус, ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.Резидент)) = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.Резидент)
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка15)
	|		КОНЕЦ,
	|		РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.ФизЛицо,
	|		РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.СуммаРасход
	|	ИЗ
	|		ВТПеречисленоНДФЛПоМесяцам КАК РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТРезидентствоФизлицНаКонецГода КАК Статусы
	|			ПО РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.ФизЛицо = Статусы.ФизЛицо
	|	ГДЕ
	|		РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.ИсчисленоПоДивидендам
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30),
	|		ПеречисленоПоСтавке13_30.ФизЛицо,
	|		ВЫБОР
	|			КОГДА ПеречисленоПоСтавке13_30.УдержаноНДФЛПоСтавке30 + ПеречисленоПоСтавке13_30.УдержаноНДФЛПоСтавке13 = 0
	|				ТОГДА 0
	|			КОГДА ПеречисленоПоСтавке13_30.УдержаноНДФЛПоСтавке13 = 0
	|				ТОГДА ПеречисленоПоСтавке13_30.СуммаРасход
	|			ИНАЧЕ ВЫРАЗИТЬ(ПеречисленоПоСтавке13_30.СуммаРасход * ПеречисленоПоСтавке13_30.УдержаноНДФЛПоСтавке30 / (ПеречисленоПоСтавке13_30.УдержаноНДФЛПоСтавке30 + ПеречисленоПоСтавке13_30.УдержаноНДФЛПоСтавке13) КАК ЧИСЛО(22, 0))
	|		КОНЕЦ
	|	ИЗ
	|		ВТПеречисленоПоСтавке13_30 КАК ПеречисленоПоСтавке13_30
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13),
	|		ПеречисленоПоСтавке13_30.ФизЛицо,
	|		ВЫБОР
	|			КОГДА ПеречисленоПоСтавке13_30.УдержаноНДФЛПоСтавке30 + ПеречисленоПоСтавке13_30.УдержаноНДФЛПоСтавке13 = 0
	|				ТОГДА 0
	|			КОГДА ПеречисленоПоСтавке13_30.УдержаноНДФЛПоСтавке30 = 0
	|				ТОГДА ПеречисленоПоСтавке13_30.СуммаРасход
	|			ИНАЧЕ ПеречисленоПоСтавке13_30.СуммаРасход - (ВЫРАЗИТЬ(ПеречисленоПоСтавке13_30.СуммаРасход * ПеречисленоПоСтавке13_30.УдержаноНДФЛПоСтавке30 / (ПеречисленоПоСтавке13_30.УдержаноНДФЛПоСтавке30 + ПеречисленоПоСтавке13_30.УдержаноНДФЛПоСтавке13) КАК ЧИСЛО(22, 0)))
	|		КОНЕЦ
	|	ИЗ
	|		ВТПеречисленоПоСтавке13_30 КАК ПеречисленоПоСтавке13_30
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ПеречисленоПоСтавке13_30.Ставка,
	|		ПеречисленоПоСтавке13_30.ФизЛицо,
	|		ВЫБОР
	|			КОГДА ПеречисленоПоСтавке13_30.УдержаноНДФЛПоСтавке30 + ПеречисленоПоСтавке13_30.УдержаноНДФЛПоСтавке13 = 0
	|				ТОГДА ПеречисленоПоСтавке13_30.СуммаРасход
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	ИЗ
	|		ВТПеречисленоПоСтавке13_30 КАК ПеречисленоПоСтавке13_30) КАК Перечислено
	|
	|СГРУППИРОВАТЬ ПО
	|	Перечислено.Ставка,
	|	Перечислено.ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Доходы.НомерСправки КАК НомерСправки,
	|	Доходы.Месяц КАК Месяц,
	|	Доходы.Ставка,
	|	Доходы.КодДохода,
	|	ВЫБОР
	|		КОГДА Доходы.СуммаВычета = 0
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.ПустаяСсылка)
	|		ИНАЧЕ Доходы.КодВычета
	|	КОНЕЦ КАК КодВычета,
	|	СУММА(Доходы.СуммаДохода) КАК СуммаДохода,
	|	СУММА(Доходы.СуммаВычета) КАК СуммаВычета
	|ПОМЕСТИТЬ ВТДоходыПоМесяцам
	|ИЗ
	|	ВТДоходыПоСтавкам КАК Доходы
	|ГДЕ
	|	Доходы.Месяц ЕСТЬ НЕ NULL 
	|
	|СГРУППИРОВАТЬ ПО
	|	Доходы.Месяц,
	|	Доходы.Ставка,
	|	Доходы.НомерСправки,
	|	Доходы.КодДохода,
	|	ВЫБОР
	|		КОГДА Доходы.СуммаВычета = 0
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.ПустаяСсылка)
	|		ИНАЧЕ Доходы.КодВычета
	|	КОНЕЦ
	|
	|ИМЕЮЩИЕ
	|	(СУММА(Доходы.СуммаДохода) <> 0
	|		ИЛИ СУММА(Доходы.СуммаВычета) <> 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Доходы.НомерСправки,
	|	Доходы.Ставка,
	|	Доходы.КодДохода,
	|	Доходы.Месяц
	|ПОМЕСТИТЬ ВТОсобыеМесяцы
	|ИЗ
	|	ВТДоходыПоМесяцам КАК Доходы
	|
	|СГРУППИРОВАТЬ ПО
	|	Доходы.НомерСправки,
	|	Доходы.Ставка,
	|	Доходы.КодДохода,
	|	Доходы.Месяц
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВЫБОР
	|			КОГДА Доходы.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.ПустаяСсылка)
	|				ТОГДА 0
	|			ИНАЧЕ 1
	|		КОНЕЦ) = 2
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МесяцыОсобыхВычетов.НомерСправки,
	|	МесяцыОсобыхВычетов.КодДохода,
	|	МесяцыОсобыхВычетов.Ставка,
	|	МесяцыОсобыхВычетов.Месяц,
	|	МАКСИМУМ(Доходы.КодВычета) КАК КодВычета
	|ПОМЕСТИТЬ ВТСтрокиСУказаннымВычетом
	|ИЗ
	|	ВТОсобыеМесяцы КАК МесяцыОсобыхВычетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДоходыПоМесяцам КАК Доходы
	|		ПО МесяцыОсобыхВычетов.НомерСправки = Доходы.НомерСправки
	|			И МесяцыОсобыхВычетов.КодДохода = Доходы.КодДохода
	|			И МесяцыОсобыхВычетов.Месяц = Доходы.Месяц
	|ГДЕ
	|	Доходы.КодВычета <> ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	МесяцыОсобыхВычетов.НомерСправки,
	|	МесяцыОсобыхВычетов.КодДохода,
	|	МесяцыОсобыхВычетов.Ставка,
	|	МесяцыОсобыхВычетов.Месяц
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МесяцыОсобыхВычетов.НомерСправки,
	|	МесяцыОсобыхВычетов.Ставка,
	|	МесяцыОсобыхВычетов.КодДохода,
	|	МесяцыОсобыхВычетов.Месяц,
	|	СУММА(Доходы.СуммаДохода) КАК СуммаДохода
	|ПОМЕСТИТЬ ВТДоходыИзСтрокБезВычетов
	|ИЗ
	|	ВТОсобыеМесяцы КАК МесяцыОсобыхВычетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДоходыПоМесяцам КАК Доходы
	|		ПО МесяцыОсобыхВычетов.НомерСправки = Доходы.НомерСправки
	|			И МесяцыОсобыхВычетов.КодДохода = Доходы.КодДохода
	|			И МесяцыОсобыхВычетов.Месяц = Доходы.Месяц
	|ГДЕ
	|	Доходы.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	МесяцыОсобыхВычетов.НомерСправки,
	|	МесяцыОсобыхВычетов.Ставка,
	|	МесяцыОсобыхВычетов.КодДохода,
	|	МесяцыОсобыхВычетов.Месяц
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Доходы.НомерСправки КАК НомерСправки,
	|	Доходы.Месяц КАК Месяц,
	|	Доходы.Ставка,
	|	Доходы.КодДохода,
	|	Доходы.КодДохода.КодДляОтчетности КАК КодДоходаКодДляОтчетности,
	|	Доходы.КодВычета КАК КодВычета,
	|	Доходы.СуммаДохода + ВЫБОР
	|		КОГДА СтрокиСУказаннымВычетом.НомерСправки ЕСТЬ NULL 
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(ДоходыИзСтрокБезВычетов.СуммаДохода, 0)
	|	КОНЕЦ КАК СуммаДохода,
	|	Доходы.СуммаВычета КАК СуммаВычета
	|ИЗ
	|	ВТДоходыПоМесяцам КАК Доходы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОсобыеМесяцы КАК СтрокиБезКодаВычета
	|		ПО Доходы.НомерСправки = СтрокиБезКодаВычета.НомерСправки
	|			И Доходы.КодДохода = СтрокиБезКодаВычета.КодДохода
	|			И Доходы.Месяц = СтрокиБезКодаВычета.Месяц
	|			И (Доходы.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.ПустаяСсылка))
	|			И Доходы.Ставка = СтрокиБезКодаВычета.Ставка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДоходыИзСтрокБезВычетов КАК ДоходыИзСтрокБезВычетов
	|		ПО Доходы.НомерСправки = ДоходыИзСтрокБезВычетов.НомерСправки
	|			И Доходы.Месяц = ДоходыИзСтрокБезВычетов.Месяц
	|			И Доходы.КодДохода = ДоходыИзСтрокБезВычетов.КодДохода
	|			И Доходы.Ставка = ДоходыИзСтрокБезВычетов.Ставка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтрокиСУказаннымВычетом КАК СтрокиСУказаннымВычетом
	|		ПО Доходы.НомерСправки = СтрокиСУказаннымВычетом.НомерСправки
	|			И Доходы.Месяц = СтрокиСУказаннымВычетом.Месяц
	|			И Доходы.КодДохода = СтрокиСУказаннымВычетом.КодДохода
	|			И Доходы.КодВычета = СтрокиСУказаннымВычетом.КодВычета
	|			И Доходы.Ставка = СтрокиСУказаннымВычетом.Ставка
	|ГДЕ
	|	СтрокиБезКодаВычета.НомерСправки ЕСТЬ NULL 
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСправки,
	|	Месяц,
	|	КодДоходаКодДляОтчетности
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Вычеты.НомерСправки КАК НомерСправки,
	|	Вычеты.КодВычета,
	|	СУММА(Вычеты.СуммаВычета) КАК СуммаВычета,
	|	Вычеты.КодВычета.КодДляОтчетности2011 + Вычеты.КодВычета.КодДляОтчетности2010 КАК КодВычетаКод
	|ИЗ
	|	ВТВычеты КАК Вычеты
	|ГДЕ
	|	Вычеты.КодВычета ЕСТЬ НЕ NULL 
	|
	|СГРУППИРОВАТЬ ПО
	|	Вычеты.НомерСправки,
	|	Вычеты.КодВычета,
	|	Вычеты.КодВычета.КодДляОтчетности2011 + Вычеты.КодВычета.КодДляОтчетности2010
	|
	|ИМЕЮЩИЕ
	|	СУММА(Вычеты.СуммаВычета) <> 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСправки,
	|	КодВычетаКод
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СотрудникиОрганизаций.НомерСправки КАК НомерСправки,
	|	ЕСТЬNULL(Ставки.Ссылка, ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)) КАК Ставка,
	|	ЕСТЬNULL(ИтогиДоходов.ОбщаяСуммаДохода, 0) КАК ОбщаяСуммаДохода,
	|	ЕСТЬNULL(ИтогиДоходов.ОблагаемаяСуммаДохода, 0) КАК ОблагаемаяСуммаДохода,
	|	ЕСТЬNULL(Налоги.НачисленоЗаТекущийГод, 0) КАК Исчислено,
	|	ЕСТЬNULL(Налоги.ЗачтеноАвансовыхПлатежейЗаТекущийГод, 0) КАК ЗачтеноАвансовыхПлатежей,
	|	ЕСТЬNULL(Налоги.УдержаноЗаТекущийГод - Налоги.ВозвращеноЗаТекущийГод, 0) КАК Удержано,
	|	ЕСТЬNULL(ПеречисленоНДФЛ.Сумма, 0) КАК Перечислено,
	|	ЕСТЬNULL(ВЫБОР
	|			КОГДА Налоги.НачисленоЗаТекущийГод - Налоги.ЗачтеноАвансовыхПлатежейЗаТекущийГод - Налоги.УдержаноЗаТекущийГод + Налоги.ВозвращеноЗаТекущийГод > 0
	|				ТОГДА Налоги.НачисленоЗаТекущийГод - Налоги.ЗачтеноАвансовыхПлатежейЗаТекущийГод - Налоги.УдержаноЗаТекущийГод + Налоги.ВозвращеноЗаТекущийГод
	|			ИНАЧЕ 0
	|		КОНЕЦ, 0) КАК Задолженность,
	|	ЕСТЬNULL(ВЫБОР
	|			КОГДА Налоги.НачисленоЗаТекущийГод - Налоги.ЗачтеноАвансовыхПлатежейЗаТекущийГод - Налоги.УдержаноЗаТекущийГод + Налоги.ВозвращеноЗаТекущийГод < 0
	|				ТОГДА -(Налоги.НачисленоЗаТекущийГод - Налоги.ЗачтеноАвансовыхПлатежейЗаТекущийГод - Налоги.УдержаноЗаТекущийГод + Налоги.ВозвращеноЗаТекущийГод)
	|			ИНАЧЕ 0
	|		КОНЕЦ, 0) КАК ИзлишнеУдержано,
	|	Уведомления.ДатаУведомления,
	|	Уведомления.КодНалоговогоОрганаУведомления,
	|	Уведомления.НомерУведомления,
	|	УведомленияСоц.ДатаУведомления КАК ДатаУведомленияСоц,
	|	УведомленияСоц.КодНалоговогоОрганаУведомления КАК КодНалоговогоОрганаУведомленияСоц,
	|	УведомленияСоц.НомерУведомления КАК НомерУведомленияСоц,
	|	УведомленияАвансовыеПлатежи.ДатаУведомления КАК ДатаУведомленияАвансовыеПлатежи,
	|	УведомленияАвансовыеПлатежи.КодНалоговогоОрганаУведомления КАК КодНалоговогоОрганаУведомленияАвансовыеПлатежи,
	|	УведомленияАвансовыеПлатежи.НомерУведомления КАК НомерУведомленияАвансовыеПлатежи
	|ИЗ
	|	ВТФизлица КАК СотрудникиОрганизаций
	|		ЛЕВОЕ СОЕДИНЕНИЕ Перечисление.НДФЛСтавки КАК Ставки
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТУведомления КАК Уведомления
	|		ПО СотрудникиОрганизаций.НомерСправки = Уведомления.НомерСправки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТУведомленияСоц КАК УведомленияСоц
	|		ПО СотрудникиОрганизаций.НомерСправки = УведомленияСоц.НомерСправки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТУведомленияАвансовыеПлатежи КАК УведомленияАвансовыеПлатежи
	|		ПО СотрудникиОрганизаций.НомерСправки = УведомленияАвансовыеПлатежи.НомерСправки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТИтогиДоходов КАК ИтогиДоходов
	|		ПО СотрудникиОрганизаций.НомерСправки = ИтогиДоходов.НомерСправки
	|			И (ЕСТЬNULL(Ставки.Ссылка, ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)) = ИтогиДоходов.Ставка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНалоги КАК Налоги
	|		ПО СотрудникиОрганизаций.НомерСправки = Налоги.НомерСправки
	|			И (ЕСТЬNULL(Ставки.Ссылка, ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)) = Налоги.Ставка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПеречисленоНДФЛ КАК ПеречисленоНДФЛ
	|		ПО СотрудникиОрганизаций.ФизЛицо = ПеречисленоНДФЛ.ФизЛицо
	|			И (ЕСТЬNULL(Ставки.Ссылка, ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)) = ПеречисленоНДФЛ.Ставка)
	|ГДЕ
	|	(ИтогиДоходов.НомерСправки ЕСТЬ НЕ NULL 
	|				И (ИтогиДоходов.ОбщаяСуммаДохода <> 0
	|					ИЛИ ИтогиДоходов.ОблагаемаяСуммаДохода <> 0)
	|			ИЛИ Налоги.НомерСправки ЕСТЬ НЕ NULL 
	|				И (Налоги.НачисленоЗаТекущийГод <> 0
	|					ИЛИ Налоги.НачисленоЗаТекущийГод - Налоги.ЗачтеноАвансовыхПлатежейЗаТекущийГод - Налоги.УдержаноЗаТекущийГод + Налоги.ВозвращеноЗаТекущийГод <> 0)
	|			ИЛИ ЕСТЬNULL(ПеречисленоНДФЛ.Сумма, 0) <> 0)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСправки";
	
	Запрос.Текст = ТекстЗапроса;
	Результаты = Запрос.ВыполнитьПакет();
	ВсегоЗапросов = Результаты.Количество();
	
	Возврат Новый Структура("Доходы, Вычеты, Итоги", Результаты[ВсегоЗапросов - 3].Выгрузить(), Результаты[ВсегоЗапросов - 2].Выгрузить(), Результаты[ВсегоЗапросов - 1].Выгрузить());
	
КонецФункции


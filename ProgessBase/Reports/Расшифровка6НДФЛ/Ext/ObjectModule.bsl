#Если ТолстыйКлиентОбычноеПриложение Тогда

Процедура СформироватьОтчетРасшифровку(Параметры, ДокументРезультат) Экспорт
	
	ИмяПоля = Лев(Параметры.ИДИменПоказателей[0], 13);
	ДатаНачалаНП = НачалоГода(Параметры.ДатаНачалаПериода);
	ДатаКонцаНП  = КонецДня(Параметры.ДатаОкончанияПериода);
	
	// Получаем данные расшифровки.
	ВнешниеНаборыДанных = Новый Структура;
	ВнешниеНаборыДанных.Вставить(Параметры.ИмяСКД); // Имя внешнего набора данных совпадает с ключем варианта.
		
	ОбособленноеПодразделение = Справочники.ПодразделенияОрганизаций.ПустаяСсылка();
    Если Параметры.Свойство("ОбособленноеПодразделение") Тогда
		ОбособленноеПодразделение = Параметры.ОбособленноеПодразделение;
	КонецЕсли;
	ЗаполнениеРегламентированнойОтчетности.РасчетПоказателей6НДФЛ(Параметры.ИмяРасчета, ДатаНачалаНП, ДатаКонцаНП, ?(Параметры.ДатаПодписи > ДатаКонцаНП, Параметры.ДатаПодписи, ДатаКонцаНП), Параметры.Организация, ОбособленноеПодразделение, ВнешниеНаборыДанных, Истина);
	
	// Удалим строки с нулевым значением показателя.
	Данные = ВнешниеНаборыДанных[Параметры.ИмяСКД];
	ВсегоСтрок = Данные.Количество();
	Для Сч = 1 По ВсегоСтрок Цикл
		СтрокаДанных = Данные[ВсегоСтрок - Сч];
		Если Не ЗначениеЗаполнено(СтрокаДанных[ИмяПоля]) Тогда
			Данные.Удалить(СтрокаДанных)
		КонецЕсли;
	КонецЦикла;
	
	// Подготовим табличку для проверки с учетом отборов.
	ДанныеДляПроверки = Неопределено;
	Если Параметры.Свойство("ЗначениеТекущегоПоказателя") Тогда
		ДанныеДляПроверки = Данные.Скопировать();
	КонецЕсли;
	
	// Настраиваем СКД и выводим отчет.
	СхемаКомпоновкиДанных = ПолучитьМакет(Параметры.ИмяСКД);
	
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиДанных));
	КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанных.ВариантыНастроек[Параметры.ИмяСКД].Настройки);
	НастройкиКомпоновки = КомпоновщикНастроек.Настройки;
	
	УстанавливатьШиринуКолонкиОтборов = Ложь;
	ТиповыеОтчеты.ДобавитьВыбранноеПоле(КомпоновщикНастроек, ИмяПоля, ?(Параметры.Свойство("ЗаголовокПоля") И ЗначениеЗаполнено(Параметры.ЗаголовокПоля), Параметры.ЗаголовокПоля, ""));
    Если Параметры.Свойство("СтавкаНалога") И ЗначениеЗаполнено(Параметры.СтавкаНалога) Тогда
		ДобавитьОтборПоПараметру(НастройкиКомпоновки, "СтавкаНалога", Параметры.СтавкаНалога, ДанныеДляПроверки)
	КонецЕсли;
    Если Параметры.Свойство("ДатаФактическогоПолученияДохода") И ЗначениеЗаполнено(Параметры.ДатаФактическогоПолученияДохода) Тогда
		ДобавитьОтборПоПараметру(НастройкиКомпоновки, "ДатаФактическогоПолученияДохода", Параметры.ДатаФактическогоПолученияДохода, ДанныеДляПроверки)
	КонецЕсли;
    Если Параметры.Свойство("ДатаУдержанияНалога") Тогда
		УстанавливатьШиринуКолонкиОтборов = Истина;
		СтрокаОтбора = 2;
		ДобавитьОтборПоПараметру(НастройкиКомпоновки, "ДатаУдержанияНалога", Параметры.ДатаУдержанияНалога, ДанныеДляПроверки)
	КонецЕсли;
    Если Параметры.Свойство("СрокПеречисленияНалога") Тогда
		ДобавитьОтборПоПараметру(НастройкиКомпоновки, "СрокПеречисленияНалога", Параметры.СрокПеречисленияНалога, ДанныеДляПроверки)
	КонецЕсли;
 	
	Если ДанныеДляПроверки <> Неопределено Тогда
		Если Параметры.ЗначениеТекущегоПоказателя <> ДанныеДляПроверки.Итог(ИмяПоля) Тогда
			СтрокаОтбора = 6;
		КонецЕсли;
		ЗаполнениеРегламентированнойОтчетности.ВывестиПредупреждениеОРасхожденииПоказателяСРасшифровкой(Параметры.ЗначениеТекущегоПоказателя, ДанныеДляПроверки.Итог(ИмяПоля), ДокументРезультат);
	КонецЕсли;
	ДанныеДляПроверки = Неопределено;
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиКомпоновки);
		
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки, Истина);
	
	Если УстанавливатьШиринуКолонкиОтборов Тогда
		ДокументРезультат.Область(СтрокаОтбора, 2, СтрокаОтбора, 2).ШиринаКолонки = 65;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	Если СхемаКомпоновкиДанных = Неопределено Тогда
		СтандартнаяОбработка = Ложь
	КонецЕсли;
КонецПроцедуры

Процедура ДобавитьОтборПоПараметру(НастройкиКомпоновки, ИмяОтбора, ЗначениеОтбора, ДанныеДляПроверки)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(НастройкиКомпоновки.Отбор, ИмяОтбора, ЗначениеОтбора, ВидСравненияКомпоновкиДанных.Равно, , Истина);
	Если ДанныеДляПроверки <> Неопределено И ДанныеДляПроверки.Колонки.Найти(ИмяОтбора) <> Неопределено Тогда
		ДанныеДляПроверки = ДанныеДляПроверки.Скопировать(ДанныеДляПроверки.НайтиСтроки(Новый Структура(ИмяОтбора, ЗначениеОтбора)))
	КонецЕсли;
	
КонецПроцедуры

#КонецЕсли

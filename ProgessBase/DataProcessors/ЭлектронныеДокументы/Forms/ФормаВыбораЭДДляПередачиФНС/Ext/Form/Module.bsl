////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьЗначениеФункциональнойОпции("ИспользоватьОбменЭД") Тогда
		ТекстСообщения = ЭлектронныеДокументыСлужебныйВызовСервера.ТекстСообщенияОНеобходимостиНастройкиСистемы("РаботаСЭД");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , , , Отказ);
	КонецЕсли;
	
	МножественныйВыбор = Параметры.МножественныйВыбор;
	
	ВидДокумента = Параметры.ВидДокумента;
	Организация = Параметры.Организация;
	Контрагент = Параметры.Контрагент;
	Элементы.Контрагент.Доступность = НЕ ЗначениеЗаполнено(Контрагент);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПодготовитьФормуНаСервере();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНД ФОРМЫ

&НаКлиенте
Процедура Выбрать(Команда)
	
	МассивВозврата = Новый Массив;
	
	Если НЕ ЭтаФорма.МножественныйВыбор Тогда
		ТекущаяСтрока = Элементы.ТаблицаДокументов.ТекущиеДанные;
		МассивВозврата.Добавить(ТекущаяСтрока.ДокументИБ)
	Иначе
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("Выбрать", Истина);
		
		СтрокиТаблицы = ТаблицаДокументов.НайтиСтроки(СтруктураОтбора);
		Для Каждого Строка Из СтрокиТаблицы Цикл
			МассивВозврата.Добавить(Строка.ДокументИБ)
		КонецЦикла;
	КонецЕсли;
	
	ОповеститьОВыборе(МассивВозврата);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьВыделенные(Команда)
	
	МассивСтрок = Элементы.ТаблицаДокументов.ВыделенныеСтроки;
	Для Каждого НомерСтроки Из МассивСтрок Цикл
		СтрокаТаблицы = ТаблицаДокументов.НайтиПоИдентификатору(НомерСтроки);
		Если СтрокаТаблицы <> Неопределено Тогда
			СтрокаТаблицы.Выбрать = Истина;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьОтметкуСоВсехСтрок(Команда)
	
	Для Каждого Строка Из ТаблицаДокументов Цикл
		Если Строка.Выбрать Тогда
			Строка.Выбрать = Ложь;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборПериода(Команда)
	
	Если НаложитьНовыеУсловияОтбора() Тогда
		ВыборСтандартногоПериода(Период);
		ЗаполнитьТаблицуДокументов();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборСтандартногоПериода(Период)

	Диалог = Новый ДиалогРедактированияСтандартногоПериода();
	Диалог.Период = Период;
	Если Диалог.Редактировать() Тогда
		Период = Диалог.Период;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьЭД(Команда)
	
	ТекущаяСтрока = Элементы.ТаблицаДокументов.ТекущиеДанные;
	Если ТекущаяСтрока <> Неопределено Тогда
		ЭлектронныеДокументыСлужебныйКлиент.ОткрытьЭДДляПросмотра(ТекущаяСтрока.ЭлектронныйДокумент);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ПОЛЕЙ ФОРМЫ

&НаКлиенте
Процедура ТаблицаДокументовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если НЕ ЭтаФорма.МножественныйВыбор Тогда
		ТекущаяСтрока = Элементы.ТаблицаДокументов.ТекущиеДанные;
		Если ТекущаяСтрока <> Неопределено Тогда
			ОповеститьОВыборе(ТекущаяСтрока.ДокументИБ);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидДокументаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если НЕ НаложитьНовыеУсловияОтбора() Тогда
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидДокументаПриИзменении(Элемент)
	
	ЗаполнитьТаблицуДокументов();
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если НЕ НаложитьНовыеУсловияОтбора() Тогда
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ЗаполнитьТаблицуДокументов();
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если НЕ НаложитьНовыеУсловияОтбора() Тогда
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	ЗаполнитьТаблицуДокументов();
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	ЗаполнитьТаблицуДокументов();
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	Элементы.ОтметитьВыделенные.Видимость = Форма.МножественныйВыбор;
	Элементы.СнятьОтметкуСоВсехСтрок.Видимость = Форма.МножественныйВыбор;
	Элементы.ТаблицаДокументовВыбрать.Видимость = Форма.МножественныйВыбор;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуДокументов()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА НЕ ЭДПрисоединенныеФайлы.ЭлектронныйДокументВладелец = ЗНАЧЕНИЕ(Справочник.ЭДПрисоединенныеФайлы.ПустаяСсылка)
	|			ТОГДА ЭДПрисоединенныеФайлы.ЭлектронныйДокументВладелец.ВидЭД
	|		ИНАЧЕ ЭДПрисоединенныеФайлы.ВидЭД
	|	КОНЕЦ КАК ВидЭД,
	|	ВЫБОР
	|		КОГДА НЕ ЭДПрисоединенныеФайлы.ЭлектронныйДокументВладелец = ЗНАЧЕНИЕ(Справочник.ЭДПрисоединенныеФайлы.ПустаяСсылка)
	|			ТОГДА ЭДПрисоединенныеФайлы.ЭлектронныйДокументВладелец.НомерДокументаОтправителя
	|		ИНАЧЕ ЭДПрисоединенныеФайлы.НомерДокументаОтправителя
	|	КОНЕЦ КАК НомерДокументаОтправителя,
	|	ВЫБОР
	|		КОГДА НЕ ЭДПрисоединенныеФайлы.ЭлектронныйДокументВладелец = ЗНАЧЕНИЕ(Справочник.ЭДПрисоединенныеФайлы.ПустаяСсылка)
	|			ТОГДА ЭДПрисоединенныеФайлы.ЭлектронныйДокументВладелец.ДатаДокументаОтправителя
	|		ИНАЧЕ ЭДПрисоединенныеФайлы.ДатаДокументаОтправителя
	|	КОНЕЦ КАК ДатаДокументаОтправителя,
	|	ВЫБОР
	|		КОГДА НЕ ЭДПрисоединенныеФайлы.ЭлектронныйДокументВладелец = ЗНАЧЕНИЕ(Справочник.ЭДПрисоединенныеФайлы.ПустаяСсылка)
	|			ТОГДА ЭДПрисоединенныеФайлы.ЭлектронныйДокументВладелец.Организация
	|		ИНАЧЕ ЭДПрисоединенныеФайлы.Организация
	|	КОНЕЦ КАК Организация,
	|	ВЫБОР
	|		КОГДА НЕ ЭДПрисоединенныеФайлы.ЭлектронныйДокументВладелец = ЗНАЧЕНИЕ(Справочник.ЭДПрисоединенныеФайлы.ПустаяСсылка)
	|			ТОГДА ЭДПрисоединенныеФайлы.ЭлектронныйДокументВладелец.Контрагент
	|		ИНАЧЕ ЭДПрисоединенныеФайлы.Контрагент
	|	КОНЕЦ КАК Контрагент,
	|	ВЫБОР
	|		КОГДА НЕ ЭДПрисоединенныеФайлы.ЭлектронныйДокументВладелец = ЗНАЧЕНИЕ(Справочник.ЭДПрисоединенныеФайлы.ПустаяСсылка)
	|			ТОГДА ЭДПрисоединенныеФайлы.ЭлектронныйДокументВладелец.ВладелецФайла
	|		ИНАЧЕ ЭДПрисоединенныеФайлы.ВладелецФайла
	|	КОНЕЦ КАК ДокументИБ,
	|	ВЫБОР
	|		КОГДА НЕ ЭДПрисоединенныеФайлы.ЭлектронныйДокументВладелец = ЗНАЧЕНИЕ(Справочник.ЭДПрисоединенныеФайлы.ПустаяСсылка)
	|			ТОГДА ЭДПрисоединенныеФайлы.ЭлектронныйДокументВладелец.Ссылка
	|		ИНАЧЕ ЭДПрисоединенныеФайлы.Ссылка
	|	КОНЕЦ КАК ЭлектронныйДокумент
	|ИЗ
	|	РегистрСведений.СостоянияЭД КАК СостоянияЭД
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЭДПрисоединенныеФайлы КАК ЭДПрисоединенныеФайлы
	|		ПО СостоянияЭД.ЭлектронныйДокумент = ЭДПрисоединенныеФайлы.Ссылка
	|ГДЕ
	|	ВЫБОР
	|			КОГДА НЕ ЭДПрисоединенныеФайлы.ЭлектронныйДокументВладелец = ЗНАЧЕНИЕ(Справочник.ЭДПрисоединенныеФайлы.ПустаяСсылка)
	|				ТОГДА ЭДПрисоединенныеФайлы.ЭлектронныйДокументВладелец.ВидЭД = &ВидДокумента
	|			ИНАЧЕ ЭДПрисоединенныеФайлы.ВидЭД = &ВидДокумента
	|		КОНЕЦ
	|	И ВЫБОР
	|			КОГДА &Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ВЫБОР
	|					КОГДА НЕ ЭДПрисоединенныеФайлы.ЭлектронныйДокументВладелец = ЗНАЧЕНИЕ(Справочник.ЭДПрисоединенныеФайлы.ПустаяСсылка)
	|						ТОГДА ЭДПрисоединенныеФайлы.ЭлектронныйДокументВладелец.Контрагент = &Контрагент
	|					ИНАЧЕ ЭДПрисоединенныеФайлы.Контрагент = &Контрагент
	|				КОНЕЦ
	|		КОНЕЦ
	|	И ВЫБОР
	|			КОГДА &Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ВЫБОР
	|					КОГДА НЕ ЭДПрисоединенныеФайлы.ЭлектронныйДокументВладелец = ЗНАЧЕНИЕ(Справочник.ЭДПрисоединенныеФайлы.ПустаяСсылка)
	|						ТОГДА ЭДПрисоединенныеФайлы.ЭлектронныйДокументВладелец.Организация = &Организация
	|					ИНАЧЕ ЭДПрисоединенныеФайлы.Организация = &Организация
	|				КОНЕЦ
	|		КОНЕЦ
	|	И ВЫБОР
	|			КОГДА НЕ ЭДПрисоединенныеФайлы.ЭлектронныйДокументВладелец = ЗНАЧЕНИЕ(Справочник.ЭДПрисоединенныеФайлы.ПустаяСсылка)
	|				ТОГДА ЭДПрисоединенныеФайлы.ЭлектронныйДокументВладелец.ДатаДокументаОтправителя МЕЖДУ &НачПериода И &КонПериода
	|			ИНАЧЕ ЭДПрисоединенныеФайлы.ДатаДокументаОтправителя МЕЖДУ &НачПериода И &КонПериода
	|		КОНЕЦ
	|	И СостоянияЭД.СостояниеВерсииЭД = ЗНАЧЕНИЕ(Перечисление.СостоянияВерсийЭД.ОбменЗавершен)";
	Запрос.УстановитьПараметр("ВидДокумента", ВидДокумента);
	Запрос.УстановитьПараметр("Контрагент",   Контрагент);
	Запрос.УстановитьПараметр("Организация",  Организация);
	Запрос.УстановитьПараметр("НачПериода",   Период.ДатаНачала);
	Запрос.УстановитьПараметр("КонПериода",   ?(ЗначениеЗаполнено(Период.ДатаОкончания), Период.ДатаОкончания, ТекущаяДатаСеанса()));
	
	Результат = Запрос.Выполнить().Выбрать();
	
	ТаблицаДокументов.Очистить();
	
	Пока Результат.Следующий() Цикл
		СтрокаТаблицы = ТаблицаДокументов.Добавить();
		СтрокаТаблицы.ВидЭД               = Результат.ВидЭД;
		СтрокаТаблицы.НомерДокумента      = Результат.НомерДокументаОтправителя;
		СтрокаТаблицы.ДатаДокумента       = Результат.ДатаДокументаОтправителя;
		СтрокаТаблицы.Организация         = Результат.Организация;
		СтрокаТаблицы.Контрагент          = Результат.Контрагент;
		СтрокаТаблицы.ДокументИБ          = Результат.ДокументИБ;
		СтрокаТаблицы.ЭлектронныйДокумент = Результат.ЭлектронныйДокумент;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция НаложитьНовыеУсловияОтбора()
	
	Результат = Истина;
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("Выбрать", Истина);
	
	СтрокиТаблицы = ТаблицаДокументов.НайтиСтроки(СтруктураОтбора);
	Если ЭтаФорма.МножественныйВыбор И ЗначениеЗаполнено(СтрокиТаблицы) Тогда
		ТекстВопроса = НСтр("ru = 'Флаги выделения документов будут сброшены. Продолжить?'");
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ОКОтмена, , КодВозвратаДиалога.Отмена, "Обновить таблицу документов.");
		
		Результат = Ответ = КодВозвратаДиалога.ОК;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции
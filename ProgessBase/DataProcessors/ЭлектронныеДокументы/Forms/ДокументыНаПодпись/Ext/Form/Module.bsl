////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

&НаСервере
Функция ИспользуетсяОбменСБанками()
	
	Возврат Константы.ИспользоватьОбменЭДСБанками.Получить();
	
КонецФункции

&НаСервере
Процедура ЗаполнитьСписокСертификатовИДокументов(МассивСтруктурСертификатов)
	
	ТаблицаДоступныхСертификатов = ЭлектронныеДокументыСлужебный.ТаблицаДоступныхДляПодписиСертификатов(МассивСтруктурСертификатов);
	ЗаполнитьСводнуюТаблицу(ТаблицаДоступныхСертификатов);
	ЗаполнитьСписокСертификатов(ТаблицаДоступныхСертификатов);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокСертификатов(ТаблицаДоступныхСертификатов)
	
	ТаблицаСертификатов.Очистить();
	Для Каждого ТекСтрока Из ТаблицаДоступныхСертификатов Цикл
		СтрокаТаблицы = ТаблицаСертификатов.Добавить();
		СтрокаТаблицы.Сертификат = ТекСтрока.Ссылка;
		ПараметрыОтбора = Новый Структура("Сертификат", ТекСтрока.Ссылка);
		СтрокаТаблицы.КоличествоДокументов = СводнаяТаблица.НайтиСтроки(ПараметрыОтбора).Количество();
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСводнуюТаблицу(ТаблицаДоступныхСертификатов)
	
	ЗапросПоДокументам = Новый Запрос;
	
	СтруктураДопОтбора = Новый Структура;
	Если ЗначениеЗаполнено(Контрагент) Тогда 
		СтруктураДопОтбора.Вставить("Контрагент", Контрагент);
		ЗапросПоДокументам.УстановитьПараметр("Контрагент", Контрагент);
	КонецЕсли;
	Если ЗначениеЗаполнено(ВидЭД) Тогда 
		СтруктураДопОтбора.Вставить("ВидЭД", ВидЭД);
		ЗапросПоДокументам.УстановитьПараметр("ВидЭД", ВидЭД);
	КонецЕсли;
	Если ЗначениеЗаполнено(НаправлениеЭД) Тогда 
		СтруктураДопОтбора.Вставить("НаправлениеЭД", НаправлениеЭД);
		ЗапросПоДокументам.УстановитьПараметр("НаправлениеЭД", НаправлениеЭД);
	КонецЕсли;
	
	ЗапросСоставаИсполнителей = Новый Запрос;
	ЗапросСоставаИсполнителей.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СертификатыЭЦП.Ссылка,
	|	СертификатыЭЦП.ПроверятьСоставИсполнителей КАК ПроверятьСоставИсполнителей,
	|	СертификатыЭЦПСоставИсполнителей.Исполнитель
	|ИЗ
	|	Справочник.СертификатыЭЦП КАК СертификатыЭЦП
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СертификатыЭЦП.СоставИсполнителей КАК СертификатыЭЦПСоставИсполнителей
	|		ПО СертификатыЭЦПСоставИсполнителей.Ссылка = СертификатыЭЦП.Ссылка
	|ГДЕ
	|	СертификатыЭЦП.Ссылка В(&СписокДоступныхСертификатов)";
	ЗапросСоставаИсполнителей.УстановитьПараметр(
		"СписокДоступныхСертификатов", ТаблицаДоступныхСертификатов.ВыгрузитьКолонку("Ссылка"));
	ВыборкаПоСоставу = ЗапросСоставаИсполнителей.Выполнить().Выгрузить();
	
	// Проверим, что нет таких сертификатов, по которым нет контроля на состав исполнителей
	ЛюбойИсполнитель = ВыборкаПоСоставу.Найти(Ложь,"ПроверятьСоставИсполнителей");
	Если ЛюбойИсполнитель = Неопределено Тогда // есть строгое ограничение на состав исполнителей по всем доступным сертификатам
		МассивИсполнителей = ВыборкаПоСоставу.ВыгрузитьКолонку("Исполнитель");
		СтруктураДопОтбора.Вставить("ОтборПоИсполнителям", Истина);
		ЗапросПоДокументам.УстановитьПараметр("СоставИсполнителей", МассивИсполнителей);
	КонецЕсли;
		
	МассивВидовСлужебныхЭД = Новый Массив;
	МассивВидовСлужебныхЭД.Добавить(Перечисления.ВидыЭД.ИзвещениеОПолучении);
	МассивВидовСлужебныхЭД.Добавить(Перечисления.ВидыЭД.УведомлениеОбУточнении);
	
	ЗапросПоДокументам.Текст = ЭлектронныеДокументы.ПолучитьТекстЗапросаЭлектронныхДокументовНаПодписи(Ложь, СтруктураДопОтбора);
	ЗапросПоДокументам.УстановитьПараметр("ТекущийПользователь", ПользователиСервер.АвторизованныйПользователь());
	ЗапросПоДокументам.УстановитьПараметр("ВидыЭД", МассивВидовСлужебныхЭД);
	Таблица = ЗапросПоДокументам.Выполнить().Выгрузить();
	
	ЗначениеВРеквизитФормы(Таблица, "СводнаяТаблица");
	
КонецПроцедуры

&НаКлиенте
Процедура ПодписатьМассивДокументов(МассивДокументовНаПодпись)
	
	Если МассивДокументовНаПодпись.Количество()=0 Тогда
		Возврат;
	КонецЕсли;
	
	МассивЭДСбербанка = Новый Массив;
	ЭлектронныеДокументыСлужебныйВызовСервера.ПеренестиЭДСбербанкаВОтдельныйМассив(
																МассивДокументовНаПодпись,
																МассивЭДСбербанка);
	
	ПарольКСертификату = Неопределено;
	
	КолПодписанных = 0;
	
	Если МассивДокументовНаПодпись.Количество() > 0 Тогда
		Если НЕ ЭлектронныеДокументыСлужебныйВызовСервера.ВыполнятьКриптооперацииНаСервере() Тогда
			КолПодписанных = ЭлектронныеДокументыСлужебныйКлиент.ПодписатьЭДОпределеннымСертификатом(
																				МассивДокументовНаПодпись,
																				СертификатПодписи,
																				ПарольКСертификату);
		Иначе
			ПараметрыСертификата =Неопределено;
			Если НЕ ЭлектронныеДокументыСлужебныйКлиент.ПолучитьПарольКСертификату(
																СертификатПодписи,
																ПарольКСертификату,
																НСтр("ru = 'Подписать электронные документы'"),
																ПараметрыСертификата,
																,
																МассивДокументовНаПодпись) Тогда
				ЭлектронныеДокументыСлужебныйВызовСервера.ОбновитьВерсиюЭД(МассивДокументовНаПодпись);
			Иначе
				КолПодписанных = ЭлектронныеДокументыСлужебныйВызовСервера.ПодписатьЭДОпределеннымСертификатом(
																					МассивДокументовНаПодпись,
																					СертификатПодписи,
																					ПарольКСертификату,
																					ПараметрыСертификата);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	КолПодписанныхДокументовСбербанка = 0;
	
	Если МассивЭДСбербанка.Количество() > 0 Тогда
		ЭлектронныеДокументыСлужебныйКлиент.ПодписатьЭДСбербанкаОпределеннымСертификатом(
														МассивЭДСбербанка,
														СертификатПодписи,
														КолПодписанныхДокументовСбербанка);
	КонецЕсли;
	
//	КолПодписанных = КолПодписанных + КолПодписанныхДокументовСбербанка;
	
	КолПодготовленных = 0;
	КолОтправленных = 0;
	МассивСтруктурСертификатов = Новый Массив;
		
	Если КолПодписанных > 0 Тогда
		СоотвСоглашенийИСтруктурСертификатов = ЭлектронныеДокументыСлужебныйКлиент.СоотвСоглашенийИСтруктурСертификатовДляАвторизации(
																											,
																											МассивДокументовНаПодпись);
		СтруктураРезультата = ЭлектронныеДокументыСлужебныйКлиент.ПодготовитьИОтправитьПЭД(
														МассивДокументовНаПодпись,
														Истина,
														СоотвСоглашенийИСтруктурСертификатов);
		КолПодготовленных = СтруктураРезультата.КолПодготовленных;
		КолОтправленных = СтруктураРезультата.КолОтправленных;
		Оповестить("ОбновитьСостояниеЭД");
		Если ЭлектронныеДокументыСлужебныйВызовСервера.ВыполнятьКриптооперацииНаСервере() Тогда
			МассивСтруктурСертификатов = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьМассивСтруктурСертификатов(Истина);
		Иначе
			МассивСтруктурСертификатов = ЭлектроннаяЦифроваяПодписьКлиент.ПолучитьМассивСтруктурСертификатов(Истина);
		КонецЕсли;
	КонецЕсли;
	
	Если КолПодписанных > 0 ИЛИ КолПодписанныхДокументовСбербанка > 0 Тогда
		Оповестить("ОбновитьСостояниеЭД");
		ЗаполнитьСписокСертификатовИДокументов(МассивСтруктурСертификатов);
		ЗаполнитьСписокДокументовПоСертификату();
	КонецЕсли;
	
	КолПодписанных = КолПодписанных + КолПодписанныхДокументовСбербанка;
	
	ЭлектронныеДокументыСлужебныйКлиент.ВывестиИнформациюОбОбработанныхЭД(
															0,
															0,
															КолПодписанных,
															КолПодготовленных,
															КолОтправленных);
		
КонецПроцедуры

&НаКлиенте
Процедура ПерезаполнитьТаблицы()
	
	Попытка
		Если ЭлектронныеДокументыСлужебныйВызовСервера.ВыполнятьКриптооперацииНаСервере() Тогда
			МассивСтруктурСертификатов = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьМассивСтруктурСертификатов(Истина);
		Иначе
			МассивСтруктурСертификатов = ЭлектроннаяЦифроваяПодписьКлиент.ПолучитьМассивСтруктурСертификатов(Истина);
		КонецЕсли;
	Исключение
		МассивСтруктурСертификатов = Новый Массив;
	КонецПопытки;
	ЗаполнитьСписокСертификатовИДокументов(МассивСтруктурСертификатов);
	ЗаполнитьСписокДокументовПоСертификату();
	
КонецПроцедуры

&НаКлиенте
Функция ЕстьДокументыНаПодпись() 
	
	ПроверочныеДанные = ?(Элементы.ТаблицаСертификатов.ТекущиеДанные = Неопределено,
		ТаблицаСертификатов[0], Элементы.ТаблицаСертификатов.ТекущиеДанные);
	
	Если ПроверочныеДанные.КоличествоДокументов = 0 Тогда
		ТекстПредупреждения = НСтр("ru = 'По данному сертификату нет документов на подпись'");
		Предупреждение(ТекстПредупреждения);
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура ПерейтиНаСтраницу(КДетализации)
	
	Если КДетализации Тогда
		Элементы.СтраницыАРМ.ТекущаяСтраница = Элементы.СтраницыАРМ.ПодчиненныеЭлементы.СтраницаДетализации;
		ЭтаФорма.Заголовок = НСтр("ru = 'Документы на подпись по сертификату'")+ ": " + СертификатПодписи;
	Иначе
		Элементы.СтраницыАРМ.ТекущаяСтраница = Элементы.СтраницыАРМ.ПодчиненныеЭлементы.СтраницаСводки;
		ЭтаФорма.Заголовок = НСтр("ru = 'Документы на подпись'");
	КонецЕсли;
	
	Если ЭтоСертификатСбербанка(СертификатПодписи) Тогда
		Элементы.Подписать.Заголовок = Нстр("ru = 'Подписать отмеченные'");
	Иначе
		Элементы.Подписать.Заголовок = Нстр("ru = 'Подписать и отправить отмеченные'");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокДокументовПоСертификату()
	
	ТаблицаДокументов.Очистить();
	ПараметрыОтбора = Новый Структура("Сертификат", СертификатПодписи);
	СтрокиДокументов = СводнаяТаблица.НайтиСтроки(ПараметрыОтбора);
	
	Для Каждого СтрокаСДокументом Из СтрокиДокументов Цикл
		СтрокаТаблицы = ТаблицаДокументов.Добавить();
		СтрокаТаблицы.ЭлектронныйДокумент = СтрокаСДокументом.ЭлектронныйДокумент;
		СтрокаТаблицы.СуммаДокумента      = СтрокаСДокументом.СуммаДокумента;
		СтрокаТаблицы.ДатаДокумента       = СтрокаСДокументом.ДатаДокумента;
		СтрокаТаблицы.Версия              = СтрокаСДокументом.Версия;
	КонецЦикла;
		
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЭтоСертификатСбербанка(Сертификат)
	
	Идентификатор = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Сертификат, "Идентификатор");
	Возврат НЕ ПустаяСтрока(Идентификатор);
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНД ФОРМЫ

&НаКлиенте
Процедура Обновить(Команда)
	
	ПерезаполнитьТаблицы();
	
КонецПроцедуры

&НаКлиенте
Процедура Подписать(Команда)
	
	МассивДокументовНаПодпись = Новый Массив;
	Для Каждого ТекСтрока Из ТаблицаДокументов Цикл
		Если ТекСтрока.Выбрать Тогда
			МассивДокументовНаПодпись.Добавить(ТекСтрока.ЭлектронныйДокумент);
		КонецЕсли;
	КонецЦикла;
	
	ПодписатьМассивДокументов(МассивДокументовНаПодпись);

КонецПроцедуры

&НаКлиенте
Процедура ПодписатьВсе(Команда)
	
	Если НЕ ЕстьДокументыНаПодпись() Тогда
		Возврат;
	КонецЕсли;
	
	// По выделенному сертификату найдем все документы на подпись
	СертификатПодписи = ?(Элементы.ТаблицаСертификатов.ТекущиеДанные = Неопределено,
		ТаблицаСертификатов[0].Сертификат, Элементы.ТаблицаСертификатов.ТекущиеДанные.Сертификат);
	
	ПараметрыОтбора = Новый Структура("Сертификат", СертификатПодписи);
	СтрокиДокументов = СводнаяТаблица.НайтиСтроки(ПараметрыОтбора);
	
	МассивДокументовНаПодпись = Новый Массив;
	Для Каждого ЭлементТаблицы Из СтрокиДокументов Цикл
		МассивДокументовНаПодпись.Добавить(ЭлементТаблицы.ЭлектронныйДокумент);
	КонецЦикла;
	
	ПодписатьМассивДокументов(МассивДокументовНаПодпись);
	
КонецПроцедуры

&НаКлиенте
Процедура ВернутьсяКСпискуСертификатов(Команда)
	
	ПерейтиНаСтраницу(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКСпискуДокументов(Команда)
	
	СертификатПодписи = ?(Элементы.ТаблицаСертификатов.ТекущиеДанные = Неопределено,
		ТаблицаСертификатов[0].Сертификат, Элементы.ТаблицаСертификатов.ТекущиеДанные.Сертификат);
		
	Если НЕ ЕстьДокументыНаПодпись() Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьСписокДокументовПоСертификату();
	ПерейтиНаСтраницу(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьВыделенные(Команда)
	
	МассивСтрок = Элементы.ТаблицаДокументов.ВыделенныеСтроки;
	Для Каждого НомерСтроки Из МассивСтрок Цикл
		СтрокаТаблицы = ТаблицаДокументов.НайтиПоИдентификатору(НомерСтроки);
		Если СтрокаТаблицы <> Неопределено Тогда
			СтрокаТаблицы.Выбрать = Истина;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьОтметкуСоВсехСтрок(Команда)
	
	Для Каждого ТекДокумент Из ТаблицаДокументов Цикл
		Если ТекДокумент.Выбрать Тогда
			ТекДокумент.Выбрать = Ложь;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ПОЛЕЙ ФОРМЫ

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	ПерезаполнитьТаблицы();
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДокументовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ЭлектронныеДокументыСлужебныйКлиент.ОткрытьЭДДляПросмотра(Элементы.ТаблицаДокументов.ТекущиеДанные.ЭлектронныйДокумент);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСертификатовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СертификатПодписи = Элементы.ТаблицаСертификатов.ТекущиеДанные.Сертификат;
	Если НЕ ЕстьДокументыНаПодпись() Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьСписокДокументовПоСертификату();
	ПерейтиНаСтраницу(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидЭДПриИзменении(Элемент)
	
	ПерезаполнитьТаблицы();
	
КонецПроцедуры

&НаКлиенте
Процедура НаправлениеЭДПриИзменении(Элемент)
	
	ПерезаполнитьТаблицы();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ИспользуетсяОбменСБанками = ИспользуетсяОбменСБанками();
	МассивСтруктурСертификатов = Новый Массив;
	
	Если ЭлектронныеДокументыСлужебныйВызовСервера.ВыполнятьКриптооперацииНаСервере() Тогда
		ЕстьКриптосредстваНаСервере = ЭлектронныеДокументыСлужебныйВызовСервера.ЕстьКриптосредстваНаСервере();
		Если НЕ (ЕстьКриптосредстваНаСервере ИЛИ ИспользуетсяОбменСБанками) Тогда
			Отказ = Истина;
		КонецЕсли;
		Если НЕ Отказ И ЕстьКриптосредстваНаСервере Тогда
			МассивСтруктурСертификатов = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьМассивСтруктурСертификатов(Истина);
		КонецЕсли;
	Иначе
		Если НЕ (ПодключитьРасширениеРаботыСКриптографией()
				ИЛИ ЭлектронныеДокументыСлужебныйКлиент.УстановитьРасширениеРаботыСКриптографиейНаКлиенте()) Тогда
			Отказ = Истина;
		КонецЕсли;
		Попытка
			МенеджерКриптографии = ЭлектроннаяЦифроваяПодписьКлиент.ПолучитьМенеджерКриптографии(Отказ);
			ЕстьКриптосредствоНаКлиенте = Истина;
		Исключение
			Если НЕ ИспользуетсяОбменСБанками Тогда
				ТекстСообщения = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьСообщениеОбОшибке("100");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
				Отказ = Истина;
			КонецЕсли;
			ЕстьКриптосредствоНаКлиенте = Ложь;
		КонецПопытки;
		Если Не Отказ И ЕстьКриптосредствоНаКлиенте Тогда
			МассивСтруктурСертификатов = ЭлектроннаяЦифроваяПодписьКлиент.ПолучитьМассивСтруктурСертификатов(Истина);
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		
		ЗаполнитьСписокСертификатовИДокументов(МассивСтруктурСертификатов);
		
		Если ТаблицаСертификатов.Количество() = 0 Тогда
			ТекстПредупреждения = НСтр("ru = 'Нет сертификатов подписи для пользователя или не настроены правила подписи документов!'"); 
			Предупреждение(ТекстПредупреждения);
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
		Если ТаблицаСертификатов.Количество() > 1 Тогда
			ПерейтиНаСтраницу(Ложь);
		Иначе
			СертификатПодписи = ТаблицаСертификатов[0].Сертификат;
			ЗаполнитьСписокДокументовПоСертификату();
			ПерейтиНаСтраницу(Истина);
			Элементы.ГруппаКнопкиНазад.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьЗначениеФункциональнойОпции("ИспользоватьОбменЭД") Тогда
		ТекстСообщения = ЭлектронныеДокументыСлужебныйВызовСервера.ТекстСообщенияОНеобходимостиНастройкиСистемы("РаботаСЭД");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , , , Отказ);
	КонецЕсли;
	
	Если НЕ ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьЗначениеФункциональнойОпции("ИспользоватьЭлектронныеЦифровыеПодписи") Тогда
		ТекстСообщения = ЭлектронныеДокументыСлужебныйВызовСервера.ТекстСообщенияОНеобходимостиНастройкиСистемы("ПодписаниеЭД");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , , , Отказ);
	КонецЕсли;
	
	Элементы.СтраницыАРМ.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновитьСостояниеЭД" Тогда
		ПерезаполнитьТаблицы();
	КонецЕсли;
	
КонецПроцедуры

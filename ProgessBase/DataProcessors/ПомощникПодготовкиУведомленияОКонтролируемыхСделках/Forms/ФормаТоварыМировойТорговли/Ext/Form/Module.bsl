

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБЩЕГО НАЗНАЧЕНИЯ

&НаСервере
Процедура ЗаполнитьСписок()
	Если ПредметыКонтролируемыхСделок.Количество() > 0 Тогда 
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПредметыСделки.ПредметСделки КАК ПредметСделки
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПредметыКонтролируемыхСделок КАК ПредметыСделки
		|	ПО Номенклатура.Ссылка = ПредметыСделки.ПредметСделки		
		|ГДЕ
		|	ПредметыСделки.ЯвляетсяТоваромМировойБиржевойТорговли";
		
	МассивПредметов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ПредметСделки");
	ПредметыКонтролируемыхСделок.Очистить();
	Для Каждого Предмет Из МассивПредметов Цикл 
		СтрокаТЧ = ПредметыКонтролируемыхСделок.Добавить();
		СтрокаТЧ.ПредметСделки = Предмет;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура СохранитьСписок()
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТаблицаПредметовМировойТорговли", ПредметыКонтролируемыхСделок.Выгрузить());
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Таблица.ПредметСделки КАК ПредметСделки
		|ПОМЕСТИТЬ ВТ_ПредметыСделок
		|ИЗ
		|	&ТаблицаПредметовМировойТорговли КАК Таблица
		|ИНДЕКСИРОВАТЬ ПО
		|	Таблица.ПредметСделки
		|;
		|
		|//////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Таблица.ПредметСделки КАК ПредметСделки
		|ИЗ
		|	ВТ_ПредметыСделок КАК Таблица
		|ГДЕ
		|	НЕ Таблица.ПредметСделки В 
		|				(ВЫБРАТЬ
		|					Таб.ПредметСделки
		|				ИЗ
		|					РегистрСведений.ПредметыКонтролируемыхСделок КАК Таб
		|				ГДЕ
		|					Таб.ЯвляетсяТоваромМировойБиржевойТорговли)
		|;
		|
		|//////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Таблица.ПредметСделки КАК ПредметСделки
		|ИЗ
		|	РегистрСведений.ПредметыКонтролируемыхСделок КАК Таблица
		|ГДЕ
		|	Таблица.ЯвляетсяТоваромМировойБиржевойТорговли
		|	И НЕ Таблица.ПредметСделки В 
		|				(ВЫБРАТЬ
		|					Таб.ПредметСделки
		|				ИЗ
		|					ВТ_ПредметыСделок КАК Таб)";
		
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	Добавляемые = РезультатЗапроса[1].Выгрузить();
	Удаляемые = РезультатЗапроса[2].Выгрузить();
	МассивВновьДобавленных = Новый Массив;
	
	Записи = РегистрыСведений.ПредметыКонтролируемыхСделок.СоздатьНаборЗаписей();
	Записи.Прочитать();
	Для Каждого Запись Из Записи Цикл 
		СтрокаУдаляемые = Удаляемые.Найти(Запись.ПредметСделки, "ПредметСделки");
		Если ЗначениеЗаполнено(СтрокаУдаляемые) Тогда 
			Запись.ЯвляетсяТоваромМировойБиржевойТорговли = Ложь;
		Иначе
			СтрокаДобавляемые = Добавляемые.Найти(Запись.ПредметСделки, "ПредметСделки");
			Если ЗначениеЗаполнено(СтрокаДобавляемые) Тогда
				Запись.ЯвляетсяТоваромМировойБиржевойТорговли = Истина;
				Добавляемые.Удалить(СтрокаДобавляемые);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого СтрокаТЧ Из Добавляемые Цикл 
		Запись = Записи.Добавить();
		Запись.ПредметСделки = СтрокаТЧ.ПредметСделки;
		Запись.ЯвляетсяТоваромМировойБиржевойТорговли = Истина;
	КонецЦикла;
	Записи.Записать();
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ

&НаКлиенте
Процедура КомандаОтмена(Команда)
	Закрыть(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура КомандаСохранить(Команда)
	СохранитьСписок();
	Закрыть(Истина);
КонецПроцедуры

&НаКлиенте
Процедура ПредметыКонтролируемыхСделокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если Элементы.ПредметыКонтролируемыхСделок.ТекущиеДанные<>Неопределено Тогда
		ПараметрыФормы = Новый Структура("Ключ", Элементы.ПредметыКонтролируемыхСделок.ТекущиеДанные.ПредметСделки);
		ОткрытьФорму("Справочник.Номенклатура.ФормаОбъекта", ПараметрыФормы, Элементы.ПредметыКонтролируемыхСделок, Элементы.ПредметыКонтролируемыхСделок);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПредметыКонтролируемыхСделокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	ПараметрыФормы = Новый Структура("РежимВыбора", Истина);
	ОткрытьФорму("Справочник.Номенклатура.ФормаВыбора", ПараметрыФормы, Элементы.ПредметыКонтролируемыхСделок, Элементы.ПредметыКонтролируемыхСделок);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредметыКонтролируемыхСделокОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.Номенклатура") Тогда
		СтандартнаяОбработка = Ложь;
		Отбор = Новый Структура("ПредметСделки", ВыбранноеЗначение);
		НайденныеСтроки = ПредметыКонтролируемыхСделок.НайтиСтроки(Отбор);
		Если НайденныеСтроки.Количество()>0 Тогда
			Элементы.ПредметыКонтролируемыхСделок.ТекущаяСтрока = ПредметыКонтролируемыхСделок.Индекс(НайденныеСтроки[0]);
		Иначе
			НоваяСтрока = ПредметыКонтролируемыхСделок.Добавить();
			НоваяСтрока.ПредметСделки = ВыбранноеЗначение;
			Элементы.ПредметыКонтролируемыхСделок.ТекущаяСтрока = ПредметыКонтролируемыхСделок.Индекс(НоваяСтрока);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(Объект, Параметры);
	ЗаполнитьСписок();
	
КонецПроцедуры




////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБЩЕГО НАЗНАЧЕНИЯ

&НаСервереБезКонтекста
Процедура ЗаписатьГраницу(ВидГраницы, Период, ПредельнаяСумма)
	
	НаборЗаписей = РегистрыСведений.ГраницыКонтролируемостиСделок.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ОсобенностьОтнесенияСделкиККонтролируемой.Значение = ВидГраницы;
	НаборЗаписей.Отбор.ОсобенностьОтнесенияСделкиККонтролируемой.Использование = Истина;
	НаборЗаписей.Отбор.Период.Значение = Период;
	НаборЗаписей.Отбор.Период.Использование = Истина;
	Запись = НаборЗаписей.Добавить();
	Запись.ОсобенностьОтнесенияСделкиККонтролируемой = ВидГраницы;
	Запись.Период = Период;
	Запись.ПредельнаяСумма = ПредельнаяСумма;
	НаборЗаписей.Записать();
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьГраницы()
	
	СуществующиеГраницы = Новый Структура("ГраницаРФОбщая, ГраницаРФНДПИ, ГраницаСпецрежим, ГраницаРФПрибыль, ГраницаРФОЭЗ, ГраницаИностранныеНезависимыеЛица, ГраницаОбщая");
	ЗаполнитьГраницы(СуществующиеГраницы);
	ПериодДляЗаписи = Дата(Объект.ОтчетныйГод, 1, 1);
	Если СуществующиеГраницы.ГраницаОбщая <> ГраницаОбщая Тогда
		ЗаписатьГраницу(Перечисления.ОсобенностиОтнесенияСделокККонтролируемым.МинимальнаяСуммаДляВключенияВУведомление, ПериодДляЗаписи, ГраницаОбщая);
	КонецЕсли;
	Если СуществующиеГраницы.ГраницаРФОбщая <> ГраницаРФОбщая Тогда
		ЗаписатьГраницу(Перечисления.ОсобенностиОтнесенияСделокККонтролируемым.ВзаимозависимыеЛица, ПериодДляЗаписи, ГраницаРФОбщая);
	КонецЕсли;
	Если СуществующиеГраницы.ГраницаРФНДПИ <> ГраницаРФНДПИ Тогда
		ЗаписатьГраницу(Перечисления.ОсобенностиОтнесенияСделокККонтролируемым.ВзаимозависимыеЛицаПлательщикиНДПИ, ПериодДляЗаписи, ГраницаРФНДПИ);
	КонецЕсли;
	Если СуществующиеГраницы.ГраницаСпецрежим <> ГраницаСпецрежим Тогда
		ЗаписатьГраницу(Перечисления.ОсобенностиОтнесенияСделокККонтролируемым.ВзаимозависимыеЛицаНаСпецрежимах, ПериодДляЗаписи, ГраницаСпецрежим);
	КонецЕсли;
	Если СуществующиеГраницы.ГраницаРФПрибыль <> ГраницаРФПрибыль Тогда
		ЗаписатьГраницу(Перечисления.ОсобенностиОтнесенияСделокККонтролируемым.ВзаимозависимыеЛицаПлательщикиНалогаНаПрибыль, ПериодДляЗаписи, ГраницаРФПрибыль);
	КонецЕсли;
	Если СуществующиеГраницы.ГраницаРФОЭЗ <> ГраницаРФОЭЗ Тогда
		ЗаписатьГраницу(Перечисления.ОсобенностиОтнесенияСделокККонтролируемым.ВзаимозависимыеЛицаОсобаяЭкономическаяЗона, ПериодДляЗаписи, ГраницаРФОЭЗ);
	КонецЕсли;
	Если СуществующиеГраницы.ГраницаИностранныеНезависимыеЛица <> ГраницаИностранныеНезависимыеЛица Тогда
		ЗаписатьГраницу(Перечисления.ОсобенностиОтнесенияСделокККонтролируемым.НезависимыеИностранныеЛица, ПериодДляЗаписи, ГраницаИностранныеНезависимыеЛица);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьГраницы(ОбъектЗаполнения)
	
	Запрос = Новый Запрос();
	Запрос.Параметры.Вставить("ОтчетныйГод", Дата(Объект.ОтчетныйГод, 1, 1));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СУММА(ВЫБОР
	|			КОГДА ГраницыКонтролируемостиСделокСрезПоследних.ОсобенностьОтнесенияСделкиККонтролируемой = ЗНАЧЕНИЕ(Перечисление.ОсобенностиОтнесенияСделокККонтролируемым.ВзаимозависимыеЛица)
	|				ТОГДА ГраницыКонтролируемостиСделокСрезПоследних.ПредельнаяСумма
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ГраницаРФОбщая,
	|	СУММА(ВЫБОР
	|			КОГДА ГраницыКонтролируемостиСделокСрезПоследних.ОсобенностьОтнесенияСделкиККонтролируемой = ЗНАЧЕНИЕ(Перечисление.ОсобенностиОтнесенияСделокККонтролируемым.ВзаимозависимыеЛицаПлательщикиНДПИ)
	|				ТОГДА ГраницыКонтролируемостиСделокСрезПоследних.ПредельнаяСумма
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ГраницаРФНДПИ,
	|	СУММА(ВЫБОР
	|			КОГДА ГраницыКонтролируемостиСделокСрезПоследних.ОсобенностьОтнесенияСделкиККонтролируемой = ЗНАЧЕНИЕ(Перечисление.ОсобенностиОтнесенияСделокККонтролируемым.ВзаимозависимыеЛицаНаСпецрежимах)
	|				ТОГДА ГраницыКонтролируемостиСделокСрезПоследних.ПредельнаяСумма
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ГраницаСпецрежим,
	|	СУММА(ВЫБОР
	|			КОГДА ГраницыКонтролируемостиСделокСрезПоследних.ОсобенностьОтнесенияСделкиККонтролируемой = ЗНАЧЕНИЕ(Перечисление.ОсобенностиОтнесенияСделокККонтролируемым.ВзаимозависимыеЛицаПлательщикиНалогаНаПрибыль)
	|				ТОГДА ГраницыКонтролируемостиСделокСрезПоследних.ПредельнаяСумма
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ГраницаРФПрибыль,
	|	СУММА(ВЫБОР
	|			КОГДА ГраницыКонтролируемостиСделокСрезПоследних.ОсобенностьОтнесенияСделкиККонтролируемой = ЗНАЧЕНИЕ(Перечисление.ОсобенностиОтнесенияСделокККонтролируемым.ВзаимозависимыеЛицаОсобаяЭкономическаяЗона)
	|				ТОГДА ГраницыКонтролируемостиСделокСрезПоследних.ПредельнаяСумма
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ГраницаРФОЭЗ,
	|	СУММА(ВЫБОР
	|			КОГДА ГраницыКонтролируемостиСделокСрезПоследних.ОсобенностьОтнесенияСделкиККонтролируемой = ЗНАЧЕНИЕ(Перечисление.ОсобенностиОтнесенияСделокККонтролируемым.НезависимыеИностранныеЛица)
	|				ТОГДА ГраницыКонтролируемостиСделокСрезПоследних.ПредельнаяСумма
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ГраницаИностранныеНезависимыеЛица,
	|	СУММА(ВЫБОР
	|			КОГДА ГраницыКонтролируемостиСделокСрезПоследних.ОсобенностьОтнесенияСделкиККонтролируемой = ЗНАЧЕНИЕ(Перечисление.ОсобенностиОтнесенияСделокККонтролируемым.МинимальнаяСуммаДляВключенияВУведомление)
	|				ТОГДА ГраницыКонтролируемостиСделокСрезПоследних.ПредельнаяСумма
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ГраницаОбщая
	|ИЗ
	|	РегистрСведений.ГраницыКонтролируемостиСделок.СрезПоследних(КОНЕЦПЕРИОДА(&ОтчетныйГод, ГОД), ) КАК ГраницыКонтролируемостиСделокСрезПоследних";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(ОбъектЗаполнения, Выборка);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(Объект, Параметры);

	ЭтаФорма.Заголовок = СтрЗаменить(НСтр("ru = 'Границы включения сделок в уведомление за %ОтчетныйГод% год'"),"%ОтчетныйГод%",Формат(Объект.ОтчетныйГод, "ЧЦ=4; ЧГ=0"));
	
	ЗаполнитьГраницы(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗаписатьИЗакрыть(Команда)
	
	ЗаписатьГраницы();
	Закрыть();
	
КонецПроцедуры

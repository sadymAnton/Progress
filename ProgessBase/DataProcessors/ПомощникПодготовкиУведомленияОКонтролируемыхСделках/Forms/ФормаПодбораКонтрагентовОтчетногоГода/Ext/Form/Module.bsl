////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБЩЕГО НАЗНАЧЕНИЯ

&НаСервере
Процедура ЗаписатьРегистрациюКонтрагентов()
	
	Для Каждого Контрагент Из КонтрагентыОтчетногоГода Цикл
		Если Контрагент.Выбран
			И ЗначениеЗаполнено(Контрагент.СтранаРегистрации) Тогда
			Записи = РегистрыСведений.УчастникиКонтролируемыхСделок.СоздатьНаборЗаписей();
			Записи.Отбор.Контрагент.Значение = Контрагент.Контрагент;
			Записи.Отбор.Контрагент.Использование = Истина;
			Запись = Записи.Добавить();
			Запись.Контрагент = Контрагент.Контрагент;
			Запись.СтранаРегистрации = Контрагент.СтранаРегистрации;
			Запись.ЗарегистрированВСтранеСЛьготнымНалогообложением = ?(Запись.СтранаРегистрации = Справочники.КлассификаторСтранМира.Россия,
				Ложь, Контрагент.ЗарегистрированВСтранеСЛьготнымНалогообложением);
			Записи.Записать();
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписок()
	
	КонтрагентыОтчетногоГода.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("НачалоГодаУведомления", НачалоГода(Дата(Объект.ОтчетныйГод, 1, 1)));
	Запрос.УстановитьПараметр("ОкончаниеГодаУведомления", КонецГода(Дата(Объект.ОтчетныйГод, 1, 1)));
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", Константы.ВалютаРегламентированногоУчета.Получить());
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫБОР
	|		КОГДА Договоры.Владелец.ОбособленноеПодразделение
	|			ТОГДА Договоры.Владелец.ГоловнойКонтрагент
	|		ИНАЧЕ Договоры.Владелец
	|	КОНЕЦ КАК Контрагент
	|ПОМЕСТИТЬ ВТ_Контрагенты
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК Договоры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Субконто КАК ХозрасчетныйСубконто
	|		ПО (ХозрасчетныйСубконто.Значение = Договоры.Ссылка)
	|ГДЕ
	|	Договоры.ВалютаВзаиморасчетов <> &ВалютаРегламентированногоУчета
	|	И Договоры.РасчетыВУсловныхЕдиницах = ЛОЖЬ
	|	И Договоры.Организация = &Организация
	|	И ХозрасчетныйСубконто.Период >= &НачалоГодаУведомления
	|	И ХозрасчетныйСубконто.Период <= &ОкончаниеГодаУведомления
	|	И ХозрасчетныйСубконто.Вид = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Контрагенты.Контрагент КАК Контрагент,
	|	УчастникиКонтролируемыхСделок.СтранаРегистрации КАК СтранаРегистрации,
	|	УчастникиКонтролируемыхСделок.ЗарегистрированВСтранеСЛьготнымНалогообложением
	|ИЗ
	|	ВТ_Контрагенты КАК Контрагенты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчастникиКонтролируемыхСделок КАК УчастникиКонтролируемыхСделок
	|		ПО Контрагенты.Контрагент = УчастникиКонтролируемыхСделок.Контрагент";
	
	Контрагенты = Запрос.Выполнить().Выгрузить();
	КонтрагентыОтчетногоГода.Загрузить(Контрагенты);

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ
//

&НаКлиенте
Процедура КонтрагентыОтчетногоГодаСтранаРегистрацииПриИзменении(Элемент)
	
	Если Элементы.КонтрагентыОтчетногоГода.ТекущиеДанные <> Неопределено Тогда
		Запись = Элементы.КонтрагентыОтчетногоГода.ТекущиеДанные;
		Если ЗначениеЗаполнено(Запись.СтранаРегистрации) Тогда
			Запись.ЗарегистрированВСтранеСЛьготнымНалогообложением = СписокОфшоров.НайтиСтроки(Новый Структура("СтранаРегистрации", Запись.СтранаРегистрации)).Количество()>0;
		КонецЕсли;
		Запись.Выбран = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаДобавитьКонтрагентов(Команда)
	
	ЗаписатьРегистрациюКонтрагентов();
	Оповестить("СписокИностранныхКонтрагентовИзменен");
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентыОтчетногоГодаЗарегистрированВСтранеСЛьготнымНалогообложениемПриИзменении(Элемент)
	
	Если Элементы.КонтрагентыОтчетногоГода.ТекущиеДанные <> Неопределено Тогда
		Элементы.КонтрагентыОтчетногоГода.ТекущиеДанные.Выбран = Истина;
	КонецЕсли;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(Объект, Параметры);
	КонтролируемыеСделки.ЗаполнитьТаблицуОфшоров(СписокОфшоров);
	ЗаполнитьСписок();
	
КонецПроцедуры




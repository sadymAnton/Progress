
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ОБРАБОТКИ

Функция Автозаполнение() Экспорт

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПустойКодПоОКАТО","");
	Запрос.Текст =
    "ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
    |	ВсеКоды.Организация,
    |	ВсеКоды.Подразделение,
    |	ВсеКоды.КодПоОКАТО,
    |	МАКСИМУМ(ВсеКоды.КодПоОКТМО) КАК КодПоОКТМО
    |ПОМЕСТИТЬ ВТВсеКоды
    |ИЗ
    |	(ВЫБРАТЬ
    |		Организации.Ссылка КАК Организация,
    |		ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) КАК Подразделение,
    |		Организации.КодПоОКАТО КАК КодПоОКАТО,
    |		Организации.КодПоОКТМО КАК КодПоОКТМО
    |	ИЗ
    |		Справочник.Организации КАК Организации
    |	
    |	ОБЪЕДИНИТЬ ВСЕ
    |	
    |	ВЫБРАТЬ
    |		ПодразделенияОрганизаций.Владелец,
    |		ПодразделенияОрганизаций.Ссылка,
    |		ПодразделенияОрганизаций.КодПоОКАТО,
    |		ПодразделенияОрганизаций.КодПоОКТМО
    |	ИЗ
    |		Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
    |	ГДЕ
    |		ПодразделенияОрганизаций.КодПоОКАТО <> &ПустойКодПоОКАТО
    |		И ПодразделенияОрганизаций.КодПоОКАТО <> ЕСТЬNULL(ПодразделенияОрганизаций.Родитель.КодПоОКАТО, &ПустойКодПоОКАТО)) КАК ВсеКоды
    |
    |СГРУППИРОВАТЬ ПО
    |	ВсеКоды.Организация,
    |	ВсеКоды.Подразделение,
    |	ВсеКоды.КодПоОКАТО
    |;
    |
    |////////////////////////////////////////////////////////////////////////////////
    |ВЫБРАТЬ
    |	ВЫБОР
    |		КОГДА Организации.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
    |			ТОГДА Организации.Ссылка
    |		ИНАЧЕ Организации.ГоловнаяОрганизация
    |	КОНЕЦ КАК Организация
    |ПОМЕСТИТЬ ВТГоловныеОрганизации
    |ИЗ
    |	Справочник.Организации КАК Организации
    |ГДЕ
    |	Организации.Ссылка В
    |			(ВЫБРАТЬ
    |				Организации.Организация
    |			ИЗ
    |				ВТВсеКоды КАК Организации)
    |;
    |
    |////////////////////////////////////////////////////////////////////////////////
    |ВЫБРАТЬ РАЗРЕШЕННЫЕ
    |	НДФЛСведенияОДоходах.КодПоОКАТО,
    |	НДФЛСведенияОДоходах.Регистратор КАК Документ
    |ПОМЕСТИТЬ ВТДанныеУчета
    |ИЗ
    |	РегистрНакопления.НДФЛСведенияОДоходах КАК НДФЛСведенияОДоходах
    |ГДЕ
    |	НДФЛСведенияОДоходах.Организация В
    |			(ВЫБРАТЬ
    |				ГоловныеОрганизации.Организация
    |			ИЗ
    |				ВТГоловныеОрганизации КАК ГоловныеОрганизации)
    |	И НДФЛСведенияОДоходах.Период МЕЖДУ ДАТАВРЕМЯ(2013, 1, 1) И ДАТАВРЕМЯ(2013, 12, 31, 23, 59, 59)
    |	И НДФЛСведенияОДоходах.КодПоОКАТО <> &ПустойКодПоОКАТО
    |	И НДФЛСведенияОДоходах.КодПоОКТМО = &ПустойКодПоОКАТО
    |	И НДФЛСведенияОДоходах.ОбособленноеПодразделение В
    |			(ВЫБРАТЬ
    |				Организации.Организация
    |			ИЗ
    |				ВТВсеКоды КАК Организации)
    |
    |ОБЪЕДИНИТЬ ВСЕ
    |
    |ВЫБРАТЬ
    |	НДФЛРасчетыСБюджетом.КодПоОКАТО,
    |	НДФЛРасчетыСБюджетом.Регистратор
    |ИЗ
    |	РегистрНакопления.НДФЛРасчетыСБюджетом КАК НДФЛРасчетыСБюджетом
    |ГДЕ
    |	НДФЛРасчетыСБюджетом.Организация В
    |			(ВЫБРАТЬ
    |				ГоловныеОрганизации.Организация
    |			ИЗ
    |				ВТГоловныеОрганизации КАК ГоловныеОрганизации)
    |	И НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода МЕЖДУ ДАТАВРЕМЯ(2013, 1, 1) И ДАТАВРЕМЯ(2013, 12, 31, 23, 59, 59)
    |	И НДФЛРасчетыСБюджетом.КодПоОКАТО <> &ПустойКодПоОКАТО
    |	И НДФЛРасчетыСБюджетом.КодПоОКТМО = &ПустойКодПоОКАТО
    |	И НДФЛРасчетыСБюджетом.ОбособленноеПодразделение В
    |			(ВЫБРАТЬ
    |				Организации.Организация
    |			ИЗ
    |				ВТВсеКоды КАК Организации)
    |
    |ОБЪЕДИНИТЬ ВСЕ
    |
    |ВЫБРАТЬ
    |	НДФЛИмущественныеВычетыФизлиц.КодПоОКАТО,
    |	НДФЛИмущественныеВычетыФизлиц.Регистратор
    |ИЗ
    |	РегистрНакопления.НДФЛИмущественныеВычетыФизлиц КАК НДФЛИмущественныеВычетыФизлиц
    |ГДЕ
    |	НДФЛИмущественныеВычетыФизлиц.Организация В
    |			(ВЫБРАТЬ
    |				ГоловныеОрганизации.Организация
    |			ИЗ
    |				ВТГоловныеОрганизации КАК ГоловныеОрганизации)
    |	И НДФЛИмущественныеВычетыФизлиц.КодПоОКАТО <> &ПустойКодПоОКАТО
    |	И НДФЛИмущественныеВычетыФизлиц.КодПоОКТМО = &ПустойКодПоОКАТО
    |	И НДФЛИмущественныеВычетыФизлиц.ОбособленноеПодразделение В
    |			(ВЫБРАТЬ
    |				Организации.Организация
    |			ИЗ
    |				ВТВсеКоды КАК Организации)
    |	И НДФЛИмущественныеВычетыФизлиц.Год = 2013
    |
    |ОБЪЕДИНИТЬ ВСЕ
    |
    |ВЫБРАТЬ
    |	НДФЛПредоставленныеСтандартныеВычетыФизЛиц.КодПоОКАТО,
    |	НДФЛПредоставленныеСтандартныеВычетыФизЛиц.Регистратор
    |ИЗ
    |	РегистрНакопления.НДФЛПредоставленныеСтандартныеВычетыФизЛиц КАК НДФЛПредоставленныеСтандартныеВычетыФизЛиц
    |ГДЕ
    |	НДФЛПредоставленныеСтандартныеВычетыФизЛиц.Организация В
    |			(ВЫБРАТЬ
    |				ГоловныеОрганизации.Организация
    |			ИЗ
    |				ВТГоловныеОрганизации КАК ГоловныеОрганизации)
    |	И НДФЛПредоставленныеСтандартныеВычетыФизЛиц.МесяцНалоговогоПериода МЕЖДУ ДАТАВРЕМЯ(2013, 1, 1) И ДАТАВРЕМЯ(2013, 12, 31, 23, 59, 59)
    |	И НДФЛПредоставленныеСтандартныеВычетыФизЛиц.КодПоОКАТО <> &ПустойКодПоОКАТО
    |	И НДФЛПредоставленныеСтандартныеВычетыФизЛиц.КодПоОКТМО = &ПустойКодПоОКАТО
    |	И НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ОбособленноеПодразделение В
    |			(ВЫБРАТЬ
    |				Организации.Организация
    |			ИЗ
    |				ВТВсеКоды КАК Организации)
    |
    |ОБЪЕДИНИТЬ ВСЕ
    |
    |ВЫБРАТЬ
    |	ПОДСТРОКА(РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.ОКАТО_КПП, 1, 11),
    |	РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.Регистратор
    |ИЗ
    |	РегистрНакопления.РасчетыНалоговыхАгентовСБюджетомПоНДФЛ КАК РасчетыНалоговыхАгентовСБюджетомПоНДФЛ
    |ГДЕ
    |	РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.Организация В
    |			(ВЫБРАТЬ
    |				Организации.Организация
    |			ИЗ
    |				ВТВсеКоды КАК Организации)
    |	И РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.МесяцНалоговогоПериода МЕЖДУ ДАТАВРЕМЯ(2013, 1, 1) И ДАТАВРЕМЯ(2013, 12, 31, 23, 59, 59)
    |	И ПОДСТРОКА(РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.ОКАТО_КПП, 1, 11) <> &ПустойКодПоОКАТО
    |	И ПОДСТРОКА(РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.ОКТМО_КПП, 1, 11) = &ПустойКодПоОКАТО
    |;
    |
    |////////////////////////////////////////////////////////////////////////////////
    |ВЫБРАТЬ
    |	ВсеКоды.Организация,
    |	ВсеКоды.Подразделение,
    |	ВсеКоды.КодПоОКАТО,
    |	ВсеКоды.КодПоОКТМО,
    |	ВЫБОР
    |		КОГДА ВсеКоды.КодПоОКТМО = &ПустойКодПоОКАТО
    |			ТОГДА ЛОЖЬ
    |		ИНАЧЕ ИСТИНА
    |	КОНЕЦ КАК Отметка
    |ИЗ
    |	ВТВсеКоды КАК ВсеКоды
    |ГДЕ
    |	(ВсеКоды.КодПоОКАТО В
    |				(ВЫБРАТЬ
    |					ДанныеУчета.КодПоОКАТО
    |				ИЗ
    |					ВТДанныеУчета КАК ДанныеУчета)
    |			ИЛИ ВсеКоды.КодПоОКАТО <> &ПустойКодПоОКАТО
    |				И ВсеКоды.КодПоОКТМО = &ПустойКодПоОКАТО)
    |;
    |
    |////////////////////////////////////////////////////////////////////////////////
    |ВЫБРАТЬ РАЗЛИЧНЫЕ
    |	ДанныеУчета.КодПоОКАТО,
    |	ДанныеУчета.Документ
    |ИЗ
    |	ВТДанныеУчета КАК ДанныеУчета";
	
	МассивЗапросов = Новый Массив;
	#Если ТолстыйКлиентОбычноеПриложение тогда
	ФормированиеПечатныхФорм.ЗапомнитьПараметрыЗапроса(Запрос, МассивЗапросов);
	#КонецЕсли
	Попытка
	
		Результаты = Запрос.ВыполнитьПакет();
	
	Исключение
		#Если ТолстыйКлиентОбычноеПриложение тогда
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ПроверитьОшибкуЗапрос(МассивЗапросов, ИнформацияОбОшибке);
		#КонецЕсли
		Возврат Ложь;
	КонецПопытки;

	ТаблицаСоответствияКодовОКАТОиОКТМО.Загрузить(Результаты[3].Выгрузить());
	Регистраторы.Загрузить(Результаты[4].Выгрузить());
	
	Возврат Истина;
	
КонецФункции

Процедура ОбновитьДанные(Форма = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	МассивКодов = Новый Массив;
	МассивКодов.Добавить("");
	МассивКодов.Добавить("           ");
	Запрос.УстановитьПараметр("ПустыеКодыПоОКАТО",МассивКодов);
	Запрос.УстановитьПараметр("ТаблицаСоответствияКодов", ТаблицаСоответствияКодовОКАТОиОКТМО);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаСоответствияКодов.Отметка,
	|	ТаблицаСоответствияКодов.Организация,
	|	ТаблицаСоответствияКодов.Подразделение,
	|	ТаблицаСоответствияКодов.КодПоОКАТО,
	|	ТаблицаСоответствияКодов.КодПоОКТМО
	|ПОМЕСТИТЬ ВТТаблицаСоответствияКодов
	|ИЗ
	|	&ТаблицаСоответствияКодов КАК ТаблицаСоответствияКодов";
	Запрос.Выполнить();

	Если ПравоДоступа("Изменение", Метаданные.Справочники.Организации) Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаОрганизаций.Организация КАК Ссылка,
		|	ТаблицаОрганизаций.КодПоОКТМО
		|ИЗ
		|	ВТТаблицаСоответствияКодов КАК ТаблицаОрганизаций
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
		|		ПО ТаблицаОрганизаций.Организация = Организации.Ссылка
		|ГДЕ
		|	Организации.КодПоОКТМО В(&ПустыеКодыПоОКАТО)
		|	И ТаблицаОрганизаций.Отметка
		|	И ТаблицаОрганизаций.Подразделение = ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
		|	И НЕ ТаблицаОрганизаций.КодПоОКТМО В (&ПустыеКодыПоОКАТО)";
	
		ОбновитьОбъектыПоЗапросу(Запрос, "Организации", Форма);
		
	КонецЕсли;
	
	Если ПравоДоступа("Изменение", Метаданные.Справочники.ПодразделенияОрганизаций) Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПодразделенияОрганизаций.Ссылка,
		|	ЕСТЬNULL(ВыбранныеПодразделения.КодПоОКТМО, МАКСИМУМ(ТаблицаСоответствияКодов.КодПоОКТМО)) КАК КодПоОКТМО
		|ИЗ
		|	Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТТаблицаСоответствияКодов КАК ВыбранныеПодразделения
		|		ПО ПодразделенияОрганизаций.Ссылка = ВыбранныеПодразделения.Подразделение
		|			И (НЕ ВыбранныеПодразделения.КодПоОКТМО В (&ПустыеКодыПоОКАТО))
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТТаблицаСоответствияКодов КАК ТаблицаСоответствияКодов
		|		ПО ПодразделенияОрганизаций.Владелец = ТаблицаСоответствияКодов.Организация
		|			И ПодразделенияОрганизаций.КодПоОКАТО = ТаблицаСоответствияКодов.КодПоОКАТО
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТТаблицаСоответствияКодов КАК НевыбранныеПодразделения
		|		ПО ПодразделенияОрганизаций.Ссылка = НевыбранныеПодразделения.Подразделение
		|			И (НЕ НевыбранныеПодразделения.Отметка)
		|ГДЕ
		|	ПодразделенияОрганизаций.КодПоОКТМО В(&ПустыеКодыПоОКАТО)
		|	И НЕ ПодразделенияОрганизаций.КодПоОКАТО В (&ПустыеКодыПоОКАТО)
		|	И ТаблицаСоответствияКодов.Отметка
		|	И НЕ ТаблицаСоответствияКодов.КодПоОКТМО В (&ПустыеКодыПоОКАТО)
		|	И НевыбранныеПодразделения.Подразделение ЕСТЬ NULL 
		|
		|СГРУППИРОВАТЬ ПО
		|	ПодразделенияОрганизаций.Ссылка,
		|	ВыбранныеПодразделения.КодПоОКТМО";
		
		ОбновитьОбъектыПоЗапросу(Запрос, "Подразделения", Форма);
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Регистраторы", Регистраторы);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Регистраторы.Документ,
	|	Регистраторы.КодПоОКАТО
	|ПОМЕСТИТЬ ВТРегистраторы
	|ИЗ
	|	&Регистраторы КАК Регистраторы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Коды.КодПоОКАТО КАК КодПоОКАТО,
	|	МАКСИМУМ(Коды.КодПоОКТМО) КАК КодПоОКТМО
	|ПОМЕСТИТЬ ВТСоответствиеОКАТОиОКТМО
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаОрганизаций.КодПоОКАТО КАК КодПоОКАТО,
	|		ТаблицаОрганизаций.КодПоОКТМО КАК КодПоОКТМО
	|	ИЗ
	|		ВТТаблицаСоответствияКодов КАК ТаблицаОрганизаций
	|	ГДЕ
	|		ТаблицаОрганизаций.Отметка
	|		И ТаблицаОрганизаций.Подразделение = ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
	|		И НЕ ТаблицаОрганизаций.КодПоОКТМО В (&ПустыеКодыПоОКАТО)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаПодразделений.КодПоОКАТО,
	|		ТаблицаПодразделений.КодПоОКТМО
	|	ИЗ
	|		ВТТаблицаСоответствияКодов КАК ТаблицаПодразделений
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТТаблицаСоответствияКодов КАК ТаблицаОрганизаций
	|			ПО ТаблицаПодразделений.КодПоОКАТО = ТаблицаОрганизаций.КодПоОКАТО
	|				И (ТаблицаОрганизаций.Подразделение = ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка))
	|				И (ТаблицаОрганизаций.Отметка)
	|	ГДЕ
	|		ТаблицаПодразделений.Отметка
	|		И ТаблицаПодразделений.Подразделение <> ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
	|		И НЕ ТаблицаПодразделений.КодПоОКТМО В (&ПустыеКодыПоОКАТО)
	|		И ТаблицаОрганизаций.Организация ЕСТЬ NULL ) КАК Коды
	|
	|СГРУППИРОВАТЬ ПО
	|	Коды.КодПоОКАТО
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КодПоОКАТО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Регистраторы.Документ
	|ПОМЕСТИТЬ ВТРегистраторыКОбработке
	|ИЗ
	|	ВТТаблицаСоответствияКодов КАК ТаблицаСоответствияКодов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРегистраторы КАК Регистраторы
	|		ПО ТаблицаСоответствияКодов.КодПоОКАТО = Регистраторы.КодПоОКАТО
	|ГДЕ
	|	ТаблицаСоответствияКодов.Отметка
	|	И НЕ ТаблицаСоответствияКодов.КодПоОКТМО В (&ПустыеКодыПоОКАТО)";
    Результат = Запрос.Выполнить().Выгрузить();
	Если Результат[0].Количество = 0 Тогда // количество документов к обработке
		#Если ТолстыйКлиентОбычноеПриложение Тогда
			ОбновитьИнформационнуюСтроку(Форма, "Обработка выбранных кодов ОКАТО завершена.");
		#КонецЕсли
		Возврат 
	КонецЕсли;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПеречислениеНДФЛвБюджет.Ссылка,
	|	СоответствиеОКАТОиОКТМО.КодПоОКТМО + ПОДСТРОКА(ПеречислениеНДФЛвБюджет.ОКАТО_КПП, 12, 10) КАК ОКТМО_КПП
	|ИЗ
	|	Документ.ПеречислениеНДФЛвБюджет КАК ПеречислениеНДФЛвБюджет
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСоответствиеОКАТОиОКТМО КАК СоответствиеОКАТОиОКТМО
	|		ПО (ПОДСТРОКА(ПеречислениеНДФЛвБюджет.ОКАТО_КПП, 1, 11) = СоответствиеОКАТОиОКТМО.КодПоОКАТО)
	|ГДЕ
	|	ПеречислениеНДФЛвБюджет.Ссылка В
	|			(ВЫБРАТЬ
	|				РегистраторыКОбработке.Документ
	|			ИЗ
	|				ВТРегистраторыКОбработке КАК РегистраторыКОбработке)
	|	И ПОДСТРОКА(ПеречислениеНДФЛвБюджет.ОКТМО_КПП, 1, 11) В (&ПустыеКодыПоОКАТО)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СправкаПоНДФЛВНалоговыйОрган.Ссылка,
	|	СоответствиеОКАТОиОКТМО.КодПоОКТМО + ПОДСТРОКА(СправкаПоНДФЛВНалоговыйОрган.ОКАТО_КПП, 12, 10)
	|ИЗ
	|	Документ.СправкаПоНДФЛВНалоговыйОрган КАК СправкаПоНДФЛВНалоговыйОрган
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСоответствиеОКАТОиОКТМО КАК СоответствиеОКАТОиОКТМО
	|		ПО (ПОДСТРОКА(СправкаПоНДФЛВНалоговыйОрган.ОКАТО_КПП, 1, 11) = СоответствиеОКАТОиОКТМО.КодПоОКАТО)
	|ГДЕ
	|	СправкаПоНДФЛВНалоговыйОрган.Ссылка В
	|			(ВЫБРАТЬ
	|				РегистраторыКОбработке.Документ
	|			ИЗ
	|				ВТРегистраторыКОбработке КАК РегистраторыКОбработке)
	|	И ПОДСТРОКА(СправкаПоНДФЛВНалоговыйОрган.ОКТМО_КПП, 1, 11) В (&ПустыеКодыПоОКАТО)";
	ОбновитьОбъектыПоЗапросу(Запрос, "Документы по перечислению НДФЛ", Форма);
	
	Запрос.Текст =
	"УНИЧТОЖИТЬ ВТРегистраторы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СтрокиРегистра.Регистратор КАК Документ
	|ПОМЕСТИТЬ ВТРегистраторы
	|ИЗ
	|	РегистрНакопления.НДФЛСведенияОДоходах КАК СтрокиРегистра
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСоответствиеОКАТОиОКТМО КАК СоответствиеОКАТОиОКТМО
	|		ПО СтрокиРегистра.КодПоОКАТО = СоответствиеОКАТОиОКТМО.КодПоОКАТО
	|ГДЕ
	|	СтрокиРегистра.Регистратор В
	|			(ВЫБРАТЬ
	|				РегистраторыКОбработке.Документ
	|			ИЗ
	|				ВТРегистраторыКОбработке КАК РегистраторыКОбработке)
	|	И СтрокиРегистра.КодПоОКТМО В(&ПустыеКодыПоОКАТО)
	|	И СоответствиеОКАТОиОКТМО.КодПоОКАТО ЕСТЬ НЕ NULL 
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДФЛСведенияОДоходах.Период,
	|	НДФЛСведенияОДоходах.Регистратор КАК Регистратор,
	|	НДФЛСведенияОДоходах.НомерСтроки КАК НомерСтроки,
	|	НДФЛСведенияОДоходах.Активность,
	|	НДФЛСведенияОДоходах.Организация,
	|	НДФЛСведенияОДоходах.ФизЛицо,
	|	НДФЛСведенияОДоходах.КодДохода,
	|	НДФЛСведенияОДоходах.ПериодРегистрации,
	|	НДФЛСведенияОДоходах.СуммаДохода,
	|	НДФЛСведенияОДоходах.СуммаВычета,
	|	НДФЛСведенияОДоходах.КодВычета,
	|	НДФЛСведенияОДоходах.ОбособленноеПодразделение,
	|	НДФЛСведенияОДоходах.ИсчисленоИзЗарплаты,
	|	НДФЛСведенияОДоходах.КодПоОКАТО,
	|	НДФЛСведенияОДоходах.КПП,
	|	НДФЛСведенияОДоходах.ПодразделениеОрганизации,
	|	НДФЛСведенияОДоходах.КоличествоДетей,
	|	НДФЛСведенияОДоходах.ВидРасчета,
	|	НДФЛСведенияОДоходах.ЗарегистрированоДляПромежуточногоРасчета,
	|	НДФЛСведенияОДоходах.ДатаПолученияДохода,
	|	ВЫБОР
	|		КОГДА НДФЛСведенияОДоходах.КодПоОКТМО В (&ПустыеКодыПоОКАТО)
	|			ТОГДА СоответствиеОКАТОиОКТМО.КодПоОКТМО
	|		ИНАЧЕ НДФЛСведенияОДоходах.КодПоОКТМО
	|	КОНЕЦ КАК КодПоОКТМО, *
	|ИЗ
	|	РегистрНакопления.НДФЛСведенияОДоходах КАК НДФЛСведенияОДоходах
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСоответствиеОКАТОиОКТМО КАК СоответствиеОКАТОиОКТМО
	|		ПО НДФЛСведенияОДоходах.КодПоОКАТО = СоответствиеОКАТОиОКТМО.КодПоОКАТО
	|ГДЕ
	|	НДФЛСведенияОДоходах.Регистратор В
	|			(ВЫБРАТЬ
	|				РегистраторыКОбработке.Документ
	|			ИЗ
	|				ВТРегистраторы КАК РегистраторыКОбработке)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Регистратор,
	|	НомерСтроки";
	ПереписатьНаборыЗаписейПоРегистраторам(РегистрыНакопления.НДФЛСведенияОДоходах, Запрос, "Данные о доходах", Форма);
	
	Запрос.Текст =
	"УНИЧТОЖИТЬ ВТРегистраторы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СтрокиРегистра.Регистратор КАК Документ
	|ПОМЕСТИТЬ ВТРегистраторы
	|ИЗ
	|	РегистрНакопления.НДФЛРасчетыСБюджетом КАК СтрокиРегистра
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСоответствиеОКАТОиОКТМО КАК СоответствиеОКАТОиОКТМО
	|		ПО СтрокиРегистра.КодПоОКАТО = СоответствиеОКАТОиОКТМО.КодПоОКАТО
	|ГДЕ
	|	СтрокиРегистра.Регистратор В
	|			(ВЫБРАТЬ
	|				РегистраторыКОбработке.Документ
	|			ИЗ
	|				ВТРегистраторыКОбработке КАК РегистраторыКОбработке)
	|	И СтрокиРегистра.КодПоОКТМО В(&ПустыеКодыПоОКАТО)
	|	И СоответствиеОКАТОиОКТМО.КодПоОКАТО ЕСТЬ НЕ NULL 
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДФЛРасчетыСБюджетом.Период,
	|	НДФЛРасчетыСБюджетом.Регистратор КАК Регистратор,
	|	НДФЛРасчетыСБюджетом.НомерСтроки КАК НомерСтроки,
	|	НДФЛРасчетыСБюджетом.Активность,
	|	НДФЛРасчетыСБюджетом.ВидДвижения,
	|	НДФЛРасчетыСБюджетом.ФизЛицо,
	|	НДФЛРасчетыСБюджетом.Организация,
	|	НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента,
	|	НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода,
	|	НДФЛРасчетыСБюджетом.Налог,
	|	НДФЛРасчетыСБюджетом.ОбособленноеПодразделение,
	|	НДФЛРасчетыСБюджетом.ВидСтроки,
	|	НДФЛРасчетыСБюджетом.ИсчисленоИзЗарплаты,
	|	НДФЛРасчетыСБюджетом.КодПоОКАТО,
	|	НДФЛРасчетыСБюджетом.КПП,
	|	НДФЛРасчетыСБюджетом.ПодразделениеОрганизации,
	|	НДФЛРасчетыСБюджетом.КодДохода,
	|	ВЫБОР
	|		КОГДА НДФЛРасчетыСБюджетом.КодПоОКТМО В(&ПустыеКодыПоОКАТО)
	|			ТОГДА СоответствиеОКАТОиОКТМО.КодПоОКТМО
	|		ИНАЧЕ НДФЛРасчетыСБюджетом.КодПоОКТМО
	|	КОНЕЦ КАК КодПоОКТМО, *
	|ИЗ
	|	РегистрНакопления.НДФЛРасчетыСБюджетом КАК НДФЛРасчетыСБюджетом
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСоответствиеОКАТОиОКТМО КАК СоответствиеОКАТОиОКТМО
	|		ПО НДФЛРасчетыСБюджетом.КодПоОКАТО = СоответствиеОКАТОиОКТМО.КодПоОКАТО
	|ГДЕ
	|	НДФЛРасчетыСБюджетом.Регистратор В
	|			(ВЫБРАТЬ
	|				РегистраторыКОбработке.Документ
	|			ИЗ
	|				ВТРегистраторы КАК РегистраторыКОбработке)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Регистратор,
	|	НомерСтроки";
	ПереписатьНаборыЗаписейПоРегистраторам(РегистрыНакопления.НДФЛРасчетыСБюджетом, Запрос, "Данные о начисленном (удержанном) налоге", Форма);
	
	Запрос.Текст =
	"УНИЧТОЖИТЬ ВТРегистраторы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СтрокиРегистра.Регистратор КАК Документ
	|ПОМЕСТИТЬ ВТРегистраторы
	|ИЗ
	|	РегистрНакопления.НДФЛПредоставленныеСтандартныеВычетыФизЛиц КАК СтрокиРегистра
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСоответствиеОКАТОиОКТМО КАК СоответствиеОКАТОиОКТМО
	|		ПО СтрокиРегистра.КодПоОКАТО = СоответствиеОКАТОиОКТМО.КодПоОКАТО
	|ГДЕ
	|	СтрокиРегистра.Регистратор В
	|			(ВЫБРАТЬ
	|				РегистраторыКОбработке.Документ
	|			ИЗ
	|				ВТРегистраторыКОбработке КАК РегистраторыКОбработке)
	|	И СтрокиРегистра.КодПоОКТМО В(&ПустыеКодыПоОКАТО)
	|	И СоответствиеОКАТОиОКТМО.КодПоОКАТО ЕСТЬ НЕ NULL 
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДФЛПредоставленныеСтандартныеВычетыФизЛиц.Период,
	|	НДФЛПредоставленныеСтандартныеВычетыФизЛиц.Регистратор КАК Регистратор,
	|	НДФЛПредоставленныеСтандартныеВычетыФизЛиц.НомерСтроки КАК НомерСтроки,
	|	НДФЛПредоставленныеСтандартныеВычетыФизЛиц.Активность,
	|	НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ФизЛицо,
	|	НДФЛПредоставленныеСтандартныеВычетыФизЛиц.Организация,
	|	НДФЛПредоставленныеСтандартныеВычетыФизЛиц.МесяцНалоговогоПериода,
	|	НДФЛПредоставленныеСтандартныеВычетыФизЛиц.КодВычета,
	|	НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ПримененныйВычет,
	|	НДФЛПредоставленныеСтандартныеВычетыФизЛиц.КодПоОКАТО,
	|	НДФЛПредоставленныеСтандартныеВычетыФизЛиц.КПП,
	|	НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ОбособленноеПодразделение,
	|	НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ПодразделениеОрганизации,
	|	ВЫБОР
	|		КОГДА НДФЛПредоставленныеСтандартныеВычетыФизЛиц.КодПоОКТМО В (&ПустыеКодыПоОКАТО)
	|			ТОГДА СоответствиеОКАТОиОКТМО.КодПоОКТМО
	|		ИНАЧЕ НДФЛПредоставленныеСтандартныеВычетыФизЛиц.КодПоОКТМО
	|	КОНЕЦ КАК КодПоОКТМО
	|ИЗ
	|	РегистрНакопления.НДФЛПредоставленныеСтандартныеВычетыФизЛиц КАК НДФЛПредоставленныеСтандартныеВычетыФизЛиц
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСоответствиеОКАТОиОКТМО КАК СоответствиеОКАТОиОКТМО
	|		ПО НДФЛПредоставленныеСтандартныеВычетыФизЛиц.КодПоОКАТО = СоответствиеОКАТОиОКТМО.КодПоОКАТО
	|ГДЕ
	|	НДФЛПредоставленныеСтандартныеВычетыФизЛиц.Регистратор В
	|			(ВЫБРАТЬ
	|				РегистраторыКОбработке.Документ
	|			ИЗ
	|				ВТРегистраторы КАК РегистраторыКОбработке)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Регистратор,
	|	НомерСтроки";
	ПереписатьНаборыЗаписейПоРегистраторам(РегистрыНакопления.НДФЛПредоставленныеСтандартныеВычетыФизЛиц, Запрос, "Данные о вычетах", Форма);
	
	Запрос.Текст =
	"УНИЧТОЖИТЬ ВТРегистраторы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СтрокиРегистра.Регистратор КАК Документ
	|ПОМЕСТИТЬ ВТРегистраторы
	|ИЗ
	|	РегистрНакопления.НДФЛИмущественныеВычетыФизлиц КАК СтрокиРегистра
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСоответствиеОКАТОиОКТМО КАК СоответствиеОКАТОиОКТМО
	|		ПО СтрокиРегистра.КодПоОКАТО = СоответствиеОКАТОиОКТМО.КодПоОКАТО
	|ГДЕ
	|	СтрокиРегистра.Регистратор В
	|			(ВЫБРАТЬ
	|				РегистраторыКОбработке.Документ
	|			ИЗ
	|				ВТРегистраторыКОбработке КАК РегистраторыКОбработке)
	|	И СтрокиРегистра.КодПоОКТМО В(&ПустыеКодыПоОКАТО)
	|	И СоответствиеОКАТОиОКТМО.КодПоОКАТО ЕСТЬ НЕ NULL 
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДФЛИмущественныеВычетыФизлиц.Период,
	|	НДФЛИмущественныеВычетыФизлиц.Регистратор КАК Регистратор,
	|	НДФЛИмущественныеВычетыФизлиц.НомерСтроки КАК НомерСтроки,
	|	НДФЛИмущественныеВычетыФизлиц.Активность,
	|	НДФЛИмущественныеВычетыФизлиц.ВидДвижения,
	|	НДФЛИмущественныеВычетыФизлиц.ФизЛицо,
	|	НДФЛИмущественныеВычетыФизлиц.Организация,
	|	НДФЛИмущественныеВычетыФизлиц.КодВычетаИмущественный,
	|	НДФЛИмущественныеВычетыФизлиц.Год,
	|	НДФЛИмущественныеВычетыФизлиц.Размер,
	|	НДФЛИмущественныеВычетыФизлиц.КодПоОКАТО,
	|	НДФЛИмущественныеВычетыФизлиц.КПП,
	|	НДФЛИмущественныеВычетыФизлиц.ОбособленноеПодразделение,
	|	НДФЛИмущественныеВычетыФизлиц.ПодразделениеОрганизации,
	|	ВЫБОР
	|		КОГДА НДФЛИмущественныеВычетыФизлиц.КодПоОКТМО В (&ПустыеКодыПоОКАТО)
	|			ТОГДА СоответствиеОКАТОиОКТМО.КодПоОКТМО
	|		ИНАЧЕ НДФЛИмущественныеВычетыФизлиц.КодПоОКТМО
	|	КОНЕЦ КАК КодПоОКТМО
	|ИЗ
	|	РегистрНакопления.НДФЛИмущественныеВычетыФизлиц КАК НДФЛИмущественныеВычетыФизлиц
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСоответствиеОКАТОиОКТМО КАК СоответствиеОКАТОиОКТМО
	|		ПО НДФЛИмущественныеВычетыФизлиц.КодПоОКАТО = СоответствиеОКАТОиОКТМО.КодПоОКАТО
	|ГДЕ
	|	НДФЛИмущественныеВычетыФизлиц.Регистратор В
	|			(ВЫБРАТЬ
	|				РегистраторыКОбработке.Документ
	|			ИЗ
	|				ВТРегистраторы КАК РегистраторыКОбработке)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Регистратор,
	|	НомерСтроки";
	ПереписатьНаборыЗаписейПоРегистраторам(РегистрыНакопления.НДФЛИмущественныеВычетыФизлиц, Запрос, "Данные о вычетах", Форма);
	
	Запрос.Текст =
	"УНИЧТОЖИТЬ ВТРегистраторы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СтрокиРегистра.Регистратор КАК Документ
	|ПОМЕСТИТЬ ВТРегистраторы
	|ИЗ
	|	РегистрНакопления.РасчетыНалоговыхАгентовСБюджетомПоНДФЛ КАК СтрокиРегистра
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСоответствиеОКАТОиОКТМО КАК СоответствиеОКАТОиОКТМО
	|		ПО (ПОДСТРОКА(СтрокиРегистра.ОКАТО_КПП, 1, 11) = СоответствиеОКАТОиОКТМО.КодПоОКАТО)
	|ГДЕ
	|	СтрокиРегистра.Регистратор В
	|			(ВЫБРАТЬ
	|				РегистраторыКОбработке.Документ
	|			ИЗ
	|				ВТРегистраторыКОбработке КАК РегистраторыКОбработке)
	|	И ПОДСТРОКА(СтрокиРегистра.ОКТМО_КПП, 1, 11) В (&ПустыеКодыПоОКАТО)
	|	И СоответствиеОКАТОиОКТМО.КодПоОКАТО ЕСТЬ НЕ NULL 
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.Период,
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.Регистратор КАК Регистратор,
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.НомерСтроки КАК НомерСтроки,
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.Активность,
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.ВидДвижения,
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.ФизЛицо,
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.Организация,
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.Ставка,
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.МесяцНалоговогоПериода,
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.ОКАТО_КПП,
	|	ВЫБОР
	|		КОГДА ПОДСТРОКА(РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.ОКТМО_КПП, 1, 11) В (&ПустыеКодыПоОКАТО)
	|			ТОГДА СоответствиеОКАТОиОКТМО.КодПоОКТМО + ПОДСТРОКА(РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.ОКАТО_КПП, 12, 10)
	|		ИНАЧЕ РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.ОКТМО_КПП
	|	КОНЕЦ КАК ОКТМО_КПП,
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.Сумма
	|ИЗ
	|	РегистрНакопления.РасчетыНалоговыхАгентовСБюджетомПоНДФЛ КАК РасчетыНалоговыхАгентовСБюджетомПоНДФЛ
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСоответствиеОКАТОиОКТМО КАК СоответствиеОКАТОиОКТМО
	|		ПО (ПОДСТРОКА(РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.ОКАТО_КПП, 1, 11) = СоответствиеОКАТОиОКТМО.КодПоОКАТО)
	|ГДЕ
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.Регистратор В
	|			(ВЫБРАТЬ
	|				РегистраторыКОбработке.Документ
	|			ИЗ
	|				ВТРегистраторы КАК РегистраторыКОбработке)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Регистратор,
	|	НомерСтроки";
	ПереписатьНаборыЗаписейПоРегистраторам(РегистрыНакопления.РасчетыНалоговыхАгентовСБюджетомПоНДФЛ, Запрос, "Данные об уплаченном налоге", Форма);	
	
	ИзмененныеДокументы = Новый Соответствие;
	Запрос.Текст =
	"УНИЧТОЖИТЬ ВТРегистраторы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СтрокиДокумента.Ссылка КАК Документ
	|ПОМЕСТИТЬ ВТРегистраторы
	|ИЗ
	|	Документ.НДФЛВозвратНалога.РаботникиОрганизации КАК СтрокиДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСоответствиеОКАТОиОКТМО КАК СоответствиеОКАТОиОКТМО
	|		ПО СтрокиДокумента.КодПоОКАТО = СоответствиеОКАТОиОКТМО.КодПоОКАТО
	|ГДЕ
	|	СтрокиДокумента.Ссылка В
	|			(ВЫБРАТЬ
	|				РегистраторыКОбработке.Документ
	|			ИЗ
	|				ВТРегистраторыКОбработке КАК РегистраторыКОбработке)
	|	И СтрокиДокумента.КодПоОКТМО В(&ПустыеКодыПоОКАТО)
	|	И СоответствиеОКАТОиОКТМО.КодПоОКАТО ЕСТЬ НЕ NULL 
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДФЛВозвратНалогаРаботникиОрганизации.Ссылка,
	|	НДФЛВозвратНалогаРаботникиОрганизации.НомерСтроки,
	|	НДФЛВозвратНалогаРаботникиОрганизации.ФизЛицо,
	|	НДФЛВозвратНалогаРаботникиОрганизации.СуммаВозвратаПоСтавке13,
	|	НДФЛВозвратНалогаРаботникиОрганизации.СуммаВозвратаПоСтавке09,
	|	НДФЛВозвратНалогаРаботникиОрганизации.СуммаВозвратаПоСтавке35,
	|	НДФЛВозвратНалогаРаботникиОрганизации.КодПоОКАТО,
	|	НДФЛВозвратНалогаРаботникиОрганизации.КПП,
	|	ВЫБОР
	|		КОГДА НДФЛВозвратНалогаРаботникиОрганизации.КодПоОКТМО В(&ПустыеКодыПоОКАТО)
	|			ТОГДА СоответствиеОКАТОиОКТМО.КодПоОКТМО
	|		ИНАЧЕ НДФЛВозвратНалогаРаботникиОрганизации.КодПоОКТМО
	|	КОНЕЦ КАК КодПоОКТМО, *
	|ИЗ
	|	Документ.НДФЛВозвратНалога.РаботникиОрганизации КАК НДФЛВозвратНалогаРаботникиОрганизации
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСоответствиеОКАТОиОКТМО КАК СоответствиеОКАТОиОКТМО
	|		ПО НДФЛВозвратНалогаРаботникиОрганизации.КодПоОКАТО = СоответствиеОКАТОиОКТМО.КодПоОКАТО
	|ГДЕ
	|	НДФЛВозвратНалогаРаботникиОрганизации.Ссылка В
	|			(ВЫБРАТЬ
	|				РегистраторыКОбработке.Документ
	|			ИЗ
	|				ВТРегистраторы КАК РегистраторыКОбработке)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НДФЛВозвратНалогаРаботникиОрганизации.Ссылка,
	|	НДФЛВозвратНалогаРаботникиОрганизации.НомерСтроки";
	ОбновитьТабличнуюЧастьОбъектаПоЗапросу(Запрос, "РаботникиОрганизации", "Документы на возврат налога", ИзмененныеДокументы, Форма);

	Запрос.Текст =
	"УНИЧТОЖИТЬ ВТРегистраторы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СтрокиДокумента.Ссылка КАК Документ
	|ПОМЕСТИТЬ ВТРегистраторы
	|ИЗ
	|	Документ.НДФЛиЕСНДоходыИНалоги.НДФЛСведенияОДоходах КАК СтрокиДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСоответствиеОКАТОиОКТМО КАК СоответствиеОКАТОиОКТМО
	|		ПО СтрокиДокумента.КодПоОКАТО = СоответствиеОКАТОиОКТМО.КодПоОКАТО
	|ГДЕ
	|	СтрокиДокумента.Ссылка В
	|			(ВЫБРАТЬ
	|				РегистраторыКОбработке.Документ
	|			ИЗ
	|				ВТРегистраторыКОбработке КАК РегистраторыКОбработке)
	|	И СтрокиДокумента.КодПоОКТМО В(&ПустыеКодыПоОКАТО)
	|	И СоответствиеОКАТОиОКТМО.КодПоОКАТО ЕСТЬ НЕ NULL 
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДФЛиЕСНДоходыИНалогиНДФЛСведенияОДоходах.Ссылка,
	|	НДФЛиЕСНДоходыИНалогиНДФЛСведенияОДоходах.НомерСтроки,
	|	НДФЛиЕСНДоходыИНалогиНДФЛСведенияОДоходах.ФизЛицо,
	|	НДФЛиЕСНДоходыИНалогиНДФЛСведенияОДоходах.ДатаДохода,
	|	НДФЛиЕСНДоходыИНалогиНДФЛСведенияОДоходах.МесяцНалоговогоПериода,
	|	НДФЛиЕСНДоходыИНалогиНДФЛСведенияОДоходах.ПериодРегистрации,
	|	НДФЛиЕСНДоходыИНалогиНДФЛСведенияОДоходах.КодДохода,
	|	НДФЛиЕСНДоходыИНалогиНДФЛСведенияОДоходах.СуммаДохода,
	|	НДФЛиЕСНДоходыИНалогиНДФЛСведенияОДоходах.КодВычета,
	|	НДФЛиЕСНДоходыИНалогиНДФЛСведенияОДоходах.СуммаВычета,
	|	НДФЛиЕСНДоходыИНалогиНДФЛСведенияОДоходах.СуммаНалогаИсчисленная,
	|	НДФЛиЕСНДоходыИНалогиНДФЛСведенияОДоходах.КПП,
	|	НДФЛиЕСНДоходыИНалогиНДФЛСведенияОДоходах.КодПоОКАТО,
	|	НДФЛиЕСНДоходыИНалогиНДФЛСведенияОДоходах.КоличествоДетей,
	|	ВЫБОР
	|		КОГДА НДФЛиЕСНДоходыИНалогиНДФЛСведенияОДоходах.КодПоОКТМО В(&ПустыеКодыПоОКАТО)
	|			ТОГДА СоответствиеОКАТОиОКТМО.КодПоОКТМО
	|		ИНАЧЕ НДФЛиЕСНДоходыИНалогиНДФЛСведенияОДоходах.КодПоОКТМО
	|	КОНЕЦ КАК КодПоОКТМО, *
	|ИЗ
	|	Документ.НДФЛиЕСНДоходыИНалоги.НДФЛСведенияОДоходах КАК НДФЛиЕСНДоходыИНалогиНДФЛСведенияОДоходах
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСоответствиеОКАТОиОКТМО КАК СоответствиеОКАТОиОКТМО
	|		ПО НДФЛиЕСНДоходыИНалогиНДФЛСведенияОДоходах.КодПоОКАТО = СоответствиеОКАТОиОКТМО.КодПоОКАТО
	|ГДЕ
	|	НДФЛиЕСНДоходыИНалогиНДФЛСведенияОДоходах.Ссылка В
	|			(ВЫБРАТЬ
	|				РегистраторыКОбработке.Документ
	|			ИЗ
	|				ВТРегистраторы КАК РегистраторыКОбработке)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НДФЛиЕСНДоходыИНалогиНДФЛСведенияОДоходах.Ссылка,
	|	НДФЛиЕСНДоходыИНалогиНДФЛСведенияОДоходах.НомерСтроки";
	ОбновитьТабличнуюЧастьОбъектаПоЗапросу(Запрос, "НДФЛСведенияОДоходах", "Документы корректировки НДФЛ", ИзмененныеДокументы, Форма);

	Запрос.Текст =
	"УНИЧТОЖИТЬ ВТРегистраторы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СтрокиДокумента.Ссылка КАК Документ
	|ПОМЕСТИТЬ ВТРегистраторы
	|ИЗ
	|	Документ.НДФЛиЕСНДоходыИНалоги.НДФЛИсчисленный13 КАК СтрокиДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСоответствиеОКАТОиОКТМО КАК СоответствиеОКАТОиОКТМО
	|		ПО СтрокиДокумента.КодПоОКАТО = СоответствиеОКАТОиОКТМО.КодПоОКАТО
	|ГДЕ
	|	СтрокиДокумента.Ссылка В
	|			(ВЫБРАТЬ
	|				РегистраторыКОбработке.Документ
	|			ИЗ
	|				ВТРегистраторыКОбработке КАК РегистраторыКОбработке)
	|	И СтрокиДокумента.КодПоОКТМО В(&ПустыеКодыПоОКАТО)
	|	И СоответствиеОКАТОиОКТМО.КодПоОКАТО ЕСТЬ НЕ NULL 
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДФЛиЕСНДоходыИНалогиНДФЛИсчисленный13.Ссылка,
	|	НДФЛиЕСНДоходыИНалогиНДФЛИсчисленный13.НомерСтроки,
	|	НДФЛиЕСНДоходыИНалогиНДФЛИсчисленный13.ФизЛицо,
	|	НДФЛиЕСНДоходыИНалогиНДФЛИсчисленный13.ПериодРегистрации,
	|	НДФЛиЕСНДоходыИНалогиНДФЛИсчисленный13.МесяцНалоговогоПериода,
	|	НДФЛиЕСНДоходыИНалогиНДФЛИсчисленный13.Налог,
	|	НДФЛиЕСНДоходыИНалогиНДФЛИсчисленный13.КодПоОКАТО,
	|	НДФЛиЕСНДоходыИНалогиНДФЛИсчисленный13.КПП,
	|	ВЫБОР
	|		КОГДА НДФЛиЕСНДоходыИНалогиНДФЛИсчисленный13.КодПоОКТМО В(&ПустыеКодыПоОКАТО)
	|			ТОГДА СоответствиеОКАТОиОКТМО.КодПоОКТМО
	|		ИНАЧЕ НДФЛиЕСНДоходыИНалогиНДФЛИсчисленный13.КодПоОКТМО
	|	КОНЕЦ КАК КодПоОКТМО, *
	|ИЗ
	|	Документ.НДФЛиЕСНДоходыИНалоги.НДФЛИсчисленный13 КАК НДФЛиЕСНДоходыИНалогиНДФЛИсчисленный13
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСоответствиеОКАТОиОКТМО КАК СоответствиеОКАТОиОКТМО
	|		ПО НДФЛиЕСНДоходыИНалогиНДФЛИсчисленный13.КодПоОКАТО = СоответствиеОКАТОиОКТМО.КодПоОКАТО
	|ГДЕ
	|	НДФЛиЕСНДоходыИНалогиНДФЛИсчисленный13.Ссылка В
	|			(ВЫБРАТЬ
	|				РегистраторыКОбработке.Документ
	|			ИЗ
	|				ВТРегистраторы КАК РегистраторыКОбработке)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НДФЛиЕСНДоходыИНалогиНДФЛИсчисленный13.Ссылка,
	|	НДФЛиЕСНДоходыИНалогиНДФЛИсчисленный13.НомерСтроки";
	ОбновитьТабличнуюЧастьОбъектаПоЗапросу(Запрос, "НДФЛИсчисленный13", "Документы корректировки", ИзмененныеДокументы, Форма);

	Запрос.Текст =
	"УНИЧТОЖИТЬ ВТРегистраторы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СтрокиДокумента.Ссылка КАК Документ
	|ПОМЕСТИТЬ ВТРегистраторы
	|ИЗ
	|	Документ.НДФЛиЕСНДоходыИНалоги.НДФЛПредоставленныеВычеты КАК СтрокиДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСоответствиеОКАТОиОКТМО КАК СоответствиеОКАТОиОКТМО
	|		ПО СтрокиДокумента.КодПоОКАТО = СоответствиеОКАТОиОКТМО.КодПоОКАТО
	|ГДЕ
	|	СтрокиДокумента.Ссылка В
	|			(ВЫБРАТЬ
	|				РегистраторыКОбработке.Документ
	|			ИЗ
	|				ВТРегистраторыКОбработке КАК РегистраторыКОбработке)
	|	И СтрокиДокумента.КодПоОКТМО В(&ПустыеКодыПоОКАТО)
	|	И СоответствиеОКАТОиОКТМО.КодПоОКАТО ЕСТЬ НЕ NULL 
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДФЛиЕСНДоходыИНалогиНДФЛПредоставленныеВычеты.Ссылка,
	|	НДФЛиЕСНДоходыИНалогиНДФЛПредоставленныеВычеты.НомерСтроки,
	|	НДФЛиЕСНДоходыИНалогиНДФЛПредоставленныеВычеты.ФизЛицо,
	|	НДФЛиЕСНДоходыИНалогиНДФЛПредоставленныеВычеты.ПериодРегистрации,
	|	НДФЛиЕСНДоходыИНалогиНДФЛПредоставленныеВычеты.МесяцНалоговогоПериода,
	|	НДФЛиЕСНДоходыИНалогиНДФЛПредоставленныеВычеты.КодВычета,
	|	НДФЛиЕСНДоходыИНалогиНДФЛПредоставленныеВычеты.ПримененныйВычет,
	|	НДФЛиЕСНДоходыИНалогиНДФЛПредоставленныеВычеты.КодПоОКАТО,
	|	НДФЛиЕСНДоходыИНалогиНДФЛПредоставленныеВычеты.КПП,
	|	ВЫБОР
	|		КОГДА НДФЛиЕСНДоходыИНалогиНДФЛПредоставленныеВычеты.КодПоОКТМО В (&ПустыеКодыПоОКАТО)
	|			ТОГДА СоответствиеОКАТОиОКТМО.КодПоОКТМО
	|		ИНАЧЕ НДФЛиЕСНДоходыИНалогиНДФЛПредоставленныеВычеты.КодПоОКТМО
	|	КОНЕЦ КАК КодПоОКТМО
	|ИЗ
	|	Документ.НДФЛиЕСНДоходыИНалоги.НДФЛПредоставленныеВычеты КАК НДФЛиЕСНДоходыИНалогиНДФЛПредоставленныеВычеты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСоответствиеОКАТОиОКТМО КАК СоответствиеОКАТОиОКТМО
	|		ПО НДФЛиЕСНДоходыИНалогиНДФЛПредоставленныеВычеты.КодПоОКАТО = СоответствиеОКАТОиОКТМО.КодПоОКАТО
	|ГДЕ
	|	НДФЛиЕСНДоходыИНалогиНДФЛПредоставленныеВычеты.Ссылка В
	|			(ВЫБРАТЬ
	|				РегистраторыКОбработке.Документ
	|			ИЗ
	|				ВТРегистраторы КАК РегистраторыКОбработке)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НДФЛиЕСНДоходыИНалогиНДФЛПредоставленныеВычеты.Ссылка,
	|	НДФЛиЕСНДоходыИНалогиНДФЛПредоставленныеВычеты.НомерСтроки";
	ОбновитьТабличнуюЧастьОбъектаПоЗапросу(Запрос, "НДФЛПредоставленныеВычеты", "Документы корректировки", ИзмененныеДокументы, Форма);

	Запрос.Текст =
	"УНИЧТОЖИТЬ ВТРегистраторы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СтрокиДокумента.Ссылка КАК Документ
	|ПОМЕСТИТЬ ВТРегистраторы
	|ИЗ
	|	Документ.НДФЛиЕСНДоходыИНалоги.НДФЛУдержанный КАК СтрокиДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСоответствиеОКАТОиОКТМО КАК СоответствиеОКАТОиОКТМО
	|		ПО СтрокиДокумента.КодПоОКАТО = СоответствиеОКАТОиОКТМО.КодПоОКАТО
	|ГДЕ
	|	СтрокиДокумента.Ссылка В
	|			(ВЫБРАТЬ
	|				РегистраторыКОбработке.Документ
	|			ИЗ
	|				ВТРегистраторыКОбработке КАК РегистраторыКОбработке)
	|	И СтрокиДокумента.КодПоОКТМО В(&ПустыеКодыПоОКАТО)
	|	И СоответствиеОКАТОиОКТМО.КодПоОКАТО ЕСТЬ НЕ NULL 
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДФЛиЕСНДоходыИНалогиНДФЛУдержанный.Ссылка,
	|	НДФЛиЕСНДоходыИНалогиНДФЛУдержанный.НомерСтроки,
	|	НДФЛиЕСНДоходыИНалогиНДФЛУдержанный.ФизЛицо,
	|	НДФЛиЕСНДоходыИНалогиНДФЛУдержанный.ПериодРегистрации,
	|	НДФЛиЕСНДоходыИНалогиНДФЛУдержанный.МесяцНалоговогоПериода,
	|	НДФЛиЕСНДоходыИНалогиНДФЛУдержанный.СтавкаНалогообложения,
	|	НДФЛиЕСНДоходыИНалогиНДФЛУдержанный.Налог,
	|	НДФЛиЕСНДоходыИНалогиНДФЛУдержанный.КодПоОКАТО,
	|	НДФЛиЕСНДоходыИНалогиНДФЛУдержанный.КПП,
	|	НДФЛиЕСНДоходыИНалогиНДФЛУдержанный.КодДохода,
	|	ВЫБОР
	|		КОГДА НДФЛиЕСНДоходыИНалогиНДФЛУдержанный.КодПоОКТМО В(&ПустыеКодыПоОКАТО)
	|			ТОГДА СоответствиеОКАТОиОКТМО.КодПоОКТМО
	|		ИНАЧЕ НДФЛиЕСНДоходыИНалогиНДФЛУдержанный.КодПоОКТМО
	|	КОНЕЦ КАК КодПоОКТМО, *
	|ИЗ
	|	Документ.НДФЛиЕСНДоходыИНалоги.НДФЛУдержанный КАК НДФЛиЕСНДоходыИНалогиНДФЛУдержанный
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСоответствиеОКАТОиОКТМО КАК СоответствиеОКАТОиОКТМО
	|		ПО НДФЛиЕСНДоходыИНалогиНДФЛУдержанный.КодПоОКАТО = СоответствиеОКАТОиОКТМО.КодПоОКАТО
	|ГДЕ
	|	НДФЛиЕСНДоходыИНалогиНДФЛУдержанный.Ссылка В
	|			(ВЫБРАТЬ
	|				РегистраторыКОбработке.Документ
	|			ИЗ
	|				ВТРегистраторы КАК РегистраторыКОбработке)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НДФЛиЕСНДоходыИНалогиНДФЛУдержанный.Ссылка,
	|	НДФЛиЕСНДоходыИНалогиНДФЛУдержанный.НомерСтроки";
	ОбновитьТабличнуюЧастьОбъектаПоЗапросу(Запрос, "НДФЛУдержанный", "Документы корректировки", ИзмененныеДокументы, Форма);
	
	ТекстСообщения = "Запись документов";
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		ВсегоДоков = ИзмененныеДокументы.Количество();
		Номер = 1;
		ОбновитьИнформационнуюСтроку(Форма, ТекстСообщения + ": обработано 0 из " + ВсегоДоков);
	#КонецЕсли
	Для каждого Элемент Из ИзмененныеДокументы Цикл
		#Если ТолстыйКлиентОбычноеПриложение Тогда
			Если Номер % 30 = 0 Тогда
				ОбновитьИнформационнуюСтроку(Форма, ТекстСообщения + ": обработано " + Номер + " из " + ВсегоДоков);
			КонецЕсли;
			Номер = Номер + 1;
		#КонецЕсли
		Элемент.Значение.Записать();
	КонецЦикла;
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		ОбновитьИнформационнуюСтроку(Форма, "Обработка выбранных кодов ОКАТО завершена.");
	#КонецЕсли
	
КонецПроцедуры

#Если ТолстыйКлиентОбычноеПриложение Тогда

Процедура ОбновитьИнформационнуюСтроку(Форма, ТекстСообщения = "", ВажностьСообщения = "Информация")  Экспорт
	
	Если Форма = Неопределено Тогда
		Состояние(ТекстСообщения);
	Иначе
		
		ЭлементыФормы = Форма.ЭлементыФормы;
		
		ВидимостьСообщения = ЗначениеЗаполнено(ТекстСообщения); 
		ЭлементыФормы.НадписьИнформацияОРезультатах.Видимость = ВидимостьСообщения;
		ЭлементыФормы.ПолеКартинкиИнформацияОРезультатах.Видимость = ВидимостьСообщения;
		Если ВидимостьСообщения Тогда
			РаботаСДиалогамиЗК.ПоказатьИнформациюОДокументе(ЭлементыФормы.НадписьИнформацияОРезультатах, ЭлементыФормы.ПолеКартинкиИнформацияОРезультатах, ТекстСообщения, ВажностьСообщения);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Процедура ОбновитьОбъектыПоЗапросу(Запрос, ТекстСообщения, Форма = Неопределено) 

	Если Запрос = Неопределено Тогда
		Возврат
	КонецЕсли;
	
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		ОбновитьИнформационнуюСтроку(Форма, ТекстСообщения + ": подготовка данных ...");
	#КонецЕсли
	ВыборкаОбъектов = Запрос.Выполнить().Выбрать();
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		ВсегоДоков = ВыборкаОбъектов.Количество();
		Номер = 1;
		ОбновитьИнформационнуюСтроку(Форма, ТекстСообщения + ": обработано 0 из " + ВсегоДоков);
	#КонецЕсли
	Пока ВыборкаОбъектов.Следующий() Цикл
		Объект = ВыборкаОбъектов.Ссылка.ПолучитьОбъект();
		Объект.ОбменДанными.Загрузка = Истина;
		ЗаполнитьЗначенияСвойств(Объект,ВыборкаОбъектов);
		#Если ТолстыйКлиентОбычноеПриложение Тогда
			Если Номер % 30 = 0 Тогда
				ОбновитьИнформационнуюСтроку(Форма, ТекстСообщения + ": обработано " + Номер + " из " + ВсегоДоков);
			КонецЕсли;
			Номер = Номер + 1;
		#КонецЕсли
		Объект.Записать();
	КонецЦикла;
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		ОбновитьИнформационнуюСтроку(Форма, ТекстСообщения + ": обработано " + ВсегоДоков + " из " + ВсегоДоков);
	#КонецЕсли
	
КонецПроцедуры

// Переписывает реквизиты некоторых строк табличной части исправляемых объектов.
//
// Параметры: 
//  Запрос - исполняемый запрос, в котором обязательно присутствуют поля Ссылка и НомерСтроки
//			 остальные поля должны содержать исправленные данные, имена полей 
//           должны соответствовать именам исправляемых реквизитов;
//           кроме того, результат запроса должен быть упорядочен по полю Ссылка
//  ИмяТЧ - строка, имя табличной части объекта
//  ТекстСообщения - строка, начало сообщения, выводимого в строке состояния
//
// Возвращаемое значение:
//  Нет
//
Процедура ОбновитьТабличнуюЧастьОбъектаПоЗапросу(Запрос, ИмяТЧ, ТекстСообщения, ИзмененныеДокументы, Форма = Неопределено) 

	#Если ТолстыйКлиентОбычноеПриложение Тогда
		ОбновитьИнформационнуюСтроку(Форма, ТекстСообщения + ": подготовка данных ...");
	#КонецЕсли
	ВыборкаОбъектов = Запрос.Выполнить().Выбрать();
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		ВсегоДоков = ВыборкаОбъектов.Количество();
		Номер = 1;
		ОбновитьИнформационнуюСтроку(Форма, ТекстСообщения + ": обработано 0 из " + ВсегоДоков);
	#КонецЕсли

	Пока ВыборкаОбъектов.СледующийПоЗначениюПоля("Ссылка") Цикл
		
		Объект = ИзмененныеДокументы[ВыборкаОбъектов.Ссылка];
		Если Объект = Неопределено Тогда
			Объект = ВыборкаОбъектов.Ссылка.ПолучитьОбъект();
			Объект.ОбменДанными.Загрузка = Истина;
		    ИзмененныеДокументы.Вставить(ВыборкаОбъектов.Ссылка, Объект)
		КонецЕсли;
		
		Объект[ИмяТЧ].Очистить();
		Пока ВыборкаОбъектов.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(Объект[ИмяТЧ].Добавить(), ВыборкаОбъектов);
			#Если ТолстыйКлиентОбычноеПриложение Тогда
				Если Номер % 30 = 0 Тогда
					ОбновитьИнформационнуюСтроку(Форма, ТекстСообщения + ": обработано " + Номер + " из " + ВсегоДоков);
				КонецЕсли;
				Номер = Номер + 1;
			#КонецЕсли
		КонецЦикла;
		
	КонецЦикла;
	
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		ОбновитьИнформационнуюСтроку(Форма, ТекстСообщения + ": обработано " + ВсегоДоков + " из " + ВсегоДоков);
	#КонецЕсли
	
КонецПроцедуры

// Перезаполняет наборы записей регистраторов исправленными данными и записывает их.
//
// Параметры: 
//  МенеджерРегистра - Регистр<Сведений,...>Менеджер.<Имя> - менеджер переписываемого регистра
//  Запрос - исполняемый запрос, в котором обязательно присутствует поле Регистратор
//			 остальные поля должны соответствовать полям записи заполняемого регистра;
//			 кроме того, результат запроса должен быть упорядочен, 
//			 первое поле упорядочивания - Регистратор
//  ТекстЗаголовка - строка, начало сообщения, выводимого в строке состояния
//
// Возвращаемое значение:
//  Нет
//
Процедура ПереписатьНаборыЗаписейПоРегистраторам(МенеджерРегистра, Запрос, ТекстЗаголовка, Форма = Неопределено) 

	#Если ТолстыйКлиентОбычноеПриложение Тогда
		ТекстСообщения = ТекстЗаголовка;
		ОбновитьИнформационнуюСтроку(Форма, ТекстСообщения + ": подготовка данных ...");
	#КонецЕсли
	ВыборкаРегистраторов = Запрос.Выполнить().Выбрать();
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		ВсегоДоков = ВыборкаРегистраторов.Количество();
		Номер = 1;
		ОбновитьИнформационнуюСтроку(Форма, ТекстСообщения + ": обработано 0 из " + ВсегоДоков);
	#КонецЕсли
	Пока ВыборкаРегистраторов.СледующийПоЗначениюПоля("Регистратор") Цикл
		НаборЗаписей = МенеджерРегистра.СоздатьНаборЗаписей();
		НаборЗаписей.ОбменДанными.Загрузка = Истина;
		НаборЗаписей.Отбор.Регистратор.Установить(ВыборкаРегистраторов.Регистратор);
		Пока ВыборкаРегистраторов.Следующий() Цикл
		    ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(),ВыборкаРегистраторов);
			#Если ТолстыйКлиентОбычноеПриложение Тогда
				Если Номер % 30 = 0 Тогда
					ОбновитьИнформационнуюСтроку(Форма, ТекстСообщения + ": обработано " + Номер + " из " + ВсегоДоков);
				КонецЕсли;
				Номер = Номер + 1;
			#КонецЕсли
		КонецЦикла;
		НаборЗаписей.Записать();
	КонецЦикла;
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		ОбновитьИнформационнуюСтроку(Форма, ТекстСообщения + ": обработано " + ВсегоДоков + " из " + ВсегоДоков);
	#КонецЕсли
	
КонецПроцедуры

#Если ТолстыйКлиентОбычноеПриложение Тогда

Процедура ПроверитьОшибкуЗапрос(Запрос, ИнформацияОбОшибке) 
	                                                                        
	Если Не (ПолныеПрава.ИспользуетсяОграниченияПравДоступаНаУровнеЗаписей() И ПолныеПрава.ОшибкаДоступа(Запрос)) Тогда
		ОбщегоНазначенияЗК.ПоказатьДиалогСИнформациейОбОшибке(ИнформацияОбОшибке);
	КонецЕсли;
	
КонецПроцедуры

#КонецЕсли


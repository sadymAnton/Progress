
Процедура ПечатьКвитанции() Экспорт
	
	мВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_КвитанцияПриемаМолока";
	
	Макет = ПолучитьМакет("Макет");
	
	//Выведем шапку
	ОбластьПечати = Макет.ПолучитьОбласть("Шапка");
	ОбластьПечати.Параметры.Организация = СокрЛП(Организация.НаименованиеПолное);
	ОбластьПечати.Параметры.КодОрганизации = СокрЛП(Организация.Код);
	ОбластьПечати.Параметры.ГородОрганизация = "г. Липецк";	
	
	ОбластьПечати.Параметры.Поставщик = СокрЛП(Поставщик.НаименованиеПолное);
	ОбластьПечати.Параметры.КодПоставщика = СокрЛП(Поставщик.Код);
	ОбластьПечати.Параметры.ГородПоставщика = "";	
	
	ОбластьПечати.Параметры.ГодКвитанции = Формат(Год(ДатаКвитанции),"ЧГ=0");
	ОбластьПечати.Параметры.МесяцКвитанции = Месяц(ДатаКвитанции);
	ОбластьПечати.Параметры.ДеньКвитанции = День(ДатаКвитанции);
	  	
	ТабДокумент.Вывести(ОбластьПечати);
	
	//Выведем строки
	
	ТабНДС = Новый ТаблицаЗначений;
	ТабНДС.Колонки.Добавить("СтавкаНДС");
	ТабНДС.Колонки.Добавить("СуммаНДС");
	
	ОбластьПечати = Макет.ПолучитьОбласть("Строка");
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПоступлениеТоваровУслугТовары.Ссылка.Дата КАК Дата,
	               |	ПоступлениеТоваровУслугТовары.Номенклатура КАК Продукт,
	               |	ПоступлениеТоваровУслугТовары.Количество*ПоступлениеТоваровУслугТовары.Коэффициент КАК Количество,
	               |	ПоступлениеТоваровУслугТовары.Коэффициент,
	               |	ПоступлениеТоваровУслугТовары.ЕдиницаИзмерения,
	               |	ПоступлениеТоваровУслугТовары.Цена КАК Цена,
	               |	ПоступлениеТоваровУслугТовары.Сумма КАК Сумма,
	               |	ПоступлениеТоваровУслугТовары.СтавкаНДС КАК СтавкаНДС,
	               |	ПоступлениеТоваровУслугТовары.СуммаНДС КАК СуммаНДС,
	               |	ПоступлениеТоваровУслугТовары.Ссылка.ВалютаДокумента,
	               |	ПоступлениеТоваровУслугТовары.Ссылка.КратностьВзаиморасчетов,
	               |	ПоступлениеТоваровУслугТовары.Ссылка.КурсВзаиморасчетов,
	               |	ЕСТЬNULL(СерииНоменклатуры.НСИ_МДЖ, 0) КАК Жир,
	               |	ЕСТЬNULL(СерииНоменклатуры.НСИ_МДБ, 0) КАК Белок,
	               |	ПоступлениеТоваровУслугТовары.Ссылка
	               |ИЗ
	               |	Документ.ПоступлениеТоваровУслуг.Товары КАК ПоступлениеТоваровУслугТовары
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СерииНоменклатуры КАК СерииНоменклатуры
	               |		ПО ПоступлениеТоваровУслугТовары.СерияНоменклатуры = СерииНоменклатуры.Ссылка
	               |ГДЕ
	               |	ПоступлениеТоваровУслугТовары.Ссылка В(&СПНакладные)
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Дата";
	
	Запрос.УстановитьПараметр("СПНакладные", Накладные.ВыгрузитьКолонку("ПоступлениеТоваров"));
	
	ИтогоСуммаСНДС = 0;
	ИтогоКоличество = 0;
	НомерПП = 0;
	Результат = Запрос.Выполнить().Выбрать();
	Пока Результат.Следующий() Цикл
		
		НомерПП = НомерПП + 1;
		
		ОбластьПечати.Параметры.НомерПП = НомерПП;
		ОбластьПечати.Параметры.Заполнить(Результат);
		
		ОбластьПечати.Параметры.Продукт = СокрЛП(Результат.Продукт.Код) + " " + СокрЛП(Результат.Продукт);
		
		Сумма = Результат.Сумма;
		СуммаНДС = Результат.СуммаНДС;
		Если НЕ Результат.Ссылка.СуммаВключаетНДС Тогда
			Сумма = Сумма + СуммаНДС;
		КонецЕсли;
		Цена = Окр(Результат.Сумма/Результат.Количество,2);
		
		ОбластьПечати.Параметры.Сумма = Сумма;
		ОбластьПечати.Параметры.Цена  = Цена;
		
		ТабДокумент.Вывести(ОбластьПечати);	
		
		СтрокаНДС = ТабНДС.Добавить();
		СтрокаНДС.СтавкаНДС = Результат.СтавкаНДС;
		СтрокаНДС.СуммаНДС  = СуммаНДС;
		
		ИтогоСуммаСНДС = ИтогоСуммаСНДС + Сумма;
		ИтогоКоличество = ИтогоКоличество + Результат.Количество;
		
		Если Результат.Дата < ДатаНачала Тогда
			ДатаНачала = Результат.Дата;
		ИначеЕсли Результат.Дата > ДатаОкончания Тогда
			ДатаОкончания = Результат.Дата;
		КонецЕсли;  		
		
	КонецЦикла;
	
	ТабНДС.Свернуть("СтавкаНДС", "СуммаНДС");
	
	//Выведем итого
	ОбластьПечати = Макет.ПолучитьОбласть("Итого");
	
	ОбластьПечати.Параметры.Количество = ИтогоКоличество;
	ОбластьПечати.Параметры.Сумма = ИтогоСуммаСНДС;
	
	ТабДокумент.Вывести(ОбластьПечати);
	
	СуммаПрописью = ОбщегоНазначения.СформироватьСуммуПрописью(ИтогоСуммаСНДС, мВалютаРегламентированногоУчета);
		
	//Выведем подвал
	ОбластьПечати = Макет.ПолучитьОбласть("Подвал");
	
	ОбластьПечати.Параметры.ПериодПоставки = " с " + Формат(ДатаНачала,"ДФ=dd.MM.yyyy") + " по " + Формат(ДатаОкончания,"ДФ=dd.MM.yyyy");
	ОбластьПечати.Параметры.СуммаПрописью = СуммаПрописью;
	ОбластьПечати.Параметры.Сумма = ИтогоСуммаСНДС;
	//ОбластьПечати.Параметры.СуммаНДС = ТабНДС.Итог("СуммаНДС");
	
	ТабДокумент.Вывести(ОбластьПечати);
	
	//Выведем таб НДС
	
	ОбластьПечати = Макет.ПолучитьОбласть("НДС");
	Для каждого СтрокаНДС Из ТабНДС Цикл
	
		ОбластьПечати.Параметры.СуммаНДСПрописью = СокрЛП(СтрокаНДС.СтавкаНДС) + " " + ОбщегоНазначения.СформироватьСуммуПрописью(СтрокаНДС.СуммаНДС, мВалютаРегламентированногоУчета);
		ОбластьПечати.Параметры.СуммаНДС = СтрокаНДС.СуммаНДС;
		ТабДокумент.Вывести(ОбластьПечати);
	КонецЦикла; 	
	
	//Выведем подписи
	ОбластьПечати = Макет.ПолучитьОбласть("Подписи");
	ОбластьПечати.Параметры.СуммаПрописью = СуммаПрописью;
	ТабДокумент.Вывести(ОбластьПечати);
	
	ТабДокумент.АвтоМасштаб			 = Истина;
	ТабДокумент.ОриентацияСтраницы	 = ОриентацияСтраницы.Портрет;

	УниверсальныеМеханизмы.НапечататьДокумент(ТабДокумент, 1, Ложь, "Печать квитанции о приеме молока");
	
КонецПроцедуры
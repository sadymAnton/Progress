Перем мПериод Экспорт; 
Перем мТаблицаДвижений Экспорт;

Процедура ВыполнитьПриход() Экспорт
	
	мТаблицаДвижений.ЗаполнитьЗначения( мПериод, "Период");
	мТаблицаДвижений.ЗаполнитьЗначения( Истина,  "Активность");
	мТаблицаДвижений.ЗаполнитьЗначения( ВидДвиженияНакопления.Приход, "ВидДвижения");

	Загрузить(мТаблицаДвижений);
	
КонецПроцедуры // ВыполнитьДвижение()

Процедура ВыполнитьРасход() Экспорт
	
	мТаблицаДвижений.ЗаполнитьЗначения( мПериод, "Период");
	мТаблицаДвижений.ЗаполнитьЗначения( Истина,  "Активность");
	мТаблицаДвижений.ЗаполнитьЗначения( ВидДвиженияНакопления.Расход, "ВидДвижения");

	Загрузить(мТаблицаДвижений);
	
КонецПроцедуры // ВыполнитьДвижение()

// Процедура контролирует остаток по данному регистру по переданному документу
// и его табличной части. В случае недостатка товаров выставляется флаг отказа и 
// выдается сообщение.
//
// Параметры:
//  ДокументОбъект    - объект проводимого документа, 
//  ИмяТабличнойЧасти - строка, имя табличной части, которая проводится по регистру, 
//  СтруктураШапкиДокумента - структура, содержащая значения "через точку" ссылочных реквизитов по шапке документа,
//  Отказ             - флаг отказа в проведении,
//  Заголовок         - строка, заголовок сообщения об ошибке проведения.
//
Процедура КонтрольОстатков(ДокументОбъект, ИмяТабличнойЧасти, СтруктураШапкиДокумента, Отказ, Заголовок, РежимПроведения) Экспорт

	Если РежимПроведения <> РежимПроведенияДокумента.Оперативный Тогда
		Возврат;
	КонецЕсли; 
	
	МетаданныеДокумента = ДокументОбъект.Метаданные();
	ИмяДокумента        = МетаданныеДокумента.Имя;
	ИмяТаблицы          = ИмяДокумента + "." + СокрЛП(ИмяТабличнойЧасти);
	
	// Текст вложенного запроса, ограничивающего номенклатуру при получении остатков
	ТекстЗапросаСписокНоменклатуры = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Номенклатура 
	|ИЗ
	|	Документ." + ИмяТаблицы +"
	|ГДЕ Ссылка = &ДокументСсылка";
	
	Запрос = Новый Запрос;

	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка", СтруктураШапкиДокумента.Ссылка);
	Запрос.УстановитьПараметр("Подразделение",  СтруктураШапкиДокумента.Подразделение);
	
	Запрос.Текст = "
	|ВЫБРАТЬ // Запрос, контролирующий остатки в производстве
	|	Док.Номенклатура.Представление                         КАК НоменклатураПредставление,
	|	Док.Номенклатура.ЕдиницаХраненияОстатков.Представление КАК ЕдиницаХраненияОстатковПредставление,
	|	Док.ХарактеристикаНоменклатуры				           КАК ХарактеристикаНоменклатуры,
	|	Док.СерияНоменклатуры				           		   КАК СерияНоменклатуры,
	|	Док.ФизЛицо					           		   		   КАК ФизЛицо,
	|	СУММА(Док.Количество * Док.Коэффициент /Док.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент) КАК ДокументКоличество,
	|	ЕСТЬNULL(МАКСИМУМ(Остатки.КоличествоОстаток), 0)       КАК ОстатокКоличество
	|ИЗ 
	|	Документ." + ИмяТаблицы + " КАК Док
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.МатериалыВЭксплуатации.Остатки(,
	|			Номенклатура В (" + ТекстЗапросаСписокНоменклатуры + ")
	|			И Подразделение = &Подразделение
	|			) КАК Остатки
	|	ПО 
	|		Док.Номенклатура                  = Остатки.Номенклатура
	| 		И Док.ХарактеристикаНоменклатуры  = Остатки.ХарактеристикаНоменклатуры
	| 		И Док.СерияНоменклатуры  		  = Остатки.СерияНоменклатуры
	| 		И Док.ФизЛицо  		  			  = Остатки.ФизЛицо
	| 		И Док.Ссылка.Подразделение 		  = Остатки.Подразделение
	|
	|ГДЕ
	|	Док.Ссылка  =  &ДокументСсылка
	|
	|СГРУППИРОВАТЬ ПО
	|
	|	Док.Номенклатура,
	|	Док.ХарактеристикаНоменклатуры,
	|	Док.СерияНоменклатуры,
	|	Док.ФизЛицо
	|
	|ИМЕЮЩИЕ ЕСТЬNULL(МАКСИМУМ(Остатки.КоличествоОстаток), 0) < СУММА(Док.Количество * Док.Коэффициент /Док.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент)
	|
	|ДЛЯ ИЗМЕНЕНИЯ РегистрНакопления.МатериалыВЭксплуатации.Остатки // Блокирующие чтение таблицы остатков регистра для разрешения коллизий многопользовательской работы
	|";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл

		СтрокаСообщения = "Остатка " + 
		УправлениеЗапасами.ПредставлениеНоменклатуры(Выборка.НоменклатураПредставление, 
								  Выборка.ХарактеристикаНоменклатуры,
								  Выборка.СерияНоменклатуры) +
		", у работника " + (Выборка.ФизЛицо) + " недостаточно.";

		УправлениеЗапасами.ОшибкаНетОстатка(СтрокаСообщения, Выборка.ОстатокКоличество, Выборка.ДокументКоличество,
		Выборка.ЕдиницаХраненияОстатковПредставление, Отказ, Заголовок);

	КонецЦикла;

КонецПроцедуры // КонтрольОстатков()

//начало изменений 
Процедура КонтрольОстатковПРГ(ДокументОбъект, ИмяТабличнойЧасти, СтруктураШапкиДокумента, Отказ, Заголовок, РежимПроведения) Экспорт

	//Если РежимПроведения <> РежимПроведенияДокумента.Оперативный Тогда
	//	Возврат;
	//КонецЕсли; 
	Если УправлениеДопПравамиПользователей.РазрешеноПревышениеОстаткаТоваровНаСкладе()
		или СтруктураШапкиДокумента.Дата >= '20140301'
		Тогда
		Возврат;
	КонецЕсли;
	
	
	
	МетаданныеДокумента = ДокументОбъект.Метаданные();
	ИмяДокумента        = МетаданныеДокумента.Имя;
	ИмяТаблицы          = ИмяДокумента + "." + СокрЛП(ИмяТабличнойЧасти);
	
	// Текст вложенного запроса, ограничивающего номенклатуру при получении остатков
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	МатВЭэкспл.Подразделение,
	                      |	МатВЭэкспл.Номенклатура,
	                      |	МатВЭэкспл.ХарактеристикаНоменклатуры,
	                      |	МатВЭэкспл.СерияНоменклатуры,
	                      |	МатВЭэкспл.ФизЛицо,
	                      |	МатВЭэкспл.НазначениеИспользования,
	                      |	МатВЭэкспл.ДокументПередачи,
	                      |	СУММА(МатВЭэкспл.Количество) КАК Количество
	                      |ПОМЕСТИТЬ МатВЭэкспл
	                      |ИЗ
	                      |	РегистрНакопления.МатериалыВЭксплуатации КАК МатВЭэкспл
	                      |ГДЕ
	                      |	МатВЭэкспл.Регистратор = &Ссылка
	                      |	И МатВЭэкспл.ВидДвижения = &ВидДвижения
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	МатВЭэкспл.ДокументПередачи,
	                      |	МатВЭэкспл.НазначениеИспользования,
	                      |	МатВЭэкспл.ФизЛицо,
	                      |	МатВЭэкспл.ХарактеристикаНоменклатуры,
	                      |	МатВЭэкспл.Подразделение,
	                      |	МатВЭэкспл.Номенклатура,
	                      |	МатВЭэкспл.СерияНоменклатуры
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	МатВЭэкспл.Подразделение,
	                      |	ПРЕДСТАВЛЕНИЕ(МатВЭэкспл.Подразделение),
	                      |	МатВЭэкспл.Номенклатура,
	                      |	ПРЕДСТАВЛЕНИЕ(МатВЭэкспл.Номенклатура),
	                      |	МатВЭэкспл.ХарактеристикаНоменклатуры,
	                      |	ПРЕДСТАВЛЕНИЕ(МатВЭэкспл.ХарактеристикаНоменклатуры),
	                      |	МатВЭэкспл.СерияНоменклатуры,
	                      |	ПРЕДСТАВЛЕНИЕ(МатВЭэкспл.СерияНоменклатуры),
	                      |	МатВЭэкспл.ФизЛицо,
	                      |	ПРЕДСТАВЛЕНИЕ(МатВЭэкспл.ФизЛицо),
	                      |	МатВЭэкспл.НазначениеИспользования,
	                      |	ПРЕДСТАВЛЕНИЕ(МатВЭэкспл.НазначениеИспользования),
	                      |	МатВЭэкспл.ДокументПередачи,
	                      |	ПРЕДСТАВЛЕНИЕ(МатВЭэкспл.ДокументПередачи),
	                      |	МатВЭэкспл.Количество КАК ДокументКоличество,
	                      |	ЕСТЬNULL(МатериалыВЭксплуатацииОстатки.КоличествоОстаток, 0) + ЕСТЬNULL(МатВЭэкспл.Количество, 0) КАК ОстатокКоличество,
	                      |	МатериалыВЭксплуатацииОстатки.Номенклатура.ЕдиницаХраненияОстатков,
	                      |	ПРЕДСТАВЛЕНИЕ(МатериалыВЭксплуатацииОстатки.Номенклатура.ЕдиницаХраненияОстатков)
	                      |ИЗ
	                      |	МатВЭэкспл КАК МатВЭэкспл
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.МатериалыВЭксплуатации.Остатки(
	                      |				&Датаостатков,
	                      |				(Подразделение, Номенклатура, ХарактеристикаНоменклатуры, СерияНоменклатуры, ФизЛицо, НазначениеИспользования, ДокументПередачи) В
	                      |					(ВЫБРАТЬ
	                      |						МатВЭэкспл.Подразделение,
	                      |						МатВЭэкспл.Номенклатура,
	                      |						МатВЭэкспл.ХарактеристикаНоменклатуры,
	                      |						МатВЭэкспл.СерияНоменклатуры,
	                      |						МатВЭэкспл.ФизЛицо,
	                      |						МатВЭэкспл.НазначениеИспользования,
	                      |						МатВЭэкспл.ДокументПередачи
	                      |					ИЗ
	                      |						МатВЭэкспл)) КАК МатериалыВЭксплуатацииОстатки
	                      |		ПО МатВЭэкспл.Подразделение = МатериалыВЭксплуатацииОстатки.Подразделение
	                      |			И МатВЭэкспл.Номенклатура = МатериалыВЭксплуатацииОстатки.Номенклатура
	                      |			И МатВЭэкспл.ХарактеристикаНоменклатуры = МатериалыВЭксплуатацииОстатки.ХарактеристикаНоменклатуры
	                      |			И МатВЭэкспл.СерияНоменклатуры = МатериалыВЭксплуатацииОстатки.СерияНоменклатуры
	                      |			И МатВЭэкспл.ФизЛицо = МатериалыВЭксплуатацииОстатки.ФизЛицо
	                      |			И МатВЭэкспл.НазначениеИспользования = МатериалыВЭксплуатацииОстатки.НазначениеИспользования
	                      |			И МатВЭэкспл.ДокументПередачи = МатериалыВЭксплуатацииОстатки.ДокументПередачи
	                      |ГДЕ
	                      |	ЕСТЬNULL(МатериалыВЭксплуатацииОстатки.КоличествоОстаток, 0) < 0");
						  
	Запрос.УстановитьПараметр("Ссылка", СтруктураШапкиДокумента.Ссылка);
	Запрос.УстановитьПараметр("ВидДвижения", ВидДвиженияНакопления.Расход);
	Если РежимПроведения <> РежимПроведенияДокумента.Оперативный Тогда
		Запрос.УстановитьПараметр("Датаостатков",Новый Граница(Новый МоментВремени(СтруктураШапкиДокумента.Дата, СтруктураШапкиДокумента.Ссылка),ВидГраницы.Включая));
	Иначе
		Запрос.УстановитьПараметр("Датаостатков",'39990101 00:00:00');
	КонецЕсли;
						  
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл

		СтрокаСообщения = "Остатка " + 
		УправлениеЗапасами.ПредставлениеНоменклатуры(Выборка.НоменклатураПредставление, 
								  Выборка.ХарактеристикаНоменклатуры,
								  Выборка.СерияНоменклатуры) +"
		|, у работника " + (Выборка.ФизЛицоПредставление) +"
		|, в подразделении " + (Выборка.ПодразделениеПредставление) + "
		|, по документу передачи " + (Выборка.ДокументПередачиПредставление)+ "
		|, назначению использования " + (Выборка.НазначениеИспользованияПредставление) +"
		| недостаточно.";

		УправлениеЗапасами.ОшибкаНетОстатка(СтрокаСообщения, Выборка.ОстатокКоличество, Выборка.ДокументКоличество,
		Выборка.НоменклатураЕдиницаХраненияОстатковПредставление, Отказ, Заголовок);

	КонецЦикла;

КонецПроцедуры // КонтрольОстатков ПРГ()
//конец изменений 
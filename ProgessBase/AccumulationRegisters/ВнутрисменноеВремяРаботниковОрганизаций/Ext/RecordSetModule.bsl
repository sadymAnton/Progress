Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
		
	ЗарегистрироватьПерерасчеты();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтотОбъект.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ЗарегистрироватьПерерасчеты();	
	
КонецПроцедуры

Процедура ЗарегистрироватьПерерасчеты()
	
	Запрос = Новый Запрос;
	МенеджерВТ = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	
	Регистратор = Отбор.Регистратор.Значение;
	Запрос.УстановитьПараметр("Регистратор", Регистратор);

	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВнутрисменноеВремя.Сотрудник КАК Сотрудник,
	|	НАЧАЛОПЕРИОДА(ВнутрисменноеВремя.Период, МЕСЯЦ) КАК ПериодДействия
	|ПОМЕСТИТЬ ВТ_СотрудникиПериодыДействия
	|ИЗ
	|	РегистрНакопления.ВнутрисменноеВремяРаботниковОрганизаций КАК ВнутрисменноеВремя
	|ГДЕ
	|	ВнутрисменноеВремя.Регистратор = &Регистратор
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник,
	|	ПериодДействия";
	Результат = Запрос.Выполнить().Выгрузить();
	
	Если Результат[0].Количество = 0 Тогда
		Возврат;
	КонецЕсли;	
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НачисленияФПД.Сотрудник КАК Сотрудник,
	|	НачисленияФПД.ПериодДействияНачало КАК ПериодДействияНачало,
	|	НачисленияФПД.ПериодДействияКонец КАК ПериодДействияКонец,
	|	НачисленияФПД.Регистратор,
	|	НачисленияФПД.Организация,
	|	НачисленияФПД.ФизЛицо
	|ПОМЕСТИТЬ ВТ_НачисленияФПД
	|ИЗ
	|	РегистрРасчета.ОсновныеНачисленияРаботниковОрганизаций.ФактическийПериодДействия(
	|			Регистратор <> &Регистратор
	|				И ВидУчетаВремени = ЗНАЧЕНИЕ(Перечисление.ВидыУчетаВремени.ПоЧасам)
	|				И (Сотрудник, ПериодДействия) В
	|					(ВЫБРАТЬ
	|						ОсновныеНачисления.Сотрудник,
	|						ОсновныеНачисления.ПериодДействия
	|					ИЗ
	|						ВТ_СотрудникиПериодыДействия КАК ОсновныеНачисления)) КАК НачисленияФПД";
	Запрос.Выполнить();
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Основные.ФизЛицо,
	|	Основные.Регистратор КАК Регистратор,
	|	Основные.Организация,
	|	Основные.Сотрудник
	|ИЗ
	|	РегистрНакопления.ВнутрисменноеВремяРаботниковОрганизаций КАК ВнутрисменноеВремя
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_НачисленияФПД КАК Основные
	|		ПО ВнутрисменноеВремя.Период >= Основные.ПериодДействияНачало
	|			И ВнутрисменноеВремя.Период <= Основные.ПериодДействияКонец
	|			И ВнутрисменноеВремя.Сотрудник = Основные.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ОсновныеНачисленияРаботниковОрганизаций.ПерерасчетОсновныхНачислений КАК Перерасчеты
	|		ПО (Перерасчеты.ОбъектПерерасчета = Основные.Регистратор)
	|			И (Перерасчеты.ФизЛицо = Основные.ФизЛицо)
	|			И (Перерасчеты.ВидРасчета = ЗНАЧЕНИЕ(ПланВидовРасчета.ОсновныеНачисленияОрганизаций.ПустаяСсылка))
	|ГДЕ
	|	ВнутрисменноеВремя.Регистратор = &Регистратор
	|	И Перерасчеты.ФизЛицо ЕСТЬ NULL 
	|	И Основные.Регистратор ЕСТЬ НЕ NULL 
	|
	|УПОРЯДОЧИТЬ ПО
	|	Регистратор";

	Выборка = Запрос.Выполнить().Выбрать();
	ПроведениеРасчетов.ДописатьПерерасчетыОсновныхНачислений(Выборка);
	
КонецПроцедуры


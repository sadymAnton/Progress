Перем мПериод          Экспорт; // Период движений
Перем мТаблицаДвижений Экспорт; // Таблица движений

// Выполняет приход по регистру.
//
// Параметры:
//  Нет.
//
Процедура ВыполнитьПриход() Экспорт

	ОбщегоНазначения.ВыполнитьДвижениеПоРегистру(ЭтотОбъект, ВидДвиженияНакопления.Приход);

КонецПроцедуры // ВыполнитьПриход()

// Выполняет расход по регистру.
//
// Параметры:
//  Нет.
//
Процедура ВыполнитьРасход() Экспорт

	ОбщегоНазначения.ВыполнитьДвижениеПоРегистру(ЭтотОбъект, ВидДвиженияНакопления.Расход);

КонецПроцедуры // ВыполнитьРасход()

// Выполняет движения по регистру.
//
// Параметры:
//  Нет.
//
Процедура ВыполнитьДвижения() Экспорт

	Загрузить(мТаблицаДвижений);

КонецПроцедуры // ВыполнитьДвижения()

// Процедура контролирует остаток по данному регистру по переданному документу
// и его табличной части. В случае недостатка товаров выставляется флаг отказа и 
// выдается сообщегние.
//
// Параметры:
//  ДокументОбъект    - объект проводимого документа, 
//  ИмяТабличнойЧасти - строка, имя табличной части, которая проводится по регистру, 
//  СтруктураШапкиДокумента - структура, содержащая значения "через точку" ссылочных реквизитов по шапке документа,
//  Отказ             - флаг отказа в проведении,
//  Заголовок         - строка, заголовок сообщения об ошибке проведения.
//
Процедура КонтрольОстатков(ДокументОбъект, ИмяТабличнойЧасти, СтруктураШапкиДокумента, Отказ, Заголовок, РежимПроведения) Экспорт
	Перем ТипРеализацияОтгруженныхТоваров;
	
	//m.ionov@a-prof.ru 21.05.2014
	Если ДокументОбъект.Проведен и ПараметрыСеанса.ПроведениеДокументов Тогда
		Возврат;
	КонецЕсли;
	//----m.ionov@a-prof.ru---
	
	//начало изменений БП 04 
	Если // РежимПроведения <> РежимПроведенияДокумента.Оперативный
		//ИЛИ 
		
		УправлениеДопПравамиПользователей.РазрешеноПревышениеОстаткаТоваровОрганизации(СтруктураШапкиДокумента.Организация) Тогда
		
		Возврат;
	КонецЕсли;
	//конец изменений БП 04 
	
	//пока отключим весь контроль 
	//помотрим как будут развиваться события
	возврат;
	//начало изменений пока уберем контроль остатокв для оперативного проведения. 
	//Посмотрим как будут развиваться события, впоследствие отключим наверное для всех
	Если РежимПроведения <> РежимПроведенияДокумента.Оперативный Тогда
		Если РольДоступна("АП_ФормированиеПеремещений") или РольДоступна("АП_ФормированиеОтгрузочныхДокументов")  Тогда
			 возврат;
		КонецЕсли;	
	КонецЕсли;	
	//конец изменений
	
	 //начало изменений БП 04
	 ИмяТабЧастиСклада = "Склад";
	 //конец изменений БП 04 
	
	
	МетаданныеДокумента = ДокументОбъект.Метаданные();
	ИмяДокумента        = МетаданныеДокумента.Имя;
	ИмяТаблицы          = ИмяДокумента + "." + СокрЛП(ИмяТабличнойЧасти);
	ЭтоРеализацияОтгруженныхТоваров = (ТипЗнч(СтруктураШапкиДокумента.Ссылка) = Тип("ДокументСсылка.РеализацияОтгруженныхТоваров"));
	Если ЭтоРеализацияОтгруженныхТоваров Тогда
		ИмяТаблицы          = "РеализацияТоваровУслуг." + СокрЛП(ИмяТабличнойЧасти);
		МетаданныеДокумента = ДокументОбъект.ДокументОтгрузки.Метаданные();
		ИмяДокумента        = "РеализацияТоваровУслуг";
	КонецЕсли;
	
	
	
	Если ТипЗнч(СтруктураШапкиДокумента.Ссылка) = Тип("ДокументСсылка.КорректировкаСерийИХарактеристикТоваров") Тогда
		ТекстХарактеристикаНоменклатуры = "ХарактеристикаНоменклатурыСтарая";
		ТекстСерияНоменклатуры = "СерияНоменклатурыСтарая";
	Иначе
		ТекстХарактеристикаНоменклатуры = "ХарактеристикаНоменклатуры";
		ТекстСерияНоменклатуры = "СерияНоменклатуры";
	КонецЕсли; 

	ТекстКачество = "Качество";
                                                          
	ЕстьСерия             = ОбщегоНазначения.ЕстьРеквизитТабЧастиДокумента(ТекстСерияНоменклатуры, МетаданныеДокумента, ИмяТабличнойЧасти);
	ЕстьХарактеристика    = ОбщегоНазначения.ЕстьРеквизитТабЧастиДокумента(ТекстХарактеристикаНоменклатуры, МетаданныеДокумента, ИмяТабличнойЧасти);
	ЕстьДокументРезерва   = ОбщегоНазначения.ЕстьРеквизитТабЧастиДокумента("ДокументРезерва", МетаданныеДокумента, ИмяТабличнойЧасти);
	ЕстьДокументПередачи  = (МетаданныеДокумента.Реквизиты.Найти("ДокументПередачи") <> Неопределено);
	ЕстьКачество          = ОбщегоНазначения.ЕстьРеквизитТабЧастиДокумента(ТекстКачество, МетаданныеДокумента, ИмяТабличнойЧасти);
	ЕстьФлагУказанияСерий = ОбщегоНазначения.ЕстьРеквизитТабЧастиДокумента("СерияУказываетсяПриОтпускеСоСклада", МетаданныеДокумента, ИмяТабличнойЧасти);
	ЕстьКоэффициент       = ОбщегоНазначения.ЕстьРеквизитТабЧастиДокумента("Коэффициент", МетаданныеДокумента, ИмяТабличнойЧасти);
	ЕстьКлючСтроки         = ОбщегоНазначения.ЕстьРеквизитТабЧастиДокумента("КлючСтроки", МетаданныеДокумента, ИмяТабличнойЧасти);
	ЕстьСкладВТабЧасти     = ОбщегоНазначения.ЕстьРеквизитТабЧастиДокумента("Склад", МетаданныеДокумента, ИмяТабличнойЧасти);
	ЕстьСоставНабора       = Ложь;
	
	//начало изменений БП 04
	Если  (ТипЗнч(СтруктураШапкиДокумента.Ссылка) = Тип("ДокументСсылка.ПеремещениеТоваров")) Тогда
		ЕстьСкладВТабЧасти = Истина;
		ИмяТабЧастиСклада = "СкладОтправитель";
	КонецЕсли;	
		
	//конец изменений БП 04 

	Если ЕстьКлючСтроки Тогда
		Если ЭтоРеализацияОтгруженныхТоваров Тогда
			ЕстьСоставНабора = МетаданныеДокумента.ТабличныеЧасти.Найти("СоставНабора") <> Неопределено И ДокументОбъект.ДокументОтгрузки.СоставНабора.Количество()>0
		Иначе	
			Если МетаданныеДокумента.ТабличныеЧасти.Найти("СоставНабора") <> Неопределено
			   И ДокументОбъект.СоставНабора.Количество() > 0 Тогда
				ЕстьСоставНабора = Истина;
			КонецЕсли;
		КонецЕсли;	
	КонецЕсли;
	
	СтруктураУП = ОбщегоНазначения.ПолучитьПараметрыУчетнойПолитикиУпр(СтруктураШапкиДокумента.Ссылка.Дата);
	Если НЕ ЗначениеЗаполнено(СтруктураУП) Тогда
		Отказ = Истина;
		КонтролироватьОстатокПоСкладу = ложь;
	Иначе
		КонтролироватьОстатокПоСкладу = СтруктураУП.ВестиУчетТоваровОрганизацийВРазрезеСкладов И НЕ ЭтоРеализацияОтгруженныхТоваров;
	КонецЕсли;
	
	//начало изменений БП 14 
	ВидНоменклатурыВода = Константы.ПРГ_ВидНоменклатурыВода.Получить();
	НеУчитыватьВоду 	= ЗначениеЗаполнено(ВидНоменклатурыВода);
	//конец изменений БП 14 
	
	// Текст вложенного запроса, ограничивающего номенклатуру при получении остатков
	Если ЕстьСоставНабора Тогда
		ТекстЗапросаСписокНоменклатуры = "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Док.Номенклатура КАК Номенклатура
		|ИЗ
		|	(
		|	ВЫБРАТЬ
		|		Док.Номенклатура
		|	ИЗ
		|		Документ." + ИмяТаблицы +" КАК Док
		|	ГДЕ
		|		Док.Ссылка = &ДокументСсылка
		|		И НЕ Док.Номенклатура.Комплект
		|		И Не Док.Номенклатура.Услуга
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		Док.Номенклатура
		|	ИЗ
		|		Документ." + ИмяДокумента + ".СоставНабора КАК Док
		|	ГДЕ
		|		Док.Ссылка = &ДокументСсылка
		|		И НЕ Док.Номенклатура.Комплект
		|		И Не Док.Номенклатура.Услуга
		|
		|	) КАК Док
		|";

		ТекстЗапросаРеквизитыДокумента = "
		|	ВЫБРАТЬ
		|			Док.Ссылка,
		|			Док.Номенклатура,
		|		" + ?(ЕстьКачество,        "Док.Качество,",                     "") + "
		|		" + ?(ЕстьХарактеристика,  "Док.ХарактеристикаНоменклатуры,",   "") + "
		|		" + ?(ЕстьСерия,           "Док.СерияНоменклатуры,",            "") + "
		|		" + ?(ЕстьКоэффициент,     "Док.Коэффициент,",                  "") + "
		|		" + ?(ЕстьДокументРезерва, "Док.ДокументРезерва,",              "") + "
		|		" + ?(ЕстьСкладВТабЧасти, "Док."+ИмяТабЧастиСклада+" как Cклад,",              "") + "
		|			Док.Количество
		|	ПОМЕСТИТЬ Док
		|		ИЗ
		|			Документ." + ИмяТаблицы + " КАК Док
		|		ГДЕ
		|			Док.Ссылка = &ДокументСсылка
		|			И НЕ Док.Номенклатура.Комплект
		|			И Не Док.Номенклатура.Услуга
		|		ОБЪЕДИНИТЬ ВСЕ
		|
		|		ВЫБРАТЬ
		|			Док.Ссылка,
		|			Док.Номенклатура,
		|		" + ?(ЕстьКачество,        "Док.Качество,",                     "") + "
		|		" + ?(ЕстьХарактеристика,  "Док.ХарактеристикаНоменклатуры,",   "") + "
		|		" + ?(ЕстьСерия,           "Док.СерияНоменклатуры,",            "") + "
		|		" + ?(ЕстьКоэффициент,     "Док.Коэффициент,",                  "") + "
		|		" + ?(ЕстьДокументРезерва, "Док.ДокументРезерва,",              "") + "
		|		" + ?(ЕстьСкладВТабЧасти, "Док."+ИмяТабЧастиСклада+" как Cклад,",              "") + "
		|			Док.Количество
		|		ИЗ
		|			(ВЫБРАТЬ
		|				ДокНаб.Ссылка,
		|				ДокНаб.Номенклатура,
		|";
		Если ЕстьКачество Тогда
		ТекстЗапросаРеквизитыДокумента = ТекстЗапросаРеквизитыДокумента + "
		|				ВЫБОР
		|					КОГДА ДокНаб.Качество = &ПустоеКачество ТОГДА ДокТов.Качество
		|					ИНАЧЕ ДокНаб.Качество
		|				КОНЕЦ КАК Качество,
		|";
		КонецЕсли;
		ТекстЗапросаРеквизитыДокумента = ТекстЗапросаРеквизитыДокумента + "
		|			" + ?(ЕстьХарактеристика,  "ДокНаб.ХарактеристикаНоменклатуры,",                   "") + "
		|			" + ?(ЕстьСерия,           "ДокНаб.СерияНоменклатуры,",                            "") + "
		|			" + ?(ЕстьКоэффициент,     "ДокНаб.ЕдиницаИзмерения.Коэффициент КАК Коэффициент,", "") + "
		|			" + ?(ЕстьДокументРезерва, "ДокТов.ДокументРезерва,",                              "") + "
		|			" + ?(ЕстьСкладВТабЧасти, "ДокТов."+ИмяТабЧастиСклада+" как Cклад,",              "") + "
		|				ДокНаб.Количество * ДокТов.Количество КАК Количество
		|			ИЗ
		|				Документ." + ИмяДокумента + ".СоставНабора   КАК ДокНаб
		|				ЛЕВОЕ СОЕДИНЕНИЕ Документ." + ИмяТаблицы + " КАК ДокТов
		|					ПО ДокТов.КлючСтроки = ДокНаб.КлючСтроки
		|					 И ДокТов.Ссылка     = &ДокументСсылка
		 |			ГДЕ
		|				ДокНаб.Ссылка = &ДокументСсылка
		|			И НЕ ДокНаб.Номенклатура.Комплект
		|			И Не ДокНаб.Номенклатура.Услуга

		|			) КАК Док
		|ИНДЕКСИРОВАТЬ ПО
		|		Номенклатура
		|		" + ?(ЕстьХарактеристика,  " , ХарактеристикаНоменклатуры",   "") + "
		|		" + ?(ЕстьСерия,           " , СерияНоменклатуры",            "") + "
		|		" + ?(ЕстьСкладВТабЧасти,  "  , Склад",              "") + "
		|		" + ?(ЕстьКачество,        " , Качество",                     "") + "
		|;
		|";
	Иначе
		ТекстЗапросаСписокНоменклатуры = "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Номенклатура 
		|ИЗ
		|	Документ." + ИмяТаблицы +"
		|ГДЕ  Ссылка = &ДокументСсылка
		|	И НЕ Номенклатура.Комплект
		|	И Не Номенклатура.Услуга "
		//начало изменений БП 14 
		+?(НеУчитыватьВоду,"И Не Номенклатура.ВидНоменклатуры = &ВидНоменклатураВода","")+" 
		//конец изменений БП 14 
		|";

		ТекстЗапросаРеквизитыДокумента = "
		|ВЫБРАТЬ
		|Док.Номенклатура, " +
		?(ЕстьХарактеристика,  "Док."+ТекстХарактеристикаНоменклатуры+", ","") +
		?(ЕстьСерия,           "Док."+ТекстСерияНоменклатуры+", ","") +
		?(ЕстьКоэффициент,     "Док.Коэффициент, ","") +
		?(ЕстьКачество,        "Док.Качество, ","") +
		?(ЕстьДокументРезерва, "Док.ДокументРезерва, ","") +
		//начало изменений БП 04 
		?(ЕстьСкладВТабЧасти,  "Док."+ИмяТабЧастиСклада+" как склад,","") + "
		//конец изменений БП 04 
		|Док.Количество
		|ПОМЕСТИТЬ Док
		|ИЗ	Документ." + ИмяТаблицы + " КАК Док
		|	ГДЕ
		|		Док.Ссылка = &ДокументСсылка
		|		И НЕ Док.Номенклатура.Комплект
		|		И Не Док.Номенклатура.Услуга "
		//начало изменений БП 14 
		+?(НеУчитыватьВоду,"И Не Номенклатура.ВидНоменклатуры = &ВидНоменклатураВода","")+" 		
		//конец изменений БП 14 
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура "+
		?(ЕстьХарактеристика, " ," + ТекстХарактеристикаНоменклатуры, "") +
		?(ЕстьСерия,          " , " + ТекстСерияНоменклатуры, "") +
		?(ЕстьСкладВТабЧасти, " , Склад","") +
		?(ЕстьКачество,       " , Качество","") + "
		|;
		|";
	КонецЕсли;

	Запрос = Новый Запрос;
	
	ИмяРеквизитаСклад = "Склад";
	Если МетаданныеДокумента.Реквизиты.Найти("СкладОрдер") <> Неопределено Тогда
		ИмяРеквизитаСклад = "СкладОрдер";
	КонецЕсли;
	//начало изменений БП 14
	Если МетаданныеДокумента.Реквизиты.Найти("СкладМатериалов") <> Неопределено Тогда
		ИмяРеквизитаСклад = "СкладМатериалов";
	КонецЕсли;
	//конец изменений БП 14 
	
	Если КонтролироватьОстатокПоСкладу Тогда
		Если ЕстьСкладВТабЧасти Тогда
			ЗапросСклады = новый Запрос;
			//начало изменений БП 04 
			ЗапросСклады.Текст = "Выбрать различные "+ИмяТабЧастиСклада+" как Склад ИЗ Документ."+ИмяТаблицы+"
			//конец изменений БП 04 
			|ГДЕ Ссылка=&ДокументСсылка";
			ЗапросСклады.УстановитьПараметр("ДокументСсылка",СтруктураШапкиДокумента.Ссылка);
			СписокСкладов = ЗапросСклады.Выполнить().Выгрузить().ВыгрузитьКолонку("Склад");
		Иначе
			СписокСкладов = новый СписокЗначений;
			СписокСкладов.Добавить(СтруктураШапкиДокумента[ИмяРеквизитаСклад]);
		КонецЕсли;
	Иначе
		СписокСкладов = новый СписокЗначений;
	КонецЕсли;

	// Установим параметры запроса
	Если ЭтоРеализацияОтгруженныхТоваров Тогда
		Запрос.УстановитьПараметр("ДокументСсылка", СтруктураШапкиДокумента.ДокументОтгрузки);
	Иначе
		Запрос.УстановитьПараметр("ДокументСсылка", СтруктураШапкиДокумента.Ссылка);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Организация",       СтруктураШапкиДокумента.Организация);
	Запрос.УстановитьПараметр("Склад",             СтруктураШапкиДокумента[ИмяРеквизитаСклад]);
	Запрос.УстановитьПараметр("СписокСкладов",     СписокСкладов);
	Запрос.УстановитьПараметр("ПустаяСерия",       Справочники.СерииНоменклатуры.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустоеКачество",    Справочники.Качество.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустойКомиссионер", Справочники.Контрагенты.ПустаяСсылка());
	
	//начало изменений БП 14
	Запрос.УстановитьПараметр("ВидНоменклатураВода",ВидНоменклатурыВода); 
	//конец изменений БП 14 

	Если СтруктураШапкиДокумента.Свойство("Комиссионер") Тогда
		Запрос.УстановитьПараметр("Комиссионер",      СтруктураШапкиДокумента.Комиссионер);
	Иначе
		Запрос.УстановитьПараметр("Комиссионер",      Справочники.Контрагенты.ПустаяСсылка());
	КонецЕсли;
	Если ЕстьДокументПередачи Тогда
		Запрос.УстановитьПараметр("ДокументПередачи", СтруктураШапкиДокумента.ДокументПередачи);
	КонецЕсли;
	//начало изменений БП 04
	ПРГ_ИспользоватьДатуОстатков = Ложь;
	Если  РежимПроведения = РежимПроведенияДокумента.Оперативный Тогда
		  ДатаОстатков = '39990101';
	  Иначе
		  ПРГ_ИспользоватьДатуОстатков = Истина;
		  ДатаОстатков = Новый Граница(Новый МоментВремени(СтруктураШапкиДокумента.Дата,СтруктураШапкиДокумента.Ссылка),ВидГраницы.Исключая);
	 КонецЕсли;	
	 Запрос.УстановитьПараметр("ДатаОстатков",ДатаОстатков);
	//конец изменений БП 04 

	Запрос.Текст = ТекстЗапросаРеквизитыДокумента + "
	|ВЫБРАТЬ
	|	КоличествоОстаток,
	|	Номенклатура
	|"	+ ?(ЕстьХарактеристика, " , ХарактеристикаНоменклатуры ", "") + "
	|"	+ ?(ЕстьСерия,          " , СерияНоменклатуры ", "") + "
	|"	+ ?(КонтролироватьОстатокПоСкладу И ЕстьСкладВТабЧасти, ", Склад ", "") + "
	|"	+ ?(ЕстьКачество,       ", Качество ", "") + "
	|ПОМЕСТИТЬ ОстаткиКПередаче
	|ИЗ
	//начало изменений БП 04 
	|	РегистрНакопления.ТоварыКПередачеОрганизаций.Остатки("+?(ПРГ_ИспользоватьДатуОстатков,"&ДатаОстатков","")+"," +
	//конец изменений БП 04 
	?(КонтролироватьОстатокПоСкладу , "Склад в (&СписокСкладов) И ","") + "
	|		Номенклатура В (" + ТекстЗапросаСписокНоменклатуры + ")
	|	И Организация = &Организация
	|	И &Комиссионер = &ПустойКомиссионер"
	+ ?(ЕстьДокументПередачи,	"
	|	И ДокументПередачи <> &ДокументПередачи", "") + "
	|	) КАК ОстаткиКПередаче
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|"	+ ?(ЕстьХарактеристика, " , ХарактеристикаНоменклатуры ", "") + "
	|"	+ ?(ЕстьСерия,          " , СерияНоменклатуры ", "") + "
	|"	+ ?(КонтролироватьОстатокПоСкладу И ЕстьСкладВТабЧасти, ", Склад ", "") + "
	|"	+ ?(ЕстьКачество,       " , Качество ", "") + "
	|;
	|ВЫБРАТЬ
	|	КоличествоОстаток,
	|	Номенклатура
	|"	+ ?(ЕстьХарактеристика, " , ХарактеристикаНоменклатуры", "") + "
	|"	+ ?(ЕстьСерия,          " , СерияНоменклатуры", "") + "
	|"	+ ?(КонтролироватьОстатокПоСкладу И ЕстьСкладВТабЧасти, " , Склад", "") + "
	|"	+ ?(ЕстьКачество,       " , Качество", "") + "
	|ПОМЕСТИТЬ Остатки
	|ИЗ
	//начало изменений БП 04 
	|	РегистрНакопления.ТоварыОрганизаций.Остатки("+?(ПРГ_ИспользоватьДатуОстатков,"&ДатаОстатков","")+","+
	//конец изменений БП 04 
	?(КонтролироватьОстатокПоСкладу , "Склад в (&СписокСкладов) И ","")+"
	|		Номенклатура В (" + ТекстЗапросаСписокНоменклатуры + ")
	|	И Организация = &Организация
	|	И Комиссионер = &Комиссионер) КАК Остатки
	|ДЛЯ ИЗМЕНЕНИЯ РегистрНакопления.ТоварыОрганизаций.Остатки // Блокирующие чтение таблицы остатков регистра для разрешения коллизий многопользовательской работы
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|"	+ ?(ЕстьХарактеристика, " , ХарактеристикаНоменклатуры", "") + "
	|"	+ ?(ЕстьСерия,          " , СерияНоменклатуры", "") + "
	|"	+ ?(КонтролироватьОстатокПоСкладу И ЕстьСкладВТабЧасти, " , Склад", "") + "
	|"	+ ?(ЕстьКачество,       " , Качество", "") + "
	|;
	|ВЫБРАТЬ // Запрос, контролирующий остатки на складах
	|	Док.Номенклатура.Представление                         КАК НоменклатураПредставление,
	|	Док.Номенклатура.ЕдиницаХраненияОстатков.Представление КАК ЕдиницаХраненияОстатковПредставление,"
	+?(ЕстьДокументРезерва, " 
	|Док.ДокументРезерва 									  КАК ДокументРезерва, ","")
	+?(ЕстьСкладВТабЧасти, "
	|Док.Склад												КАК Склад,","")
	+ ?(ЕстьХарактеристика, "
	|	Док." + ТекстХарактеристикаНоменклатуры + "           КАК ХарактеристикаНоменклатуры,"
	,"")
	+ ?(ЕстьСерия, "
	|	Док." + ТекстСерияНоменклатуры + "                    КАК СерияНоменклатуры,"
	,"") + "
	|   &Комиссионер                                           КАК Комиссионер," 
	+ ?(ЕстьКоэффициент, "
	|	СУММА(ВЫРАЗИТЬ(Док.Количество * Док.Коэффициент /Док.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент КАК Число(15,3))) КАК ДокументКоличество,", "
	|	СУММА(Док.Количество)                                  КАК ДокументКоличество,") + "
	|	ЕСТЬNULL(МАКСИМУМ(Остатки.КоличествоОстаток), 0)          КАК ОстатокКоличество,
	|	ЕСТЬNULL(МАКСИМУМ(ОстаткиКПередаче.КоличествоОстаток), 0) КАК ОстатокКПередаче,
	|	0                                                      КАК ОстатокКПередачеПоДокументу
	|ИЗ
	|	Док
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	Остатки КАК Остатки
	|ПО
	|	Док.Номенклатура        = Остатки.Номенклатура "
	+ ?(ЕстьХарактеристика, "
	| И Док." + ТекстХарактеристикаНоменклатуры + "= Остатки.ХарактеристикаНоменклатуры"
	,"")
	+ ?(ЕстьСерия, "
	| И Док." + ТекстСерияНоменклатуры + "           = Остатки.СерияНоменклатуры"
	,"")
	+ ?(КонтролироватьОстатокПоСкладу И ЕстьСкладВТабЧасти, "
	| И Док.Склад = Остатки.Склад"
	,"")
	+ ?(ЕстьКачество, "
	| И Док." + ТекстКачество + "                   = Остатки.Качество"
	,"") + "
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	ОстаткиКПередаче КАК ОстаткиКПередаче
	|ПО
	|	Док.Номенклатура                            = ОстаткиКПередаче.Номенклатура "
	+ ?(ЕстьХарактеристика, "
	| И Док." + ТекстХарактеристикаНоменклатуры + " = ОстаткиКПередаче.ХарактеристикаНоменклатуры"
	,"")
	+ ?(ЕстьСерия, "
	| И Док." + ТекстСерияНоменклатуры + "= ОстаткиКПередаче.СерияНоменклатуры"
	,"")
	+ ?(КонтролироватьОстатокПоСкладу И ЕстьСкладВТабЧасти, "
	| И Док.Склад = ОстаткиКПередаче.Склад"
	,"")
	+ ?(ЕстьКачество, "
	| И Док." + ТекстКачество + "                   = ОстаткиКПередаче.Качество"
	,"")+" 
	|
	|СГРУППИРОВАТЬ ПО
	|
	|	Док.Номенклатура "
	+ ?(ЕстьХарактеристика, "
	|	, Док." + ТекстХарактеристикаНоменклатуры
	,"")
	+ ?(ЕстьСерия, "
	|	, Док." + ТекстСерияНоменклатуры
	,"")
	+ ?(ЕстьКачество, "
	|	, Док." + ТекстКачество
	,"") + 
	?(ЕстьДокументРезерва, ", Док.ДокументРезерва","")+
	?(ЕстьСкладВТабЧасти, ", Док.Склад","")+"
	|
	|ИМЕЮЩИЕ ЕСТЬNULL(МАКСИМУМ(Остатки.КоличествоОстаток), 0) - ЕСТЬNULL(МАКСИМУМ(ОстаткиКПередаче.КоличествоОстаток), 0) < "	
	+ ?(ЕстьКоэффициент, "СУММА(ВЫРАЗИТЬ(Док.Количество * Док.Коэффициент /Док.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент КАК ЧИСЛО(15,3)))", "СУММА(Док.Количество)") + "
	|
	|";

	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл

		КоличествоКПередачеПоДокументу = 0;//?(Выборка.ОстатокКПередачеПоДокументу = NULL, 0, Выборка.ОстатокКПередачеПоДокументу);

		СтрокаСообщения = "Остатка по организации " + СтруктураШапкиДокумента.Организация +
		?(КонтролироватьОстатокПоСкладу," по складу "+?(ЕстьСкладВТабЧасти,Выборка.Склад,СтруктураШапкиДокумента[ИмяРеквизитаСклад]),"")+
		" товара " +  
		УправлениеЗапасами.ПредставлениеНоменклатуры(Выборка.НоменклатураПредставление, 
								  ?(ЕстьХарактеристика, Выборка.ХарактеристикаНоменклатуры, ""),
								  ?(ЕстьСерия, Выборка.СерияНоменклатуры,"")) + 
								  ?(НЕ ЗначениеЗаполнено(Выборка.Комиссионер), "", " у комиссионера " + Выборка.Комиссионер) +
		" недостаточно.";
		СвободныйОстаток = Выборка.ОстатокКоличество - (Выборка.ОстатокКПередаче - КоличествоКПередачеПоДокументу);

		УправлениеЗапасами.ОшибкаНетОстатка(
			СтрокаСообщения, 
			СвободныйОстаток, 
			Выборка.ДокументКоличество,
			Выборка.ЕдиницаХраненияОстатковПредставление, 
			Отказ, 
			Заголовок);
		
		Если (Выборка.ОстатокКПередаче - КоличествоКПередачеПоДокументу)<>0 Тогда
			Сообщить("Зарезервировано под выписанные документы: " + (Выборка.ОстатокКПередаче - КоличествоКПередачеПоДокументу));
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры // КонтрольОстатков()

// Процедура контролирует остаток по данному регистру по переданному документу
// и его табличной части. В случае недостатка товаров выставляется флаг отказа и 
// выдается сообщегние.
//
// Параметры:
//  ДокументОбъект    - объект проводимого документа, 
//  ИмяТабличнойЧасти - строка, имя табличной части, которая проводится по регистру, 
//  СтруктураШапкиДокумента - структура, содержащая значения "через точку" ссылочных реквизитов по шапке документа,
//  Отказ             - флаг отказа в проведении,
//  Заголовок         - строка, заголовок сообщения об ошибке проведения.
//
Процедура КонтрольОстатковРазукомплектация(ДокументОбъект, СтруктураШапкиДокумента, Отказ, Заголовок) Экспорт

	Если УправлениеДопПравамиПользователей.РазрешеноПревышениеОстаткаТоваровОрганизации(СтруктураШапкиДокумента.Организация) Тогда
		Возврат;
	КонецЕсли;
	
	МетаданныеДокумента = ДокументОбъект.Метаданные();
	ИмяДокумента        = МетаданныеДокумента.Имя;
	ИмяТаблицы          = ИмяДокумента;
	
	Если ТипЗнч(СтруктураШапкиДокумента.Ссылка) = Тип("ДокументСсылка.КорректировкаСерийИХарактеристикТоваров") Тогда
		ТекстХарактеристикаНоменклатуры = "ХарактеристикаНоменклатурыСтарая";
		ТекстСерияНоменклатуры = "СерияНоменклатурыСтарая";
	Иначе
		ТекстХарактеристикаНоменклатуры = "ХарактеристикаНоменклатуры";
		ТекстСерияНоменклатуры = "СерияНоменклатуры";
	КонецЕсли; 
	                                            
	ЕстьСерия           = (МетаданныеДокумента.Реквизиты.Найти(ТекстСерияНоменклатуры) <> Неопределено);
	ЕстьХарактеристика  = (МетаданныеДокумента.Реквизиты.Найти(ТекстХарактеристикаНоменклатуры) <> Неопределено);
	ЕстьДокументРезерва = (МетаданныеДокумента.Реквизиты.Найти("ДокументРезерва") <> Неопределено);

	// Текст вложенного запроса, ограничивающего номенклатуру при получении остатков
	ТекстЗапросаСписокНоменклатуры = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Номенклатура 
	|ИЗ
	|	Документ." + ИмяТаблицы +"
	|ГДЕ Ссылка = &ДокументСсылка
	|И Не Номенклатура.Услуга";
	
	ТекстЗапросаРеквизитыДокумента = "(ВЫБРАТЬ
	|Док.Номенклатура, "+
	?(ЕстьХарактеристика, "Док.ХарактеристикаНоменклатуры, ","")+
	?(ЕстьСерия, "Док.СерияНоменклатуры, ","")+"
	|Док.Коэффициент, 
	|Док.Количество
	|ИЗ	Документ." + ИмяТаблицы + " КАК Док
	|	ГДЕ
	|		Док.Ссылка = &ДокументСсылка
	|       И Не Док.Номенклатура.Услуга "+
		?(ЕстьДокументРезерва, "
	| // Если мы списываем горячий резерв, то контроль остатков по организации не нужен,
	| //поскольку с организации этот товар уже списан
	|   И НЕ (Док.ДокументРезерва ССЫЛКА Документ.РеализацияТоваровУслуг
	|         ИЛИ Док.ДокументРезерва ССЫЛКА Документ.ПередачаТоваров)", "") + "
	|) КАК Док
	|";

	Запрос = Новый Запрос;

	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка", СтруктураШапкиДокумента.Ссылка);
	Запрос.УстановитьПараметр("Организация", СтруктураШапкиДокумента.Организация); 
	Запрос.УстановитьПараметр("Склад", СтруктураШапкиДокумента.Склад); 

	Если СтруктураШапкиДокумента.Свойство("Комиссионер") Тогда
		Запрос.УстановитьПараметр("Комиссионер", СтруктураШапкиДокумента.Комиссионер); 
	Иначе
		Запрос.УстановитьПараметр("Комиссионер", Справочники.Контрагенты.ПустаяСсылка()); 
	КонецЕсли;
	
	СтруктураУП = ОбщегоНазначения.ПолучитьПараметрыУчетнойПолитикиУпр(СтруктураШапкиДокумента.Ссылка.Дата);
    Если НЕ ЗначениеЗаполнено(СтруктураУП) Тогда
		Отказ = Истина;
	КонецЕсли; 
	КонтролироватьОстатокПоСкладу = СтруктураУП.ВестиУчетТоваровОрганизацийВРазрезеСкладов;


	Запрос.Текст = "
	|ВЫБРАТЬ // Запрос, контролирующий остатки на складах
	|	Док.Номенклатура.Представление                         КАК НоменклатураПредставление,
	|	Док.Номенклатура.ЕдиницаХраненияОстатков.Представление КАК ЕдиницаХраненияОстатковПредставление,"
	+ ?(ЕстьХарактеристика, "
	|	Док." + ТекстХарактеристикаНоменклатуры + "           КАК ХарактеристикаНоменклатуры,"
	,"")
	+ ?(ЕстьСерия, "
	|	Док." + ТекстСерияНоменклатуры + "                    КАК СерияНоменклатуры,"
	,"") + "
	|   &Комиссионер                                           КАК Комиссионер,
	|	СУММА(Док.Количество * Док.Коэффициент /Док.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент) КАК ДокументКоличество, 
	|	ЕСТЬNULL(МАКСИМУМ(Остатки.КоличествоОстаток), 0)                    КАК ОстатокКоличество
	|ИЗ "+ТекстЗапросаРеквизитыДокумента+"
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрНакопления.ТоварыОрганизаций.Остатки(,
	|		Номенклатура В (" + ТекстЗапросаСписокНоменклатуры + ")
	|	И Организация = &Организация
	|   И Комиссионер = &Комиссионер "+
	?(КонтролироватьОстатокПоСкладу,"И Склад = &Склад","")+"
	|	) КАК Остатки
	|ПО 
	|	Док.Номенклатура        = Остатки.Номенклатура"
	+ ?(ЕстьХарактеристика, "
	| И Док." + ТекстХарактеристикаНоменклатуры + "   = Остатки.ХарактеристикаНоменклатуры"
	,"")
	+ ?(ЕстьСерия, "
	| И Док." + ТекстСерияНоменклатуры + "           = Остатки.СерияНоменклатуры"
	,"") + "
	|
	|СГРУППИРОВАТЬ ПО
	|
	|	Док.Номенклатура"
	+ ?(ЕстьХарактеристика, "
	|	, Док." + ТекстХарактеристикаНоменклатуры
	,"")
	+ ?(ЕстьСерия, "
	|	, Док." + ТекстСерияНоменклатуры
	,"") + "
	|	
	|ИМЕЮЩИЕ ЕСТЬNULL(МАКСИМУМ(Остатки.КоличествоОстаток), 0) < СУММА(Док.Количество * Док.Коэффициент /Док.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент)
	|
	|ДЛЯ ИЗМЕНЕНИЯ РегистрНакопления.ТоварыОрганизаций.Остатки // Блокирующие чтение таблицы остатков регистра для разрешения коллизий многопользовательской работы
	|";

	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл

		СтрокаСообщения = "Остатка по организации " + СтруктураШапкиДокумента.Организация 
		+ ?(КонтролироватьОстатокПоСкладу, " по складу "+СтруктураШапкиДокумента.Склад,"")
		+ " товара " +  
		УправлениеЗапасами.ПредставлениеНоменклатуры(Выборка.НоменклатураПредставление, 
								  ?(ЕстьХарактеристика, Выборка.ХарактеристикаНоменклатуры, ""),
								  ?(ЕстьСерия, Выборка.СерияНоменклатуры,"")) + 
								  ?(НЕ ЗначениеЗаполнено(Выборка.Комиссионер), "", " у комиссионера " + Выборка.Комиссионер) +
		" недостаточно.";

		УправлениеЗапасами.ОшибкаНетОстатка(
			СтрокаСообщения, 
			Выборка.ОстатокКоличество,
			Выборка.ДокументКоличество,
			Выборка.ЕдиницаХраненияОстатковПредставление, 
			Отказ,
			Заголовок);

	КонецЦикла;

КонецПроцедуры // КонтрольОстатков()


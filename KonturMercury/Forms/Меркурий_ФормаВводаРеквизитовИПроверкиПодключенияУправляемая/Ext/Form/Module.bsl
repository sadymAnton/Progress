&НаСервере
Перем ОбработкаОбъект;

//******************************* Общие **********************************

&НаСервере
Функция МодульОбъекта()

	Если ОбработкаОбъект=Неопределено Тогда
		
		ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
		
	КонецЕсли;	
	
	Возврат ОбработкаОбъект;
	
КонецФункции

&НаКлиенте
Процедура ПредупреждениеМеркурий_Оповещение(Парам1, Парам2) Экспорт
КонецПроцедуры

&НаКлиенте
Функция ПредупреждениеМеркурий(ТекстПредупреждения)
	
	Кнопки = новый СписокЗначений;
	Кнопки.Добавить("Всё понятно.");
	
	Если Параметры.МодальностьЗапрещена Тогда
		Выполнить("ПоказатьВопрос(Новый ОписаниеОповещения(""ПредупреждениеМеркурий_Оповещение"",ЭтаФорма),ТекстПредупреждения,Кнопки,,,""Контур.Меркурий"")");
	Иначе
		Вопрос(ТекстПредупреждения, Кнопки,,,"Контур.Меркурий");
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ЯПолучалРеквизитыНоПотерялИхВПочте(Команда)
	ПредупреждениеМеркурий("В таком случае вам следует поискать их в почте по таким ключевым словам:"+Символы.ПС+
	"""no-reply@vetrf.ru""/""Предоставление доступа""/""Ниже перечислены реквизиты"""+Символы.ПС+
	"Если же письмо так и не найдется - проверьде другие почтовые ящики, на которые вы могли запросить реквизиты доступа."+Символы.ПС+
	"А в случае неудачи - обратитесь в техподдержку Меркурия по телефону 8 (4922) 52-99-29");
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьРеквизитыПомощь(Команда)
	ПредупреждениеМеркурий("Сейчас мы откроем вам официальную страницу Меркурия с подробным описанием этого процесса 
	|- Достаточно просто выполнить то что там написано.");
	ЗапуститьПриложение("http://help.vetrf.ru/wiki/%D0%92%D0%B5%D1%82%D0%B8%D1%81.API#.D0.9F.D1.80.D0.B5.D0.B4.D0.BE.D1.81.D1.82.D0.B0.D0.B2.D0.BB.D0.B5.D0.BD.D0.B8.D0.B5_.D0.B4.D0.BE.D1.81.D1.82.D1.83.D0.BF.D0.B0");
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	//попробовать прочитать реквизиты
	Попытка
		ЛогинAPI = МодульОбъекта().локПолучитьКонстантуEDI("М_ЛогинAPI");
		ПарольAPI = МодульОбъекта().локПолучитьКонстантуEDI("М_ПарольAPI");
		
		КлючAPI = МодульОбъекта().локПолучитьКонстантуEDI("М_КлючAPI");
		IssuerID = МодульОбъекта().локПолучитьКонстантуEDI("М_issuerId");
		ЛогинПользователя = МодульОбъекта().локПолучитьКонстантуEDI("М_ЛогинПользователя");
		
		НачинаемРаботуНаТестовомСервереНастройка = МодульОбъекта().локПолучитьКонстантуEDI("М_ИспользуетсяТестовыйAPI");
		Если НачинаемРаботуНаТестовомСервереНастройка = Ложь Тогда 
			НачинаемРаботуНаТестовомСервере = Ложь;
		Иначе
			НачинаемРаботуНаТестовомСервере = Истина;
		КонецЕсли;
	Исключение	
	КонецПопытки;
	
	//ЛогинAPIПриИзменении();

КонецПроцедуры


&НаСервере
Функция ПодключитьМодульEDIНаСервере()
	Возврат МодульОбъекта().ПриветствиеПодключитьМодульEDI();
КонецФункции


&НаКлиенте
Процедура РаспознатьИзТекстаПисьмаОповещение(ВведеннаяСтрока, Парам2 = неопределено) Экспорт
	//Login Ivanov Password ********* APIKey xxxxxxx ServiceID ssssssss IssuerID iiiiiiii
	Попытка
		Если СокрЛП(ВведеннаяСтрока) = "" ТОгда ПредупреждениеМеркурий("Ничего не вставилось. Может попробуете еще раз?"); Возврат; КонецЕсли;
		
		Если Найти(ВведеннаяСтрока,"Login") = 0 ТОгда ПредупреждениеМеркурий("Мы не нашли в тексте логина API. Может попробуете еще раз?"); Возврат; КонецЕсли;
		Если Найти(ВведеннаяСтрока,"IssuerID") = 0 ТОгда ПредупреждениеМеркурий("Мы не нашли в тексте IssuerID. Может попробуете еще раз?"); Возврат; КонецЕсли;
		
		ПозицияLOGIN = Найти(ВведеннаяСтрока,"Login");
		ПозицияPASS = Найти(ВведеннаяСтрока,"Password");
		ПозицияAPIKEY = Найти(ВведеннаяСтрока,"APIKey");
		ПозицияIssuerID = Найти(ВведеннаяСтрока,"IssuerID");
		ПозицияServiceID = Найти(ВведеннаяСтрока,"ServiceID");
		       
		ЛогинAPI 	= СокрЛП(Сред(ВведеннаяСтрока,ПозицияLOGIN+6,ПозицияPASS-ПозицияLOGIN-6));
		ПарольAPI 	= СокрЛП(Сред(ВведеннаяСтрока,ПозицияPASS+9,ПозицияAPIKEY-ПозицияPASS-9));
		КлючAPI 	= СокрЛП(Сред(ВведеннаяСтрока,ПозицияAPIKEY+7,ПозицияServiceID-ПозицияAPIKEY-7));
		IssuerID    = СокрЛП(Сред(ВведеннаяСтрока,ПозицияIssuerID+9,38));
		
		ПредупреждениеМеркурий("Like a boss..."+Символы.ПС+Символы.ПС+"Логин пользователя системы (Account name) мог прийти отдельным письмом на эл. почту, указанную при регистрации пользователя
		| Этот реквизит (Логин пользователя) придется заполнить самостоятельно");
		
	Исключение
		ПредупреждениеМеркурий("Что-то пошло не так с чтением строки, попробуйте заново");
	КонецПопытки;
	//ЛогинAPIПриИзменении();

КонецПроцедуры

&НаКлиенте
Процедура РаспознатьИзТекстаПисьма(Команда)
	ВведеннаяСтрока = "скопируйте сюда фрагмент текст письма от no-reply@vetrf.ru с реквизитами доступа ";
	Подсказка = "Сейчас мы все сами вытащим из письма";
	Если Параметры.МодальностьЗапрещена Тогда
		Выполнить("ПоказатьВводСтроки(Новый ОписаниеОповещения(""РаспознатьИзТекстаПисьмаОповещение"", ЭтаФорма), ВведеннаяСтрока, Подсказка,,Истина)");
	Иначе
		Если ВвестиСтроку(ВведеннаяСтрока,Подсказка,,Истина) Тогда
			РаспознатьИзТекстаПисьмаОповещение(ВведеннаяСтрока);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ПроверитьПодключениеНаСервере()
	Возврат МодульОбъекта().ПриветствиеПроверитьПодключение(Новый Структура("КлючAPI,ЛогинAPI,ПарольAPI,IssuerID,ЛогинПользователя,НачинаемРаботуНаТестовомСервере",КлючAPI,ЛогинAPI,ПарольAPI,IssuerID,ЛогинПользователя,НачинаемРаботуНаТестовомСервере));
КонецФункции

&НаКлиенте
Функция СохранитьОповещение(Парам1, Парам2 = Неопределено) Экспорт
	
	Если Парам1 = КодВозвратаДиалога.Да Тогда
		//Проверяем подключение
		УспешноПроверилиПодключение = ПроверитьПодключениеНаСервере();
		
		Если УспешноПроверилиПодключение = Истина Тогда
			
			ТекстОшибки = ПодключитьМодульEDIНаСервере();
			Если ТекстОшибки = "" Тогда
				ЭтаФорма.Закрыть(Истина);
			Иначе
				ПредупреждениеМеркурий(ТекстОшибки);
			КонецЕсли;
		Иначе
			ПредупреждениеМеркурий("Кажется, что-то пошло не так. Внимательно прочитайте сообщение об ошибке. Еще раз внимательно проверьте реквизиты подключения. Проверьте интернет соединение. Попросите вашего системного администратора проверить доступность точки доступа: https://api2.vetrf.ru:8002.");// Проверьте доступность сервера меркурий на нашем сайте: https://kontur.ru/lp/edi-mercury ");
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат "";
	
КонецФункции

&НаКлиенте
Процедура Сохранить(Команда)
	
	Если Не ЗначениеЗаполнено(КлючAPI) ИЛИ
		Не ЗначениеЗаполнено(ЛогинAPI) ИЛИ
		Не ЗначениеЗаполнено(ПарольAPI) ИЛИ
		Не ЗначениеЗаполнено(IssuerID) ИЛИ
		Не ЗначениеЗаполнено(ЛогинПользователя)
		Тогда 
		ПредупреждениеМеркурий("Необходимо заполнить все 5 реквизитов доступа.");
		возврат;
	КонецЕсли;
	
	
	Если НачинаемРаботуНаТестовомСервере Тогда 
		СохранитьОповещение(КодВозвратаДиалога.Да);
	Иначе
		ТекстВопроса = "Вы уверены что хотите начать работу на боевом сервере";
		Если Параметры.МодальностьЗапрещена Тогда
			Выполнить("ПоказатьВопрос(Новый ОписаниеОповещения(""СохранитьОповещение"", ЭтаФорма), ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена)");
		Иначе
			СохранитьОповещение(КодВозвратаДиалога.Да);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура МнеНужнаПомощь(Команда)
	ЗапуститьПриложение("https://kontur.ru/support");
КонецПроцедуры


